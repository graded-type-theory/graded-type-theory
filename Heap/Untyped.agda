------------------------------------------------------------------------
-- A resource aware heap semantics. Some basic definitions.
------------------------------------------------------------------------

open import Graded.Modality
open import Tools.Bool

module Heap.Untyped
  {a} {M : Set a} (ğ•„ : Modality M)
  where
open Modality ğ•„

open import Tools.Empty
open import Tools.Fin
open import Tools.Function
open import Tools.Nat hiding (_â‰¤_)
open import Tools.PropositionalEquality
open import Tools.Product
open import Tools.Relation

open import Definition.Untyped M hiding (head)
open import Graded.Modality.Properties.Subtraction semiring-with-meet
open import Graded.Modality.Nr-instances

private variable
  n nâ€² m mâ€² mâ€³ nâ€³ : Nat
  Î“ : Con Term _
  t tâ‚ tâ‚‚ u v A B : Term _
  x : Fin _
  p q r : M
  s : Strength
  b : BinderMode
  Ï : Wk _ _

infixl 20 _âŠ¢_â†¦[_]_â¨¾_
infixl 24 _âˆ™_
infix   2 âŸ¨_,_,_,_âŸ©

------------------------------------------------------------------------
-- Pointers, closures and environments

-- Pointers as de Bruijn indices

Ptr : Nat â†’ Set
Ptr = Fin

pattern y0 = x0

-- An enivronment is a weakening,
-- mapping variables (de Bruijn indices) to pointers (de Bruijn indices).

Env : (m n : Nat) â†’ Set
Env = Wk

-- Closures, indexed by the size of the heap and the number of
-- free variables of the term

Closure : (m n : Nat) â†’ Set a
Closure m n = Term n Ã— Env m n

-- Closures with a grade

Closureâ‚˜ : (m n : Nat) â†’ Set a
Closureâ‚˜ m n = M Ã— Closure m n

-- Weakening of closures

wká¶œ : Wk mâ€² m â†’ Closure m n â†’ Closure mâ€² n
wká¶œ Ï (t , E) = t , Ï â€¢ E

wk1á¶œ : Closure m n â†’ Closure (1+ m) n
wk1á¶œ = wká¶œ (step id)

------------------------------------------------------------------------
-- Eliminators and Evaluation stacks

-- Eliminators, indexed by the size of the heap

data Elim (m : Nat) : Set a where
  âˆ˜â‚‘        : (p : M) (u : Term n) (E : Env m n) â†’ Elim m
  fstâ‚‘      : M â†’ Elim m
  sndâ‚‘      : M â†’ Elim m
  prodrecâ‚‘  : (r p q : M) (A : Term (1+ n)) (u : Term (2+ n)) (E : Env m n) â†’ Elim m
  natrecâ‚‘   : (p q r : M) (A : Term (1+ n)) (z : Term n)
              (s : Term (2+ n)) (E : Env m n) â†’ Elim m
  unitrecâ‚‘  : (p q : M) (A : Term (1+ n)) (u : Term n) (E : Env m n) â†’ Elim m
  Jâ‚‘        : (p q : M) (A t : Term n) (B : Term (2+ n))
              (u v : Term n) (E : Env m n) â†’ Elim m
  Kâ‚‘        : (p : M) (A t : Term n) (B : Term (1+ n))
              (u : Term n) (E : Env m n) â†’ Elim m
  []-congâ‚‘  : (s : Strength) (A t u : Term n) (E : Env m n) â†’ Elim m
  sucâ‚‘      : Elim m

-- Weakening of eliminators

wkáµ‰ : Wk mâ€² m â†’ Elim m â†’ Elim mâ€²
wkáµ‰ Ï (âˆ˜â‚‘ p u E) = âˆ˜â‚‘ p u (Ï â€¢ E)
wkáµ‰ Ï (fstâ‚‘ p) = fstâ‚‘ p
wkáµ‰ Ï (sndâ‚‘ p) = sndâ‚‘ p
wkáµ‰ Ï (natrecâ‚‘ p q r A z s E) = natrecâ‚‘ p q r A z s (Ï â€¢ E)
wkáµ‰ Ï (prodrecâ‚‘ r p q A u E) = prodrecâ‚‘ r p q A u (Ï â€¢ E)
wkáµ‰ Ï (unitrecâ‚‘ p q A u E) = unitrecâ‚‘ p q A u (Ï â€¢ E)
wkáµ‰ Ï (Jâ‚‘ p q A t B u v E) = Jâ‚‘ p q A t B u v (Ï â€¢ E)
wkáµ‰ Ï (Kâ‚‘ p A t B u E) = Kâ‚‘ p A t B u (Ï â€¢ E)
wkáµ‰ Ï ([]-congâ‚‘ s A t u E) = []-congâ‚‘ s A t u (Ï â€¢ E)
wkáµ‰ Ï sucâ‚‘ = sucâ‚‘

wk1áµ‰ : Elim m â†’ Elim (1+ m)
wk1áµ‰ = wkáµ‰ (step id)

wk2áµ‰ : Elim m â†’ Elim (2+ m)
wk2áµ‰ = wkáµ‰ (step (step id))

-- Multiplicity of an eliminator, representing how many copies need to be evaluated

âˆ£_âˆ£áµ‰ : â¦ƒ _ : Has-nr M semiring-with-meet â¦„
     â†’ â¦ƒ _ : Has-factoring-nr M semiring-with-meet â¦„
     â†’ Elim m â†’ M
âˆ£ âˆ˜â‚‘ _ _ _ âˆ£áµ‰ = ğŸ™
âˆ£ fstâ‚‘ _ âˆ£áµ‰ = ğŸ™
âˆ£ sndâ‚‘ _ âˆ£áµ‰ = ğŸ™
âˆ£ prodrecâ‚‘ r _ _ _ _ _ âˆ£áµ‰ = r
âˆ£ natrecâ‚‘ p _ r _ _ _ _ âˆ£áµ‰ = nrâ‚‚ p r
âˆ£ unitrecâ‚‘ p _ _ _ _ âˆ£áµ‰ = p
âˆ£ Jâ‚‘ _ _ _ _ _ _ _ _ âˆ£áµ‰ = Ï‰
âˆ£ Kâ‚‘ _ _ _ _ _ _ âˆ£áµ‰ = Ï‰
âˆ£ []-congâ‚‘ _ _ _ _ _ âˆ£áµ‰ = ğŸ™
âˆ£ sucâ‚‘ âˆ£áµ‰ = ğŸ™

-- Evaluation stacks, indexed by the size of the heap

data Stack (m : Nat) : Set a where
  Îµ : Stack m
  _âˆ™_ : (e : Elim m) â†’ (S : Stack m) â†’ Stack m

-- Multiplicity of a stack, representing how many copies are currently being evaluated

âˆ£_âˆ£ : â¦ƒ _ : Has-nr M semiring-with-meet â¦„
    â†’ â¦ƒ _ : Has-factoring-nr M semiring-with-meet â¦„
    â†’  Stack m â†’ M
âˆ£ Îµ âˆ£ = ğŸ™
âˆ£ e âˆ™ S âˆ£ = âˆ£ S âˆ£ Â· âˆ£ e âˆ£áµ‰

-- Weakening of stacks

wkË¢ : Wk mâ€² m â†’ Stack m â†’ Stack mâ€²
wkË¢ Ï Îµ = Îµ
wkË¢ Ï (e âˆ™ S) = wkáµ‰ Ï e âˆ™ wkË¢ Ï S

wk1Ë¢ : Stack m â†’ Stack (1+ m)
wk1Ë¢ = wkË¢ (step id)

wk2Ë¢ : Stack m â†’ Stack (2+ m)
wk2Ë¢ = wkË¢ (step (step id))

-- Concatenation of stacks

_++_ : (S Sâ€² : Stack m) â†’ Stack m
Îµ ++ Sâ€² = Sâ€²
(e âˆ™ S) ++ Sâ€² = e âˆ™ (S ++ Sâ€²)

sucâ‚› : Nat â†’ Stack m
sucâ‚› 0 = Îµ
sucâ‚› (1+ n) = sucâ‚‘ âˆ™ sucâ‚› n

------------------------------------------------------------------------
-- Heaps

-- Heaps are collections of bindings.

data Heap : (m : Nat) â†’ Set a where
  Îµ   : Heap 0
  _âˆ™_ : (H : Heap m) â†’ (c : Closureâ‚˜ m n) â†’ Heap (1+ m)

private variable
  H Hâ€² : Heap _
  c : Closure _ _
  câ€² : Closureâ‚˜ _ _
  E Eâ€² : Env _ _
  S : Stack _

-- Heap lookup (with grade update)
-- Note that lookup fails e.g. when the grade is ğŸ˜.

data _âŠ¢_â†¦[_]_â¨¾_ : (H : Heap m) (y : Ptr m) (q : M)
                  (c : Closure m n) (Hâ€² : Heap m) â†’ Set a where
  here : p - q â‰¡ r
       â†’ H âˆ™ (p , c) âŠ¢ y0 â†¦[ q ] wk1á¶œ c â¨¾ H âˆ™ (r , c)
  there : {y : Ptr m} {H : Heap m}
        â†’ H âŠ¢ y â†¦[ q ] c â¨¾ Hâ€²
        â†’ H âˆ™ câ€² âŠ¢ y +1 â†¦[ q ] wk1á¶œ c â¨¾ Hâ€² âˆ™ câ€²

-- Heap lookup (without grade update)

data _âŠ¢_â†¦_ : (H : Heap m) (y : Ptr m) (c : Closure m n) â†’ Set a where
  here : H âˆ™ (p , c) âŠ¢ y0 â†¦ wk1á¶œ c
  there : {y : Ptr m} {H : Heap m}
        â†’ H âŠ¢ y â†¦ c
        â†’ H âˆ™ câ€² âŠ¢ y +1 â†¦ wk1á¶œ c

-- Equality of heaps up to grades

data _~Ê°_ : (H Hâ€² : Heap m) â†’ Set a where
  Îµ : Îµ ~Ê° Îµ
  _âˆ™_ : H ~Ê° Hâ€² â†’ (c : Closure m n) â†’ H âˆ™ (p , c) ~Ê° Hâ€² âˆ™ (q , c)

-- Weakening of heaps

data _âˆ·_âŠ‡Ê°_ : (Ï : Wk m n) (H : Heap m) (Hâ€² : Heap n) â†’ Set a where
  id : id âˆ· H âŠ‡Ê° H
  step : Ï âˆ· H âŠ‡Ê° Hâ€² â†’ step Ï âˆ· H âˆ™ câ€² âŠ‡Ê° Hâ€²
  -- lift : Ï âˆ· H âŠ‡Ê° Hâ€² â†’ lift Ï âˆ· H âˆ™ (p , wká¶œ Ï c) âŠ‡Ê° Hâ€² âˆ™ (p , c)

-- Heaps as substitutions

toSubstâ‚• : Heap m â†’ Subst 0 m
toSubstâ‚• Îµ = idSubst
toSubstâ‚• (H âˆ™ (_ , t , E)) =
  let Ïƒ = toSubstâ‚• H
  in  consSubst Ïƒ (wk E t [ Ïƒ ])

infix 25 _[_]â‚•
infix 25 _[_]â‡‘â‚•
infix 25 _[_]â‡‘Â²â‚•

_[_]â‚• : Term m â†’ Heap m â†’ Term 0
t [ H ]â‚• = t [ toSubstâ‚• H ]

_[_]â‡‘â‚• : Term (1+ m) â†’ Heap m â†’ Term 1
t [ H ]â‡‘â‚• = t [ liftSubst (toSubstâ‚• H) ]

_[_]â‡‘Â²â‚• : Term (2+ m) â†’ Heap m â†’ Term 2
t [ H ]â‡‘Â²â‚• = t [ liftSubstn (toSubstâ‚• H) 2 ]

------------------------------------------------------------------------
-- Evaluation states

-- States, indexed by the size of the heap and the number of free
-- variables in the head.

record State (m n : Nat) : Set a where
  constructor âŸ¨_,_,_,_âŸ©
  field
    heap : Heap m
    head : Term n
    env : Env m n
    stack : Stack m

-- Converting states back to terms

â¦…_â¦†áµ‰ : Elim m â†’ (Term m â†’ Term m)
â¦… âˆ˜â‚‘ p u E â¦†áµ‰ = _âˆ˜âŸ¨ p âŸ© wk E u
â¦… fstâ‚‘ p â¦†áµ‰ = fst p
â¦… sndâ‚‘ p â¦†áµ‰ = snd p
â¦… prodrecâ‚‘ r p q A u E â¦†áµ‰ t =
  prodrec r p q (wk (lift E) A) t (wk (liftn E 2) u)
â¦… natrecâ‚‘ p q r A z s E â¦†áµ‰ t =
  natrec p q r (wk (lift E) A) (wk E z) (wk (liftn E 2) s) t
â¦… unitrecâ‚‘ p q A u E â¦†áµ‰ t =
  unitrec p q (wk (lift E) A) t (wk E u)
â¦… Jâ‚‘ p q A t B u v E â¦†áµ‰ w =
  J p q (wk E A) (wk E t) (wk (liftn E 2) B) (wk E u) (wk E v) w
â¦… Kâ‚‘ p A t B u E â¦†áµ‰ v =
  K p (wk E A) (wk E t) (wk (lift E) B) (wk E u) v
â¦… []-congâ‚‘ s A t u E â¦†áµ‰ v =
  []-cong s (wk E A ) (wk E t) (wk E u) v
â¦… sucâ‚‘ â¦†áµ‰ = suc

â¦…_â¦† : Stack m â†’ (Term m â†’ Term m)
â¦… Îµ â¦† = idá¶ 
â¦… e âˆ™ S â¦† = â¦… S â¦† âˆ˜á¶  â¦… e â¦†áµ‰

norm : State m n â†’ Term 0
norm âŸ¨ H , t , E , S âŸ© = â¦… S â¦† (wk E t) [ H ]â‚•

initial : Term 0 â†’ State 0 0
initial t = âŸ¨ Îµ , t , id , Îµ âŸ©

------------------------------------------------------------------------
-- Values and normal form head terms

-- Values are those terms that do not evaluate further

data Val {n : Nat} : (t : Term n) â†’ Set a where
  lamáµ¥ : Val (lam p t)
  zeroáµ¥ : Val zero
  sucáµ¥ : Val (suc t)
  -- sucáµ¥â€² : â¦ƒ â„•-Fullred â¦„ â†’ Val t â†’ Val (suc t)
  staráµ¥ : Val (star s)
  prodáµ¥ : Val (prod s p u t)
  rfláµ¥ : Val rfl
  Uáµ¥ : Val U
  Î Î£áµ¥ : Val (Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B)
  â„•áµ¥ : Val â„•
  Unitáµ¥ : Val (Unit s)
  Emptyáµ¥ : Val Empty
  Idáµ¥ : Val (Id A t u)

-- Terms which represent normal form states when in head position

data Normal {n : Nat} : (t : Term n) â†’ Set a where
  val : Val t â†’ Normal t
  emptyrecâ‚™ : Normal (emptyrec p A t)
