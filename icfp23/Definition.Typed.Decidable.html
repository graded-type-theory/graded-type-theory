<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Definition.Typed.Decidable</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- Decidability of typing.</a>
<a id="101" class="Comment">------------------------------------------------------------------------</a>

<a id="175" class="Keyword">open</a> <a id="180" class="Keyword">import</a> <a id="187" href="Definition.Typed.Restrictions.html" class="Module">Definition.Typed.Restrictions</a>
<a id="217" class="Keyword">import</a> <a id="224" href="Tools.PropositionalEquality.html" class="Module">Tools.PropositionalEquality</a> <a id="252" class="Symbol">as</a> <a id="255" class="Module">PE</a>
<a id="258" class="Keyword">open</a> <a id="263" class="Keyword">import</a> <a id="270" href="Tools.Relation.html" class="Module">Tools.Relation</a>

<a id="286" class="Keyword">module</a> <a id="293" href="Definition.Typed.Decidable.html" class="Module">Definition.Typed.Decidable</a>
  <a id="322" class="Symbol">{</a><a id="323" href="Definition.Typed.Decidable.html#323" class="Bound">a</a><a id="324" class="Symbol">}</a> <a id="326" class="Symbol">{</a><a id="327" href="Definition.Typed.Decidable.html#327" class="Bound">M</a> <a id="329" class="Symbol">:</a> <a id="331" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="335" href="Definition.Typed.Decidable.html#323" class="Bound">a</a><a id="336" class="Symbol">}</a>
  <a id="340" class="Symbol">(</a><a id="341" href="Definition.Typed.Decidable.html#341" class="Bound">R</a> <a id="343" class="Symbol">:</a> <a id="345" href="Definition.Typed.Restrictions.html#406" class="Record">Type-restrictions</a> <a id="363" href="Definition.Typed.Decidable.html#327" class="Bound">M</a><a id="364" class="Symbol">)</a>
  <a id="368" class="Symbol">(</a><a id="369" class="Keyword">open</a> <a id="374" href="Definition.Typed.Restrictions.html#406" class="Module">Type-restrictions</a> <a id="392" href="Definition.Typed.Decidable.html#341" class="Bound">R</a><a id="393" class="Symbol">)</a>
  <a id="397" class="Comment">-- Equality is assumed to be decidable for M.</a>
  <a id="445" class="Symbol">(</a><a id="446" href="Definition.Typed.Decidable.html#446" class="Bound Operator">_≟_</a> <a id="450" class="Symbol">:</a> <a id="452" href="Relation.Binary.Definitions.html#4575" class="Function">Decidable</a> <a id="462" class="Symbol">(</a><a id="463" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">PE._≡_</a> <a id="470" class="Symbol">{</a><a id="471" class="Argument">A</a> <a id="473" class="Symbol">=</a> <a id="475" href="Definition.Typed.Decidable.html#327" class="Bound">M</a><a id="476" class="Symbol">}))</a>
  <a id="482" class="Comment">-- It is decidable whether the Unit type is allowed.</a>
  <a id="537" class="Symbol">(</a><a id="538" href="Definition.Typed.Decidable.html#538" class="Bound">Unit-ok?</a> <a id="547" class="Symbol">:</a> <a id="549" href="Relation.Nullary.html#1511" class="Record">Dec</a> <a id="553" href="Definition.Typed.Restrictions.html#565" class="Field">Unit-allowed</a><a id="565" class="Symbol">)</a>
  <a id="569" class="Comment">-- ΠΣ-allowed is pointwise decidable.</a>
  <a id="609" class="Symbol">(</a><a id="610" href="Definition.Typed.Decidable.html#610" class="Bound">ΠΣ-ok?</a> <a id="617" class="Symbol">:</a> <a id="619" class="Symbol">∀</a> <a id="621" href="Definition.Typed.Decidable.html#621" class="Bound">b</a> <a id="623" href="Definition.Typed.Decidable.html#623" class="Bound">p</a> <a id="625" href="Definition.Typed.Decidable.html#625" class="Bound">q</a> <a id="627" class="Symbol">→</a> <a id="629" href="Relation.Nullary.html#1511" class="Record">Dec</a> <a id="633" class="Symbol">(</a><a id="634" href="Definition.Typed.Restrictions.html#640" class="Field">ΠΣ-allowed</a> <a id="645" href="Definition.Typed.Decidable.html#621" class="Bound">b</a> <a id="647" href="Definition.Typed.Decidable.html#623" class="Bound">p</a> <a id="649" href="Definition.Typed.Decidable.html#625" class="Bound">q</a><a id="650" class="Symbol">))</a>
  <a id="655" class="Keyword">where</a>

<a id="662" class="Keyword">open</a> <a id="667" class="Keyword">import</a> <a id="674" href="Definition.Untyped.html" class="Module">Definition.Untyped</a> <a id="693" href="Definition.Typed.Decidable.html#327" class="Bound">M</a> <a id="695" class="Keyword">hiding</a> <a id="702" class="Symbol">(</a><a id="703" href="Definition.Untyped.NotParametrised.html#1148" class="InductiveConstructor Operator">_∷_</a><a id="706" class="Symbol">)</a>
<a id="708" class="Keyword">open</a> <a id="713" class="Keyword">import</a> <a id="720" href="Definition.Typed.html" class="Module">Definition.Typed</a> <a id="737" href="Definition.Typed.Decidable.html#341" class="Bound">R</a>
<a id="739" class="Keyword">open</a> <a id="744" class="Keyword">import</a> <a id="751" href="Definition.Typed.Consequences.Syntactic.html" class="Module">Definition.Typed.Consequences.Syntactic</a> <a id="791" href="Definition.Typed.Decidable.html#341" class="Bound">R</a>
<a id="793" class="Keyword">open</a> <a id="798" class="Keyword">import</a> <a id="805" href="Definition.Typed.Properties.html" class="Module">Definition.Typed.Properties</a> <a id="833" href="Definition.Typed.Decidable.html#341" class="Bound">R</a>
<a id="835" class="Keyword">open</a> <a id="840" class="Keyword">import</a> <a id="847" href="Definition.Typechecking.html" class="Module">Definition.Typechecking</a> <a id="871" href="Definition.Typed.Decidable.html#341" class="Bound">R</a>
<a id="873" class="Keyword">open</a> <a id="878" class="Keyword">import</a> <a id="885" href="Definition.Typechecking.Soundness.html" class="Module">Definition.Typechecking.Soundness</a> <a id="919" href="Definition.Typed.Decidable.html#341" class="Bound">R</a>
<a id="921" class="Keyword">open</a> <a id="926" class="Keyword">import</a> <a id="933" href="Definition.Typechecking.Completeness.html" class="Module">Definition.Typechecking.Completeness</a> <a id="970" href="Definition.Typed.Decidable.html#341" class="Bound">R</a>
<a id="972" class="Keyword">open</a> <a id="977" class="Keyword">import</a> <a id="984" href="Definition.Typechecking.Decidable.html" class="Module">Definition.Typechecking.Decidable</a> <a id="1018" href="Definition.Typed.Decidable.html#341" class="Bound">R</a> <a id="1020" href="Definition.Typed.Decidable.html#446" class="Bound Operator">_≟_</a> <a id="1024" href="Definition.Typed.Decidable.html#538" class="Bound">Unit-ok?</a> <a id="1033" href="Definition.Typed.Decidable.html#610" class="Bound">ΠΣ-ok?</a>

<a id="1041" class="Keyword">open</a> <a id="1046" class="Keyword">import</a> <a id="1053" href="Tools.Function.html" class="Module">Tools.Function</a>
<a id="1068" class="Keyword">open</a> <a id="1073" class="Keyword">import</a> <a id="1080" href="Tools.Nat.html" class="Module">Tools.Nat</a> <a id="1090" class="Keyword">using</a> <a id="1096" class="Symbol">(</a><a id="1097" href="Agda.Builtin.Nat.html#186" class="Datatype">Nat</a><a id="1100" class="Symbol">)</a>
<a id="1102" class="Keyword">open</a> <a id="1107" class="Keyword">import</a> <a id="1114" href="Tools.Nullary.html" class="Module">Tools.Nullary</a> <a id="1128" class="Symbol">as</a> <a id="1131" class="Module">Dec</a>
<a id="1135" class="Keyword">open</a> <a id="1140" class="Keyword">import</a> <a id="1147" href="Tools.Product.html" class="Module">Tools.Product</a>

<a id="1162" class="Keyword">private</a>
  <a id="1172" class="Keyword">variable</a>
    <a id="1185" href="Definition.Typed.Decidable.html#1185" class="Generalizable">n</a> <a id="1187" class="Symbol">:</a> <a id="1189" href="Agda.Builtin.Nat.html#186" class="Datatype">Nat</a>
    <a id="1197" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="1199" class="Symbol">:</a> <a id="1201" href="Definition.Untyped.NotParametrised.html#791" class="Datatype">Con</a> <a id="1205" href="Definition.Untyped.html#2121" class="Datatype">Term</a> <a id="1210" href="Definition.Typed.Decidable.html#1185" class="Generalizable">n</a>
    <a id="1216" href="Definition.Typed.Decidable.html#1216" class="Generalizable">A</a> <a id="1218" href="Definition.Typed.Decidable.html#1218" class="Generalizable">t</a> <a id="1220" class="Symbol">:</a> <a id="1222" href="Definition.Untyped.html#2121" class="Datatype">Term</a> <a id="1227" href="Definition.Typed.Decidable.html#1185" class="Generalizable">n</a>

<a id="1230" class="Comment">-- Re-export decidability of type and term equality</a>
<a id="1282" class="Keyword">open</a> <a id="1287" class="Keyword">import</a> <a id="1294" href="Definition.Typed.Decidable.Equality.html" class="Module">Definition.Typed.Decidable.Equality</a> <a id="1330" href="Definition.Typed.Decidable.html#341" class="Bound">R</a> <a id="1332" href="Definition.Typed.Decidable.html#446" class="Bound Operator">_≟_</a> <a id="1336" class="Keyword">public</a>

<a id="1344" class="Comment">-- If Γ is well-formed and A is checkable, then Γ ⊢ A is decidable.</a>

<a id="dec"></a><a id="1413" href="Definition.Typed.Decidable.html#1413" class="Function">dec</a> <a id="1417" class="Symbol">:</a> <a id="1419" href="Definition.Typed.html#964" class="Datatype Operator">⊢</a> <a id="1421" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="1423" class="Symbol">→</a> <a id="1425" href="Definition.Typechecking.html#3484" class="Datatype">Checkable</a> <a id="1435" href="Definition.Typed.Decidable.html#1216" class="Generalizable">A</a> <a id="1437" class="Symbol">→</a> <a id="1439" href="Relation.Nullary.html#1511" class="Record">Dec</a> <a id="1443" class="Symbol">(</a><a id="1444" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="1446" href="Definition.Typed.html#1086" class="Datatype Operator">⊢</a> <a id="1448" href="Definition.Typed.Decidable.html#1216" class="Generalizable">A</a><a id="1449" class="Symbol">)</a>
<a id="1451" href="Definition.Typed.Decidable.html#1413" class="Function">dec</a> <a id="1455" href="Definition.Typed.Decidable.html#1455" class="Bound">⊢Γ</a> <a id="1458" href="Definition.Typed.Decidable.html#1458" class="Bound">A</a> <a id="1460" class="Symbol">=</a>
  <a id="1464" href="Tools.Nullary.html#342" class="Function">Dec.map</a> <a id="1472" class="Symbol">(</a><a id="1473" href="Definition.Typechecking.Soundness.html#1131" class="Function">soundness⇇Type</a> <a id="1488" href="Definition.Typed.Decidable.html#1455" class="Bound">⊢Γ</a><a id="1490" class="Symbol">)</a> <a id="1492" class="Symbol">(</a><a id="1493" href="Definition.Typechecking.Completeness.html#1254" class="Function">completeness⇇Type</a> <a id="1511" href="Definition.Typed.Decidable.html#1458" class="Bound">A</a><a id="1512" class="Symbol">)</a> <a id="1514" class="Symbol">(</a><a id="1515" href="Definition.Typechecking.Decidable.html#17763" class="Function">dec⇇Type</a> <a id="1524" href="Definition.Typed.Decidable.html#1455" class="Bound">⊢Γ</a> <a id="1527" href="Definition.Typed.Decidable.html#1458" class="Bound">A</a><a id="1528" class="Symbol">)</a>

<a id="1531" class="Comment">-- Type-checking for well-formed types: if Γ ⊢ A holds and t is</a>
<a id="1595" class="Comment">-- checkable, then Γ ⊢ t ∷ A is decidable.</a>

<a id="decTermᶜ"></a><a id="1639" href="Definition.Typed.Decidable.html#1639" class="Function">decTermᶜ</a> <a id="1648" class="Symbol">:</a> <a id="1650" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="1652" href="Definition.Typed.html#1086" class="Datatype Operator">⊢</a> <a id="1654" href="Definition.Typed.Decidable.html#1216" class="Generalizable">A</a> <a id="1656" class="Symbol">→</a> <a id="1658" href="Definition.Typechecking.html#3484" class="Datatype">Checkable</a> <a id="1668" href="Definition.Typed.Decidable.html#1218" class="Generalizable">t</a> <a id="1670" class="Symbol">→</a> <a id="1672" href="Relation.Nullary.html#1511" class="Record">Dec</a> <a id="1676" class="Symbol">(</a><a id="1677" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="1679" href="Definition.Typed.html#1453" class="Datatype Operator">⊢</a> <a id="1681" href="Definition.Typed.Decidable.html#1218" class="Generalizable">t</a> <a id="1683" href="Definition.Typed.html#1453" class="Datatype Operator">∷</a> <a id="1685" href="Definition.Typed.Decidable.html#1216" class="Generalizable">A</a><a id="1686" class="Symbol">)</a>
<a id="1688" href="Definition.Typed.Decidable.html#1639" class="Function">decTermᶜ</a> <a id="1697" href="Definition.Typed.Decidable.html#1697" class="Bound">⊢A</a> <a id="1700" href="Definition.Typed.Decidable.html#1700" class="Bound">t</a> <a id="1702" class="Symbol">=</a> <a id="1704" href="Tools.Nullary.html#342" class="Function">Dec.map</a> <a id="1712" class="Symbol">(</a><a id="1713" href="Definition.Typechecking.Soundness.html#3573" class="Function">soundness⇇</a> <a id="1724" href="Definition.Typed.Decidable.html#1771" class="Function">⊢Γ</a><a id="1726" class="Symbol">)</a> <a id="1728" class="Symbol">(</a><a id="1729" href="Definition.Typechecking.Completeness.html#4626" class="Function">completeness⇇</a> <a id="1743" href="Definition.Typed.Decidable.html#1700" class="Bound">t</a><a id="1744" class="Symbol">)</a> <a id="1746" class="Symbol">(</a><a id="1747" href="Definition.Typechecking.Decidable.html#19609" class="Function">dec⇇</a> <a id="1752" href="Definition.Typed.Decidable.html#1771" class="Function">⊢Γ</a> <a id="1755" href="Definition.Typed.Decidable.html#1700" class="Bound">t</a> <a id="1757" href="Definition.Typed.Decidable.html#1697" class="Bound">⊢A</a><a id="1759" class="Symbol">)</a>
  <a id="1763" class="Keyword">where</a>
  <a id="1771" href="Definition.Typed.Decidable.html#1771" class="Function">⊢Γ</a> <a id="1774" class="Symbol">=</a> <a id="1776" href="Definition.Typed.Properties.html#1279" class="Function">wf</a> <a id="1779" href="Definition.Typed.Decidable.html#1697" class="Bound">⊢A</a>

<a id="1783" class="Comment">-- Type-checking for arbitrary checkable types: if Γ is well-formed</a>
<a id="1851" class="Comment">-- and A and t are checkable, then Γ ⊢ t ∷ A is decidable.</a>

<a id="decTermTypeᶜ"></a><a id="1911" href="Definition.Typed.Decidable.html#1911" class="Function">decTermTypeᶜ</a> <a id="1924" class="Symbol">:</a> <a id="1926" href="Definition.Typed.html#964" class="Datatype Operator">⊢</a> <a id="1928" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="1930" class="Symbol">→</a> <a id="1932" href="Definition.Typechecking.html#3484" class="Datatype">Checkable</a> <a id="1942" href="Definition.Typed.Decidable.html#1216" class="Generalizable">A</a> <a id="1944" class="Symbol">→</a> <a id="1946" href="Definition.Typechecking.html#3484" class="Datatype">Checkable</a> <a id="1956" href="Definition.Typed.Decidable.html#1218" class="Generalizable">t</a> <a id="1958" class="Symbol">→</a> <a id="1960" href="Relation.Nullary.html#1511" class="Record">Dec</a> <a id="1964" class="Symbol">(</a><a id="1965" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="1967" href="Definition.Typed.html#1453" class="Datatype Operator">⊢</a> <a id="1969" href="Definition.Typed.Decidable.html#1218" class="Generalizable">t</a> <a id="1971" href="Definition.Typed.html#1453" class="Datatype Operator">∷</a> <a id="1973" href="Definition.Typed.Decidable.html#1216" class="Generalizable">A</a><a id="1974" class="Symbol">)</a>
<a id="1976" href="Definition.Typed.Decidable.html#1911" class="Function">decTermTypeᶜ</a> <a id="1989" href="Definition.Typed.Decidable.html#1989" class="Bound">⊢Γ</a> <a id="1992" href="Definition.Typed.Decidable.html#1992" class="Bound">A</a> <a id="1994" href="Definition.Typed.Decidable.html#1994" class="Bound">t</a> <a id="1996" class="Symbol">=</a>
  <a id="2000" href="Function.Base.html#4121" class="Function Operator">case</a> <a id="2005" href="Definition.Typed.Decidable.html#1413" class="Function">dec</a> <a id="2009" href="Definition.Typed.Decidable.html#1989" class="Bound">⊢Γ</a> <a id="2012" href="Definition.Typed.Decidable.html#1992" class="Bound">A</a> <a id="2014" href="Function.Base.html#4121" class="Function Operator">of</a> <a id="2017" class="Symbol">λ</a> <a id="2019" class="Keyword">where</a>
    <a id="2029" class="Symbol">(</a><a id="2030" href="Relation.Nullary.html#1648" class="InductiveConstructor">yes</a> <a id="2034" href="Definition.Typed.Decidable.html#2034" class="Bound">⊢A</a><a id="2036" class="Symbol">)</a> <a id="2038" class="Symbol">→</a> <a id="2040" href="Definition.Typed.Decidable.html#1639" class="Function">decTermᶜ</a> <a id="2049" href="Definition.Typed.Decidable.html#2034" class="Bound">⊢A</a> <a id="2052" href="Definition.Typed.Decidable.html#1994" class="Bound">t</a>
    <a id="2058" class="Symbol">(</a><a id="2059" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a> <a id="2062" href="Definition.Typed.Decidable.html#2062" class="Bound">¬⊢A</a><a id="2065" class="Symbol">)</a> <a id="2067" class="Symbol">→</a> <a id="2069" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a> <a id="2072" class="Symbol">(</a><a id="2073" href="Definition.Typed.Decidable.html#2062" class="Bound">¬⊢A</a> <a id="2077" href="Function.Base.html#1031" class="Function Operator">∘→</a> <a id="2080" href="Definition.Typed.Consequences.Syntactic.html#1142" class="Function">syntacticTerm</a><a id="2093" class="Symbol">)</a>

<a id="2096" class="Comment">-- Type inference: if ⊢ Γ holds and t is inferable, then</a>
<a id="2153" class="Comment">-- ∃ λ A → Γ ⊢ t ∷ A is decidable.</a>

<a id="decTermᵢ"></a><a id="2189" href="Definition.Typed.Decidable.html#2189" class="Function">decTermᵢ</a> <a id="2198" class="Symbol">:</a> <a id="2200" href="Definition.Typed.html#964" class="Datatype Operator">⊢</a> <a id="2202" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="2204" class="Symbol">→</a> <a id="2206" href="Definition.Typechecking.html#2701" class="Datatype">Inferable</a> <a id="2216" href="Definition.Typed.Decidable.html#1218" class="Generalizable">t</a> <a id="2218" class="Symbol">→</a> <a id="2220" href="Relation.Nullary.html#1511" class="Record">Dec</a> <a id="2224" class="Symbol">(</a><a id="2225" href="Data.Product.html#1369" class="Function">∃</a> <a id="2227" class="Symbol">λ</a> <a id="2229" href="Definition.Typed.Decidable.html#2229" class="Bound">A</a> <a id="2231" class="Symbol">→</a> <a id="2233" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="2235" href="Definition.Typed.html#1453" class="Datatype Operator">⊢</a> <a id="2237" href="Definition.Typed.Decidable.html#1218" class="Generalizable">t</a> <a id="2239" href="Definition.Typed.html#1453" class="Datatype Operator">∷</a> <a id="2241" href="Definition.Typed.Decidable.html#2229" class="Bound">A</a><a id="2242" class="Symbol">)</a>
<a id="2244" href="Definition.Typed.Decidable.html#2189" class="Function">decTermᵢ</a> <a id="2253" href="Definition.Typed.Decidable.html#2253" class="Bound">⊢Γ</a> <a id="2256" href="Definition.Typed.Decidable.html#2256" class="Bound">t</a> <a id="2258" class="Symbol">=</a> <a id="2260" href="Tools.Nullary.html#342" class="Function">Dec.map</a>
  <a id="2270" class="Symbol">(λ</a> <a id="2273" class="Symbol">{</a> <a id="2275" class="Symbol">(</a><a id="2276" href="Definition.Typed.Decidable.html#2276" class="Bound">A</a> <a id="2278" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2280" href="Definition.Typed.Decidable.html#2280" class="Bound">t⇉A</a><a id="2283" class="Symbol">)</a> <a id="2285" class="Symbol">→</a> <a id="2287" href="Definition.Typed.Decidable.html#2276" class="Bound">A</a> <a id="2289" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2291" class="Symbol">(</a><a id="2292" href="Agda.Builtin.Sigma.html#246" class="Field">proj₂</a> <a id="2298" class="Symbol">(</a><a id="2299" href="Definition.Typechecking.Soundness.html#1497" class="Function">soundness⇉</a> <a id="2310" href="Definition.Typed.Decidable.html#2253" class="Bound">⊢Γ</a> <a id="2313" href="Definition.Typed.Decidable.html#2280" class="Bound">t⇉A</a><a id="2316" class="Symbol">))})</a>
  <a id="2323" class="Symbol">(λ</a> <a id="2326" class="Symbol">{</a> <a id="2328" class="Symbol">(</a><a id="2329" href="Definition.Typed.Decidable.html#2329" class="Bound">A</a> <a id="2331" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2333" href="Definition.Typed.Decidable.html#2333" class="Bound">⊢t</a><a id="2335" class="Symbol">)</a>  <a id="2338" class="Symbol">→</a> <a id="2340" class="Symbol">_</a> <a id="2342" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2344" class="Symbol">(</a><a id="2345" href="Agda.Builtin.Sigma.html#234" class="Field">proj₁</a> <a id="2351" class="Symbol">(</a><a id="2352" href="Agda.Builtin.Sigma.html#246" class="Field">proj₂</a> <a id="2358" class="Symbol">(</a><a id="2359" href="Definition.Typechecking.Completeness.html#1678" class="Function">completeness⇉</a> <a id="2373" href="Definition.Typed.Decidable.html#2256" class="Bound">t</a> <a id="2375" href="Definition.Typed.Decidable.html#2333" class="Bound">⊢t</a><a id="2377" class="Symbol">)))})</a>
  <a id="2385" class="Symbol">(</a><a id="2386" href="Definition.Typechecking.Decidable.html#18105" class="Function">dec⇉</a> <a id="2391" href="Definition.Typed.Decidable.html#2253" class="Bound">⊢Γ</a> <a id="2394" href="Definition.Typed.Decidable.html#2256" class="Bound">t</a><a id="2395" class="Symbol">)</a>

<a id="2398" class="Comment">-- If Γ is a checkable context, then ⊢ Γ is decidable.</a>

<a id="decWfCon"></a><a id="2454" href="Definition.Typed.Decidable.html#2454" class="Function">decWfCon</a> <a id="2463" class="Symbol">:</a> <a id="2465" href="Definition.Typechecking.html#3740" class="Datatype">CheckableCon</a> <a id="2478" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="2480" class="Symbol">→</a> <a id="2482" href="Relation.Nullary.html#1511" class="Record">Dec</a> <a id="2486" class="Symbol">(</a><a id="2487" href="Definition.Typed.html#964" class="Datatype Operator">⊢</a> <a id="2489" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a><a id="2490" class="Symbol">)</a>
<a id="2492" href="Definition.Typed.Decidable.html#2454" class="Function">decWfCon</a> <a id="2501" href="Definition.Typechecking.html#3788" class="InductiveConstructor">ε</a> <a id="2503" class="Symbol">=</a> <a id="2505" href="Relation.Nullary.html#1648" class="InductiveConstructor">yes</a> <a id="2509" href="Definition.Typed.html#998" class="InductiveConstructor">ε</a>
<a id="2511" href="Definition.Typed.Decidable.html#2454" class="Function">decWfCon</a> <a id="2520" class="Symbol">(</a><a id="2521" href="Definition.Typed.Decidable.html#2521" class="Bound">Γ</a> <a id="2523" href="Definition.Typechecking.html#3811" class="InductiveConstructor Operator">∙</a> <a id="2525" href="Definition.Typed.Decidable.html#2525" class="Bound">A</a><a id="2526" class="Symbol">)</a> <a id="2528" class="Symbol">=</a> <a id="2530" href="Function.Base.html#4121" class="Function Operator">case</a> <a id="2535" href="Definition.Typed.Decidable.html#2454" class="Function">decWfCon</a> <a id="2544" href="Definition.Typed.Decidable.html#2521" class="Bound">Γ</a> <a id="2546" href="Function.Base.html#4121" class="Function Operator">of</a> <a id="2549" class="Symbol">λ</a> <a id="2551" class="Keyword">where</a>
  <a id="2559" class="Symbol">(</a><a id="2560" href="Relation.Nullary.html#1648" class="InductiveConstructor">yes</a> <a id="2564" href="Definition.Typed.Decidable.html#2564" class="Bound">⊢Γ</a><a id="2566" class="Symbol">)</a> <a id="2568" class="Symbol">→</a> <a id="2570" href="Function.Base.html#4121" class="Function Operator">case</a> <a id="2575" href="Definition.Typed.Decidable.html#1413" class="Function">dec</a> <a id="2579" href="Definition.Typed.Decidable.html#2564" class="Bound">⊢Γ</a> <a id="2582" href="Definition.Typed.Decidable.html#2525" class="Bound">A</a> <a id="2584" href="Function.Base.html#4121" class="Function Operator">of</a> <a id="2587" class="Symbol">λ</a> <a id="2589" class="Keyword">where</a>
    <a id="2599" class="Symbol">(</a><a id="2600" href="Relation.Nullary.html#1648" class="InductiveConstructor">yes</a> <a id="2604" href="Definition.Typed.Decidable.html#2604" class="Bound">⊢A</a><a id="2606" class="Symbol">)</a> <a id="2608" class="Symbol">→</a> <a id="2610" href="Relation.Nullary.html#1648" class="InductiveConstructor">yes</a> <a id="2614" class="Symbol">(</a><a id="2615" href="Definition.Typed.Decidable.html#2564" class="Bound">⊢Γ</a> <a id="2618" href="Definition.Typed.html#1012" class="InductiveConstructor Operator">∙</a> <a id="2620" href="Definition.Typed.Decidable.html#2604" class="Bound">⊢A</a><a id="2622" class="Symbol">)</a>
    <a id="2628" class="Symbol">(</a><a id="2629" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a> <a id="2632" href="Definition.Typed.Decidable.html#2632" class="Bound">⊬A</a><a id="2634" class="Symbol">)</a> <a id="2636" class="Symbol">→</a> <a id="2638" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a> <a id="2641" class="Symbol">λ</a> <a id="2643" class="Keyword">where</a>
      <a id="2655" class="Symbol">(</a><a id="2656" href="Definition.Typed.Decidable.html#2656" class="Bound">⊢Γ</a> <a id="2659" href="Definition.Typed.html#1012" class="InductiveConstructor Operator">∙</a> <a id="2661" href="Definition.Typed.Decidable.html#2661" class="Bound">⊢A</a><a id="2663" class="Symbol">)</a> <a id="2665" class="Symbol">→</a> <a id="2667" href="Definition.Typed.Decidable.html#2632" class="Bound">⊬A</a> <a id="2670" href="Definition.Typed.Decidable.html#2661" class="Bound">⊢A</a>
  <a id="2675" class="Symbol">(</a><a id="2676" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a> <a id="2679" href="Definition.Typed.Decidable.html#2679" class="Bound">⊬Γ</a><a id="2681" class="Symbol">)</a> <a id="2683" class="Symbol">→</a> <a id="2685" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a> <a id="2688" class="Symbol">λ</a> <a id="2690" class="Keyword">where</a>
    <a id="2700" class="Symbol">(</a><a id="2701" href="Definition.Typed.Decidable.html#2701" class="Bound">⊢Γ</a> <a id="2704" href="Definition.Typed.html#1012" class="InductiveConstructor Operator">∙</a> <a id="2706" href="Definition.Typed.Decidable.html#2706" class="Bound">⊢A</a><a id="2708" class="Symbol">)</a> <a id="2710" class="Symbol">→</a> <a id="2712" href="Definition.Typed.Decidable.html#2679" class="Bound">⊬Γ</a> <a id="2715" href="Definition.Typed.Decidable.html#2701" class="Bound">⊢Γ</a>

<a id="2719" class="Comment">-- If Γ and A are checkable, then Γ ⊢ A is decidable.</a>

<a id="decConTypeᶜ"></a><a id="2774" href="Definition.Typed.Decidable.html#2774" class="Function">decConTypeᶜ</a> <a id="2786" class="Symbol">:</a> <a id="2788" href="Definition.Typechecking.html#3740" class="Datatype">CheckableCon</a> <a id="2801" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="2803" class="Symbol">→</a> <a id="2805" href="Definition.Typechecking.html#3484" class="Datatype">Checkable</a> <a id="2815" href="Definition.Typed.Decidable.html#1216" class="Generalizable">A</a> <a id="2817" class="Symbol">→</a> <a id="2819" href="Relation.Nullary.html#1511" class="Record">Dec</a> <a id="2823" class="Symbol">(</a><a id="2824" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="2826" href="Definition.Typed.html#1086" class="Datatype Operator">⊢</a> <a id="2828" href="Definition.Typed.Decidable.html#1216" class="Generalizable">A</a><a id="2829" class="Symbol">)</a>
<a id="2831" href="Definition.Typed.Decidable.html#2774" class="Function">decConTypeᶜ</a> <a id="2843" href="Definition.Typed.Decidable.html#2843" class="Bound">Γ</a> <a id="2845" href="Definition.Typed.Decidable.html#2845" class="Bound">A</a> <a id="2847" class="Symbol">=</a>
  <a id="2851" href="Function.Base.html#4121" class="Function Operator">case</a> <a id="2856" href="Definition.Typed.Decidable.html#2454" class="Function">decWfCon</a> <a id="2865" href="Definition.Typed.Decidable.html#2843" class="Bound">Γ</a> <a id="2867" href="Function.Base.html#4121" class="Function Operator">of</a> <a id="2870" class="Symbol">λ</a> <a id="2872" class="Keyword">where</a>
    <a id="2882" class="Symbol">(</a><a id="2883" href="Relation.Nullary.html#1648" class="InductiveConstructor">yes</a> <a id="2887" href="Definition.Typed.Decidable.html#2887" class="Bound">⊢Γ</a><a id="2889" class="Symbol">)</a> <a id="2891" class="Symbol">→</a> <a id="2893" href="Definition.Typed.Decidable.html#1413" class="Function">dec</a> <a id="2897" href="Definition.Typed.Decidable.html#2887" class="Bound">⊢Γ</a> <a id="2900" href="Definition.Typed.Decidable.html#2845" class="Bound">A</a>
    <a id="2906" class="Symbol">(</a><a id="2907" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a> <a id="2910" href="Definition.Typed.Decidable.html#2910" class="Bound">¬⊢Γ</a><a id="2913" class="Symbol">)</a> <a id="2915" class="Symbol">→</a> <a id="2917" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a> <a id="2920" class="Symbol">(</a><a id="2921" href="Definition.Typed.Decidable.html#2910" class="Bound">¬⊢Γ</a> <a id="2925" href="Function.Base.html#1031" class="Function Operator">∘→</a> <a id="2928" href="Definition.Typed.Properties.html#1279" class="Function">wf</a><a id="2930" class="Symbol">)</a>

<a id="2933" class="Comment">-- Type-checking for arbitrary checkable contexts and types: if Γ, A</a>
<a id="3002" class="Comment">-- and t are checkable, then Γ ⊢ t ∷ A is decidable.</a>

<a id="decConTermTypeᶜ"></a><a id="3056" href="Definition.Typed.Decidable.html#3056" class="Function">decConTermTypeᶜ</a> <a id="3072" class="Symbol">:</a>
  <a id="3076" href="Definition.Typechecking.html#3740" class="Datatype">CheckableCon</a> <a id="3089" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="3091" class="Symbol">→</a> <a id="3093" href="Definition.Typechecking.html#3484" class="Datatype">Checkable</a> <a id="3103" href="Definition.Typed.Decidable.html#1216" class="Generalizable">A</a> <a id="3105" class="Symbol">→</a> <a id="3107" href="Definition.Typechecking.html#3484" class="Datatype">Checkable</a> <a id="3117" href="Definition.Typed.Decidable.html#1218" class="Generalizable">t</a> <a id="3119" class="Symbol">→</a> <a id="3121" href="Relation.Nullary.html#1511" class="Record">Dec</a> <a id="3125" class="Symbol">(</a><a id="3126" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="3128" href="Definition.Typed.html#1453" class="Datatype Operator">⊢</a> <a id="3130" href="Definition.Typed.Decidable.html#1218" class="Generalizable">t</a> <a id="3132" href="Definition.Typed.html#1453" class="Datatype Operator">∷</a> <a id="3134" href="Definition.Typed.Decidable.html#1216" class="Generalizable">A</a><a id="3135" class="Symbol">)</a>
<a id="3137" href="Definition.Typed.Decidable.html#3056" class="Function">decConTermTypeᶜ</a> <a id="3153" href="Definition.Typed.Decidable.html#3153" class="Bound">Γ</a> <a id="3155" href="Definition.Typed.Decidable.html#3155" class="Bound">A</a> <a id="3157" href="Definition.Typed.Decidable.html#3157" class="Bound">t</a> <a id="3159" class="Symbol">=</a>
  <a id="3163" href="Function.Base.html#4121" class="Function Operator">case</a> <a id="3168" href="Definition.Typed.Decidable.html#2454" class="Function">decWfCon</a> <a id="3177" href="Definition.Typed.Decidable.html#3153" class="Bound">Γ</a> <a id="3179" href="Function.Base.html#4121" class="Function Operator">of</a> <a id="3182" class="Symbol">λ</a> <a id="3184" class="Keyword">where</a>
    <a id="3194" class="Symbol">(</a><a id="3195" href="Relation.Nullary.html#1648" class="InductiveConstructor">yes</a> <a id="3199" href="Definition.Typed.Decidable.html#3199" class="Bound">⊢Γ</a><a id="3201" class="Symbol">)</a> <a id="3203" class="Symbol">→</a> <a id="3205" href="Definition.Typed.Decidable.html#1911" class="Function">decTermTypeᶜ</a> <a id="3218" href="Definition.Typed.Decidable.html#3199" class="Bound">⊢Γ</a> <a id="3221" href="Definition.Typed.Decidable.html#3155" class="Bound">A</a> <a id="3223" href="Definition.Typed.Decidable.html#3157" class="Bound">t</a>
    <a id="3229" class="Symbol">(</a><a id="3230" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a> <a id="3233" href="Definition.Typed.Decidable.html#3233" class="Bound">¬⊢Γ</a><a id="3236" class="Symbol">)</a> <a id="3238" class="Symbol">→</a> <a id="3240" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a> <a id="3243" class="Symbol">(</a><a id="3244" href="Definition.Typed.Decidable.html#3233" class="Bound">¬⊢Γ</a> <a id="3248" href="Function.Base.html#1031" class="Function Operator">∘→</a> <a id="3251" href="Definition.Typed.Properties.html#720" class="Function">wfTerm</a><a id="3257" class="Symbol">)</a>

<a id="3260" class="Comment">-- Type inference for arbitrary checkable contexts: if Γ is checkable</a>
<a id="3330" class="Comment">-- and t is inferable, then ∃ λ A → Γ ⊢ t ∷ A is decidable.</a>

<a id="decConTermᵢ"></a><a id="3391" href="Definition.Typed.Decidable.html#3391" class="Function">decConTermᵢ</a> <a id="3403" class="Symbol">:</a> <a id="3405" href="Definition.Typechecking.html#3740" class="Datatype">CheckableCon</a> <a id="3418" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="3420" class="Symbol">→</a> <a id="3422" href="Definition.Typechecking.html#2701" class="Datatype">Inferable</a> <a id="3432" href="Definition.Typed.Decidable.html#1218" class="Generalizable">t</a> <a id="3434" class="Symbol">→</a> <a id="3436" href="Relation.Nullary.html#1511" class="Record">Dec</a> <a id="3440" class="Symbol">(</a><a id="3441" href="Data.Product.html#1369" class="Function">∃</a> <a id="3443" class="Symbol">λ</a> <a id="3445" href="Definition.Typed.Decidable.html#3445" class="Bound">A</a> <a id="3447" class="Symbol">→</a> <a id="3449" href="Definition.Typed.Decidable.html#1197" class="Generalizable">Γ</a> <a id="3451" href="Definition.Typed.html#1453" class="Datatype Operator">⊢</a> <a id="3453" href="Definition.Typed.Decidable.html#1218" class="Generalizable">t</a> <a id="3455" href="Definition.Typed.html#1453" class="Datatype Operator">∷</a> <a id="3457" href="Definition.Typed.Decidable.html#3445" class="Bound">A</a><a id="3458" class="Symbol">)</a>
<a id="3460" href="Definition.Typed.Decidable.html#3391" class="Function">decConTermᵢ</a> <a id="3472" href="Definition.Typed.Decidable.html#3472" class="Bound">Γ</a> <a id="3474" href="Definition.Typed.Decidable.html#3474" class="Bound">t</a> <a id="3476" class="Symbol">=</a>
  <a id="3480" href="Function.Base.html#4121" class="Function Operator">case</a> <a id="3485" href="Definition.Typed.Decidable.html#2454" class="Function">decWfCon</a> <a id="3494" href="Definition.Typed.Decidable.html#3472" class="Bound">Γ</a> <a id="3496" href="Function.Base.html#4121" class="Function Operator">of</a> <a id="3499" class="Symbol">λ</a> <a id="3501" class="Keyword">where</a>
    <a id="3511" class="Symbol">(</a><a id="3512" href="Relation.Nullary.html#1648" class="InductiveConstructor">yes</a> <a id="3516" href="Definition.Typed.Decidable.html#3516" class="Bound">⊢Γ</a><a id="3518" class="Symbol">)</a> <a id="3520" class="Symbol">→</a> <a id="3522" href="Definition.Typed.Decidable.html#2189" class="Function">decTermᵢ</a> <a id="3531" href="Definition.Typed.Decidable.html#3516" class="Bound">⊢Γ</a> <a id="3534" href="Definition.Typed.Decidable.html#3474" class="Bound">t</a>
    <a id="3540" class="Symbol">(</a><a id="3541" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a> <a id="3544" href="Definition.Typed.Decidable.html#3544" class="Bound">¬⊢Γ</a><a id="3547" class="Symbol">)</a> <a id="3549" class="Symbol">→</a> <a id="3551" href="Relation.Nullary.html#1685" class="InductiveConstructor">no</a> <a id="3554" class="Symbol">(</a><a id="3555" href="Definition.Typed.Decidable.html#3544" class="Bound">¬⊢Γ</a> <a id="3559" href="Function.Base.html#1031" class="Function Operator">∘→</a> <a id="3562" href="Definition.Typed.Properties.html#720" class="Function">wfTerm</a> <a id="3569" href="Function.Base.html#1031" class="Function Operator">∘→</a> <a id="3572" href="Agda.Builtin.Sigma.html#246" class="Field">proj₂</a><a id="3577" class="Symbol">)</a>
</pre></body></html>