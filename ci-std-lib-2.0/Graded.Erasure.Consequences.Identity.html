<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Graded.Erasure.Consequences.Identity</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- Some consequences of the fundamental lemma that are related to</a>
<a id="140" class="Comment">-- identity types</a>
<a id="158" class="Comment">------------------------------------------------------------------------</a>

<a id="232" class="Symbol">{-#</a> <a id="236" class="Keyword">OPTIONS</a> <a id="244" class="Pragma">--hidden-argument-puns</a> <a id="267" class="Symbol">#-}</a>

<a id="272" class="Keyword">open</a> <a id="277" class="Keyword">import</a> <a id="284" href="Definition.Typed.Restrictions.html" class="Module">Definition.Typed.Restrictions</a>
<a id="314" class="Keyword">import</a> <a id="321" href="Graded.Modality.html" class="Module">Graded.Modality</a>
<a id="337" class="Keyword">open</a> <a id="342" class="Keyword">import</a> <a id="349" href="Graded.Usage.Restrictions.html" class="Module">Graded.Usage.Restrictions</a>

<a id="376" class="Keyword">module</a> <a id="383" href="Graded.Erasure.Consequences.Identity.html" class="Module">Graded.Erasure.Consequences.Identity</a>
  <a id="422" class="Symbol">{</a><a id="423" href="Graded.Erasure.Consequences.Identity.html#423" class="Bound">a</a><a id="424" class="Symbol">}</a> <a id="426" class="Symbol">{</a><a id="427" href="Graded.Erasure.Consequences.Identity.html#427" class="Bound">M</a> <a id="429" class="Symbol">:</a> <a id="431" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="435" href="Graded.Erasure.Consequences.Identity.html#423" class="Bound">a</a><a id="436" class="Symbol">}</a>
  <a id="440" class="Symbol">(</a><a id="441" class="Keyword">open</a> <a id="446" href="Graded.Modality.html" class="Module">Graded.Modality</a> <a id="462" href="Graded.Erasure.Consequences.Identity.html#427" class="Bound">M</a><a id="463" class="Symbol">)</a>
  <a id="467" class="Symbol">{</a><a id="468" href="Graded.Erasure.Consequences.Identity.html#468" class="Bound">𝕄</a> <a id="470" class="Symbol">:</a> <a id="472" href="Graded.Modality.html#7142" class="Record">Modality</a><a id="480" class="Symbol">}</a>
  <a id="484" class="Symbol">(</a><a id="485" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a> <a id="488" class="Symbol">:</a> <a id="490" href="Definition.Typed.Restrictions.html#524" class="Record">Type-restrictions</a> <a id="508" href="Graded.Erasure.Consequences.Identity.html#468" class="Bound">𝕄</a><a id="509" class="Symbol">)</a>
  <a id="513" class="Symbol">(</a><a id="514" href="Graded.Erasure.Consequences.Identity.html#514" class="Bound">UR</a> <a id="517" class="Symbol">:</a> <a id="519" href="Graded.Usage.Restrictions.html#424" class="Record">Usage-restrictions</a> <a id="538" href="Graded.Erasure.Consequences.Identity.html#427" class="Bound">M</a><a id="539" class="Symbol">)</a>
  <a id="543" class="Keyword">where</a>

<a id="550" class="Keyword">open</a> <a id="555" href="Graded.Modality.html#7142" class="Module">Modality</a> <a id="564" href="Graded.Erasure.Consequences.Identity.html#468" class="Bound">𝕄</a>
<a id="566" class="Keyword">open</a> <a id="571" href="Definition.Typed.Restrictions.html#524" class="Module">Type-restrictions</a> <a id="589" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a>

<a id="593" class="Keyword">open</a> <a id="598" class="Keyword">import</a> <a id="605" href="Definition.Typed.html" class="Module">Definition.Typed</a> <a id="622" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a>
<a id="625" class="Keyword">open</a> <a id="630" class="Keyword">import</a> <a id="637" href="Definition.Typed.Consequences.DerivedRules.html" class="Module">Definition.Typed.Consequences.DerivedRules</a> <a id="680" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a>
<a id="683" class="Keyword">open</a> <a id="688" class="Keyword">import</a> <a id="695" href="Definition.Typed.Consequences.Inversion.html" class="Module">Definition.Typed.Consequences.Inversion</a> <a id="735" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a>
<a id="738" class="Keyword">open</a> <a id="743" class="Keyword">import</a> <a id="750" href="Definition.Typed.Consequences.Syntactic.html" class="Module">Definition.Typed.Consequences.Syntactic</a> <a id="790" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a>
<a id="793" class="Keyword">open</a> <a id="798" class="Keyword">import</a> <a id="805" href="Definition.Typed.EqRelInstance.html" class="Module">Definition.Typed.EqRelInstance</a> <a id="836" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a>
<a id="839" class="Keyword">open</a> <a id="844" class="Keyword">import</a> <a id="851" href="Definition.Typed.Properties.html" class="Module">Definition.Typed.Properties</a> <a id="879" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a>
<a id="882" class="Keyword">open</a> <a id="887" class="Keyword">import</a> <a id="894" href="Definition.Untyped.html" class="Module">Definition.Untyped</a> <a id="913" href="Graded.Erasure.Consequences.Identity.html#427" class="Bound">M</a> <a id="915" class="Keyword">hiding</a> <a id="922" class="Symbol">(</a><a id="923" href="Definition.Untyped.NotParametrised.html#1148" class="InductiveConstructor Operator">_∷_</a><a id="926" class="Symbol">)</a>

<a id="929" class="Keyword">open</a> <a id="934" class="Keyword">import</a> <a id="941" href="Graded.Context.html" class="Module">Graded.Context</a> <a id="956" href="Graded.Erasure.Consequences.Identity.html#468" class="Bound">𝕄</a>
<a id="958" class="Keyword">open</a> <a id="963" class="Keyword">import</a> <a id="970" href="Graded.Derived.Erased.Eta.Typed.html" class="Module">Graded.Derived.Erased.Eta.Typed</a> <a id="1002" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a>
<a id="1005" class="Keyword">import</a> <a id="1012" href="Graded.Derived.Erased.Untyped.html" class="Module">Graded.Derived.Erased.Untyped</a> <a id="1042" href="Graded.Erasure.Consequences.Identity.html#468" class="Bound">𝕄</a> as <a id="Erased"></a><a id="1047" href="Graded.Erasure.Consequences.Identity.html#1047" class="Module">Erased</a>
<a id="1054" class="Keyword">open</a> <a id="1059" class="Keyword">import</a> <a id="1066" href="Graded.Derived.Erased.Eta.Usage.html" class="Module">Graded.Derived.Erased.Eta.Usage</a> <a id="1098" href="Graded.Erasure.Consequences.Identity.html#468" class="Bound">𝕄</a> <a id="1100" href="Graded.Erasure.Consequences.Identity.html#514" class="Bound">UR</a>
<a id="1103" class="Keyword">import</a> <a id="1110" href="Graded.Erasure.LogicalRelation.html" class="Module">Graded.Erasure.LogicalRelation</a> <a id="1141" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a> as <a id="L"></a><a id="1147" href="Graded.Erasure.Consequences.Identity.html#1147" class="Module">L</a>
<a id="1149" class="Keyword">open</a> <a id="1154" class="Keyword">import</a> <a id="1161" href="Graded.Erasure.LogicalRelation.Fundamental.html" class="Module">Graded.Erasure.LogicalRelation.Fundamental</a> <a id="1204" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a> <a id="1207" href="Graded.Erasure.Consequences.Identity.html#514" class="Bound">UR</a>
<a id="1210" class="Keyword">open</a> <a id="1215" class="Keyword">import</a> <a id="1222" href="Graded.Erasure.LogicalRelation.Fundamental.Assumptions.html" class="Module">Graded.Erasure.LogicalRelation.Fundamental.Assumptions</a> <a id="1277" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a> <a id="1280" href="Graded.Erasure.Consequences.Identity.html#514" class="Bound">UR</a>
<a id="1283" class="Keyword">import</a> <a id="1290" href="Graded.Erasure.LogicalRelation.Hidden.html" class="Module">Graded.Erasure.LogicalRelation.Hidden</a> <a id="1328" href="Graded.Erasure.Consequences.Identity.html#485" class="Bound">TR</a> as <a id="H"></a><a id="1334" href="Graded.Erasure.Consequences.Identity.html#1334" class="Module">H</a>
<a id="1336" class="Keyword">open</a> <a id="1341" class="Keyword">import</a> <a id="1348" href="Graded.Modality.Properties.html" class="Module">Graded.Modality.Properties</a> <a id="1375" href="Graded.Erasure.Consequences.Identity.html#468" class="Bound">𝕄</a>
<a id="1377" class="Keyword">open</a> <a id="1382" class="Keyword">import</a> <a id="1389" href="Graded.Mode.html" class="Module">Graded.Mode</a> <a id="1401" href="Graded.Erasure.Consequences.Identity.html#468" class="Bound">𝕄</a>
<a id="1403" class="Keyword">open</a> <a id="1408" class="Keyword">import</a> <a id="1415" href="Graded.Usage.html" class="Module">Graded.Usage</a> <a id="1428" href="Graded.Erasure.Consequences.Identity.html#468" class="Bound">𝕄</a> <a id="1430" href="Graded.Erasure.Consequences.Identity.html#514" class="Bound">UR</a>
<a id="1433" class="Keyword">open</a> <a id="1438" class="Keyword">import</a> <a id="1445" href="Graded.Usage.Properties.html" class="Module">Graded.Usage.Properties</a> <a id="1469" href="Graded.Erasure.Consequences.Identity.html#468" class="Bound">𝕄</a> <a id="1471" href="Graded.Erasure.Consequences.Identity.html#514" class="Bound">UR</a>

<a id="1475" class="Keyword">open</a> <a id="1480" class="Keyword">import</a> <a id="1487" href="Tools.Bool.html" class="Module">Tools.Bool</a> <a id="1498" class="Keyword">using</a> <a id="1504" class="Symbol">(</a><a id="1505" href="Data.Bool.Base.html#1348" class="Function">T</a><a id="1506" class="Symbol">)</a>
<a id="1508" class="Keyword">open</a> <a id="1513" class="Keyword">import</a> <a id="1520" href="Tools.Function.html" class="Module">Tools.Function</a>
<a id="1535" class="Keyword">open</a> <a id="1540" class="Keyword">import</a> <a id="1547" href="Tools.Product.html" class="Module">Tools.Product</a>
<a id="1561" class="Keyword">import</a> <a id="1568" href="Tools.PropositionalEquality.html" class="Module">Tools.PropositionalEquality</a> <a id="1596" class="Symbol">as</a> <a id="1599" class="Module">PE</a>

<a id="1603" class="Keyword">private</a> <a id="1611" class="Keyword">variable</a>
  <a id="1622" href="Graded.Erasure.Consequences.Identity.html#1622" class="Generalizable">Γ</a>           <a id="1634" class="Symbol">:</a> <a id="1636" href="Definition.Untyped.NotParametrised.html#791" class="Datatype">Con</a> <a id="1640" href="Definition.Untyped.html#2457" class="Datatype">Term</a> <a id="1645" class="Symbol">_</a>
  <a id="1649" href="Graded.Erasure.Consequences.Identity.html#1649" class="Generalizable">γ₁</a> <a id="1652" href="Graded.Erasure.Consequences.Identity.html#1652" class="Generalizable">γ₂</a> <a id="1655" href="Graded.Erasure.Consequences.Identity.html#1655" class="Generalizable">γ₃</a> <a id="1658" href="Graded.Erasure.Consequences.Identity.html#1658" class="Generalizable">γ₄</a> <a id="1661" class="Symbol">:</a> <a id="1663" href="Graded.Context.html#692" class="Datatype">Conₘ</a> <a id="1668" class="Symbol">_</a>
  <a id="1672" href="Graded.Erasure.Consequences.Identity.html#1672" class="Generalizable">A</a> <a id="1674" href="Graded.Erasure.Consequences.Identity.html#1674" class="Generalizable">t</a> <a id="1676" href="Graded.Erasure.Consequences.Identity.html#1676" class="Generalizable">u</a> <a id="1678" href="Graded.Erasure.Consequences.Identity.html#1678" class="Generalizable">v</a>     <a id="1684" class="Symbol">:</a> <a id="1686" href="Definition.Untyped.html#2457" class="Datatype">Term</a> <a id="1691" class="Symbol">_</a>
  <a id="1695" href="Graded.Erasure.Consequences.Identity.html#1695" class="Generalizable">s</a>           <a id="1707" class="Symbol">:</a> <a id="1709" href="Definition.Untyped.NotParametrised.html#1382" class="Datatype">Strength</a>

<a id="1719" class="Keyword">opaque</a>

  <a id="1729" class="Comment">-- If the modality&#39;s zero is well-behaved, the type Id A t u is</a>
  <a id="1795" class="Comment">-- inhabited in a context that satisfies Fundamental-assumptions⁻,</a>
  <a id="1864" class="Comment">-- and the witness of inhabitance is a term that is well-resourced</a>
  <a id="1933" class="Comment">-- with respect to 𝟘ᶜ, then t is definitionally equal to u.</a>

  <a id="Id→≡"></a><a id="1996" href="Graded.Erasure.Consequences.Identity.html#1996" class="Function">Id→≡</a> <a id="2001" class="Symbol">:</a>
    <a id="2007" class="Symbol">⦃</a> <a id="2009" href="Graded.Erasure.Consequences.Identity.html#2009" class="Bound">𝟘-well-behaved</a> <a id="2024" class="Symbol">:</a> <a id="2026" href="Graded.Modality.html#3304" class="Record">Has-well-behaved-zero</a> <a id="2048" href="Graded.Modality.html#7255" class="Function">semiring-with-meet</a> <a id="2067" class="Symbol">⦄</a> <a id="2069" class="Symbol">→</a>
    <a id="2075" href="Graded.Erasure.LogicalRelation.Fundamental.Assumptions.html#942" class="Record">Fundamental-assumptions⁻</a> <a id="2100" href="Graded.Erasure.Consequences.Identity.html#1622" class="Generalizable">Γ</a> <a id="2102" class="Symbol">→</a>
    <a id="2108" href="Graded.Erasure.Consequences.Identity.html#1622" class="Generalizable">Γ</a> <a id="2110" href="Definition.Typed.html#1702" class="Datatype Operator">⊢</a> <a id="2112" href="Graded.Erasure.Consequences.Identity.html#1678" class="Generalizable">v</a> <a id="2114" href="Definition.Typed.html#1702" class="Datatype Operator">∷</a> <a id="2116" href="Definition.Untyped.html#4533" class="InductiveConstructor">Id</a> <a id="2119" href="Graded.Erasure.Consequences.Identity.html#1672" class="Generalizable">A</a> <a id="2121" href="Graded.Erasure.Consequences.Identity.html#1674" class="Generalizable">t</a> <a id="2123" href="Graded.Erasure.Consequences.Identity.html#1676" class="Generalizable">u</a> <a id="2125" class="Symbol">→</a>
    <a id="2131" href="Graded.Context.html#2498" class="Function">𝟘ᶜ</a> <a id="2134" href="Graded.Usage.html#5588" class="Datatype Operator">▸[</a> <a id="2137" href="Graded.Mode.html#1117" class="InductiveConstructor">𝟙ᵐ</a> <a id="2140" href="Graded.Usage.html#5588" class="Datatype Operator">]</a> <a id="2142" href="Graded.Erasure.Consequences.Identity.html#1678" class="Generalizable">v</a> <a id="2144" class="Symbol">→</a>
    <a id="2150" href="Graded.Erasure.Consequences.Identity.html#1622" class="Generalizable">Γ</a> <a id="2152" href="Definition.Typed.html#5341" class="Datatype Operator">⊢</a> <a id="2154" href="Graded.Erasure.Consequences.Identity.html#1674" class="Generalizable">t</a> <a id="2156" href="Definition.Typed.html#5341" class="Datatype Operator">≡</a> <a id="2158" href="Graded.Erasure.Consequences.Identity.html#1676" class="Generalizable">u</a> <a id="2160" href="Definition.Typed.html#5341" class="Datatype Operator">∷</a> <a id="2162" href="Graded.Erasure.Consequences.Identity.html#1672" class="Generalizable">A</a>
  <a id="2166" href="Graded.Erasure.Consequences.Identity.html#1996" class="Function">Id→≡</a> <a id="2171" href="Graded.Erasure.Consequences.Identity.html#2171" class="Bound">ok</a> <a id="2174" href="Graded.Erasure.Consequences.Identity.html#2174" class="Bound">⊢v</a> <a id="2177" href="Graded.Erasure.Consequences.Identity.html#2177" class="Bound">▸v</a> <a id="2180" class="Symbol">=</a>
    <a id="2186" href="Function.Base.html#4042" class="Function Operator">case</a> <a id="2191" href="Graded.Erasure.LogicalRelation.Hidden.html#4156" class="Function">®-Id</a> <a id="2196" href="Function.Base.html#1974" class="Function Operator">$</a>
         <a id="2207" href="Graded.Erasure.LogicalRelation.Fundamental.html#28159" class="Function">Fundamental.fundamentalErased-𝟙ᵐ</a>
           <a id="2251" class="Symbol">(</a><a id="2252" class="Keyword">record</a>
              <a id="2273" class="Symbol">{</a> <a id="2275" href="Graded.Erasure.LogicalRelation.Fundamental.Assumptions.html#1469" class="Field">well-formed</a>       <a id="2293" class="Symbol">=</a> <a id="2295" href="Definition.Typed.Properties.Well-formed.html#767" class="Function">wfTerm</a> <a id="2302" href="Graded.Erasure.Consequences.Identity.html#2174" class="Bound">⊢v</a>
              <a id="2319" class="Symbol">;</a> <a id="2321" href="Graded.Erasure.LogicalRelation.Fundamental.Assumptions.html#1517" class="Field">other-assumptions</a> <a id="2339" class="Symbol">=</a> <a id="2341" href="Graded.Erasure.Consequences.Identity.html#2171" class="Bound">ok</a>
              <a id="2358" class="Symbol">})</a>
           <a id="2372" href="Graded.Erasure.Consequences.Identity.html#2174" class="Bound">⊢v</a> <a id="2375" href="Graded.Erasure.Consequences.Identity.html#2177" class="Bound">▸v</a> <a id="2378" href="Function.Base.html#4042" class="Function Operator">of</a> <a id="2381" class="Symbol">λ</a> <a id="2383" class="Symbol">{</a>
      <a id="2391" class="Symbol">(</a><a id="2392" href="Graded.Erasure.LogicalRelation.html#2534" class="InductiveConstructor">rflᵣ</a> <a id="2397" href="Graded.Erasure.Consequences.Identity.html#2397" class="Bound">v⇒rfl</a> <a id="2403" class="Symbol">_)</a> <a id="2406" class="Symbol">→</a>
    <a id="2412" href="Definition.Typed.Consequences.Inversion.html#10169" class="Function">inversion-rfl-Id</a>
      <a id="2435" class="Symbol">(</a><a id="2436" href="Definition.Typed.Consequences.Syntactic.html#1365" class="Function">syntacticEqTerm</a> <a id="2452" class="Symbol">(</a><a id="2453" href="Definition.Typed.Properties.html#3019" class="Function">subset*Term</a> <a id="2465" href="Graded.Erasure.Consequences.Identity.html#2397" class="Bound">v⇒rfl</a><a id="2470" class="Symbol">)</a> <a id="2472" class="Symbol">.</a><a id="2473" href="Data.Product.Base.html#650" class="Field">proj₂</a> <a id="2479" class="Symbol">.</a><a id="2480" href="Data.Product.Base.html#650" class="Field">proj₂</a><a id="2485" class="Symbol">)</a> <a id="2487" class="Symbol">}</a>
    <a id="2493" class="Keyword">where</a>
    <a id="2503" class="Keyword">open</a> <a id="2508" href="Graded.Erasure.LogicalRelation.Fundamental.Assumptions.html#942" class="Module">Fundamental-assumptions⁻</a> <a id="2533" href="Graded.Erasure.Consequences.Identity.html#2171" class="Bound">ok</a>
    <a id="2540" class="Keyword">open</a> <a id="2545" href="Graded.Erasure.Consequences.Identity.html#1334" class="Module">H</a> <a id="2547" href="Graded.Modality.html#3513" class="Field">is-𝟘?</a> <a id="2553" class="Symbol">(</a><a id="2554" href="Definition.Typed.Properties.Well-formed.html#767" class="Function">wfTerm</a> <a id="2561" href="Graded.Erasure.Consequences.Identity.html#2174" class="Bound">⊢v</a><a id="2563" class="Symbol">)</a>
    <a id="2569" class="Keyword">open</a> <a id="2574" href="Graded.Erasure.Consequences.Identity.html#1147" class="Module">L</a> <a id="2576" href="Graded.Modality.html#3513" class="Field">is-𝟘?</a> <a id="2582" class="Symbol">(</a><a id="2583" href="Definition.Typed.Properties.Well-formed.html#767" class="Function">wfTerm</a> <a id="2590" href="Graded.Erasure.Consequences.Identity.html#2174" class="Bound">⊢v</a><a id="2592" class="Symbol">)</a>

<a id="2595" class="Keyword">opaque</a>

  <a id="2605" class="Comment">-- A variant of the previous lemma: If the modality&#39;s zero is</a>
  <a id="2669" class="Comment">-- well-behaved, []-cong is allowed, the type Id A t u is inhabited</a>
  <a id="2739" class="Comment">-- in a context that satisfies Fundamental-assumptions⁻, and the</a>
  <a id="2806" class="Comment">-- witness of inhabitance as well as the terms A, t and u are</a>
  <a id="2870" class="Comment">-- well-resourced with respect to any context and the mode 𝟘ᵐ?, then</a>
  <a id="2941" class="Comment">-- t is definitionally equal to u.</a>
  <a id="2978" class="Comment">--</a>
  <a id="2983" class="Comment">-- Note that if []-cong is allowed, then (at the time of writing)</a>
  <a id="3051" class="Comment">-- Fundamental-assumptions⁻ only holds for the empty context.</a>

  <a id="Id→≡′"></a><a id="3116" href="Graded.Erasure.Consequences.Identity.html#3116" class="Function">Id→≡′</a> <a id="3122" class="Symbol">:</a>
    <a id="3128" class="Symbol">⦃</a> <a id="3130" href="Graded.Erasure.Consequences.Identity.html#3130" class="Bound">𝟘-well-behaved</a> <a id="3145" class="Symbol">:</a> <a id="3147" href="Graded.Modality.html#3304" class="Record">Has-well-behaved-zero</a> <a id="3169" href="Graded.Modality.html#7255" class="Function">semiring-with-meet</a> <a id="3188" class="Symbol">⦄</a> <a id="3190" class="Symbol">→</a>
    <a id="3196" href="Definition.Typed.Restrictions.html#2151" class="Function">[]-cong-allowed</a> <a id="3212" href="Graded.Erasure.Consequences.Identity.html#1695" class="Generalizable">s</a> <a id="3214" class="Symbol">→</a>
    <a id="3220" href="Graded.Erasure.LogicalRelation.Fundamental.Assumptions.html#942" class="Record">Fundamental-assumptions⁻</a> <a id="3245" href="Graded.Erasure.Consequences.Identity.html#1622" class="Generalizable">Γ</a> <a id="3247" class="Symbol">→</a>
    <a id="3253" href="Graded.Erasure.Consequences.Identity.html#1649" class="Generalizable">γ₁</a> <a id="3256" href="Graded.Usage.html#5588" class="Datatype Operator">▸[</a> <a id="3259" href="Graded.Mode.html#2306" class="Function">𝟘ᵐ?</a> <a id="3263" href="Graded.Usage.html#5588" class="Datatype Operator">]</a> <a id="3265" href="Graded.Erasure.Consequences.Identity.html#1672" class="Generalizable">A</a> <a id="3267" class="Symbol">→</a>
    <a id="3273" href="Graded.Erasure.Consequences.Identity.html#1652" class="Generalizable">γ₂</a> <a id="3276" href="Graded.Usage.html#5588" class="Datatype Operator">▸[</a> <a id="3279" href="Graded.Mode.html#2306" class="Function">𝟘ᵐ?</a> <a id="3283" href="Graded.Usage.html#5588" class="Datatype Operator">]</a> <a id="3285" href="Graded.Erasure.Consequences.Identity.html#1674" class="Generalizable">t</a> <a id="3287" class="Symbol">→</a>
    <a id="3293" href="Graded.Erasure.Consequences.Identity.html#1655" class="Generalizable">γ₃</a> <a id="3296" href="Graded.Usage.html#5588" class="Datatype Operator">▸[</a> <a id="3299" href="Graded.Mode.html#2306" class="Function">𝟘ᵐ?</a> <a id="3303" href="Graded.Usage.html#5588" class="Datatype Operator">]</a> <a id="3305" href="Graded.Erasure.Consequences.Identity.html#1676" class="Generalizable">u</a> <a id="3307" class="Symbol">→</a>
    <a id="3313" href="Graded.Erasure.Consequences.Identity.html#1658" class="Generalizable">γ₄</a> <a id="3316" href="Graded.Usage.html#5588" class="Datatype Operator">▸[</a> <a id="3319" href="Graded.Mode.html#2306" class="Function">𝟘ᵐ?</a> <a id="3323" href="Graded.Usage.html#5588" class="Datatype Operator">]</a> <a id="3325" href="Graded.Erasure.Consequences.Identity.html#1678" class="Generalizable">v</a> <a id="3327" class="Symbol">→</a>
    <a id="3333" href="Graded.Erasure.Consequences.Identity.html#1622" class="Generalizable">Γ</a> <a id="3335" href="Definition.Typed.html#1702" class="Datatype Operator">⊢</a> <a id="3337" href="Graded.Erasure.Consequences.Identity.html#1678" class="Generalizable">v</a> <a id="3339" href="Definition.Typed.html#1702" class="Datatype Operator">∷</a> <a id="3341" href="Definition.Untyped.html#4533" class="InductiveConstructor">Id</a> <a id="3344" href="Graded.Erasure.Consequences.Identity.html#1672" class="Generalizable">A</a> <a id="3346" href="Graded.Erasure.Consequences.Identity.html#1674" class="Generalizable">t</a> <a id="3348" href="Graded.Erasure.Consequences.Identity.html#1676" class="Generalizable">u</a> <a id="3350" class="Symbol">→</a>
    <a id="3356" href="Graded.Erasure.Consequences.Identity.html#1622" class="Generalizable">Γ</a> <a id="3358" href="Definition.Typed.html#5341" class="Datatype Operator">⊢</a> <a id="3360" href="Graded.Erasure.Consequences.Identity.html#1674" class="Generalizable">t</a> <a id="3362" href="Definition.Typed.html#5341" class="Datatype Operator">≡</a> <a id="3364" href="Graded.Erasure.Consequences.Identity.html#1676" class="Generalizable">u</a> <a id="3366" href="Definition.Typed.html#5341" class="Datatype Operator">∷</a> <a id="3368" href="Graded.Erasure.Consequences.Identity.html#1672" class="Generalizable">A</a>
  <a id="3372" href="Graded.Erasure.Consequences.Identity.html#3116" class="Function">Id→≡′</a> <a id="3378" class="Symbol">{</a><a id="3379" href="Graded.Erasure.Consequences.Identity.html#3379" class="Bound">s</a><a id="3380" class="Symbol">}</a> <a id="3382" class="Symbol">{</a><a id="3383" href="Graded.Erasure.Consequences.Identity.html#3383" class="Bound">Γ</a><a id="3384" class="Symbol">}</a> <a id="3386" class="Symbol">{</a><a id="3387" href="Graded.Erasure.Consequences.Identity.html#3387" class="Bound">A</a><a id="3388" class="Symbol">}</a> <a id="3390" class="Symbol">{</a><a id="3391" href="Graded.Erasure.Consequences.Identity.html#3391" class="Bound">t</a><a id="3392" class="Symbol">}</a> <a id="3394" class="Symbol">{</a><a id="3395" href="Graded.Erasure.Consequences.Identity.html#3395" class="Bound">u</a><a id="3396" class="Symbol">}</a> <a id="3398" class="Symbol">{</a><a id="3399" href="Graded.Erasure.Consequences.Identity.html#3399" class="Bound">v</a><a id="3400" class="Symbol">}</a> <a id="3402" href="Graded.Erasure.Consequences.Identity.html#3402" class="Bound">[]-cong-ok</a> <a id="3413" href="Graded.Erasure.Consequences.Identity.html#3413" class="Bound">ok</a> <a id="3416" href="Graded.Erasure.Consequences.Identity.html#3416" class="Bound">▸A</a> <a id="3419" href="Graded.Erasure.Consequences.Identity.html#3419" class="Bound">▸t</a> <a id="3422" href="Graded.Erasure.Consequences.Identity.html#3422" class="Bound">▸u</a> <a id="3425" href="Graded.Erasure.Consequences.Identity.html#3425" class="Bound">▸v</a> <a id="3428" class="Symbol">=</a>
    <a id="3434" href="Graded.Erasure.Consequences.Identity.html#3383" class="Bound">Γ</a> <a id="3436" href="Definition.Typed.html#1702" class="Datatype Operator">⊢</a> <a id="3438" href="Graded.Erasure.Consequences.Identity.html#3399" class="Bound">v</a> <a id="3440" href="Definition.Typed.html#1702" class="Datatype Operator">∷</a> <a id="3442" href="Definition.Untyped.html#4533" class="InductiveConstructor">Id</a> <a id="3445" href="Graded.Erasure.Consequences.Identity.html#3387" class="Bound">A</a> <a id="3447" href="Graded.Erasure.Consequences.Identity.html#3391" class="Bound">t</a> <a id="3449" href="Graded.Erasure.Consequences.Identity.html#3395" class="Bound">u</a>                                       <a id="3489" href="Tools.Function.html#840" class="Function">→⟨</a> <a id="3492" href="Definition.Typed.Consequences.DerivedRules.Identity.html#9729" class="Function">[]-congⱼ′</a> <a id="3502" href="Graded.Erasure.Consequences.Identity.html#3402" class="Bound">[]-cong-ok</a> <a id="3513" href="Tools.Function.html#840" class="Function">⟩</a>
    <a id="3519" href="Graded.Erasure.Consequences.Identity.html#3383" class="Bound">Γ</a> <a id="3521" href="Definition.Typed.html#1702" class="Datatype Operator">⊢</a> <a id="3523" href="Definition.Untyped.html#4814" class="InductiveConstructor">[]-cong</a> <a id="3531" class="Symbol">_</a> <a id="3533" href="Graded.Erasure.Consequences.Identity.html#3387" class="Bound">A</a> <a id="3535" href="Graded.Erasure.Consequences.Identity.html#3391" class="Bound">t</a> <a id="3537" href="Graded.Erasure.Consequences.Identity.html#3395" class="Bound">u</a> <a id="3539" href="Graded.Erasure.Consequences.Identity.html#3399" class="Bound">v</a> <a id="3541" href="Definition.Typed.html#1702" class="Datatype Operator">∷</a> <a id="3543" href="Definition.Untyped.html#4533" class="InductiveConstructor">Id</a> <a id="3546" class="Symbol">(</a><a id="3547" href="Graded.Derived.Erased.Untyped.html#624" class="Function">Erased</a> <a id="3554" href="Graded.Erasure.Consequences.Identity.html#3387" class="Bound">A</a><a id="3555" class="Symbol">)</a> <a id="3557" class="Symbol">(</a><a id="3558" href="Graded.Derived.Erased.Untyped.html#712" class="Function Operator">[</a> <a id="3560" href="Graded.Erasure.Consequences.Identity.html#3391" class="Bound">t</a> <a id="3562" href="Graded.Derived.Erased.Untyped.html#712" class="Function Operator">]</a><a id="3563" class="Symbol">)</a> <a id="3565" class="Symbol">(</a><a id="3566" href="Graded.Derived.Erased.Untyped.html#712" class="Function Operator">[</a> <a id="3568" href="Graded.Erasure.Consequences.Identity.html#3395" class="Bound">u</a> <a id="3570" href="Graded.Derived.Erased.Untyped.html#712" class="Function Operator">]</a><a id="3571" class="Symbol">)</a>  <a id="3574" href="Tools.Function.html#840" class="Function">→⟨</a> <a id="3577" href="Function.Base.html#1638" class="Function">flip</a> <a id="3582" class="Symbol">(</a><a id="3583" href="Graded.Erasure.Consequences.Identity.html#1996" class="Function">Id→≡</a> <a id="3588" href="Graded.Erasure.Consequences.Identity.html#3413" class="Bound">ok</a><a id="3590" class="Symbol">)</a> <a id="3592" class="Symbol">(</a><a id="3593" href="Graded.Usage.html#10810" class="InductiveConstructor">[]-congₘ</a> <a id="3602" href="Graded.Erasure.Consequences.Identity.html#3416" class="Bound">▸A</a> <a id="3605" href="Graded.Erasure.Consequences.Identity.html#3419" class="Bound">▸t</a> <a id="3608" href="Graded.Erasure.Consequences.Identity.html#3422" class="Bound">▸u</a> <a id="3611" href="Graded.Erasure.Consequences.Identity.html#3425" class="Bound">▸v</a><a id="3613" class="Symbol">)</a> <a id="3615" href="Tools.Function.html#840" class="Function">⟩</a>
    <a id="3621" href="Graded.Erasure.Consequences.Identity.html#3383" class="Bound">Γ</a> <a id="3623" href="Definition.Typed.html#5341" class="Datatype Operator">⊢</a> <a id="3625" class="Symbol">(</a><a id="3626" href="Graded.Derived.Erased.Untyped.html#712" class="Function Operator">[</a> <a id="3628" href="Graded.Erasure.Consequences.Identity.html#3391" class="Bound">t</a> <a id="3630" href="Graded.Derived.Erased.Untyped.html#712" class="Function Operator">]</a><a id="3631" class="Symbol">)</a> <a id="3633" href="Definition.Typed.html#5341" class="Datatype Operator">≡</a> <a id="3635" class="Symbol">(</a><a id="3636" href="Graded.Derived.Erased.Untyped.html#712" class="Function Operator">[</a> <a id="3638" href="Graded.Erasure.Consequences.Identity.html#3395" class="Bound">u</a> <a id="3640" href="Graded.Derived.Erased.Untyped.html#712" class="Function Operator">]</a><a id="3641" class="Symbol">)</a> <a id="3643" href="Definition.Typed.html#5341" class="Datatype Operator">∷</a> <a id="3645" href="Graded.Derived.Erased.Untyped.html#624" class="Function">Erased</a> <a id="3652" href="Graded.Erasure.Consequences.Identity.html#3387" class="Bound">A</a>                       <a id="3676" href="Tools.Function.html#840" class="Function">→⟨</a> <a id="3679" href="Data.Product.Base.html#636" class="Field">proj₁</a> <a id="3685" href="Function.Base.html#1115" class="Function Operator">∘→</a> <a id="3688" href="Data.Product.Base.html#650" class="Field">proj₂</a> <a id="3694" href="Function.Base.html#1115" class="Function Operator">∘→</a> <a id="3697" href="Definition.Typed.Consequences.DerivedRules.Sigma.html#47237" class="Function">prod-cong⁻¹</a> <a id="3709" href="Tools.Function.html#840" class="Function">⟩</a>
    <a id="3715" href="Graded.Erasure.Consequences.Identity.html#3383" class="Bound">Γ</a> <a id="3717" href="Definition.Typed.html#5341" class="Datatype Operator">⊢</a> <a id="3719" href="Graded.Erasure.Consequences.Identity.html#3391" class="Bound">t</a> <a id="3721" href="Definition.Typed.html#5341" class="Datatype Operator">≡</a> <a id="3723" href="Graded.Erasure.Consequences.Identity.html#3395" class="Bound">u</a> <a id="3725" href="Definition.Typed.html#5341" class="Datatype Operator">∷</a> <a id="3727" href="Graded.Erasure.Consequences.Identity.html#3387" class="Bound">A</a>                                          <a id="3770" href="Tools.Function.html#1068" class="Function Operator">□</a>
    <a id="3776" class="Keyword">where</a>
    <a id="3786" class="Keyword">open</a> <a id="3791" href="Graded.Erasure.Consequences.Identity.html#1047" class="Module">Erased</a> <a id="3798" href="Graded.Erasure.Consequences.Identity.html#3379" class="Bound">s</a>

<a id="3801" class="Keyword">opaque</a>

  <a id="3811" class="Comment">-- Another variant of Id→≡.</a>
  <a id="3841" class="Comment">--</a>
  <a id="3846" class="Comment">-- Note that if []-cong is allowed, then (at the time of writing)</a>
  <a id="3914" class="Comment">-- Fundamental-assumptions⁻ only holds for the empty context.</a>

  <a id="Id→≡″"></a><a id="3979" href="Graded.Erasure.Consequences.Identity.html#3979" class="Function">Id→≡″</a> <a id="3985" class="Symbol">:</a>
    <a id="3991" class="Symbol">⦃</a> <a id="3993" href="Graded.Erasure.Consequences.Identity.html#3993" class="Bound">𝟘-well-behaved</a> <a id="4008" class="Symbol">:</a> <a id="4010" href="Graded.Modality.html#3304" class="Record">Has-well-behaved-zero</a> <a id="4032" href="Graded.Modality.html#7255" class="Function">semiring-with-meet</a> <a id="4051" class="Symbol">⦄</a>
    <a id="4057" class="Symbol">⦃</a> <a id="4059" href="Graded.Erasure.Consequences.Identity.html#4059" class="Bound">ok</a> <a id="4062" class="Symbol">:</a> <a id="4064" href="Data.Bool.Base.html#1348" class="Function">T</a> <a id="4066" href="Graded.Modality.Variant.html#791" class="Function">𝟘ᵐ-allowed</a> <a id="4077" class="Symbol">⦄</a> <a id="4079" class="Symbol">→</a>
    <a id="4085" href="Definition.Typed.Restrictions.html#2151" class="Function">[]-cong-allowed</a> <a id="4101" href="Graded.Erasure.Consequences.Identity.html#1695" class="Generalizable">s</a> <a id="4103" class="Symbol">→</a>
    <a id="4109" href="Graded.Erasure.LogicalRelation.Fundamental.Assumptions.html#942" class="Record">Fundamental-assumptions⁻</a> <a id="4134" href="Graded.Erasure.Consequences.Identity.html#1622" class="Generalizable">Γ</a> <a id="4136" class="Symbol">→</a>
    <a id="4142" href="Graded.Erasure.Consequences.Identity.html#1649" class="Generalizable">γ₁</a> <a id="4145" href="Graded.Usage.html#5588" class="Datatype Operator">▸[</a> <a id="4148" href="Graded.Mode.html#1081" class="InductiveConstructor">𝟘ᵐ</a> <a id="4151" href="Graded.Usage.html#5588" class="Datatype Operator">]</a> <a id="4153" href="Graded.Erasure.Consequences.Identity.html#1672" class="Generalizable">A</a> <a id="4155" class="Symbol">→</a>
    <a id="4161" href="Graded.Erasure.Consequences.Identity.html#1652" class="Generalizable">γ₂</a> <a id="4164" href="Graded.Usage.html#5588" class="Datatype Operator">▸[</a> <a id="4167" href="Graded.Mode.html#1081" class="InductiveConstructor">𝟘ᵐ</a> <a id="4170" href="Graded.Usage.html#5588" class="Datatype Operator">]</a> <a id="4172" href="Graded.Erasure.Consequences.Identity.html#1674" class="Generalizable">t</a> <a id="4174" class="Symbol">→</a>
    <a id="4180" href="Graded.Erasure.Consequences.Identity.html#1655" class="Generalizable">γ₃</a> <a id="4183" href="Graded.Usage.html#5588" class="Datatype Operator">▸[</a> <a id="4186" href="Graded.Mode.html#1081" class="InductiveConstructor">𝟘ᵐ</a> <a id="4189" href="Graded.Usage.html#5588" class="Datatype Operator">]</a> <a id="4191" href="Graded.Erasure.Consequences.Identity.html#1676" class="Generalizable">u</a> <a id="4193" class="Symbol">→</a>
    <a id="4199" href="Graded.Erasure.Consequences.Identity.html#1658" class="Generalizable">γ₄</a> <a id="4202" href="Graded.Usage.html#5588" class="Datatype Operator">▸[</a> <a id="4205" href="Graded.Mode.html#1081" class="InductiveConstructor">𝟘ᵐ</a> <a id="4208" href="Graded.Usage.html#5588" class="Datatype Operator">]</a> <a id="4210" href="Graded.Erasure.Consequences.Identity.html#1678" class="Generalizable">v</a> <a id="4212" class="Symbol">→</a>
    <a id="4218" href="Graded.Erasure.Consequences.Identity.html#1622" class="Generalizable">Γ</a> <a id="4220" href="Definition.Typed.html#1702" class="Datatype Operator">⊢</a> <a id="4222" href="Graded.Erasure.Consequences.Identity.html#1678" class="Generalizable">v</a> <a id="4224" href="Definition.Typed.html#1702" class="Datatype Operator">∷</a> <a id="4226" href="Graded.Derived.Erased.Untyped.html#624" class="Function">Erased.Erased</a> <a id="4240" href="Definition.Untyped.NotParametrised.html#1405" class="InductiveConstructor">𝕤</a> <a id="4242" class="Symbol">(</a><a id="4243" href="Definition.Untyped.html#4533" class="InductiveConstructor">Id</a> <a id="4246" href="Graded.Erasure.Consequences.Identity.html#1672" class="Generalizable">A</a> <a id="4248" href="Graded.Erasure.Consequences.Identity.html#1674" class="Generalizable">t</a> <a id="4250" href="Graded.Erasure.Consequences.Identity.html#1676" class="Generalizable">u</a><a id="4251" class="Symbol">)</a> <a id="4253" class="Symbol">→</a>
    <a id="4259" href="Graded.Erasure.Consequences.Identity.html#1622" class="Generalizable">Γ</a> <a id="4261" href="Definition.Typed.html#5341" class="Datatype Operator">⊢</a> <a id="4263" href="Graded.Erasure.Consequences.Identity.html#1674" class="Generalizable">t</a> <a id="4265" href="Definition.Typed.html#5341" class="Datatype Operator">≡</a> <a id="4267" href="Graded.Erasure.Consequences.Identity.html#1676" class="Generalizable">u</a> <a id="4269" href="Definition.Typed.html#5341" class="Datatype Operator">∷</a> <a id="4271" href="Graded.Erasure.Consequences.Identity.html#1672" class="Generalizable">A</a>
  <a id="4275" href="Graded.Erasure.Consequences.Identity.html#3979" class="Function">Id→≡″</a> <a id="4281" class="Symbol">{</a><a id="4282" href="Graded.Erasure.Consequences.Identity.html#4282" class="Bound">Γ</a><a id="4283" class="Symbol">}</a> <a id="4285" class="Symbol">{</a><a id="4286" href="Graded.Erasure.Consequences.Identity.html#4286" class="Bound">A</a><a id="4287" class="Symbol">}</a> <a id="4289" class="Symbol">{</a><a id="4290" href="Graded.Erasure.Consequences.Identity.html#4290" class="Bound">t</a><a id="4291" class="Symbol">}</a> <a id="4293" class="Symbol">{</a><a id="4294" href="Graded.Erasure.Consequences.Identity.html#4294" class="Bound">u</a><a id="4295" class="Symbol">}</a> <a id="4297" class="Symbol">{</a><a id="4298" href="Graded.Erasure.Consequences.Identity.html#4298" class="Bound">v</a><a id="4299" class="Symbol">}</a> <a id="4301" href="Graded.Erasure.Consequences.Identity.html#4301" class="Bound">[]-cong-ok</a> <a id="4312" href="Graded.Erasure.Consequences.Identity.html#4312" class="Bound">ok</a> <a id="4315" href="Graded.Erasure.Consequences.Identity.html#4315" class="Bound">▸A</a> <a id="4318" href="Graded.Erasure.Consequences.Identity.html#4318" class="Bound">▸t</a> <a id="4321" href="Graded.Erasure.Consequences.Identity.html#4321" class="Bound">▸u</a> <a id="4324" href="Graded.Erasure.Consequences.Identity.html#4324" class="Bound">▸v</a> <a id="4327" class="Symbol">=</a>
    <a id="4333" href="Graded.Erasure.Consequences.Identity.html#4282" class="Bound">Γ</a> <a id="4335" href="Definition.Typed.html#1702" class="Datatype Operator">⊢</a> <a id="4337" href="Graded.Erasure.Consequences.Identity.html#4298" class="Bound">v</a> <a id="4339" href="Definition.Typed.html#1702" class="Datatype Operator">∷</a> <a id="4341" href="Graded.Derived.Erased.Untyped.html#624" class="Function">Erased</a> <a id="4348" class="Symbol">(</a><a id="4349" href="Definition.Untyped.html#4533" class="InductiveConstructor">Id</a> <a id="4352" href="Graded.Erasure.Consequences.Identity.html#4286" class="Bound">A</a> <a id="4354" href="Graded.Erasure.Consequences.Identity.html#4290" class="Bound">t</a> <a id="4356" href="Graded.Erasure.Consequences.Identity.html#4294" class="Bound">u</a><a id="4357" class="Symbol">)</a>  <a id="4360" href="Tools.Function.html#840" class="Function">→⟨</a> <a id="4363" href="Graded.Derived.Erased.Eta.Typed.html#1322" class="Function">erasedⱼ</a> <a id="4371" href="Tools.Function.html#840" class="Function">⟩</a>
    <a id="4377" href="Graded.Erasure.Consequences.Identity.html#4282" class="Bound">Γ</a> <a id="4379" href="Definition.Typed.html#1702" class="Datatype Operator">⊢</a> <a id="4381" href="Graded.Derived.Erased.Eta.Untyped.html#530" class="Function">erased</a> <a id="4388" href="Graded.Erasure.Consequences.Identity.html#4298" class="Bound">v</a> <a id="4390" href="Definition.Typed.html#1702" class="Datatype Operator">∷</a> <a id="4392" href="Definition.Untyped.html#4533" class="InductiveConstructor">Id</a> <a id="4395" href="Graded.Erasure.Consequences.Identity.html#4286" class="Bound">A</a> <a id="4397" href="Graded.Erasure.Consequences.Identity.html#4290" class="Bound">t</a> <a id="4399" href="Graded.Erasure.Consequences.Identity.html#4294" class="Bound">u</a>    <a id="4404" href="Tools.Function.html#840" class="Function">→⟨</a> <a id="4407" href="Graded.Erasure.Consequences.Identity.html#3116" class="Function">Id→≡′</a> <a id="4413" href="Graded.Erasure.Consequences.Identity.html#4301" class="Bound">[]-cong-ok</a> <a id="4424" href="Graded.Erasure.Consequences.Identity.html#4312" class="Bound">ok</a>
                                    <a id="4463" class="Symbol">(</a><a id="4464" href="Graded.Usage.Properties.html#2456" class="Function">▸-cong</a> <a id="4471" class="Symbol">(</a><a id="4472" href="Relation.Binary.PropositionalEquality.Core.html#1712" class="Function">PE.sym</a> <a id="4479" href="Graded.Mode.html#6263" class="Function">𝟘ᵐ?≡𝟘ᵐ</a><a id="4485" class="Symbol">)</a> <a id="4487" href="Graded.Erasure.Consequences.Identity.html#4315" class="Bound">▸A</a><a id="4489" class="Symbol">)</a> <a id="4491" class="Symbol">(</a><a id="4492" href="Graded.Usage.Properties.html#2456" class="Function">▸-cong</a> <a id="4499" class="Symbol">(</a><a id="4500" href="Relation.Binary.PropositionalEquality.Core.html#1712" class="Function">PE.sym</a> <a id="4507" href="Graded.Mode.html#6263" class="Function">𝟘ᵐ?≡𝟘ᵐ</a><a id="4513" class="Symbol">)</a> <a id="4515" href="Graded.Erasure.Consequences.Identity.html#4318" class="Bound">▸t</a><a id="4517" class="Symbol">)</a>
                                    <a id="4555" class="Symbol">(</a><a id="4556" href="Graded.Usage.Properties.html#2456" class="Function">▸-cong</a> <a id="4563" class="Symbol">(</a><a id="4564" href="Relation.Binary.PropositionalEquality.Core.html#1712" class="Function">PE.sym</a> <a id="4571" href="Graded.Mode.html#6263" class="Function">𝟘ᵐ?≡𝟘ᵐ</a><a id="4577" class="Symbol">)</a> <a id="4579" href="Graded.Erasure.Consequences.Identity.html#4321" class="Bound">▸u</a><a id="4581" class="Symbol">)</a> <a id="4583" class="Symbol">(</a><a id="4584" href="Graded.Usage.Properties.html#2456" class="Function">▸-cong</a> <a id="4591" class="Symbol">(</a><a id="4592" href="Relation.Binary.PropositionalEquality.Core.html#1712" class="Function">PE.sym</a> <a id="4599" href="Graded.Mode.html#6263" class="Function">𝟘ᵐ?≡𝟘ᵐ</a><a id="4605" class="Symbol">)</a> <a id="4607" href="Function.Base.html#1974" class="Function Operator">$</a> <a id="4609" href="Graded.Derived.Erased.Eta.Usage.html#1407" class="Function">▸erased</a> <a id="4617" href="Graded.Erasure.Consequences.Identity.html#4324" class="Bound">▸v</a><a id="4619" class="Symbol">)</a> <a id="4621" href="Tools.Function.html#840" class="Function">⟩</a>
    <a id="4627" href="Graded.Erasure.Consequences.Identity.html#4282" class="Bound">Γ</a> <a id="4629" href="Definition.Typed.html#5341" class="Datatype Operator">⊢</a> <a id="4631" href="Graded.Erasure.Consequences.Identity.html#4290" class="Bound">t</a> <a id="4633" href="Definition.Typed.html#5341" class="Datatype Operator">≡</a> <a id="4635" href="Graded.Erasure.Consequences.Identity.html#4294" class="Bound">u</a> <a id="4637" href="Definition.Typed.html#5341" class="Datatype Operator">∷</a> <a id="4639" href="Graded.Erasure.Consequences.Identity.html#4286" class="Bound">A</a>              <a id="4654" href="Tools.Function.html#1068" class="Function Operator">□</a>
    <a id="4660" class="Keyword">where</a>
    <a id="4670" class="Keyword">open</a> <a id="4675" class="Keyword">import</a> <a id="4682" href="Graded.Derived.Erased.Eta.Untyped.html" class="Module">Graded.Derived.Erased.Eta.Untyped</a> <a id="4716" href="Graded.Erasure.Consequences.Identity.html#468" class="Bound">𝕄</a>
</pre></body></html>