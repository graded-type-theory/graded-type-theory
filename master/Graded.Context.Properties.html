<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Graded.Context.Properties</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- Properties of modality (grade) contexts.</a>
<a id="118" class="Comment">------------------------------------------------------------------------</a>

<a id="192" class="Keyword">open</a> <a id="197" class="Keyword">import</a> <a id="204" href="Graded.Modality.html" class="Module">Graded.Modality</a>

<a id="221" class="Keyword">module</a> <a id="228" href="Graded.Context.Properties.html" class="Module">Graded.Context.Properties</a>
  <a id="256" class="Symbol">{</a><a id="257" href="Graded.Context.Properties.html#257" class="Bound">a</a><a id="258" class="Symbol">}</a> <a id="260" class="Symbol">{</a><a id="261" href="Graded.Context.Properties.html#261" class="Bound">M</a> <a id="263" class="Symbol">:</a> <a id="265" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="269" href="Graded.Context.Properties.html#257" class="Bound">a</a><a id="270" class="Symbol">}</a> <a id="272" class="Symbol">(</a><a id="273" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a> <a id="275" class="Symbol">:</a> <a id="277" href="Graded.Modality.html#5226" class="Record">Modality</a> <a id="286" href="Graded.Context.Properties.html#261" class="Bound">M</a><a id="287" class="Symbol">)</a> <a id="289" class="Keyword">where</a>

<a id="296" class="Keyword">open</a> <a id="301" href="Graded.Modality.html#5226" class="Module">Modality</a> <a id="310" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a>

<a id="313" class="Keyword">open</a> <a id="318" class="Keyword">import</a> <a id="325" href="Graded.Modality.Properties.html" class="Module">Graded.Modality.Properties</a> <a id="352" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a>
<a id="354" class="Keyword">open</a> <a id="359" class="Keyword">import</a> <a id="366" href="Graded.Context.html" class="Module">Graded.Context</a> <a id="381" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a>

<a id="384" class="Keyword">open</a> <a id="389" class="Keyword">import</a> <a id="396" href="Tools.Algebra.html" class="Module">Tools.Algebra</a> <a id="410" href="Graded.Context.Properties.html#261" class="Bound">M</a>
<a id="412" class="Keyword">open</a> <a id="417" class="Keyword">import</a> <a id="424" href="Tools.Nat.html" class="Module">Tools.Nat</a> <a id="434" class="Keyword">using</a> <a id="440" class="Symbol">(</a><a id="441" href="Agda.Builtin.Nat.html#186" class="Datatype">Nat</a><a id="444" class="Symbol">;</a> <a id="446" href="Tools.Nat.html#1071" class="InductiveConstructor">1+</a><a id="448" class="Symbol">)</a>
<a id="450" class="Keyword">open</a> <a id="455" class="Keyword">import</a> <a id="462" href="Tools.PropositionalEquality.html" class="Module">Tools.PropositionalEquality</a>
<a id="490" class="Keyword">import</a> <a id="497" href="Tools.Reasoning.Equivalence.html" class="Module">Tools.Reasoning.Equivalence</a>

<a id="526" class="Keyword">open</a> <a id="531" class="Keyword">import</a> <a id="538" href="Graded.Context.Properties.Addition.html" class="Module">Graded.Context.Properties.Addition</a> <a id="573" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a> <a id="575" class="Keyword">public</a>
<a id="582" class="Keyword">open</a> <a id="587" class="Keyword">import</a> <a id="594" href="Graded.Context.Properties.Equivalence.html" class="Module">Graded.Context.Properties.Equivalence</a> <a id="632" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a> <a id="634" class="Keyword">public</a>
<a id="641" class="Keyword">open</a> <a id="646" class="Keyword">import</a> <a id="653" href="Graded.Context.Properties.Has-well-behaved-zero.html" class="Module">Graded.Context.Properties.Has-well-behaved-zero</a> <a id="701" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a> <a id="703" class="Keyword">public</a>
<a id="710" class="Keyword">open</a> <a id="715" class="Keyword">import</a> <a id="722" href="Graded.Context.Properties.Lookup.html" class="Module">Graded.Context.Properties.Lookup</a> <a id="755" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a> <a id="757" class="Keyword">public</a>
<a id="764" class="Keyword">open</a> <a id="769" class="Keyword">import</a> <a id="776" href="Graded.Context.Properties.Meet.html" class="Module">Graded.Context.Properties.Meet</a> <a id="807" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a> <a id="809" class="Keyword">public</a>
<a id="816" class="Keyword">open</a> <a id="821" class="Keyword">import</a> <a id="828" href="Graded.Context.Properties.Multiplication.html" class="Module">Graded.Context.Properties.Multiplication</a> <a id="869" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a> <a id="871" class="Keyword">public</a>
<a id="878" class="Keyword">open</a> <a id="883" class="Keyword">import</a> <a id="890" href="Graded.Context.Properties.PartialOrder.html" class="Module">Graded.Context.Properties.PartialOrder</a> <a id="929" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a> <a id="931" class="Keyword">public</a>
<a id="938" class="Keyword">open</a> <a id="943" class="Keyword">import</a> <a id="950" href="Graded.Context.Properties.Star.html" class="Module">Graded.Context.Properties.Star</a> <a id="981" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a> <a id="983" class="Keyword">public</a>
<a id="990" class="Keyword">open</a> <a id="995" class="Keyword">import</a> <a id="1002" href="Graded.Context.Properties.Update.html" class="Module">Graded.Context.Properties.Update</a> <a id="1035" href="Graded.Context.Properties.html#273" class="Bound">𝕄</a> <a id="1037" class="Keyword">public</a>

<a id="1045" class="Keyword">private</a>
  <a id="1055" class="Keyword">variable</a>
    <a id="1068" href="Graded.Context.Properties.html#1068" class="Generalizable">n</a> <a id="1070" class="Symbol">:</a> <a id="1072" href="Agda.Builtin.Nat.html#186" class="Datatype">Nat</a>
    <a id="1080" href="Graded.Context.Properties.html#1080" class="Generalizable">p</a> <a id="1082" href="Graded.Context.Properties.html#1082" class="Generalizable">q</a> <a id="1084" href="Graded.Context.Properties.html#1084" class="Generalizable">r</a> <a id="1086" href="Graded.Context.Properties.html#1086" class="Generalizable">r′</a> <a id="1089" class="Symbol">:</a> <a id="1091" href="Graded.Context.Properties.html#261" class="Bound">M</a>
    <a id="1097" href="Graded.Context.Properties.html#1097" class="Generalizable">γ</a> <a id="1099" href="Graded.Context.Properties.html#1099" class="Generalizable">γ′</a> <a id="1102" href="Graded.Context.Properties.html#1102" class="Generalizable">δ</a> <a id="1104" href="Graded.Context.Properties.html#1104" class="Generalizable">δ′</a> <a id="1107" href="Graded.Context.Properties.html#1107" class="Generalizable">η</a> <a id="1109" class="Symbol">:</a> <a id="1111" href="Graded.Context.html#701" class="Datatype">Conₘ</a> <a id="1116" href="Graded.Context.Properties.html#1068" class="Generalizable">n</a>

<a id="1119" class="Comment">-- Context extension is monotone w.r.t the tail</a>
<a id="1167" class="Comment">-- If γ ≤ᶜ δ then γ ∙ p ≤ᶜ δ ∙ p</a>

<a id="∙-monotoneˡ"></a><a id="1201" href="Graded.Context.Properties.html#1201" class="Function">∙-monotoneˡ</a> <a id="1213" class="Symbol">:</a> <a id="1215" class="Symbol">{</a><a id="1216" href="Graded.Context.Properties.html#1216" class="Bound">γ</a> <a id="1218" href="Graded.Context.Properties.html#1218" class="Bound">δ</a> <a id="1220" class="Symbol">:</a> <a id="1222" href="Graded.Context.html#701" class="Datatype">Conₘ</a> <a id="1227" href="Graded.Context.Properties.html#1068" class="Generalizable">n</a><a id="1228" class="Symbol">}</a> <a id="1230" class="Symbol">{</a><a id="1231" href="Graded.Context.Properties.html#1231" class="Bound">p</a> <a id="1233" class="Symbol">:</a> <a id="1235" href="Graded.Context.Properties.html#261" class="Bound">M</a><a id="1236" class="Symbol">}</a> <a id="1238" class="Symbol">→</a> <a id="1240" href="Graded.Context.Properties.html#1216" class="Bound">γ</a> <a id="1242" href="Graded.Context.html#1864" class="Function Operator">≤ᶜ</a> <a id="1245" href="Graded.Context.Properties.html#1218" class="Bound">δ</a> <a id="1247" class="Symbol">→</a> <a id="1249" href="Graded.Context.Properties.html#1216" class="Bound">γ</a> <a id="1251" class="InductiveConstructor Operator">∙</a> <a id="1253" href="Graded.Context.Properties.html#1231" class="Bound">p</a> <a id="1255" href="Graded.Context.html#1864" class="Function Operator">≤ᶜ</a> <a id="1258" href="Graded.Context.Properties.html#1218" class="Bound">δ</a> <a id="1260" class="InductiveConstructor Operator">∙</a> <a id="1262" href="Graded.Context.Properties.html#1231" class="Bound">p</a>
<a id="1264" href="Graded.Context.Properties.html#1201" class="Function">∙-monotoneˡ</a> <a id="1276" href="Graded.Context.Properties.html#1276" class="Bound">γ≤δ</a> <a id="1280" class="Symbol">=</a> <a id="1282" href="Graded.Context.Properties.html#1276" class="Bound">γ≤δ</a> <a id="1286" href="Graded.Context.html#894" class="InductiveConstructor Operator">∙</a> <a id="1288" href="Graded.Modality.Properties.PartialOrder.html#573" class="Function">≤-refl</a>

<a id="1296" class="Comment">-- Context extension is monotone w.r.t the head</a>
<a id="1344" class="Comment">-- If p ≤ q then γ ∙ p ≤ᶜ γ ∙ q</a>

<a id="∙-monotoneʳ"></a><a id="1377" href="Graded.Context.Properties.html#1377" class="Function">∙-monotoneʳ</a> <a id="1389" class="Symbol">:</a> <a id="1391" class="Symbol">{</a><a id="1392" href="Graded.Context.Properties.html#1392" class="Bound">γ</a> <a id="1394" class="Symbol">:</a> <a id="1396" href="Graded.Context.html#701" class="Datatype">Conₘ</a> <a id="1401" href="Graded.Context.Properties.html#1068" class="Generalizable">n</a><a id="1402" class="Symbol">}</a> <a id="1404" class="Symbol">{</a><a id="1405" href="Graded.Context.Properties.html#1405" class="Bound">p</a> <a id="1407" href="Graded.Context.Properties.html#1407" class="Bound">q</a> <a id="1409" class="Symbol">:</a> <a id="1411" href="Graded.Context.Properties.html#261" class="Bound">M</a><a id="1412" class="Symbol">}</a> <a id="1414" class="Symbol">→</a> <a id="1416" href="Graded.Context.Properties.html#1405" class="Bound">p</a> <a id="1418" href="Graded.Modality.html#1271" class="Function Operator">≤</a> <a id="1420" href="Graded.Context.Properties.html#1407" class="Bound">q</a> <a id="1422" class="Symbol">→</a> <a id="1424" href="Graded.Context.Properties.html#1392" class="Bound">γ</a> <a id="1426" class="InductiveConstructor Operator">∙</a> <a id="1428" href="Graded.Context.Properties.html#1405" class="Bound">p</a> <a id="1430" href="Graded.Context.html#1864" class="Function Operator">≤ᶜ</a> <a id="1433" href="Graded.Context.Properties.html#1392" class="Bound">γ</a> <a id="1435" class="InductiveConstructor Operator">∙</a> <a id="1437" href="Graded.Context.Properties.html#1407" class="Bound">q</a>
<a id="1439" href="Graded.Context.Properties.html#1377" class="Function">∙-monotoneʳ</a> <a id="1451" href="Graded.Context.Properties.html#1451" class="Bound">p≤q</a> <a id="1455" class="Symbol">=</a> <a id="1457" href="Graded.Context.Properties.PartialOrder.html#620" class="Function">≤ᶜ-refl</a> <a id="1465" href="Graded.Context.html#894" class="InductiveConstructor Operator">∙</a> <a id="1467" href="Graded.Context.Properties.html#1451" class="Bound">p≤q</a>

<a id="1472" class="Comment">-- Context extension is monotone</a>
<a id="1505" class="Comment">-- If γ ≤ᶜ δ and p ≤ q then γ ∙ p ≤ᶜ δ ∙ q</a>

<a id="∙-monotone"></a><a id="1549" href="Graded.Context.Properties.html#1549" class="Function">∙-monotone</a> <a id="1560" class="Symbol">:</a> <a id="1562" class="Symbol">{</a><a id="1563" href="Graded.Context.Properties.html#1563" class="Bound">γ</a> <a id="1565" href="Graded.Context.Properties.html#1565" class="Bound">δ</a> <a id="1567" class="Symbol">:</a> <a id="1569" href="Graded.Context.html#701" class="Datatype">Conₘ</a> <a id="1574" href="Graded.Context.Properties.html#1068" class="Generalizable">n</a><a id="1575" class="Symbol">}</a> <a id="1577" class="Symbol">{</a><a id="1578" href="Graded.Context.Properties.html#1578" class="Bound">p</a> <a id="1580" href="Graded.Context.Properties.html#1580" class="Bound">q</a> <a id="1582" class="Symbol">:</a> <a id="1584" href="Graded.Context.Properties.html#261" class="Bound">M</a><a id="1585" class="Symbol">}</a> <a id="1587" class="Symbol">→</a> <a id="1589" href="Graded.Context.Properties.html#1563" class="Bound">γ</a> <a id="1591" href="Graded.Context.html#1864" class="Function Operator">≤ᶜ</a> <a id="1594" href="Graded.Context.Properties.html#1565" class="Bound">δ</a> <a id="1596" class="Symbol">→</a> <a id="1598" href="Graded.Context.Properties.html#1578" class="Bound">p</a> <a id="1600" href="Graded.Modality.html#1271" class="Function Operator">≤</a> <a id="1602" href="Graded.Context.Properties.html#1580" class="Bound">q</a> <a id="1604" class="Symbol">→</a> <a id="1606" href="Graded.Context.Properties.html#1563" class="Bound">γ</a> <a id="1608" class="InductiveConstructor Operator">∙</a> <a id="1610" href="Graded.Context.Properties.html#1578" class="Bound">p</a> <a id="1612" href="Graded.Context.html#1864" class="Function Operator">≤ᶜ</a> <a id="1615" href="Graded.Context.Properties.html#1565" class="Bound">δ</a> <a id="1617" class="InductiveConstructor Operator">∙</a> <a id="1619" href="Graded.Context.Properties.html#1580" class="Bound">q</a>
<a id="1621" href="Graded.Context.Properties.html#1549" class="Function">∙-monotone</a> <a id="1632" href="Graded.Context.Properties.html#1632" class="Bound">γ≤δ</a> <a id="1636" href="Graded.Context.Properties.html#1636" class="Bound">p≤q</a> <a id="1640" class="Symbol">=</a> <a id="1642" href="Graded.Context.Properties.PartialOrder.html#754" class="Function">≤ᶜ-trans</a> <a id="1651" class="Symbol">(</a><a id="1652" href="Graded.Context.Properties.html#1201" class="Function">∙-monotoneˡ</a> <a id="1664" href="Graded.Context.Properties.html#1632" class="Bound">γ≤δ</a><a id="1667" class="Symbol">)</a> <a id="1669" class="Symbol">(</a><a id="1670" href="Graded.Context.Properties.html#1377" class="Function">∙-monotoneʳ</a> <a id="1682" href="Graded.Context.Properties.html#1636" class="Bound">p≤q</a><a id="1685" class="Symbol">)</a>

<a id="1688" class="Comment">----------------------------------</a>
<a id="1723" class="Comment">-- Propeties of headₘ and tailₘ --</a>
<a id="1758" class="Comment">----------------------------------</a>

<a id="1794" class="Comment">-- tailₘ distributes over meet</a>
<a id="1825" class="Comment">-- tailₘ (γ ∧ᶜ δ) ≡ tailₘ γ ∧ᶜ tailₘ δ</a>

<a id="tailₘ-distrib-∧ᶜ"></a><a id="1865" href="Graded.Context.Properties.html#1865" class="Function">tailₘ-distrib-∧ᶜ</a> <a id="1882" class="Symbol">:</a> <a id="1884" class="Symbol">(</a><a id="1885" href="Graded.Context.Properties.html#1885" class="Bound">γ</a> <a id="1887" href="Graded.Context.Properties.html#1887" class="Bound">δ</a> <a id="1889" class="Symbol">:</a> <a id="1891" href="Graded.Context.html#701" class="Datatype">Conₘ</a> <a id="1896" class="Symbol">(</a><a id="1897" href="Tools.Nat.html#1071" class="InductiveConstructor">1+</a> <a id="1900" href="Graded.Context.Properties.html#1068" class="Generalizable">n</a><a id="1901" class="Symbol">))</a> <a id="1904" class="Symbol">→</a> <a id="1906" href="Graded.Context.html#1014" class="Function">tailₘ</a> <a id="1912" class="Symbol">(</a><a id="1913" href="Graded.Context.Properties.html#1885" class="Bound">γ</a> <a id="1915" href="Graded.Context.html#1630" class="Function Operator">∧ᶜ</a> <a id="1918" href="Graded.Context.Properties.html#1887" class="Bound">δ</a><a id="1919" class="Symbol">)</a> <a id="1921" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1923" class="Symbol">(</a><a id="1924" href="Graded.Context.html#1014" class="Function">tailₘ</a> <a id="1930" href="Graded.Context.Properties.html#1885" class="Bound">γ</a><a id="1931" class="Symbol">)</a> <a id="1933" href="Graded.Context.html#1630" class="Function Operator">∧ᶜ</a> <a id="1936" class="Symbol">(</a><a id="1937" href="Graded.Context.html#1014" class="Function">tailₘ</a> <a id="1943" href="Graded.Context.Properties.html#1887" class="Bound">δ</a><a id="1944" class="Symbol">)</a>
<a id="1946" href="Graded.Context.Properties.html#1865" class="Function">tailₘ-distrib-∧ᶜ</a> <a id="1963" class="Symbol">(</a><a id="1964" href="Graded.Context.html#728" class="InductiveConstructor">ε</a> <a id="1966" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="1968" href="Graded.Context.Properties.html#1968" class="Bound">p</a><a id="1969" class="Symbol">)</a> <a id="1971" class="Symbol">(</a><a id="1972" href="Graded.Context.html#728" class="InductiveConstructor">ε</a> <a id="1974" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="1976" href="Graded.Context.Properties.html#1976" class="Bound">q</a><a id="1977" class="Symbol">)</a> <a id="1979" class="Symbol">=</a> <a id="1981" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
<a id="1986" href="Graded.Context.Properties.html#1865" class="Function">tailₘ-distrib-∧ᶜ</a> <a id="2003" class="Symbol">(</a><a id="2004" href="Graded.Context.Properties.html#2004" class="Bound">γ</a> <a id="2006" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="2008" href="Graded.Context.Properties.html#2008" class="Bound">p′</a> <a id="2011" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="2013" href="Graded.Context.Properties.html#2013" class="Bound">p</a><a id="2014" class="Symbol">)</a> <a id="2016" class="Symbol">(</a><a id="2017" href="Graded.Context.Properties.html#2017" class="Bound">δ</a> <a id="2019" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="2021" href="Graded.Context.Properties.html#2021" class="Bound">q′</a> <a id="2024" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="2026" href="Graded.Context.Properties.html#2026" class="Bound">q</a><a id="2027" class="Symbol">)</a> <a id="2029" class="Symbol">=</a> <a id="2031" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="2036" class="Symbol">(</a><a id="2037" href="Graded.Context.html#743" class="InductiveConstructor Operator">_∙</a> <a id="2040" class="Symbol">_)</a> <a id="2043" class="Symbol">(</a><a id="2044" href="Graded.Context.Properties.html#1865" class="Function">tailₘ-distrib-∧ᶜ</a> <a id="2061" class="Symbol">(</a><a id="2062" href="Graded.Context.Properties.html#2004" class="Bound">γ</a> <a id="2064" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="2066" href="Graded.Context.Properties.html#2013" class="Bound">p</a><a id="2067" class="Symbol">)</a> <a id="2069" class="Symbol">(</a><a id="2070" href="Graded.Context.Properties.html#2017" class="Bound">δ</a> <a id="2072" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="2074" href="Graded.Context.Properties.html#2026" class="Bound">q</a><a id="2075" class="Symbol">))</a>

<a id="2079" class="Comment">-- headₘ distributes over meet</a>
<a id="2110" class="Comment">-- headₘ (γ ∧ᶜ δ) ≡ headₘ γ ∧ headₘ δ</a>

<a id="head-distrib-∧"></a><a id="2149" href="Graded.Context.Properties.html#2149" class="Function">head-distrib-∧</a> <a id="2164" class="Symbol">:</a> <a id="2166" class="Symbol">(</a><a id="2167" href="Graded.Context.Properties.html#2167" class="Bound">γ</a> <a id="2169" href="Graded.Context.Properties.html#2169" class="Bound">δ</a> <a id="2171" class="Symbol">:</a> <a id="2173" href="Graded.Context.html#701" class="Datatype">Conₘ</a> <a id="2178" class="Symbol">(</a><a id="2179" href="Tools.Nat.html#1071" class="InductiveConstructor">1+</a> <a id="2182" href="Graded.Context.Properties.html#1068" class="Generalizable">n</a><a id="2183" class="Symbol">))</a> <a id="2186" class="Symbol">→</a> <a id="2188" href="Graded.Context.html#965" class="Function">headₘ</a> <a id="2194" class="Symbol">(</a><a id="2195" href="Graded.Context.Properties.html#2167" class="Bound">γ</a> <a id="2197" href="Graded.Context.html#1630" class="Function Operator">∧ᶜ</a> <a id="2200" href="Graded.Context.Properties.html#2169" class="Bound">δ</a><a id="2201" class="Symbol">)</a> <a id="2203" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="2205" class="Symbol">(</a><a id="2206" href="Graded.Context.html#965" class="Function">headₘ</a> <a id="2212" href="Graded.Context.Properties.html#2167" class="Bound">γ</a><a id="2213" class="Symbol">)</a> <a id="2215" href="Graded.Modality.html#775" class="Function Operator">∧</a> <a id="2217" class="Symbol">(</a><a id="2218" href="Graded.Context.html#965" class="Function">headₘ</a> <a id="2224" href="Graded.Context.Properties.html#2169" class="Bound">δ</a><a id="2225" class="Symbol">)</a>
<a id="2227" href="Graded.Context.Properties.html#2149" class="Function">head-distrib-∧</a> <a id="2242" class="Symbol">(</a><a id="2243" href="Graded.Context.Properties.html#2243" class="Bound">γ</a> <a id="2245" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="2247" href="Graded.Context.Properties.html#2247" class="Bound">p</a><a id="2248" class="Symbol">)</a> <a id="2250" class="Symbol">(</a><a id="2251" href="Graded.Context.Properties.html#2251" class="Bound">δ</a> <a id="2253" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="2255" href="Graded.Context.Properties.html#2255" class="Bound">q</a><a id="2256" class="Symbol">)</a> <a id="2258" class="Symbol">=</a> <a id="2260" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>

<a id="2266" class="Comment">-- The headₘ and tailₘ functions correctly give the head and tail of the context</a>
<a id="2347" class="Comment">-- tailₘ γ ∙ headₘ γ ≡ γ</a>

<a id="headₘ-tailₘ-correct"></a><a id="2373" href="Graded.Context.Properties.html#2373" class="Function">headₘ-tailₘ-correct</a> <a id="2393" class="Symbol">:</a> <a id="2395" class="Symbol">(</a><a id="2396" href="Graded.Context.Properties.html#2396" class="Bound">γ</a> <a id="2398" class="Symbol">:</a> <a id="2400" href="Graded.Context.html#701" class="Datatype">Conₘ</a> <a id="2405" class="Symbol">(</a><a id="2406" href="Tools.Nat.html#1071" class="InductiveConstructor">1+</a> <a id="2409" href="Graded.Context.Properties.html#1068" class="Generalizable">n</a><a id="2410" class="Symbol">))</a> <a id="2413" class="Symbol">→</a> <a id="2415" href="Graded.Context.html#1014" class="Function">tailₘ</a> <a id="2421" href="Graded.Context.Properties.html#2396" class="Bound">γ</a> <a id="2423" class="InductiveConstructor Operator">∙</a> <a id="2425" href="Graded.Context.html#965" class="Function">headₘ</a> <a id="2431" href="Graded.Context.Properties.html#2396" class="Bound">γ</a> <a id="2433" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="2435" href="Graded.Context.Properties.html#2396" class="Bound">γ</a>
<a id="2437" href="Graded.Context.Properties.html#2373" class="Function">headₘ-tailₘ-correct</a> <a id="2457" class="Symbol">(</a><a id="2458" href="Graded.Context.Properties.html#2458" class="Bound">γ</a> <a id="2460" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="2462" href="Graded.Context.Properties.html#2462" class="Bound">p</a><a id="2463" class="Symbol">)</a> <a id="2465" class="Symbol">=</a> <a id="2467" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>

<a id="2473" class="Comment">-- Congruence of tailₘ</a>
<a id="2496" class="Comment">-- If γ ≈ᶜ δ then tailₘ γ ≈ᶜ tailₘ δ</a>

<a id="tailₘ-cong"></a><a id="2534" href="Graded.Context.Properties.html#2534" class="Function">tailₘ-cong</a> <a id="2545" class="Symbol">:</a> <a id="2547" class="Symbol">{</a><a id="2548" href="Graded.Context.Properties.html#2548" class="Bound">γ</a> <a id="2550" href="Graded.Context.Properties.html#2550" class="Bound">δ</a> <a id="2552" class="Symbol">:</a> <a id="2554" href="Graded.Context.html#701" class="Datatype">Conₘ</a> <a id="2559" class="Symbol">(</a><a id="2560" href="Tools.Nat.html#1071" class="InductiveConstructor">1+</a> <a id="2563" href="Graded.Context.Properties.html#1068" class="Generalizable">n</a><a id="2564" class="Symbol">)}</a> <a id="2567" class="Symbol">→</a> <a id="2569" href="Graded.Context.Properties.html#2548" class="Bound">γ</a> <a id="2571" href="Graded.Context.html#843" class="Datatype Operator">≈ᶜ</a> <a id="2574" href="Graded.Context.Properties.html#2550" class="Bound">δ</a> <a id="2576" class="Symbol">→</a> <a id="2578" href="Graded.Context.html#1014" class="Function">tailₘ</a> <a id="2584" href="Graded.Context.Properties.html#2548" class="Bound">γ</a> <a id="2586" href="Graded.Context.html#843" class="Datatype Operator">≈ᶜ</a> <a id="2589" href="Graded.Context.html#1014" class="Function">tailₘ</a> <a id="2595" href="Graded.Context.Properties.html#2550" class="Bound">δ</a>
<a id="2597" href="Graded.Context.Properties.html#2534" class="Function">tailₘ-cong</a> <a id="2608" class="Symbol">(</a><a id="2609" href="Graded.Context.Properties.html#2609" class="Bound">γ≈ᶜδ</a> <a id="2614" href="Graded.Context.html#894" class="InductiveConstructor Operator">∙</a> <a id="2616" class="Symbol">_)</a> <a id="2619" class="Symbol">=</a> <a id="2621" href="Graded.Context.Properties.html#2609" class="Bound">γ≈ᶜδ</a>

<a id="2627" class="Comment">-- Congruence for headₘ.</a>

<a id="headₘ-cong"></a><a id="2653" href="Graded.Context.Properties.html#2653" class="Function">headₘ-cong</a> <a id="2664" class="Symbol">:</a> <a id="2666" class="Symbol">{</a><a id="2667" href="Graded.Context.Properties.html#2667" class="Bound">γ</a> <a id="2669" href="Graded.Context.Properties.html#2669" class="Bound">δ</a> <a id="2671" class="Symbol">:</a> <a id="2673" href="Graded.Context.html#701" class="Datatype">Conₘ</a> <a id="2678" class="Symbol">(</a><a id="2679" href="Tools.Nat.html#1071" class="InductiveConstructor">1+</a> <a id="2682" href="Graded.Context.Properties.html#1068" class="Generalizable">n</a><a id="2683" class="Symbol">)}</a> <a id="2686" class="Symbol">→</a> <a id="2688" href="Graded.Context.Properties.html#2667" class="Bound">γ</a> <a id="2690" href="Graded.Context.html#843" class="Datatype Operator">≈ᶜ</a> <a id="2693" href="Graded.Context.Properties.html#2669" class="Bound">δ</a> <a id="2695" class="Symbol">→</a> <a id="2697" href="Graded.Context.html#965" class="Function">headₘ</a> <a id="2703" href="Graded.Context.Properties.html#2667" class="Bound">γ</a> <a id="2705" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="2707" href="Graded.Context.html#965" class="Function">headₘ</a> <a id="2713" href="Graded.Context.Properties.html#2669" class="Bound">δ</a>
<a id="2715" href="Graded.Context.Properties.html#2653" class="Function">headₘ-cong</a> <a id="2726" class="Symbol">(_</a> <a id="2729" href="Graded.Context.html#894" class="InductiveConstructor Operator">∙</a> <a id="2731" href="Graded.Context.Properties.html#2731" class="Bound">p≡q</a><a id="2734" class="Symbol">)</a> <a id="2736" class="Symbol">=</a> <a id="2738" href="Graded.Context.Properties.html#2731" class="Bound">p≡q</a>

<a id="2743" class="Comment">-- tailₘ is monotone</a>
<a id="2764" class="Comment">-- If γ ≤ᶜ δ then tailₘ γ ≤ᶜ tailₘ δ</a>

<a id="tailₘ-monotone"></a><a id="2802" href="Graded.Context.Properties.html#2802" class="Function">tailₘ-monotone</a> <a id="2817" class="Symbol">:</a> <a id="2819" class="Symbol">{</a><a id="2820" href="Graded.Context.Properties.html#2820" class="Bound">γ</a> <a id="2822" href="Graded.Context.Properties.html#2822" class="Bound">δ</a> <a id="2824" class="Symbol">:</a> <a id="2826" href="Graded.Context.html#701" class="Datatype">Conₘ</a> <a id="2831" class="Symbol">(</a><a id="2832" href="Tools.Nat.html#1071" class="InductiveConstructor">1+</a> <a id="2835" href="Graded.Context.Properties.html#1068" class="Generalizable">n</a><a id="2836" class="Symbol">)}</a> <a id="2839" class="Symbol">→</a> <a id="2841" href="Graded.Context.Properties.html#2820" class="Bound">γ</a> <a id="2843" href="Graded.Context.html#1864" class="Function Operator">≤ᶜ</a> <a id="2846" href="Graded.Context.Properties.html#2822" class="Bound">δ</a> <a id="2848" class="Symbol">→</a> <a id="2850" href="Graded.Context.html#1014" class="Function">tailₘ</a> <a id="2856" href="Graded.Context.Properties.html#2820" class="Bound">γ</a> <a id="2858" href="Graded.Context.html#1864" class="Function Operator">≤ᶜ</a> <a id="2861" href="Graded.Context.html#1014" class="Function">tailₘ</a> <a id="2867" href="Graded.Context.Properties.html#2822" class="Bound">δ</a>
<a id="2869" href="Graded.Context.Properties.html#2802" class="Function">tailₘ-monotone</a> <a id="2884" class="Symbol">{</a><a id="2885" class="Argument">γ</a> <a id="2887" class="Symbol">=</a> <a id="2889" href="Graded.Context.Properties.html#2889" class="Bound">γ</a> <a id="2891" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="2893" href="Graded.Context.Properties.html#2893" class="Bound">p</a><a id="2894" class="Symbol">}</a> <a id="2896" class="Symbol">{</a><a id="2897" href="Graded.Context.Properties.html#2897" class="Bound">δ</a> <a id="2899" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="2901" href="Graded.Context.Properties.html#2901" class="Bound">q</a><a id="2902" class="Symbol">}</a> <a id="2904" class="Symbol">(</a><a id="2905" href="Graded.Context.Properties.html#2905" class="Bound">γ≤δ</a> <a id="2909" href="Graded.Context.html#894" class="InductiveConstructor Operator">∙</a> <a id="2911" href="Graded.Context.Properties.html#2911" class="Bound">p≤q</a><a id="2914" class="Symbol">)</a> <a id="2916" class="Symbol">=</a> <a id="2918" href="Graded.Context.Properties.html#2905" class="Bound">γ≤δ</a>

<a id="2923" class="Comment">-- headₘ is monotone</a>
<a id="2944" class="Comment">-- If γ ≤ᶜ δ then headₘ γ ≤ᶜ headₘ δ</a>

<a id="headₘ-monotone"></a><a id="2982" href="Graded.Context.Properties.html#2982" class="Function">headₘ-monotone</a> <a id="2997" class="Symbol">:</a> <a id="2999" class="Symbol">{</a><a id="3000" href="Graded.Context.Properties.html#3000" class="Bound">γ</a> <a id="3002" href="Graded.Context.Properties.html#3002" class="Bound">δ</a> <a id="3004" class="Symbol">:</a> <a id="3006" href="Graded.Context.html#701" class="Datatype">Conₘ</a> <a id="3011" class="Symbol">(</a><a id="3012" href="Tools.Nat.html#1071" class="InductiveConstructor">1+</a> <a id="3015" href="Graded.Context.Properties.html#1068" class="Generalizable">n</a><a id="3016" class="Symbol">)}</a> <a id="3019" class="Symbol">→</a> <a id="3021" href="Graded.Context.Properties.html#3000" class="Bound">γ</a> <a id="3023" href="Graded.Context.html#1864" class="Function Operator">≤ᶜ</a> <a id="3026" href="Graded.Context.Properties.html#3002" class="Bound">δ</a> <a id="3028" class="Symbol">→</a> <a id="3030" href="Graded.Context.html#965" class="Function">headₘ</a> <a id="3036" href="Graded.Context.Properties.html#3000" class="Bound">γ</a> <a id="3038" href="Graded.Modality.html#1271" class="Function Operator">≤</a> <a id="3040" href="Graded.Context.html#965" class="Function">headₘ</a> <a id="3046" href="Graded.Context.Properties.html#3002" class="Bound">δ</a>
<a id="3048" href="Graded.Context.Properties.html#2982" class="Function">headₘ-monotone</a> <a id="3063" class="Symbol">{</a><a id="3064" class="Argument">γ</a> <a id="3066" class="Symbol">=</a> <a id="3068" href="Graded.Context.Properties.html#3068" class="Bound">γ</a> <a id="3070" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="3072" href="Graded.Context.Properties.html#3072" class="Bound">p</a><a id="3073" class="Symbol">}</a> <a id="3075" class="Symbol">{</a><a id="3076" href="Graded.Context.Properties.html#3076" class="Bound">δ</a> <a id="3078" href="Graded.Context.html#743" class="InductiveConstructor Operator">∙</a> <a id="3080" href="Graded.Context.Properties.html#3080" class="Bound">q</a><a id="3081" class="Symbol">}</a> <a id="3083" class="Symbol">(</a><a id="3084" href="Graded.Context.Properties.html#3084" class="Bound">γ≤δ</a> <a id="3088" href="Graded.Context.html#894" class="InductiveConstructor Operator">∙</a> <a id="3090" href="Graded.Context.Properties.html#3090" class="Bound">p≤q</a><a id="3093" class="Symbol">)</a> <a id="3095" class="Symbol">=</a> <a id="3097" href="Graded.Context.Properties.html#3090" class="Bound">p≤q</a>

<a id="3102" class="Comment">------------------------------------------------------------------------</a>
<a id="3175" class="Comment">-- Properties that hold if 𝟙 ≡ 𝟘</a>

<a id="3209" class="Comment">-- If 𝟙 ≡ 𝟘, then every vector is equal to 𝟘ᶜ.</a>

<a id="≈ᶜ𝟘ᶜ"></a><a id="3257" href="Graded.Context.Properties.html#3257" class="Function">≈ᶜ𝟘ᶜ</a> <a id="3262" class="Symbol">:</a> <a id="3264" href="Graded.Modality.html#846" class="Function">𝟙</a> <a id="3266" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="3268" href="Graded.Modality.html#836" class="Function">𝟘</a> <a id="3270" class="Symbol">→</a> <a id="3272" href="Graded.Context.Properties.html#1097" class="Generalizable">γ</a> <a id="3274" href="Graded.Context.html#843" class="Datatype Operator">≈ᶜ</a> <a id="3277" href="Graded.Context.html#2211" class="Function">𝟘ᶜ</a>
<a id="3280" href="Graded.Context.Properties.html#3257" class="Function">≈ᶜ𝟘ᶜ</a> <a id="3285" class="Symbol">{</a><a id="3286" class="Argument">γ</a> <a id="3288" class="Symbol">=</a> <a id="3290" href="Graded.Context.Properties.html#3290" class="Bound">γ</a><a id="3291" class="Symbol">}</a> <a id="3293" href="Graded.Context.Properties.html#3293" class="Bound">𝟙≡𝟘</a> <a id="3297" class="Symbol">=</a> <a id="3299" href="Relation.Binary.Reasoning.Base.Double.html#2213" class="Function Operator">begin</a>
  <a id="3307" href="Graded.Context.Properties.html#3290" class="Bound">γ</a>       <a id="3315" href="Relation.Binary.Reasoning.Base.Double.html#2963" class="Function">≈˘⟨</a> <a id="3319" href="Graded.Context.Properties.Multiplication.html#701" class="Function">·ᶜ-identityˡ</a> <a id="3332" class="Symbol">_</a> <a id="3334" href="Relation.Binary.Reasoning.Base.Double.html#2963" class="Function">⟩</a>
  <a id="3338" href="Graded.Modality.html#846" class="Function">𝟙</a> <a id="3340" href="Graded.Context.html#1742" class="Function Operator">·ᶜ</a> <a id="3343" href="Graded.Context.Properties.html#3290" class="Bound">γ</a>  <a id="3346" href="Relation.Binary.Reasoning.Base.Double.html#2721" class="Function">≈⟨</a> <a id="3349" href="Graded.Context.Properties.Multiplication.html#3002" class="Function">·ᶜ-congʳ</a> <a id="3358" href="Graded.Context.Properties.html#3293" class="Bound">𝟙≡𝟘</a> <a id="3362" href="Relation.Binary.Reasoning.Base.Double.html#2721" class="Function">⟩</a>
  <a id="3366" href="Graded.Modality.html#836" class="Function">𝟘</a> <a id="3368" href="Graded.Context.html#1742" class="Function Operator">·ᶜ</a> <a id="3371" href="Graded.Context.Properties.html#3290" class="Bound">γ</a>  <a id="3374" href="Relation.Binary.Reasoning.Base.Double.html#2721" class="Function">≈⟨</a> <a id="3377" href="Graded.Context.Properties.Multiplication.html#898" class="Function">·ᶜ-zeroˡ</a> <a id="3386" class="Symbol">_</a> <a id="3388" href="Relation.Binary.Reasoning.Base.Double.html#2721" class="Function">⟩</a>
  <a id="3392" href="Graded.Context.html#2211" class="Function">𝟘ᶜ</a>      <a id="3400" href="Relation.Binary.Reasoning.Base.Double.html#3647" class="Function Operator">∎</a>
  <a id="3404" class="Keyword">where</a>
  <a id="3412" class="Keyword">open</a> <a id="3417" href="Tools.Reasoning.Equivalence.html" class="Module">Tools.Reasoning.Equivalence</a> <a id="3445" href="Graded.Context.Properties.Equivalence.html#1193" class="Function">Conₘ-setoid</a>

<a id="3458" class="Comment">-- If 𝟙 ≡ 𝟘, then _≈ᶜ_ is trivial.</a>

<a id="≈ᶜ-trivial"></a><a id="3494" href="Graded.Context.Properties.html#3494" class="Function">≈ᶜ-trivial</a> <a id="3505" class="Symbol">:</a> <a id="3507" href="Graded.Modality.html#846" class="Function">𝟙</a> <a id="3509" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="3511" href="Graded.Modality.html#836" class="Function">𝟘</a> <a id="3513" class="Symbol">→</a> <a id="3515" href="Graded.Context.Properties.html#1097" class="Generalizable">γ</a> <a id="3517" href="Graded.Context.html#843" class="Datatype Operator">≈ᶜ</a> <a id="3520" href="Graded.Context.Properties.html#1102" class="Generalizable">δ</a>
<a id="3522" href="Graded.Context.Properties.html#3494" class="Function">≈ᶜ-trivial</a> <a id="3533" class="Symbol">{</a><a id="3534" class="Argument">γ</a> <a id="3536" class="Symbol">=</a> <a id="3538" href="Graded.Context.Properties.html#3538" class="Bound">γ</a><a id="3539" class="Symbol">}</a> <a id="3541" class="Symbol">{</a><a id="3542" class="Argument">δ</a> <a id="3544" class="Symbol">=</a> <a id="3546" href="Graded.Context.Properties.html#3546" class="Bound">δ</a><a id="3547" class="Symbol">}</a> <a id="3549" href="Graded.Context.Properties.html#3549" class="Bound">𝟙≡𝟘</a> <a id="3553" class="Symbol">=</a> <a id="3555" href="Relation.Binary.Reasoning.Base.Double.html#2213" class="Function Operator">begin</a>
  <a id="3563" href="Graded.Context.Properties.html#3538" class="Bound">γ</a>   <a id="3567" href="Relation.Binary.Reasoning.Base.Double.html#2721" class="Function">≈⟨</a> <a id="3570" href="Graded.Context.Properties.html#3257" class="Function">≈ᶜ𝟘ᶜ</a> <a id="3575" href="Graded.Context.Properties.html#3549" class="Bound">𝟙≡𝟘</a> <a id="3579" href="Relation.Binary.Reasoning.Base.Double.html#2721" class="Function">⟩</a>
  <a id="3583" href="Graded.Context.html#2211" class="Function">𝟘ᶜ</a>  <a id="3587" href="Relation.Binary.Reasoning.Base.Double.html#2963" class="Function">≈˘⟨</a> <a id="3591" href="Graded.Context.Properties.html#3257" class="Function">≈ᶜ𝟘ᶜ</a> <a id="3596" href="Graded.Context.Properties.html#3549" class="Bound">𝟙≡𝟘</a> <a id="3600" href="Relation.Binary.Reasoning.Base.Double.html#2963" class="Function">⟩</a>
  <a id="3604" href="Graded.Context.Properties.html#3546" class="Bound">δ</a>   <a id="3608" href="Relation.Binary.Reasoning.Base.Double.html#3647" class="Function Operator">∎</a>
  <a id="3612" class="Keyword">where</a>
  <a id="3620" class="Keyword">open</a> <a id="3625" href="Tools.Reasoning.Equivalence.html" class="Module">Tools.Reasoning.Equivalence</a> <a id="3653" href="Graded.Context.Properties.Equivalence.html#1193" class="Function">Conₘ-setoid</a>

<a id="3666" class="Comment">-- Contexts form a preleft semimodule</a>

<a id="Conₘ-preSemimodule"></a><a id="3705" href="Graded.Context.Properties.html#3705" class="Function">Conₘ-preSemimodule</a> <a id="3724" class="Symbol">:</a> <a id="3726" class="Symbol">∀</a> <a id="3728" class="Symbol">{</a><a id="3729" href="Graded.Context.Properties.html#3729" class="Bound">n</a><a id="3730" class="Symbol">}</a> <a id="3732" class="Symbol">→</a> <a id="3734" href="Algebra.Module.Structures.html#837" class="Record">IsPreleftSemimodule</a> <a id="3754" href="Graded.Modality.html#1709" class="Function">+-·-Semiring′</a> <a id="3768" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a> <a id="3772" href="Graded.Context.html#1510" class="Function Operator">_+ᶜ_</a> <a id="3777" class="Symbol">(</a><a id="3778" href="Graded.Context.html#2211" class="Function">𝟘ᶜ</a> <a id="3781" class="Symbol">{</a><a id="3782" href="Graded.Context.Properties.html#3729" class="Bound">n</a><a id="3783" class="Symbol">})</a> <a id="3786" href="Graded.Context.html#1742" class="Function Operator">_·ᶜ_</a>
<a id="3791" href="Graded.Context.Properties.html#3705" class="Function">Conₘ-preSemimodule</a> <a id="3810" class="Symbol">=</a> <a id="3812" class="Keyword">record</a>
  <a id="3821" class="Symbol">{</a> <a id="3823" href="Algebra.Module.Structures.html#941" class="Field">*ₗ-cong</a> <a id="3831" class="Symbol">=</a> <a id="3833" href="Relation.Binary.PropositionalEquality.Core.html#1367" class="Function">cong₂</a> <a id="3839" href="Graded.Context.html#1742" class="Function Operator">_·ᶜ_</a>
  <a id="3846" class="Symbol">;</a> <a id="3848" href="Algebra.Module.Structures.html#974" class="Field">*ₗ-zeroˡ</a> <a id="3857" class="Symbol">=</a> <a id="3859" class="Symbol">λ</a> <a id="3861" href="Graded.Context.Properties.html#3861" class="Bound">γ</a> <a id="3863" class="Symbol">→</a> <a id="3865" href="Graded.Context.Properties.Equivalence.html#1360" class="Function">≈ᶜ→≡</a> <a id="3870" class="Symbol">(</a><a id="3871" href="Graded.Context.Properties.Multiplication.html#898" class="Function">·ᶜ-zeroˡ</a> <a id="3880" href="Graded.Context.Properties.html#3861" class="Bound">γ</a><a id="3881" class="Symbol">)</a>
  <a id="3885" class="Symbol">;</a> <a id="3887" href="Algebra.Module.Structures.html#1009" class="Field">*ₗ-distribʳ</a> <a id="3899" class="Symbol">=</a> <a id="3901" class="Symbol">λ</a> <a id="3903" href="Graded.Context.Properties.html#3903" class="Bound">γ</a> <a id="3905" href="Graded.Context.Properties.html#3905" class="Bound">p</a> <a id="3907" href="Graded.Context.Properties.html#3907" class="Bound">q</a> <a id="3909" class="Symbol">→</a> <a id="3911" href="Graded.Context.Properties.Equivalence.html#1360" class="Function">≈ᶜ→≡</a> <a id="3916" class="Symbol">(</a><a id="3917" href="Graded.Context.Properties.Multiplication.html#1881" class="Function">·ᶜ-distribʳ-+ᶜ</a> <a id="3932" href="Graded.Context.Properties.html#3905" class="Bound">p</a> <a id="3934" href="Graded.Context.Properties.html#3907" class="Bound">q</a> <a id="3936" href="Graded.Context.Properties.html#3903" class="Bound">γ</a><a id="3937" class="Symbol">)</a>
  <a id="3941" class="Symbol">;</a> <a id="3943" href="Algebra.Module.Structures.html#1058" class="Field">*ₗ-identityˡ</a> <a id="3956" class="Symbol">=</a> <a id="3958" class="Symbol">λ</a> <a id="3960" href="Graded.Context.Properties.html#3960" class="Bound">γ</a> <a id="3962" class="Symbol">→</a> <a id="3964" href="Graded.Context.Properties.Equivalence.html#1360" class="Function">≈ᶜ→≡</a> <a id="3969" class="Symbol">(</a><a id="3970" href="Graded.Context.Properties.Multiplication.html#701" class="Function">·ᶜ-identityˡ</a> <a id="3983" href="Graded.Context.Properties.html#3960" class="Bound">γ</a><a id="3984" class="Symbol">)</a>
  <a id="3988" class="Symbol">;</a> <a id="3990" href="Algebra.Module.Structures.html#1098" class="Field">*ₗ-assoc</a> <a id="3999" class="Symbol">=</a> <a id="4001" class="Symbol">λ</a> <a id="4003" href="Graded.Context.Properties.html#4003" class="Bound">p</a> <a id="4005" href="Graded.Context.Properties.html#4005" class="Bound">q</a> <a id="4007" href="Graded.Context.Properties.html#4007" class="Bound">γ</a> <a id="4009" class="Symbol">→</a> <a id="4011" href="Graded.Context.Properties.Equivalence.html#1360" class="Function">≈ᶜ→≡</a> <a id="4016" class="Symbol">(</a><a id="4017" href="Graded.Context.Properties.Multiplication.html#1305" class="Function">·ᶜ-assoc</a> <a id="4026" href="Graded.Context.Properties.html#4003" class="Bound">p</a> <a id="4028" href="Graded.Context.Properties.html#4005" class="Bound">q</a> <a id="4030" href="Graded.Context.Properties.html#4007" class="Bound">γ</a><a id="4031" class="Symbol">)</a>
  <a id="4035" class="Symbol">;</a> <a id="4037" href="Algebra.Module.Structures.html#1134" class="Field">*ₗ-zeroʳ</a> <a id="4046" class="Symbol">=</a> <a id="4048" class="Symbol">λ</a> <a id="4050" href="Graded.Context.Properties.html#4050" class="Bound">p</a> <a id="4052" class="Symbol">→</a> <a id="4054" href="Graded.Context.Properties.Equivalence.html#1360" class="Function">≈ᶜ→≡</a> <a id="4059" class="Symbol">(</a><a id="4060" href="Graded.Context.Properties.Multiplication.html#1091" class="Function">·ᶜ-zeroʳ</a> <a id="4069" href="Graded.Context.Properties.html#4050" class="Bound">p</a><a id="4070" class="Symbol">)</a>
  <a id="4074" class="Symbol">;</a> <a id="4076" href="Algebra.Module.Structures.html#1167" class="Field">*ₗ-distribˡ</a> <a id="4088" class="Symbol">=</a> <a id="4090" class="Symbol">λ</a> <a id="4092" href="Graded.Context.Properties.html#4092" class="Bound">p</a> <a id="4094" href="Graded.Context.Properties.html#4094" class="Bound">γ</a> <a id="4096" href="Graded.Context.Properties.html#4096" class="Bound">δ</a> <a id="4098" class="Symbol">→</a> <a id="4100" href="Graded.Context.Properties.Equivalence.html#1360" class="Function">≈ᶜ→≡</a> <a id="4105" class="Symbol">(</a><a id="4106" href="Graded.Context.Properties.Multiplication.html#1567" class="Function">·ᶜ-distribˡ-+ᶜ</a> <a id="4121" href="Graded.Context.Properties.html#4092" class="Bound">p</a> <a id="4123" href="Graded.Context.Properties.html#4094" class="Bound">γ</a> <a id="4125" href="Graded.Context.Properties.html#4096" class="Bound">δ</a><a id="4126" class="Symbol">)</a>
  <a id="4130" class="Symbol">}</a>

<a id="4133" class="Comment">-- Contexts form a left semimodule</a>

<a id="Conₘ-semimodule"></a><a id="4169" href="Graded.Context.Properties.html#4169" class="Function">Conₘ-semimodule</a> <a id="4185" class="Symbol">:</a> <a id="4187" class="Symbol">∀</a> <a id="4189" class="Symbol">{</a><a id="4190" href="Graded.Context.Properties.html#4190" class="Bound">n</a><a id="4191" class="Symbol">}</a> <a id="4193" class="Symbol">→</a> <a id="4195" href="Algebra.Module.Structures.html#1214" class="Record">IsLeftSemimodule</a> <a id="4212" href="Graded.Modality.html#1709" class="Function">+-·-Semiring′</a> <a id="4226" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a> <a id="4230" href="Graded.Context.html#1510" class="Function Operator">_+ᶜ_</a> <a id="4235" class="Symbol">(</a><a id="4236" href="Graded.Context.html#2211" class="Function">𝟘ᶜ</a> <a id="4239" class="Symbol">{</a><a id="4240" href="Graded.Context.Properties.html#4190" class="Bound">n</a><a id="4241" class="Symbol">})</a> <a id="4244" href="Graded.Context.html#1742" class="Function Operator">_·ᶜ_</a>
<a id="4249" href="Graded.Context.Properties.html#4169" class="Function">Conₘ-semimodule</a> <a id="4265" class="Symbol">=</a> <a id="4267" class="Keyword">record</a>
  <a id="4276" class="Symbol">{</a> <a id="4278" href="Algebra.Module.Structures.html#1315" class="Field">+ᴹ-isCommutativeMonoid</a> <a id="4301" class="Symbol">=</a> <a id="4303" href="Graded.Context.Properties.Addition.html#3592" class="Function">+ᶜ-commutativeMonoid</a>
  <a id="4326" class="Symbol">;</a> <a id="4328" href="Algebra.Module.Structures.html#1375" class="Field">isPreleftSemimodule</a> <a id="4348" class="Symbol">=</a> <a id="4350" href="Graded.Context.Properties.html#3705" class="Function">Conₘ-preSemimodule</a>
  <a id="4371" class="Symbol">}</a>
</pre></body></html>