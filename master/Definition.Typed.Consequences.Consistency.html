<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Definition.Typed.Consequences.Consistency</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- Consistency of equality of natural numbers.</a>
<a id="121" class="Comment">------------------------------------------------------------------------</a>

<a id="195" class="Keyword">open</a> <a id="200" class="Keyword">import</a> <a id="207" href="Definition.Typed.Restrictions.html" class="Module">Definition.Typed.Restrictions</a>
<a id="237" class="Keyword">open</a> <a id="242" class="Keyword">import</a> <a id="249" href="Graded.Modality.html" class="Module">Graded.Modality</a>

<a id="266" class="Keyword">module</a> <a id="273" href="Definition.Typed.Consequences.Consistency.html" class="Module">Definition.Typed.Consequences.Consistency</a>
  <a id="317" class="Symbol">{</a><a id="318" href="Definition.Typed.Consequences.Consistency.html#318" class="Bound">a</a><a id="319" class="Symbol">}</a> <a id="321" class="Symbol">{</a><a id="322" href="Definition.Typed.Consequences.Consistency.html#322" class="Bound">M</a> <a id="324" class="Symbol">:</a> <a id="326" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="330" href="Definition.Typed.Consequences.Consistency.html#318" class="Bound">a</a><a id="331" class="Symbol">}</a>
  <a id="335" class="Symbol">{</a><a id="336" href="Definition.Typed.Consequences.Consistency.html#336" class="Bound">ùïÑ</a> <a id="338" class="Symbol">:</a> <a id="340" href="Graded.Modality.html#8202" class="Record">Modality</a> <a id="349" href="Definition.Typed.Consequences.Consistency.html#322" class="Bound">M</a><a id="350" class="Symbol">}</a>
  <a id="354" class="Symbol">(</a><a id="355" href="Definition.Typed.Consequences.Consistency.html#355" class="Bound">R</a> <a id="357" class="Symbol">:</a> <a id="359" href="Definition.Typed.Restrictions.html#831" class="Record">Type-restrictions</a> <a id="377" href="Definition.Typed.Consequences.Consistency.html#336" class="Bound">ùïÑ</a><a id="378" class="Symbol">)</a>
  <a id="382" class="Keyword">where</a>

<a id="389" class="Keyword">open</a> <a id="394" href="Definition.Typed.Restrictions.html#831" class="Module">Type-restrictions</a> <a id="412" href="Definition.Typed.Consequences.Consistency.html#355" class="Bound">R</a>

<a id="415" class="Keyword">open</a> <a id="420" class="Keyword">import</a> <a id="427" href="Definition.Untyped.html" class="Module">Definition.Untyped</a> <a id="446" href="Definition.Typed.Consequences.Consistency.html#322" class="Bound">M</a>
<a id="448" class="Keyword">open</a> <a id="453" class="Keyword">import</a> <a id="460" href="Definition.Typed.html" class="Module">Definition.Typed</a> <a id="477" href="Definition.Typed.Consequences.Consistency.html#355" class="Bound">R</a>
<a id="479" class="Keyword">open</a> <a id="484" class="Keyword">import</a> <a id="491" href="Definition.Typed.Consequences.Canonicity.html" class="Module">Definition.Typed.Consequences.Canonicity</a> <a id="532" href="Definition.Typed.Consequences.Consistency.html#355" class="Bound">R</a>
<a id="534" class="Keyword">open</a> <a id="539" class="Keyword">import</a> <a id="546" href="Definition.Typed.EqRelInstance.html" class="Module">Definition.Typed.EqRelInstance</a> <a id="577" href="Definition.Typed.Consequences.Consistency.html#355" class="Bound">R</a>
<a id="579" class="Keyword">open</a> <a id="584" class="Keyword">import</a> <a id="591" href="Definition.Typed.Properties.html" class="Module">Definition.Typed.Properties</a> <a id="619" href="Definition.Typed.Consequences.Consistency.html#355" class="Bound">R</a>
<a id="621" class="Keyword">open</a> <a id="626" class="Keyword">import</a> <a id="633" href="Definition.Typed.Substitution.html" class="Module">Definition.Typed.Substitution</a> <a id="663" href="Definition.Typed.Consequences.Consistency.html#355" class="Bound">R</a>
<a id="665" class="Keyword">open</a> <a id="670" class="Keyword">import</a> <a id="677" href="Definition.LogicalRelation.html" class="Module">Definition.LogicalRelation</a> <a id="704" href="Definition.Typed.Consequences.Consistency.html#355" class="Bound">R</a>
<a id="706" class="Keyword">open</a> <a id="711" class="Keyword">import</a> <a id="718" href="Definition.LogicalRelation.Hidden.html" class="Module">Definition.LogicalRelation.Hidden</a> <a id="752" href="Definition.Typed.Consequences.Consistency.html#355" class="Bound">R</a>
<a id="754" class="Keyword">open</a> <a id="759" class="Keyword">import</a> <a id="766" href="Definition.LogicalRelation.Substitution.Introductions.html" class="Module">Definition.LogicalRelation.Substitution.Introductions</a> <a id="820" href="Definition.Typed.Consequences.Consistency.html#355" class="Bound">R</a>
<a id="822" class="Keyword">open</a> <a id="827" class="Keyword">import</a> <a id="834" href="Definition.LogicalRelation.Fundamental.Reducibility.html" class="Module">Definition.LogicalRelation.Fundamental.Reducibility</a> <a id="886" href="Definition.Typed.Consequences.Consistency.html#355" class="Bound">R</a>

<a id="889" class="Keyword">open</a> <a id="894" class="Keyword">import</a> <a id="901" href="Tools.Empty.html" class="Module">Tools.Empty</a>
<a id="913" class="Keyword">open</a> <a id="918" class="Keyword">import</a> <a id="925" href="Tools.Function.html" class="Module">Tools.Function</a>
<a id="940" class="Keyword">open</a> <a id="945" class="Keyword">import</a> <a id="952" href="Tools.Nat.html" class="Module">Tools.Nat</a>
<a id="962" class="Keyword">open</a> <a id="967" class="Keyword">import</a> <a id="974" href="Tools.Product.html" class="Module">Tools.Product</a>
<a id="988" class="Keyword">open</a> <a id="993" class="Keyword">import</a> <a id="1000" href="Tools.Relation.html" class="Module">Tools.Relation</a>

<a id="1016" class="Keyword">private</a>
  <a id="1026" class="Keyword">variable</a>
    <a id="1039" href="Definition.Typed.Consequences.Consistency.html#1039" class="Generalizable">n</a>   <a id="1043" class="Symbol">:</a> <a id="1045" href="Agda.Builtin.Nat.html#203" class="Datatype">Nat</a>
    <a id="1053" href="Definition.Typed.Consequences.Consistency.html#1053" class="Generalizable">Œì</a>   <a id="1057" class="Symbol">:</a> <a id="1059" href="Definition.Untyped.NotParametrised.html#882" class="Datatype">Con</a> <a id="1063" href="Definition.Untyped.html#1181" class="Datatype">Term</a> <a id="1068" href="Definition.Typed.Consequences.Consistency.html#1039" class="Generalizable">n</a>
    <a id="1074" href="Definition.Typed.Consequences.Consistency.html#1074" class="Generalizable">œÉ</a>   <a id="1078" class="Symbol">:</a> <a id="1080" href="Definition.Untyped.html#11546" class="Function">Subst</a> <a id="1086" class="Symbol">_</a> <a id="1088" class="Symbol">_</a>
    <a id="1094" href="Definition.Typed.Consequences.Consistency.html#1094" class="Generalizable">t</a> <a id="1096" href="Definition.Typed.Consequences.Consistency.html#1096" class="Generalizable">u</a> <a id="1098" class="Symbol">:</a> <a id="1100" href="Definition.Untyped.html#1181" class="Datatype">Term</a> <a id="1105" href="Definition.Typed.Consequences.Consistency.html#1039" class="Generalizable">n</a>

<a id="1108" class="Keyword">opaque</a>

  <a id="1118" class="Comment">-- If there is some way to instantiate all the types in¬†Œì, then Œì is</a>
  <a id="1189" class="Comment">-- consistent.</a>

  <a id="inhabited-consistent"></a><a id="1207" href="Definition.Typed.Consequences.Consistency.html#1207" class="Function">inhabited-consistent</a> <a id="1228" class="Symbol">:</a> <a id="1230" class="InductiveConstructor">Œµ</a> <a id="1232" href="Definition.Typed.Substitution.Primitive.Primitive.html#3155" class="Function Operator">‚ä¢À¢ ∑</a> <a id="1236" href="Definition.Typed.Consequences.Consistency.html#1074" class="Generalizable">œÉ</a> <a id="1238" href="Definition.Typed.Substitution.Primitive.Primitive.html#3155" class="Function Operator">‚à∑</a> <a id="1240" href="Definition.Typed.Consequences.Consistency.html#1053" class="Generalizable">Œì</a> <a id="1242" class="Symbol">‚Üí</a> <a id="1244" href="Definition.Typed.html#18998" class="Function">Consistent</a> <a id="1255" href="Definition.Typed.Consequences.Consistency.html#1053" class="Generalizable">Œì</a>
  <a id="1259" href="Definition.Typed.Consequences.Consistency.html#1207" class="Function">inhabited-consistent</a> <a id="1280" href="Definition.Typed.Consequences.Consistency.html#1280" class="Bound">‚ä¢œÉ</a> <a id="1283" class="Symbol">_</a> <a id="1285" href="Definition.Typed.Consequences.Consistency.html#1285" class="Bound">‚ä¢t</a> <a id="1288" class="Symbol">=</a> <a id="1290" href="Definition.Typed.Consequences.Canonicity.html#1853" class="Function">¬¨Empty</a> <a id="1297" class="Symbol">(</a><a id="1298" href="Definition.Typed.Substitution.Primitive.Primitive.html#54020" class="Function">subst-‚ä¢‚à∑</a> <a id="1307" href="Definition.Typed.Consequences.Consistency.html#1285" class="Bound">‚ä¢t</a> <a id="1310" href="Definition.Typed.Consequences.Consistency.html#1280" class="Bound">‚ä¢œÉ</a><a id="1312" class="Symbol">)</a>

<a id="1315" class="Keyword">opaque</a>

  <a id="1325" class="Comment">-- If equality reflection is not allowed or the context is empty,</a>
  <a id="1393" class="Comment">-- then zero is not definitionally equal to suc¬†t.</a>

  <a id="zero‚â¢suc"></a><a id="1447" href="Definition.Typed.Consequences.Consistency.html#1447" class="Function">zero‚â¢suc</a> <a id="1456" class="Symbol">:</a>
    <a id="1462" class="Symbol">‚¶É</a> <a id="1464" href="Definition.Typed.Consequences.Consistency.html#1464" class="Bound">ok</a> <a id="1467" class="Symbol">:</a> <a id="1469" href="Definition.Typed.Restrictions.html#3219" class="Datatype">No-equality-reflection</a> <a id="1492" href="Definition.Untyped.NotParametrised.html#1265" class="Datatype Operator">or-empty</a> <a id="1501" href="Definition.Typed.Consequences.Consistency.html#1053" class="Generalizable">Œì</a> <a id="1503" class="Symbol">‚¶Ñ</a> <a id="1505" class="Symbol">‚Üí</a>
    <a id="1511" href="Relation.Nullary.Negation.Core.html#658" class="Function Operator">¬¨</a> <a id="1513" href="Definition.Typed.Consequences.Consistency.html#1053" class="Generalizable">Œì</a> <a id="1515" href="Definition.Typed.html#5241" class="Datatype Operator">‚ä¢</a> <a id="1517" href="Definition.Untyped.html#1702" class="InductiveConstructor">zero</a> <a id="1522" href="Definition.Typed.html#5241" class="Datatype Operator">‚â°</a> <a id="1524" href="Definition.Untyped.html#1718" class="InductiveConstructor">suc</a> <a id="1528" href="Definition.Typed.Consequences.Consistency.html#1094" class="Generalizable">t</a> <a id="1530" href="Definition.Typed.html#5241" class="Datatype Operator">‚à∑</a> <a id="1532" href="Definition.Untyped.html#1689" class="InductiveConstructor">‚Ñï</a>
  <a id="1536" href="Definition.Typed.Consequences.Consistency.html#1447" class="Function">zero‚â¢suc</a> <a id="1545" class="Symbol">{</a><a id="1546" href="Definition.Typed.Consequences.Consistency.html#1546" class="Bound">Œì</a><a id="1547" class="Symbol">}</a> <a id="1549" class="Symbol">{</a><a id="1550" href="Definition.Typed.Consequences.Consistency.html#1550" class="Bound">t</a><a id="1551" class="Symbol">}</a> <a id="1553" class="Symbol">=</a>
    <a id="1559" href="Definition.Typed.Consequences.Consistency.html#1546" class="Bound">Œì</a> <a id="1561" href="Definition.Typed.html#5241" class="Datatype Operator">‚ä¢</a> <a id="1563" href="Definition.Untyped.html#1702" class="InductiveConstructor">zero</a> <a id="1568" href="Definition.Typed.html#5241" class="Datatype Operator">‚â°</a> <a id="1570" href="Definition.Untyped.html#1718" class="InductiveConstructor">suc</a> <a id="1574" href="Definition.Typed.Consequences.Consistency.html#1550" class="Bound">t</a> <a id="1576" href="Definition.Typed.html#5241" class="Datatype Operator">‚à∑</a> <a id="1578" href="Definition.Untyped.html#1689" class="InductiveConstructor">‚Ñï</a>                 <a id="1596" href="Tools.Function.html#911" class="Function">‚Üí‚ü®</a> <a id="1599" href="Definition.LogicalRelation.Fundamental.Reducibility.html#1691" class="Function">reducible-‚ä©‚â°‚à∑</a> <a id="1613" href="Tools.Function.html#911" class="Function">‚ü©</a>
    <a id="1619" class="Symbol">(</a><a id="1620" href="Data.Product.Base.html#852" class="Function">‚àÉ</a> <a id="1622" class="Symbol">Œª</a> <a id="1624" href="Definition.Typed.Consequences.Consistency.html#1624" class="Bound">l</a> <a id="1626" class="Symbol">‚Üí</a> <a id="1628" href="Definition.Typed.Consequences.Consistency.html#1546" class="Bound">Œì</a> <a id="1630" href="Definition.LogicalRelation.Hidden.html#2021" class="Function Operator">‚ä©‚ü®</a> <a id="1633" href="Definition.Typed.Consequences.Consistency.html#1624" class="Bound">l</a> <a id="1635" href="Definition.LogicalRelation.Hidden.html#2021" class="Function Operator">‚ü©</a> <a id="1637" href="Definition.Untyped.html#1702" class="InductiveConstructor">zero</a> <a id="1642" href="Definition.LogicalRelation.Hidden.html#2021" class="Function Operator">‚â°</a> <a id="1644" href="Definition.Untyped.html#1718" class="InductiveConstructor">suc</a> <a id="1648" href="Definition.Typed.Consequences.Consistency.html#1550" class="Bound">t</a> <a id="1650" href="Definition.LogicalRelation.Hidden.html#2021" class="Function Operator">‚à∑</a> <a id="1652" href="Definition.Untyped.html#1689" class="InductiveConstructor">‚Ñï</a><a id="1653" class="Symbol">)</a>  <a id="1656" href="Tools.Function.html#911" class="Function">‚Üí‚ü®</a> <a id="1659" href="Definition.LogicalRelation.Substitution.Introductions.Nat.html#8261" class="Function">‚ä©zero‚â°suc‚à∑‚Ñï‚áî</a> <a id="1672" class="Symbol">.</a><a id="1673" href="Data.Product.Base.html#636" class="Field">proj‚ÇÅ</a> <a id="1679" href="Function.Base.html#1115" class="Function Operator">‚àò‚Üí</a> <a id="1682" href="Data.Product.Base.html#650" class="Field">proj‚ÇÇ</a> <a id="1688" href="Tools.Function.html#911" class="Function">‚ü©</a>
    <a id="1694" href="Data.Empty.html#914" class="Function">‚ä•</a>                                    <a id="1731" href="Tools.Function.html#1139" class="Function Operator">‚ñ°</a>

<a id="1734" class="Keyword">opaque</a>

  <a id="1744" class="Comment">-- If equality reflection is not allowed or the context is empty,</a>
  <a id="1812" class="Comment">-- then zero is not definitionally equal to one.</a>

  <a id="zero‚â¢one"></a><a id="1864" href="Definition.Typed.Consequences.Consistency.html#1864" class="Function">zero‚â¢one</a> <a id="1873" class="Symbol">:</a>
    <a id="1879" class="Symbol">‚¶É</a> <a id="1881" href="Definition.Typed.Consequences.Consistency.html#1881" class="Bound">ok</a> <a id="1884" class="Symbol">:</a> <a id="1886" href="Definition.Typed.Restrictions.html#3219" class="Datatype">No-equality-reflection</a> <a id="1909" href="Definition.Untyped.NotParametrised.html#1265" class="Datatype Operator">or-empty</a> <a id="1918" href="Definition.Typed.Consequences.Consistency.html#1053" class="Generalizable">Œì</a> <a id="1920" class="Symbol">‚¶Ñ</a> <a id="1922" class="Symbol">‚Üí</a>
    <a id="1928" href="Relation.Nullary.Negation.Core.html#658" class="Function Operator">¬¨</a> <a id="1930" href="Definition.Typed.Consequences.Consistency.html#1053" class="Generalizable">Œì</a> <a id="1932" href="Definition.Typed.html#5241" class="Datatype Operator">‚ä¢</a> <a id="1934" href="Definition.Untyped.html#1702" class="InductiveConstructor">zero</a> <a id="1939" href="Definition.Typed.html#5241" class="Datatype Operator">‚â°</a> <a id="1941" href="Definition.Untyped.html#1718" class="InductiveConstructor">suc</a> <a id="1945" href="Definition.Untyped.html#1702" class="InductiveConstructor">zero</a> <a id="1950" href="Definition.Typed.html#5241" class="Datatype Operator">‚à∑</a> <a id="1952" href="Definition.Untyped.html#1689" class="InductiveConstructor">‚Ñï</a>
  <a id="1956" href="Definition.Typed.Consequences.Consistency.html#1864" class="Function">zero‚â¢one</a> <a id="1965" class="Symbol">=</a> <a id="1967" href="Definition.Typed.Consequences.Consistency.html#1447" class="Function">zero‚â¢suc</a>

<a id="1977" class="Keyword">opaque</a>

  <a id="1987" class="Comment">-- If equality reflection is allowed, then there is a context for</a>
  <a id="2055" class="Comment">-- which zero is definitionally equal to one.</a>

  <a id="zero‚â°one"></a><a id="2104" href="Definition.Typed.Consequences.Consistency.html#2104" class="Function">zero‚â°one</a> <a id="2113" class="Symbol">:</a>
    <a id="2119" href="Definition.Typed.Restrictions.html#3013" class="Field">Equality-reflection</a> <a id="2139" class="Symbol">‚Üí</a>
    <a id="2145" href="Data.Product.Base.html#852" class="Function">‚àÉ</a> <a id="2147" class="Symbol">Œª</a> <a id="2149" class="Symbol">(</a><a id="2150" href="Definition.Typed.Consequences.Consistency.html#2150" class="Bound">Œì</a> <a id="2152" class="Symbol">:</a> <a id="2154" href="Definition.Untyped.NotParametrised.html#882" class="Datatype">Con</a> <a id="2158" href="Definition.Untyped.html#1181" class="Datatype">Term</a> <a id="2163" class="Number">1</a><a id="2164" class="Symbol">)</a> <a id="2166" class="Symbol">‚Üí</a> <a id="2168" href="Definition.Typed.Consequences.Consistency.html#2150" class="Bound">Œì</a> <a id="2170" href="Definition.Typed.html#5241" class="Datatype Operator">‚ä¢</a> <a id="2172" href="Definition.Untyped.html#1702" class="InductiveConstructor">zero</a> <a id="2177" href="Definition.Typed.html#5241" class="Datatype Operator">‚â°</a> <a id="2179" href="Definition.Untyped.html#1718" class="InductiveConstructor">suc</a> <a id="2183" href="Definition.Untyped.html#1702" class="InductiveConstructor">zero</a> <a id="2188" href="Definition.Typed.html#5241" class="Datatype Operator">‚à∑</a> <a id="2190" href="Definition.Untyped.html#1689" class="InductiveConstructor">‚Ñï</a>
  <a id="2194" href="Definition.Typed.Consequences.Consistency.html#2104" class="Function">zero‚â°one</a> <a id="2203" href="Definition.Typed.Consequences.Consistency.html#2203" class="Bound">ok</a> <a id="2206" class="Symbol">=</a>
    <a id="2212" href="Definition.Untyped.NotParametrised.html#926" class="InductiveConstructor">Œµ</a> <a id="2214" href="Definition.Untyped.NotParametrised.html#995" class="InductiveConstructor Operator">‚àô</a> <a id="2216" href="Definition.Untyped.html#2096" class="InductiveConstructor">Id</a> <a id="2219" href="Definition.Untyped.html#1689" class="InductiveConstructor">‚Ñï</a> <a id="2221" href="Definition.Untyped.html#1702" class="InductiveConstructor">zero</a> <a id="2226" class="Symbol">(</a><a id="2227" href="Definition.Untyped.html#1718" class="InductiveConstructor">suc</a> <a id="2231" href="Definition.Untyped.html#1702" class="InductiveConstructor">zero</a><a id="2235" class="Symbol">)</a> <a id="2237" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a>
    <a id="2243" href="Definition.Typed.Properties.Admissible.Identity.html#32171" class="Function">equality-reflection‚Ä≤</a> <a id="2264" href="Definition.Typed.Consequences.Consistency.html#2203" class="Bound">ok</a> <a id="2267" class="Symbol">(</a><a id="2268" href="Definition.Typed.Properties.Admissible.Var.html#599" class="Function">var‚ÇÄ</a> <a id="2273" class="Symbol">(</a><a id="2274" href="Definition.Typed.Properties.Admissible.Identity.Primitive.html#770" class="Function">Id‚±º‚Ä≤</a> <a id="2279" class="Symbol">(</a><a id="2280" href="Definition.Typed.html#3118" class="InductiveConstructor">zero‚±º</a> <a id="2286" href="Definition.Typed.html#1198" class="InductiveConstructor">Œµ</a><a id="2287" class="Symbol">)</a> <a id="2289" class="Symbol">(</a><a id="2290" href="Definition.Typed.html#3167" class="InductiveConstructor">suc‚±º</a> <a id="2295" class="Symbol">(</a><a id="2296" href="Definition.Typed.html#3118" class="InductiveConstructor">zero‚±º</a> <a id="2302" href="Definition.Typed.html#1198" class="InductiveConstructor">Œµ</a><a id="2303" class="Symbol">))))</a>

<a id="2309" class="Keyword">opaque</a>

  <a id="2319" class="Comment">-- A variant of zero‚â¢suc: the identity type Id¬†‚Ñï¬†zero¬†(suc¬†t) is not</a>
  <a id="2390" class="Comment">-- inhabited in the empty context.</a>

  <a id="¬¨-Id-‚Ñï-zero-suc"></a><a id="2428" href="Definition.Typed.Consequences.Consistency.html#2428" class="Function">¬¨-Id-‚Ñï-zero-suc</a> <a id="2444" class="Symbol">:</a> <a id="2446" href="Relation.Nullary.Negation.Core.html#658" class="Function Operator">¬¨</a> <a id="2448" class="InductiveConstructor">Œµ</a> <a id="2450" href="Definition.Typed.html#1707" class="Datatype Operator">‚ä¢</a> <a id="2452" href="Definition.Typed.Consequences.Consistency.html#1096" class="Generalizable">u</a> <a id="2454" href="Definition.Typed.html#1707" class="Datatype Operator">‚à∑</a> <a id="2456" href="Definition.Untyped.html#2096" class="InductiveConstructor">Id</a> <a id="2459" href="Definition.Untyped.html#1689" class="InductiveConstructor">‚Ñï</a> <a id="2461" href="Definition.Untyped.html#1702" class="InductiveConstructor">zero</a> <a id="2466" class="Symbol">(</a><a id="2467" href="Definition.Untyped.html#1718" class="InductiveConstructor">suc</a> <a id="2471" href="Definition.Typed.Consequences.Consistency.html#1094" class="Generalizable">t</a><a id="2472" class="Symbol">)</a>
  <a id="2476" href="Definition.Typed.Consequences.Consistency.html#2428" class="Function">¬¨-Id-‚Ñï-zero-suc</a> <a id="2492" class="Symbol">{</a><a id="2493" href="Definition.Typed.Consequences.Consistency.html#2493" class="Bound">u</a><a id="2494" class="Symbol">}</a> <a id="2496" class="Symbol">{</a><a id="2497" href="Definition.Typed.Consequences.Consistency.html#2497" class="Bound">t</a><a id="2498" class="Symbol">}</a> <a id="2500" class="Symbol">=</a>
    <a id="2506" href="Definition.Untyped.NotParametrised.html#926" class="InductiveConstructor">Œµ</a> <a id="2508" href="Definition.Typed.html#1707" class="Datatype Operator">‚ä¢</a> <a id="2510" href="Definition.Typed.Consequences.Consistency.html#2493" class="Bound">u</a> <a id="2512" href="Definition.Typed.html#1707" class="Datatype Operator">‚à∑</a> <a id="2514" href="Definition.Untyped.html#2096" class="InductiveConstructor">Id</a> <a id="2517" href="Definition.Untyped.html#1689" class="InductiveConstructor">‚Ñï</a> <a id="2519" href="Definition.Untyped.html#1702" class="InductiveConstructor">zero</a> <a id="2524" class="Symbol">(</a><a id="2525" href="Definition.Untyped.html#1718" class="InductiveConstructor">suc</a> <a id="2529" href="Definition.Typed.Consequences.Consistency.html#2497" class="Bound">t</a><a id="2530" class="Symbol">)</a>  <a id="2533" href="Tools.Function.html#911" class="Function">‚Üí‚ü®</a> <a id="2536" href="Definition.Typed.Consequences.Canonicity.html#2545" class="Function">Œµ‚ä¢‚à∑Id‚ÜíŒµ‚ä¢‚â°‚à∑</a> <a id="2547" href="Tools.Function.html#911" class="Function">‚ü©</a>
    <a id="2553" href="Definition.Untyped.NotParametrised.html#926" class="InductiveConstructor">Œµ</a> <a id="2555" href="Definition.Typed.html#5241" class="Datatype Operator">‚ä¢</a> <a id="2557" href="Definition.Untyped.html#1702" class="InductiveConstructor">zero</a> <a id="2562" href="Definition.Typed.html#5241" class="Datatype Operator">‚â°</a> <a id="2564" href="Definition.Untyped.html#1718" class="InductiveConstructor">suc</a> <a id="2568" href="Definition.Typed.Consequences.Consistency.html#2497" class="Bound">t</a> <a id="2570" href="Definition.Typed.html#5241" class="Datatype Operator">‚à∑</a> <a id="2572" href="Definition.Untyped.html#1689" class="InductiveConstructor">‚Ñï</a>       <a id="2580" href="Tools.Function.html#911" class="Function">‚Üí‚ü®</a> <a id="2583" href="Definition.Typed.Consequences.Consistency.html#1447" class="Function">zero‚â¢suc</a> <a id="2592" class="Symbol">‚¶É</a> <a id="2594" class="Argument">ok</a> <a id="2597" class="Symbol">=</a> <a id="2599" href="Definition.Untyped.NotParametrised.html#1380" class="InductiveConstructor">Œµ</a> <a id="2601" class="Symbol">‚¶Ñ</a> <a id="2603" href="Tools.Function.html#911" class="Function">‚ü©</a>
    <a id="2609" href="Data.Empty.html#914" class="Function">‚ä•</a>                          <a id="2636" href="Tools.Function.html#1139" class="Function Operator">‚ñ°</a>
</pre></body></html>