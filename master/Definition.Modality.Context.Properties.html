<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Definition.Modality.Context.Properties</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- Properties of moodality (grade) contexts.</a>
<a id="119" class="Comment">------------------------------------------------------------------------</a>

<a id="193" class="Keyword">open</a> <a id="198" class="Keyword">import</a> <a id="205" href="Definition.Modality.html" class="Module">Definition.Modality</a>

<a id="226" class="Keyword">module</a> <a id="233" href="Definition.Modality.Context.Properties.html" class="Module">Definition.Modality.Context.Properties</a>
  <a id="274" class="Symbol">{</a><a id="275" href="Definition.Modality.Context.Properties.html#275" class="Bound">a</a><a id="276" class="Symbol">}</a> <a id="278" class="Symbol">{</a><a id="279" href="Definition.Modality.Context.Properties.html#279" class="Bound">M</a> <a id="281" class="Symbol">:</a> <a id="283" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="287" href="Definition.Modality.Context.Properties.html#275" class="Bound">a</a><a id="288" class="Symbol">}</a> <a id="290" class="Symbol">(</a><a id="291" href="Definition.Modality.Context.Properties.html#291" class="Bound">𝕄</a> <a id="293" class="Symbol">:</a> <a id="295" href="Definition.Modality.html#5110" class="Record">Modality</a> <a id="304" href="Definition.Modality.Context.Properties.html#279" class="Bound">M</a><a id="305" class="Symbol">)</a> <a id="307" class="Keyword">where</a>

<a id="314" class="Keyword">open</a> <a id="319" href="Definition.Modality.html#5110" class="Module">Modality</a> <a id="328" href="Definition.Modality.Context.Properties.html#291" class="Bound">𝕄</a>

<a id="331" class="Keyword">open</a> <a id="336" class="Keyword">import</a> <a id="343" href="Definition.Modality.Properties.html" class="Module">Definition.Modality.Properties</a> <a id="374" href="Definition.Modality.Context.Properties.html#291" class="Bound">𝕄</a>
<a id="376" class="Keyword">open</a> <a id="381" class="Keyword">import</a> <a id="388" href="Definition.Modality.Context.html" class="Module">Definition.Modality.Context</a> <a id="416" href="Definition.Modality.Context.Properties.html#291" class="Bound">𝕄</a>

<a id="419" class="Keyword">open</a> <a id="424" class="Keyword">import</a> <a id="431" href="Tools.Nat.html" class="Module">Tools.Nat</a> <a id="441" class="Keyword">renaming</a> <a id="450" class="Symbol">(</a><a id="451" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">_+_</a> <a id="455" class="Symbol">to</a> <a id="458" class="Primitive Operator">_+ⁿ_</a><a id="462" class="Symbol">)</a>
<a id="464" class="Keyword">open</a> <a id="469" class="Keyword">import</a> <a id="476" href="Tools.PropositionalEquality.html" class="Module">Tools.PropositionalEquality</a> <a id="504" class="Symbol">as</a> <a id="507" class="Module">PE</a>
<a id="510" class="Keyword">import</a> <a id="517" href="Tools.Reasoning.Equivalence.html" class="Module">Tools.Reasoning.Equivalence</a>

<a id="546" class="Keyword">open</a> <a id="551" class="Keyword">import</a> <a id="558" href="Definition.Modality.Context.Properties.Addition.html" class="Module">Definition.Modality.Context.Properties.Addition</a> <a id="606" href="Definition.Modality.Context.Properties.html#291" class="Bound">𝕄</a> <a id="608" class="Keyword">public</a>
<a id="615" class="Keyword">open</a> <a id="620" class="Keyword">import</a> <a id="627" href="Definition.Modality.Context.Properties.Equivalence.html" class="Module">Definition.Modality.Context.Properties.Equivalence</a> <a id="678" href="Definition.Modality.Context.Properties.html#291" class="Bound">𝕄</a> <a id="680" class="Keyword">public</a>
<a id="687" class="Keyword">open</a> <a id="692" class="Keyword">import</a> <a id="699" href="Definition.Modality.Context.Properties.Lookup.html" class="Module">Definition.Modality.Context.Properties.Lookup</a> <a id="745" href="Definition.Modality.Context.Properties.html#291" class="Bound">𝕄</a> <a id="747" class="Keyword">public</a>
<a id="754" class="Keyword">open</a> <a id="759" class="Keyword">import</a> <a id="766" href="Definition.Modality.Context.Properties.Meet.html" class="Module">Definition.Modality.Context.Properties.Meet</a> <a id="810" href="Definition.Modality.Context.Properties.html#291" class="Bound">𝕄</a> <a id="812" class="Keyword">public</a>
<a id="819" class="Keyword">open</a> <a id="824" class="Keyword">import</a> <a id="831" href="Definition.Modality.Context.Properties.Multiplication.html" class="Module">Definition.Modality.Context.Properties.Multiplication</a> <a id="885" href="Definition.Modality.Context.Properties.html#291" class="Bound">𝕄</a> <a id="887" class="Keyword">public</a>
<a id="894" class="Keyword">open</a> <a id="899" class="Keyword">import</a> <a id="906" href="Definition.Modality.Context.Properties.PartialOrder.html" class="Module">Definition.Modality.Context.Properties.PartialOrder</a> <a id="958" href="Definition.Modality.Context.Properties.html#291" class="Bound">𝕄</a> <a id="960" class="Keyword">public</a>
<a id="967" class="Keyword">open</a> <a id="972" class="Keyword">import</a> <a id="979" href="Definition.Modality.Context.Properties.Star.html" class="Module">Definition.Modality.Context.Properties.Star</a> <a id="1023" href="Definition.Modality.Context.Properties.html#291" class="Bound">𝕄</a> <a id="1025" class="Keyword">public</a>
<a id="1032" class="Keyword">open</a> <a id="1037" class="Keyword">import</a> <a id="1044" href="Definition.Modality.Context.Properties.Update.html" class="Module">Definition.Modality.Context.Properties.Update</a> <a id="1090" href="Definition.Modality.Context.Properties.html#291" class="Bound">𝕄</a> <a id="1092" class="Keyword">public</a>

<a id="1100" class="Keyword">private</a>
  <a id="1110" class="Keyword">variable</a>
    <a id="1123" href="Definition.Modality.Context.Properties.html#1123" class="Generalizable">n</a> <a id="1125" class="Symbol">:</a> <a id="1127" href="Agda.Builtin.Nat.html#186" class="Datatype">Nat</a>
    <a id="1135" href="Definition.Modality.Context.Properties.html#1135" class="Generalizable">p</a> <a id="1137" href="Definition.Modality.Context.Properties.html#1137" class="Generalizable">q</a> <a id="1139" href="Definition.Modality.Context.Properties.html#1139" class="Generalizable">r</a> <a id="1141" href="Definition.Modality.Context.Properties.html#1141" class="Generalizable">r′</a> <a id="1144" class="Symbol">:</a> <a id="1146" href="Definition.Modality.Context.Properties.html#279" class="Bound">M</a>
    <a id="1152" href="Definition.Modality.Context.Properties.html#1152" class="Generalizable">γ</a> <a id="1154" href="Definition.Modality.Context.Properties.html#1154" class="Generalizable">γ′</a> <a id="1157" href="Definition.Modality.Context.Properties.html#1157" class="Generalizable">δ</a> <a id="1159" href="Definition.Modality.Context.Properties.html#1159" class="Generalizable">δ′</a> <a id="1162" href="Definition.Modality.Context.Properties.html#1162" class="Generalizable">η</a> <a id="1164" class="Symbol">:</a> <a id="1166" href="Definition.Modality.Context.html#627" class="Datatype">Conₘ</a> <a id="1171" href="Definition.Modality.Context.Properties.html#1123" class="Generalizable">n</a>

<a id="1174" class="Comment">-- Context extension is monotone w.r.t the tail</a>
<a id="1222" class="Comment">-- If γ ≤ᶜ δ then γ ∙ p ≤ᶜ δ ∙ p</a>

<a id="∙-monotoneˡ"></a><a id="1256" href="Definition.Modality.Context.Properties.html#1256" class="Function">∙-monotoneˡ</a> <a id="1268" class="Symbol">:</a> <a id="1270" class="Symbol">{</a><a id="1271" href="Definition.Modality.Context.Properties.html#1271" class="Bound">γ</a> <a id="1273" href="Definition.Modality.Context.Properties.html#1273" class="Bound">δ</a> <a id="1275" class="Symbol">:</a> <a id="1277" href="Definition.Modality.Context.html#627" class="Datatype">Conₘ</a> <a id="1282" href="Definition.Modality.Context.Properties.html#1123" class="Generalizable">n</a><a id="1283" class="Symbol">}</a> <a id="1285" class="Symbol">{</a><a id="1286" href="Definition.Modality.Context.Properties.html#1286" class="Bound">p</a> <a id="1288" class="Symbol">:</a> <a id="1290" href="Definition.Modality.Context.Properties.html#279" class="Bound">M</a><a id="1291" class="Symbol">}</a> <a id="1293" class="Symbol">→</a> <a id="1295" href="Definition.Modality.Context.Properties.html#1271" class="Bound">γ</a> <a id="1297" href="Definition.Modality.Context.html#1790" class="Function Operator">≤ᶜ</a> <a id="1300" href="Definition.Modality.Context.Properties.html#1273" class="Bound">δ</a> <a id="1302" class="Symbol">→</a> <a id="1304" href="Definition.Modality.Context.Properties.html#1271" class="Bound">γ</a> <a id="1306" class="InductiveConstructor Operator">∙</a> <a id="1308" href="Definition.Modality.Context.Properties.html#1286" class="Bound">p</a> <a id="1310" href="Definition.Modality.Context.html#1790" class="Function Operator">≤ᶜ</a> <a id="1313" href="Definition.Modality.Context.Properties.html#1273" class="Bound">δ</a> <a id="1315" class="InductiveConstructor Operator">∙</a> <a id="1317" href="Definition.Modality.Context.Properties.html#1286" class="Bound">p</a>
<a id="1319" href="Definition.Modality.Context.Properties.html#1256" class="Function">∙-monotoneˡ</a> <a id="1331" href="Definition.Modality.Context.Properties.html#1331" class="Bound">γ≤δ</a> <a id="1335" class="Symbol">=</a> <a id="1337" href="Definition.Modality.Context.Properties.html#1331" class="Bound">γ≤δ</a> <a id="1341" href="Definition.Modality.Context.html#820" class="InductiveConstructor Operator">∙</a> <a id="1343" href="Definition.Modality.Properties.PartialOrder.html#575" class="Function">≤-refl</a>

<a id="1351" class="Comment">-- Context extension is monotone w.r.t the head</a>
<a id="1399" class="Comment">-- If p ≤ q then γ ∙ p ≤ᶜ γ ∙ q</a>

<a id="∙-monotoneʳ"></a><a id="1432" href="Definition.Modality.Context.Properties.html#1432" class="Function">∙-monotoneʳ</a> <a id="1444" class="Symbol">:</a> <a id="1446" class="Symbol">{</a><a id="1447" href="Definition.Modality.Context.Properties.html#1447" class="Bound">γ</a> <a id="1449" class="Symbol">:</a> <a id="1451" href="Definition.Modality.Context.html#627" class="Datatype">Conₘ</a> <a id="1456" href="Definition.Modality.Context.Properties.html#1123" class="Generalizable">n</a><a id="1457" class="Symbol">}</a> <a id="1459" class="Symbol">{</a><a id="1460" href="Definition.Modality.Context.Properties.html#1460" class="Bound">p</a> <a id="1462" href="Definition.Modality.Context.Properties.html#1462" class="Bound">q</a> <a id="1464" class="Symbol">:</a> <a id="1466" href="Definition.Modality.Context.Properties.html#279" class="Bound">M</a><a id="1467" class="Symbol">}</a> <a id="1469" class="Symbol">→</a> <a id="1471" href="Definition.Modality.Context.Properties.html#1460" class="Bound">p</a> <a id="1473" href="Definition.Modality.html#1241" class="Function Operator">≤</a> <a id="1475" href="Definition.Modality.Context.Properties.html#1462" class="Bound">q</a> <a id="1477" class="Symbol">→</a> <a id="1479" href="Definition.Modality.Context.Properties.html#1447" class="Bound">γ</a> <a id="1481" class="InductiveConstructor Operator">∙</a> <a id="1483" href="Definition.Modality.Context.Properties.html#1460" class="Bound">p</a> <a id="1485" href="Definition.Modality.Context.html#1790" class="Function Operator">≤ᶜ</a> <a id="1488" href="Definition.Modality.Context.Properties.html#1447" class="Bound">γ</a> <a id="1490" class="InductiveConstructor Operator">∙</a> <a id="1492" href="Definition.Modality.Context.Properties.html#1462" class="Bound">q</a>
<a id="1494" href="Definition.Modality.Context.Properties.html#1432" class="Function">∙-monotoneʳ</a> <a id="1506" href="Definition.Modality.Context.Properties.html#1506" class="Bound">p≤q</a> <a id="1510" class="Symbol">=</a> <a id="1512" href="Definition.Modality.Context.Properties.PartialOrder.html#655" class="Function">≤ᶜ-refl</a> <a id="1520" href="Definition.Modality.Context.html#820" class="InductiveConstructor Operator">∙</a> <a id="1522" href="Definition.Modality.Context.Properties.html#1506" class="Bound">p≤q</a>

<a id="1527" class="Comment">-- Context extension is monotone</a>
<a id="1560" class="Comment">-- If γ ≤ᶜ δ and p ≤ q then γ ∙ p ≤ᶜ δ ∙ q</a>

<a id="∙-monotone"></a><a id="1604" href="Definition.Modality.Context.Properties.html#1604" class="Function">∙-monotone</a> <a id="1615" class="Symbol">:</a> <a id="1617" class="Symbol">{</a><a id="1618" href="Definition.Modality.Context.Properties.html#1618" class="Bound">γ</a> <a id="1620" href="Definition.Modality.Context.Properties.html#1620" class="Bound">δ</a> <a id="1622" class="Symbol">:</a> <a id="1624" href="Definition.Modality.Context.html#627" class="Datatype">Conₘ</a> <a id="1629" href="Definition.Modality.Context.Properties.html#1123" class="Generalizable">n</a><a id="1630" class="Symbol">}</a> <a id="1632" class="Symbol">{</a><a id="1633" href="Definition.Modality.Context.Properties.html#1633" class="Bound">p</a> <a id="1635" href="Definition.Modality.Context.Properties.html#1635" class="Bound">q</a> <a id="1637" class="Symbol">:</a> <a id="1639" href="Definition.Modality.Context.Properties.html#279" class="Bound">M</a><a id="1640" class="Symbol">}</a> <a id="1642" class="Symbol">→</a> <a id="1644" href="Definition.Modality.Context.Properties.html#1618" class="Bound">γ</a> <a id="1646" href="Definition.Modality.Context.html#1790" class="Function Operator">≤ᶜ</a> <a id="1649" href="Definition.Modality.Context.Properties.html#1620" class="Bound">δ</a> <a id="1651" class="Symbol">→</a> <a id="1653" href="Definition.Modality.Context.Properties.html#1633" class="Bound">p</a> <a id="1655" href="Definition.Modality.html#1241" class="Function Operator">≤</a> <a id="1657" href="Definition.Modality.Context.Properties.html#1635" class="Bound">q</a> <a id="1659" class="Symbol">→</a> <a id="1661" href="Definition.Modality.Context.Properties.html#1618" class="Bound">γ</a> <a id="1663" class="InductiveConstructor Operator">∙</a> <a id="1665" href="Definition.Modality.Context.Properties.html#1633" class="Bound">p</a> <a id="1667" href="Definition.Modality.Context.html#1790" class="Function Operator">≤ᶜ</a> <a id="1670" href="Definition.Modality.Context.Properties.html#1620" class="Bound">δ</a> <a id="1672" class="InductiveConstructor Operator">∙</a> <a id="1674" href="Definition.Modality.Context.Properties.html#1635" class="Bound">q</a>
<a id="1676" href="Definition.Modality.Context.Properties.html#1604" class="Function">∙-monotone</a> <a id="1687" href="Definition.Modality.Context.Properties.html#1687" class="Bound">γ≤δ</a> <a id="1691" href="Definition.Modality.Context.Properties.html#1691" class="Bound">p≤q</a> <a id="1695" class="Symbol">=</a> <a id="1697" href="Definition.Modality.Context.Properties.PartialOrder.html#789" class="Function">≤ᶜ-trans</a> <a id="1706" class="Symbol">(</a><a id="1707" href="Definition.Modality.Context.Properties.html#1256" class="Function">∙-monotoneˡ</a> <a id="1719" href="Definition.Modality.Context.Properties.html#1687" class="Bound">γ≤δ</a><a id="1722" class="Symbol">)</a> <a id="1724" class="Symbol">(</a><a id="1725" href="Definition.Modality.Context.Properties.html#1432" class="Function">∙-monotoneʳ</a> <a id="1737" href="Definition.Modality.Context.Properties.html#1691" class="Bound">p≤q</a><a id="1740" class="Symbol">)</a>

<a id="1743" class="Comment">----------------------------------</a>
<a id="1778" class="Comment">-- Propeties of headₘ and tailₘ --</a>
<a id="1813" class="Comment">----------------------------------</a>

<a id="1849" class="Comment">-- tailₘ distributes over meet</a>
<a id="1880" class="Comment">-- tailₘ (γ ∧ᶜ δ) ≡ tailₘ γ ∧ᶜ tailₘ δ</a>

<a id="tailₘ-distrib-∧ᶜ"></a><a id="1920" href="Definition.Modality.Context.Properties.html#1920" class="Function">tailₘ-distrib-∧ᶜ</a> <a id="1937" class="Symbol">:</a> <a id="1939" class="Symbol">(</a><a id="1940" href="Definition.Modality.Context.Properties.html#1940" class="Bound">γ</a> <a id="1942" href="Definition.Modality.Context.Properties.html#1942" class="Bound">δ</a> <a id="1944" class="Symbol">:</a> <a id="1946" href="Definition.Modality.Context.html#627" class="Datatype">Conₘ</a> <a id="1951" class="Symbol">(</a><a id="1952" href="Tools.Nat.html#408" class="InductiveConstructor">1+</a> <a id="1955" href="Definition.Modality.Context.Properties.html#1123" class="Generalizable">n</a><a id="1956" class="Symbol">))</a> <a id="1959" class="Symbol">→</a> <a id="1961" href="Definition.Modality.Context.html#940" class="Function">tailₘ</a> <a id="1967" class="Symbol">(</a><a id="1968" href="Definition.Modality.Context.Properties.html#1940" class="Bound">γ</a> <a id="1970" href="Definition.Modality.Context.html#1556" class="Function Operator">∧ᶜ</a> <a id="1973" href="Definition.Modality.Context.Properties.html#1942" class="Bound">δ</a><a id="1974" class="Symbol">)</a> <a id="1976" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1978" class="Symbol">(</a><a id="1979" href="Definition.Modality.Context.html#940" class="Function">tailₘ</a> <a id="1985" href="Definition.Modality.Context.Properties.html#1940" class="Bound">γ</a><a id="1986" class="Symbol">)</a> <a id="1988" href="Definition.Modality.Context.html#1556" class="Function Operator">∧ᶜ</a> <a id="1991" class="Symbol">(</a><a id="1992" href="Definition.Modality.Context.html#940" class="Function">tailₘ</a> <a id="1998" href="Definition.Modality.Context.Properties.html#1942" class="Bound">δ</a><a id="1999" class="Symbol">)</a>
<a id="2001" href="Definition.Modality.Context.Properties.html#1920" class="Function">tailₘ-distrib-∧ᶜ</a> <a id="2018" class="Symbol">(</a><a id="2019" href="Definition.Modality.Context.html#654" class="InductiveConstructor">ε</a> <a id="2021" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="2023" href="Definition.Modality.Context.Properties.html#2023" class="Bound">p</a><a id="2024" class="Symbol">)</a> <a id="2026" class="Symbol">(</a><a id="2027" href="Definition.Modality.Context.html#654" class="InductiveConstructor">ε</a> <a id="2029" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="2031" href="Definition.Modality.Context.Properties.html#2031" class="Bound">q</a><a id="2032" class="Symbol">)</a> <a id="2034" class="Symbol">=</a> <a id="2036" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">PE.refl</a>
<a id="2044" href="Definition.Modality.Context.Properties.html#1920" class="Function">tailₘ-distrib-∧ᶜ</a> <a id="2061" class="Symbol">(</a><a id="2062" href="Definition.Modality.Context.Properties.html#2062" class="Bound">γ</a> <a id="2064" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="2066" href="Definition.Modality.Context.Properties.html#2066" class="Bound">p′</a> <a id="2069" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="2071" href="Definition.Modality.Context.Properties.html#2071" class="Bound">p</a><a id="2072" class="Symbol">)</a> <a id="2074" class="Symbol">(</a><a id="2075" href="Definition.Modality.Context.Properties.html#2075" class="Bound">δ</a> <a id="2077" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="2079" href="Definition.Modality.Context.Properties.html#2079" class="Bound">q′</a> <a id="2082" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="2084" href="Definition.Modality.Context.Properties.html#2084" class="Bound">q</a><a id="2085" class="Symbol">)</a> <a id="2087" class="Symbol">=</a> <a id="2089" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="2094" class="Symbol">(</a><a id="2095" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">_∙</a> <a id="2098" class="Symbol">_)</a> <a id="2101" class="Symbol">(</a><a id="2102" href="Definition.Modality.Context.Properties.html#1920" class="Function">tailₘ-distrib-∧ᶜ</a> <a id="2119" class="Symbol">(</a><a id="2120" href="Definition.Modality.Context.Properties.html#2062" class="Bound">γ</a> <a id="2122" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="2124" href="Definition.Modality.Context.Properties.html#2071" class="Bound">p</a><a id="2125" class="Symbol">)</a> <a id="2127" class="Symbol">(</a><a id="2128" href="Definition.Modality.Context.Properties.html#2075" class="Bound">δ</a> <a id="2130" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="2132" href="Definition.Modality.Context.Properties.html#2084" class="Bound">q</a><a id="2133" class="Symbol">))</a>

<a id="2137" class="Comment">-- headₘ distributes over meet</a>
<a id="2168" class="Comment">-- headₘ (γ ∧ᶜ δ) ≡ headₘ γ ∧ headₘ δ</a>

<a id="head-distrib-∧"></a><a id="2207" href="Definition.Modality.Context.Properties.html#2207" class="Function">head-distrib-∧</a> <a id="2222" class="Symbol">:</a> <a id="2224" class="Symbol">(</a><a id="2225" href="Definition.Modality.Context.Properties.html#2225" class="Bound">γ</a> <a id="2227" href="Definition.Modality.Context.Properties.html#2227" class="Bound">δ</a> <a id="2229" class="Symbol">:</a> <a id="2231" href="Definition.Modality.Context.html#627" class="Datatype">Conₘ</a> <a id="2236" class="Symbol">(</a><a id="2237" href="Tools.Nat.html#408" class="InductiveConstructor">1+</a> <a id="2240" href="Definition.Modality.Context.Properties.html#1123" class="Generalizable">n</a><a id="2241" class="Symbol">))</a> <a id="2244" class="Symbol">→</a> <a id="2246" href="Definition.Modality.Context.html#891" class="Function">headₘ</a> <a id="2252" class="Symbol">(</a><a id="2253" href="Definition.Modality.Context.Properties.html#2225" class="Bound">γ</a> <a id="2255" href="Definition.Modality.Context.html#1556" class="Function Operator">∧ᶜ</a> <a id="2258" href="Definition.Modality.Context.Properties.html#2227" class="Bound">δ</a><a id="2259" class="Symbol">)</a> <a id="2261" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="2263" class="Symbol">(</a><a id="2264" href="Definition.Modality.Context.html#891" class="Function">headₘ</a> <a id="2270" href="Definition.Modality.Context.Properties.html#2225" class="Bound">γ</a><a id="2271" class="Symbol">)</a> <a id="2273" href="Definition.Modality.html#746" class="Function Operator">∧</a> <a id="2275" class="Symbol">(</a><a id="2276" href="Definition.Modality.Context.html#891" class="Function">headₘ</a> <a id="2282" href="Definition.Modality.Context.Properties.html#2227" class="Bound">δ</a><a id="2283" class="Symbol">)</a>
<a id="2285" href="Definition.Modality.Context.Properties.html#2207" class="Function">head-distrib-∧</a> <a id="2300" class="Symbol">(</a><a id="2301" href="Definition.Modality.Context.Properties.html#2301" class="Bound">γ</a> <a id="2303" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="2305" href="Definition.Modality.Context.Properties.html#2305" class="Bound">p</a><a id="2306" class="Symbol">)</a> <a id="2308" class="Symbol">(</a><a id="2309" href="Definition.Modality.Context.Properties.html#2309" class="Bound">δ</a> <a id="2311" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="2313" href="Definition.Modality.Context.Properties.html#2313" class="Bound">q</a><a id="2314" class="Symbol">)</a> <a id="2316" class="Symbol">=</a> <a id="2318" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">PE.refl</a>

<a id="2327" class="Comment">-- The headₘ and tailₘ functions correctly give the head and tail of the context</a>
<a id="2408" class="Comment">-- tailₘ γ ∙ headₘ γ ≡ γ</a>

<a id="headₘ-tailₘ-correct"></a><a id="2434" href="Definition.Modality.Context.Properties.html#2434" class="Function">headₘ-tailₘ-correct</a> <a id="2454" class="Symbol">:</a> <a id="2456" class="Symbol">(</a><a id="2457" href="Definition.Modality.Context.Properties.html#2457" class="Bound">γ</a> <a id="2459" class="Symbol">:</a> <a id="2461" href="Definition.Modality.Context.html#627" class="Datatype">Conₘ</a> <a id="2466" class="Symbol">(</a><a id="2467" href="Tools.Nat.html#408" class="InductiveConstructor">1+</a> <a id="2470" href="Definition.Modality.Context.Properties.html#1123" class="Generalizable">n</a><a id="2471" class="Symbol">))</a> <a id="2474" class="Symbol">→</a> <a id="2476" href="Definition.Modality.Context.html#940" class="Function">tailₘ</a> <a id="2482" href="Definition.Modality.Context.Properties.html#2457" class="Bound">γ</a> <a id="2484" class="InductiveConstructor Operator">∙</a> <a id="2486" href="Definition.Modality.Context.html#891" class="Function">headₘ</a> <a id="2492" href="Definition.Modality.Context.Properties.html#2457" class="Bound">γ</a> <a id="2494" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="2496" href="Definition.Modality.Context.Properties.html#2457" class="Bound">γ</a>
<a id="2498" href="Definition.Modality.Context.Properties.html#2434" class="Function">headₘ-tailₘ-correct</a> <a id="2518" class="Symbol">(</a><a id="2519" href="Definition.Modality.Context.Properties.html#2519" class="Bound">γ</a> <a id="2521" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="2523" href="Definition.Modality.Context.Properties.html#2523" class="Bound">p</a><a id="2524" class="Symbol">)</a> <a id="2526" class="Symbol">=</a> <a id="2528" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">PE.refl</a>

<a id="2537" class="Comment">-- Congruence of tailₘ</a>
<a id="2560" class="Comment">-- If γ ≈ᶜ δ then tailₘ γ ≈ᶜ tailₘ δ</a>

<a id="tailₘ-cong"></a><a id="2598" href="Definition.Modality.Context.Properties.html#2598" class="Function">tailₘ-cong</a> <a id="2609" class="Symbol">:</a> <a id="2611" class="Symbol">{</a><a id="2612" href="Definition.Modality.Context.Properties.html#2612" class="Bound">γ</a> <a id="2614" href="Definition.Modality.Context.Properties.html#2614" class="Bound">δ</a> <a id="2616" class="Symbol">:</a> <a id="2618" href="Definition.Modality.Context.html#627" class="Datatype">Conₘ</a> <a id="2623" class="Symbol">(</a><a id="2624" href="Tools.Nat.html#408" class="InductiveConstructor">1+</a> <a id="2627" href="Definition.Modality.Context.Properties.html#1123" class="Generalizable">n</a><a id="2628" class="Symbol">)}</a> <a id="2631" class="Symbol">→</a> <a id="2633" href="Definition.Modality.Context.Properties.html#2612" class="Bound">γ</a> <a id="2635" href="Definition.Modality.Context.html#769" class="Datatype Operator">≈ᶜ</a> <a id="2638" href="Definition.Modality.Context.Properties.html#2614" class="Bound">δ</a> <a id="2640" class="Symbol">→</a> <a id="2642" href="Definition.Modality.Context.html#940" class="Function">tailₘ</a> <a id="2648" href="Definition.Modality.Context.Properties.html#2612" class="Bound">γ</a> <a id="2650" href="Definition.Modality.Context.html#769" class="Datatype Operator">≈ᶜ</a> <a id="2653" href="Definition.Modality.Context.html#940" class="Function">tailₘ</a> <a id="2659" href="Definition.Modality.Context.Properties.html#2614" class="Bound">δ</a>
<a id="2661" href="Definition.Modality.Context.Properties.html#2598" class="Function">tailₘ-cong</a> <a id="2672" class="Symbol">(</a><a id="2673" href="Definition.Modality.Context.Properties.html#2673" class="Bound">γ≈δ</a> <a id="2677" href="Definition.Modality.Context.html#820" class="InductiveConstructor Operator">∙</a> <a id="2679" href="Definition.Modality.Context.Properties.html#2679" class="Bound">p≈q</a><a id="2682" class="Symbol">)</a> <a id="2684" class="Symbol">=</a> <a id="2686" href="Definition.Modality.Context.Properties.html#2673" class="Bound">γ≈δ</a>

<a id="2691" class="Comment">-- Congruence of headₘ</a>
<a id="2714" class="Comment">-- If γ ≈ᶜ δ then headₘ γ ≈ᶜ headₘ δ</a>

<a id="headₘ-cong"></a><a id="2752" href="Definition.Modality.Context.Properties.html#2752" class="Function">headₘ-cong</a> <a id="2763" class="Symbol">:</a> <a id="2765" class="Symbol">{</a><a id="2766" href="Definition.Modality.Context.Properties.html#2766" class="Bound">γ</a> <a id="2768" href="Definition.Modality.Context.Properties.html#2768" class="Bound">δ</a> <a id="2770" class="Symbol">:</a> <a id="2772" href="Definition.Modality.Context.html#627" class="Datatype">Conₘ</a> <a id="2777" class="Symbol">(</a><a id="2778" href="Tools.Nat.html#408" class="InductiveConstructor">1+</a> <a id="2781" href="Definition.Modality.Context.Properties.html#1123" class="Generalizable">n</a><a id="2782" class="Symbol">)}</a> <a id="2785" class="Symbol">→</a> <a id="2787" href="Definition.Modality.Context.Properties.html#2766" class="Bound">γ</a> <a id="2789" href="Definition.Modality.Context.html#769" class="Datatype Operator">≈ᶜ</a> <a id="2792" href="Definition.Modality.Context.Properties.html#2768" class="Bound">δ</a> <a id="2794" class="Symbol">→</a> <a id="2796" href="Definition.Modality.Context.html#891" class="Function">headₘ</a> <a id="2802" href="Definition.Modality.Context.Properties.html#2766" class="Bound">γ</a> <a id="2804" href="Relation.Binary.Bundles.html#1098" class="Function Operator">≈</a> <a id="2806" href="Definition.Modality.Context.html#891" class="Function">headₘ</a> <a id="2812" href="Definition.Modality.Context.Properties.html#2768" class="Bound">δ</a>
<a id="2814" href="Definition.Modality.Context.Properties.html#2752" class="Function">headₘ-cong</a> <a id="2825" class="Symbol">(</a><a id="2826" href="Definition.Modality.Context.Properties.html#2826" class="Bound">γ≈δ</a> <a id="2830" href="Definition.Modality.Context.html#820" class="InductiveConstructor Operator">∙</a> <a id="2832" href="Definition.Modality.Context.Properties.html#2832" class="Bound">p≈q</a><a id="2835" class="Symbol">)</a> <a id="2837" class="Symbol">=</a> <a id="2839" href="Definition.Modality.Context.Properties.html#2832" class="Bound">p≈q</a>

<a id="2844" class="Comment">-- tailₘ is monotone</a>
<a id="2865" class="Comment">-- If γ ≤ᶜ δ then tailₘ γ ≤ᶜ tailₘ δ</a>

<a id="tailₘ-monotone"></a><a id="2903" href="Definition.Modality.Context.Properties.html#2903" class="Function">tailₘ-monotone</a> <a id="2918" class="Symbol">:</a> <a id="2920" class="Symbol">{</a><a id="2921" href="Definition.Modality.Context.Properties.html#2921" class="Bound">γ</a> <a id="2923" href="Definition.Modality.Context.Properties.html#2923" class="Bound">δ</a> <a id="2925" class="Symbol">:</a> <a id="2927" href="Definition.Modality.Context.html#627" class="Datatype">Conₘ</a> <a id="2932" class="Symbol">(</a><a id="2933" href="Tools.Nat.html#408" class="InductiveConstructor">1+</a> <a id="2936" href="Definition.Modality.Context.Properties.html#1123" class="Generalizable">n</a><a id="2937" class="Symbol">)}</a> <a id="2940" class="Symbol">→</a> <a id="2942" href="Definition.Modality.Context.Properties.html#2921" class="Bound">γ</a> <a id="2944" href="Definition.Modality.Context.html#1790" class="Function Operator">≤ᶜ</a> <a id="2947" href="Definition.Modality.Context.Properties.html#2923" class="Bound">δ</a> <a id="2949" class="Symbol">→</a> <a id="2951" href="Definition.Modality.Context.html#940" class="Function">tailₘ</a> <a id="2957" href="Definition.Modality.Context.Properties.html#2921" class="Bound">γ</a> <a id="2959" href="Definition.Modality.Context.html#1790" class="Function Operator">≤ᶜ</a> <a id="2962" href="Definition.Modality.Context.html#940" class="Function">tailₘ</a> <a id="2968" href="Definition.Modality.Context.Properties.html#2923" class="Bound">δ</a>
<a id="2970" href="Definition.Modality.Context.Properties.html#2903" class="Function">tailₘ-monotone</a> <a id="2985" class="Symbol">{</a><a id="2986" class="Argument">γ</a> <a id="2988" class="Symbol">=</a> <a id="2990" href="Definition.Modality.Context.Properties.html#2990" class="Bound">γ</a> <a id="2992" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="2994" href="Definition.Modality.Context.Properties.html#2994" class="Bound">p</a><a id="2995" class="Symbol">}</a> <a id="2997" class="Symbol">{</a><a id="2998" href="Definition.Modality.Context.Properties.html#2998" class="Bound">δ</a> <a id="3000" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="3002" href="Definition.Modality.Context.Properties.html#3002" class="Bound">q</a><a id="3003" class="Symbol">}</a> <a id="3005" class="Symbol">(</a><a id="3006" href="Definition.Modality.Context.Properties.html#3006" class="Bound">γ≤δ</a> <a id="3010" href="Definition.Modality.Context.html#820" class="InductiveConstructor Operator">∙</a> <a id="3012" href="Definition.Modality.Context.Properties.html#3012" class="Bound">p≤q</a><a id="3015" class="Symbol">)</a> <a id="3017" class="Symbol">=</a> <a id="3019" href="Definition.Modality.Context.Properties.html#3006" class="Bound">γ≤δ</a>

<a id="3024" class="Comment">-- headₘ is monotone</a>
<a id="3045" class="Comment">-- If γ ≤ᶜ δ then headₘ γ ≤ᶜ headₘ δ</a>

<a id="headₘ-monotone"></a><a id="3083" href="Definition.Modality.Context.Properties.html#3083" class="Function">headₘ-monotone</a> <a id="3098" class="Symbol">:</a> <a id="3100" class="Symbol">{</a><a id="3101" href="Definition.Modality.Context.Properties.html#3101" class="Bound">γ</a> <a id="3103" href="Definition.Modality.Context.Properties.html#3103" class="Bound">δ</a> <a id="3105" class="Symbol">:</a> <a id="3107" href="Definition.Modality.Context.html#627" class="Datatype">Conₘ</a> <a id="3112" class="Symbol">(</a><a id="3113" href="Tools.Nat.html#408" class="InductiveConstructor">1+</a> <a id="3116" href="Definition.Modality.Context.Properties.html#1123" class="Generalizable">n</a><a id="3117" class="Symbol">)}</a> <a id="3120" class="Symbol">→</a> <a id="3122" href="Definition.Modality.Context.Properties.html#3101" class="Bound">γ</a> <a id="3124" href="Definition.Modality.Context.html#1790" class="Function Operator">≤ᶜ</a> <a id="3127" href="Definition.Modality.Context.Properties.html#3103" class="Bound">δ</a> <a id="3129" class="Symbol">→</a> <a id="3131" href="Definition.Modality.Context.html#891" class="Function">headₘ</a> <a id="3137" href="Definition.Modality.Context.Properties.html#3101" class="Bound">γ</a> <a id="3139" href="Definition.Modality.html#1241" class="Function Operator">≤</a> <a id="3141" href="Definition.Modality.Context.html#891" class="Function">headₘ</a> <a id="3147" href="Definition.Modality.Context.Properties.html#3103" class="Bound">δ</a>
<a id="3149" href="Definition.Modality.Context.Properties.html#3083" class="Function">headₘ-monotone</a> <a id="3164" class="Symbol">{</a><a id="3165" class="Argument">γ</a> <a id="3167" class="Symbol">=</a> <a id="3169" href="Definition.Modality.Context.Properties.html#3169" class="Bound">γ</a> <a id="3171" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="3173" href="Definition.Modality.Context.Properties.html#3173" class="Bound">p</a><a id="3174" class="Symbol">}</a> <a id="3176" class="Symbol">{</a><a id="3177" href="Definition.Modality.Context.Properties.html#3177" class="Bound">δ</a> <a id="3179" href="Definition.Modality.Context.html#669" class="InductiveConstructor Operator">∙</a> <a id="3181" href="Definition.Modality.Context.Properties.html#3181" class="Bound">q</a><a id="3182" class="Symbol">}</a> <a id="3184" class="Symbol">(</a><a id="3185" href="Definition.Modality.Context.Properties.html#3185" class="Bound">γ≤δ</a> <a id="3189" href="Definition.Modality.Context.html#820" class="InductiveConstructor Operator">∙</a> <a id="3191" href="Definition.Modality.Context.Properties.html#3191" class="Bound">p≤q</a><a id="3194" class="Symbol">)</a> <a id="3196" class="Symbol">=</a> <a id="3198" href="Definition.Modality.Context.Properties.html#3191" class="Bound">p≤q</a>

<a id="3203" class="Comment">------------------------------------------------------------------------</a>
<a id="3276" class="Comment">-- Properties that hold if 𝟙 ≈ 𝟘</a>

<a id="3310" class="Comment">-- If 𝟙 ≈ 𝟘, then every vector is equal to 𝟘ᶜ.</a>

<a id="≈ᶜ𝟘ᶜ"></a><a id="3358" href="Definition.Modality.Context.Properties.html#3358" class="Function">≈ᶜ𝟘ᶜ</a> <a id="3363" class="Symbol">:</a> <a id="3365" href="Definition.Modality.html#817" class="Function">𝟙</a> <a id="3367" href="Relation.Binary.Bundles.html#1098" class="Function Operator">≈</a> <a id="3369" href="Definition.Modality.html#807" class="Function">𝟘</a> <a id="3371" class="Symbol">→</a> <a id="3373" href="Definition.Modality.Context.Properties.html#1152" class="Generalizable">γ</a> <a id="3375" href="Definition.Modality.Context.html#769" class="Datatype Operator">≈ᶜ</a> <a id="3378" href="Definition.Modality.Context.html#2011" class="Function">𝟘ᶜ</a>
<a id="3381" href="Definition.Modality.Context.Properties.html#3358" class="Function">≈ᶜ𝟘ᶜ</a> <a id="3386" class="Symbol">{</a><a id="3387" class="Argument">γ</a> <a id="3389" class="Symbol">=</a> <a id="3391" href="Definition.Modality.Context.Properties.html#3391" class="Bound">γ</a><a id="3392" class="Symbol">}</a> <a id="3394" href="Definition.Modality.Context.Properties.html#3394" class="Bound">𝟙≈𝟘</a> <a id="3398" class="Symbol">=</a> <a id="3400" href="Relation.Binary.Reasoning.Base.Double.html#2213" class="Function Operator">begin</a>
  <a id="3408" href="Definition.Modality.Context.Properties.html#3391" class="Bound">γ</a>       <a id="3416" href="Relation.Binary.Reasoning.Base.Double.html#2963" class="Function">≈˘⟨</a> <a id="3420" href="Definition.Modality.Context.Properties.Multiplication.html#758" class="Function">·ᶜ-identityˡ</a> <a id="3433" class="Symbol">_</a> <a id="3435" href="Relation.Binary.Reasoning.Base.Double.html#2963" class="Function">⟩</a>
  <a id="3439" href="Definition.Modality.html#817" class="Function">𝟙</a> <a id="3441" href="Definition.Modality.Context.html#1668" class="Function Operator">·ᶜ</a> <a id="3444" href="Definition.Modality.Context.Properties.html#3391" class="Bound">γ</a>  <a id="3447" href="Relation.Binary.Reasoning.Base.Double.html#2721" class="Function">≈⟨</a> <a id="3450" href="Definition.Modality.Context.Properties.Multiplication.html#3144" class="Function">·ᶜ-congʳ</a> <a id="3459" href="Definition.Modality.Context.Properties.html#3394" class="Bound">𝟙≈𝟘</a> <a id="3463" href="Relation.Binary.Reasoning.Base.Double.html#2721" class="Function">⟩</a>
  <a id="3467" href="Definition.Modality.html#807" class="Function">𝟘</a> <a id="3469" href="Definition.Modality.Context.html#1668" class="Function Operator">·ᶜ</a> <a id="3472" href="Definition.Modality.Context.Properties.html#3391" class="Bound">γ</a>  <a id="3475" href="Relation.Binary.Reasoning.Base.Double.html#2721" class="Function">≈⟨</a> <a id="3478" href="Definition.Modality.Context.Properties.Multiplication.html#955" class="Function">·ᶜ-zeroˡ</a> <a id="3487" class="Symbol">_</a> <a id="3489" href="Relation.Binary.Reasoning.Base.Double.html#2721" class="Function">⟩</a>
  <a id="3493" href="Definition.Modality.Context.html#2011" class="Function">𝟘ᶜ</a>      <a id="3501" href="Relation.Binary.Reasoning.Base.Double.html#3647" class="Function Operator">∎</a>
  <a id="3505" class="Keyword">where</a>
  <a id="3513" class="Keyword">open</a> <a id="3518" href="Tools.Reasoning.Equivalence.html" class="Module">Tools.Reasoning.Equivalence</a> <a id="3546" href="Definition.Modality.Context.Properties.Equivalence.html#1231" class="Function">Conₘ-setoid</a>

<a id="3559" class="Comment">-- If 𝟙 ≈ 𝟘, then _≈ᶜ_ is trivial.</a>

<a id="≈ᶜ-trivial"></a><a id="3595" href="Definition.Modality.Context.Properties.html#3595" class="Function">≈ᶜ-trivial</a> <a id="3606" class="Symbol">:</a> <a id="3608" href="Definition.Modality.html#817" class="Function">𝟙</a> <a id="3610" href="Relation.Binary.Bundles.html#1098" class="Function Operator">≈</a> <a id="3612" href="Definition.Modality.html#807" class="Function">𝟘</a> <a id="3614" class="Symbol">→</a> <a id="3616" href="Definition.Modality.Context.Properties.html#1152" class="Generalizable">γ</a> <a id="3618" href="Definition.Modality.Context.html#769" class="Datatype Operator">≈ᶜ</a> <a id="3621" href="Definition.Modality.Context.Properties.html#1157" class="Generalizable">δ</a>
<a id="3623" href="Definition.Modality.Context.Properties.html#3595" class="Function">≈ᶜ-trivial</a> <a id="3634" class="Symbol">{</a><a id="3635" class="Argument">γ</a> <a id="3637" class="Symbol">=</a> <a id="3639" href="Definition.Modality.Context.Properties.html#3639" class="Bound">γ</a><a id="3640" class="Symbol">}</a> <a id="3642" class="Symbol">{</a><a id="3643" class="Argument">δ</a> <a id="3645" class="Symbol">=</a> <a id="3647" href="Definition.Modality.Context.Properties.html#3647" class="Bound">δ</a><a id="3648" class="Symbol">}</a> <a id="3650" href="Definition.Modality.Context.Properties.html#3650" class="Bound">𝟙≈𝟘</a> <a id="3654" class="Symbol">=</a> <a id="3656" href="Relation.Binary.Reasoning.Base.Double.html#2213" class="Function Operator">begin</a>
  <a id="3664" href="Definition.Modality.Context.Properties.html#3639" class="Bound">γ</a>   <a id="3668" href="Relation.Binary.Reasoning.Base.Double.html#2721" class="Function">≈⟨</a> <a id="3671" href="Definition.Modality.Context.Properties.html#3358" class="Function">≈ᶜ𝟘ᶜ</a> <a id="3676" href="Definition.Modality.Context.Properties.html#3650" class="Bound">𝟙≈𝟘</a> <a id="3680" href="Relation.Binary.Reasoning.Base.Double.html#2721" class="Function">⟩</a>
  <a id="3684" href="Definition.Modality.Context.html#2011" class="Function">𝟘ᶜ</a>  <a id="3688" href="Relation.Binary.Reasoning.Base.Double.html#2963" class="Function">≈˘⟨</a> <a id="3692" href="Definition.Modality.Context.Properties.html#3358" class="Function">≈ᶜ𝟘ᶜ</a> <a id="3697" href="Definition.Modality.Context.Properties.html#3650" class="Bound">𝟙≈𝟘</a> <a id="3701" href="Relation.Binary.Reasoning.Base.Double.html#2963" class="Function">⟩</a>
  <a id="3705" href="Definition.Modality.Context.Properties.html#3647" class="Bound">δ</a>   <a id="3709" href="Relation.Binary.Reasoning.Base.Double.html#3647" class="Function Operator">∎</a>
  <a id="3713" class="Keyword">where</a>
  <a id="3721" class="Keyword">open</a> <a id="3726" href="Tools.Reasoning.Equivalence.html" class="Module">Tools.Reasoning.Equivalence</a> <a id="3754" href="Definition.Modality.Context.Properties.Equivalence.html#1231" class="Function">Conₘ-setoid</a>
</pre></body></html>