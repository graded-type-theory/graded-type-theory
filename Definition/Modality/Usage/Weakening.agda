------------------------------------------------------------------------
-- The usage relation is closed under weakening.
------------------------------------------------------------------------

open import Definition.Modality using (Modality)
open import Definition.Modality.Usage.Restrictions

module Definition.Modality.Usage.Weakening
  {a} {M : Set a}
  (ğ•„ : Modality M)
  (R : Usage-restrictions M)
  where

open Modality ğ•„

open import Definition.Modality M hiding (Modality)
open import Definition.Modality.Context ğ•„
open import Definition.Modality.Context.Properties ğ•„
import Definition.Modality.Properties.Has-well-behaved-zero as WBğŸ˜
open import Definition.Modality.Properties.PartialOrder
  semiring-with-meet
open import Definition.Modality.Properties.Star
  semiring-with-meet-and-star
open import Definition.Modality.Usage ğ•„ R
open import Definition.Modality.Usage.Properties ğ•„ R
open import Definition.Mode ğ•„
open import Definition.Untyped M hiding (_âˆ™_ ; subst)
open import Definition.Untyped.Inversion M

open import Tools.Bool using (T)
open import Tools.Fin
open import Tools.Function
open import Tools.Nat renaming (_+_ to _+â¿_)
open import Tools.Product
open import Tools.PropositionalEquality as PE
import Tools.Reasoning.PartialOrder
import Tools.Reasoning.PropositionalEquality
open import Tools.Sum

private
  variable
    â„“ n m : Nat
    x : Fin n
    Ï : Wk m n
    p r : M
    Î³ Î³â€² Î´ Î· Î¸ : Conâ‚˜ n
    t tâ€² : Term n
    mâ€² : Mode

-- Weakenings of modality contexts

wkConâ‚˜ : Wk m n â†’ Conâ‚˜ n â†’ Conâ‚˜ m
wkConâ‚˜ id Î³ = Î³
wkConâ‚˜ (step Ï) Î³ = (wkConâ‚˜ Ï Î³) âˆ™ ğŸ˜
wkConâ‚˜ (lift Ï) (Î³ âˆ™ p) = wkConâ‚˜ Ï Î³ âˆ™ p

-- Weakening the zero context is the zero context
-- wkConâ‚˜ Ï ğŸ˜á¶œ â‰¡ ğŸ˜á¶œ

wk-ğŸ˜á¶œ : (Ï : Wk m n) â†’ wkConâ‚˜ Ï ğŸ˜á¶œ â‰¡ ğŸ˜á¶œ
wk-ğŸ˜á¶œ id = PE.refl
wk-ğŸ˜á¶œ (step Ï) = cong (Î» Î³ â†’ Î³ âˆ™ ğŸ˜) (wk-ğŸ˜á¶œ Ï)
wk-ğŸ˜á¶œ (lift Ï) = cong (Î» Î³ â†’ Î³ âˆ™ ğŸ˜) (wk-ğŸ˜á¶œ Ï)

-- Weakening of modality contexts distribute over addition
-- wkConâ‚˜ Ï (Î³ +á¶œ Î´) â‰ˆá¶œ wkConâ‚˜ Ï Î³ +á¶œ wkConâ‚˜ Ï Î´

wk-+á¶œ : (Ï : Wk m n) â†’ wkConâ‚˜ Ï (Î³ +á¶œ Î´) â‰ˆá¶œ wkConâ‚˜ Ï Î³ +á¶œ wkConâ‚˜ Ï Î´
wk-+á¶œ id = â‰ˆá¶œ-refl
wk-+á¶œ (step Ï) = (wk-+á¶œ Ï) âˆ™ (â‰ˆ-sym (+-identityË¡ ğŸ˜))
wk-+á¶œ {Î³ = Î³ âˆ™ p} {Î´ âˆ™ q} (lift Ï) = (wk-+á¶œ Ï) âˆ™ â‰ˆ-refl

-- Weakening of modality contexts distribute over multiplication
-- wkConâ‚˜ Ï (p Â·á¶œ Î³) â‰ˆá¶œ p Â·á¶œ wkConâ‚˜ Ï Î³

wk-Â·á¶œ : (Ï : Wk m n) â†’ wkConâ‚˜ Ï (p Â·á¶œ Î³) â‰ˆá¶œ p Â·á¶œ wkConâ‚˜ Ï Î³
wk-Â·á¶œ id = â‰ˆá¶œ-refl
wk-Â·á¶œ (step Ï) = (wk-Â·á¶œ Ï) âˆ™ (â‰ˆ-sym (Â·-zeroÊ³ _))
wk-Â·á¶œ {Î³ = Î³ âˆ™ p} (lift Ï) = (wk-Â·á¶œ Ï) âˆ™ â‰ˆ-refl

-- Weakening of modality contexts distribute over meet
-- wkConâ‚˜ Ï (Î³ âˆ§á¶œ Î´) â‰ˆá¶œ wkConâ‚˜ Ï Î³ âˆ§á¶œ wkConâ‚˜ Ï Î´

wk-âˆ§á¶œ : (Ï : Wk m n) â†’ wkConâ‚˜ Ï (Î³ âˆ§á¶œ Î´) â‰ˆá¶œ wkConâ‚˜ Ï Î³ âˆ§á¶œ wkConâ‚˜ Ï Î´
wk-âˆ§á¶œ id = â‰ˆá¶œ-refl
wk-âˆ§á¶œ (step Ï) = (wk-âˆ§á¶œ Ï) âˆ™ (â‰ˆ-sym (âˆ§-idem ğŸ˜))
wk-âˆ§á¶œ {Î³ = Î³ âˆ™ p} {Î´ âˆ™ q} (lift Ï) = (wk-âˆ§á¶œ Ï) âˆ™ â‰ˆ-refl

-- Weakening of modality contexts distribute over the reccurence operator
-- wkConâ‚˜ Ï (Î³ âŠ›áµ£ Î´) â‰ˆá¶œ (wkConâ‚˜ Ï Î³) âŠ›áµ£ (wkConâ‚˜ Ï Î´)

wk-âŠ›á¶œ : (Ï : Wk m n) â†’ wkConâ‚˜ Ï (Î³ âŠ›á¶œ Î´ â–· r) â‰ˆá¶œ (wkConâ‚˜ Ï Î³) âŠ›á¶œ (wkConâ‚˜ Ï Î´) â–· r
wk-âŠ›á¶œ id = â‰ˆá¶œ-refl
wk-âŠ›á¶œ (step Ï) = wk-âŠ›á¶œ Ï âˆ™ â‰ˆ-sym (âŠ›-idem-ğŸ˜ _)
wk-âŠ›á¶œ {Î³ = Î³ âˆ™ p} {Î´ âˆ™ q} (lift Ï) = wk-âŠ›á¶œ Ï âˆ™ â‰ˆ-refl

-- Weakening of modality contexts is monotone
-- If Î³ â‰¤á¶œ Î´ then wkConâ‚˜ Ï Î³ â‰¤á¶œ wkConâ‚˜ Ï Î´

wk-â‰¤á¶œ : (Ï : Wk m n) â†’ Î³ â‰¤á¶œ Î´ â†’ wkConâ‚˜ Ï Î³ â‰¤á¶œ wkConâ‚˜ Ï Î´
wk-â‰¤á¶œ id Î³â‰¤Î´ = Î³â‰¤Î´
wk-â‰¤á¶œ (step Ï) Î³â‰¤Î´ = (wk-â‰¤á¶œ Ï Î³â‰¤Î´) âˆ™ â‰¤-refl
wk-â‰¤á¶œ {Î³ = Î³ âˆ™ p} {Î´ âˆ™ q} (lift Ï) (Î³â‰¤Î´ âˆ™ pâ‰¤q) = wk-â‰¤á¶œ Ï Î³â‰¤Î´ âˆ™ pâ‰¤q

-- Lemma for usage of weakened variables

wkUsageVar : (Ï : Wk m n) â†’ (x : Fin n)
           â†’ wkConâ‚˜ Ï (ğŸ˜á¶œ , x â‰” p) â‰¡ ğŸ˜á¶œ , wkVar Ï x â‰” p
wkUsageVar id x = PE.refl
wkUsageVar (step Ï) x = cong (Î» Î³ â†’ Î³ âˆ™ ğŸ˜) (wkUsageVar Ï x)
wkUsageVar (lift Ï) x0 = cong (Î» Î³ â†’ Î³ âˆ™ _) (wk-ğŸ˜á¶œ Ï)
wkUsageVar (lift Ï) (x +1) = cong (Î» Î³ â†’ Î³ âˆ™ ğŸ˜) (wkUsageVar Ï x)

-- Usage of weakened terms: if Î³Â â–¸[Â mÂ ]Â t then
-- wkConâ‚˜Â ÏÂ Î³Â â–¸[Â mÂ ]Â wkÂ ÏÂ t.

wkUsage :
  {Î³ : Conâ‚˜ n} â†’ (Ï : Wk m n) â†’ Î³ â–¸[ mâ€² ] t â†’ wkConâ‚˜ Ï Î³ â–¸[ mâ€² ] wk Ï t
wkUsage Ï Uâ‚˜ = PE.subst (Î» Î³ â†’ Î³ â–¸[ _ ] U) (PE.sym (wk-ğŸ˜á¶œ Ï)) Uâ‚˜
wkUsage Ï â„•â‚˜ = PE.subst (Î» Î³ â†’ Î³ â–¸[ _ ] â„•) (PE.sym (wk-ğŸ˜á¶œ Ï)) â„•â‚˜
wkUsage Ï Emptyâ‚˜ =
  PE.subst (Î» Î³ â†’ Î³ â–¸[ _ ] Empty) (PE.sym (wk-ğŸ˜á¶œ Ï)) Emptyâ‚˜
wkUsage Ï Unitâ‚˜ =
  PE.subst (Î» Î³ â†’ Î³ â–¸[ _ ] Unit) (PE.sym (wk-ğŸ˜á¶œ Ï)) Unitâ‚˜
wkUsage Ï (Î Î£â‚˜ Î³â–¸F Î´â–¸G) =
  sub (Î Î£â‚˜ (wkUsage Ï Î³â–¸F) (wkUsage (lift Ï) Î´â–¸G))
      (â‰¤á¶œ-reflexive (wk-+á¶œ Ï))
wkUsage Ï var =
  PE.subst (Î» Î³ â†’ Î³ â–¸[ _ ] wk Ï (var _)) (PE.sym (wkUsageVar Ï _)) var
wkUsage Ï (lamâ‚˜ Î³â–¸t) = lamâ‚˜ (wkUsage (lift Ï) Î³â–¸t)
wkUsage Ï (Î³â–¸t âˆ˜â‚˜ Î´â–¸u) =
  sub ((wkUsage Ï Î³â–¸t) âˆ˜â‚˜ (wkUsage Ï Î´â–¸u))
      (â‰¤á¶œ-reflexive (â‰ˆá¶œ-trans (wk-+á¶œ Ï) (+á¶œ-congË¡ (wk-Â·á¶œ Ï))))
wkUsage Ï (prodáµ£â‚˜ Î³â–¸t Î´â–¸u) =
  sub (prodáµ£â‚˜ (wkUsage Ï Î³â–¸t) (wkUsage Ï Î´â–¸u))
      (â‰¤á¶œ-reflexive (â‰ˆá¶œ-trans (wk-+á¶œ Ï) (+á¶œ-congÊ³ (wk-Â·á¶œ Ï))))
wkUsage Ï (prodâ‚šâ‚˜ Î³â–¸t Î³â–¸u) = sub
  (prodâ‚šâ‚˜ (wkUsage Ï Î³â–¸t) (wkUsage Ï Î³â–¸u))
  (â‰¤á¶œ-reflexive (â‰ˆá¶œ-trans (wk-âˆ§á¶œ Ï) (âˆ§á¶œ-congÊ³ (wk-Â·á¶œ Ï))))
wkUsage Ï (fstâ‚˜ m Î³â–¸t PE.refl ok) = fstâ‚˜ m (wkUsage Ï Î³â–¸t) PE.refl ok
wkUsage Ï (sndâ‚˜ Î³â–¸t) = sndâ‚˜ (wkUsage Ï Î³â–¸t)
wkUsage Ï (prodrecâ‚˜ Î³â–¸t Î´â–¸u Î·â–¸A ok) =
  sub (prodrecâ‚˜ (wkUsage Ï Î³â–¸t) (wkUsage (liftn Ï 2) Î´â–¸u)
         (wkUsage (lift Ï) Î·â–¸A) ok)
    (â‰¤á¶œ-reflexive (â‰ˆá¶œ-trans (wk-+á¶œ Ï) (+á¶œ-congÊ³ (wk-Â·á¶œ Ï))))
wkUsage Ï zeroâ‚˜ =
  PE.subst (Î» Î³ â†’ Î³ â–¸[ _ ] zero) (PE.sym (wk-ğŸ˜á¶œ Ï)) zeroâ‚˜
wkUsage Ï (sucâ‚˜ Î³â–¸t) = sucâ‚˜ (wkUsage Ï Î³â–¸t)
wkUsage Ï (natrecâ‚˜ Î³â–¸z Î´â–¸s Î·â–¸n Î¸â–¸A) =
  sub (natrecâ‚˜ (wkUsage Ï Î³â–¸z) (wkUsage (liftn Ï 2) Î´â–¸s) (wkUsage Ï Î·â–¸n) (wkUsage (lift Ï) Î¸â–¸A))
      (â‰¤á¶œ-reflexive (â‰ˆá¶œ-trans (wk-âŠ›á¶œ Ï)
                              (âŠ›áµ£á¶œ-cong (wk-âˆ§á¶œ Ï)
                                       (â‰ˆá¶œ-trans (wk-+á¶œ Ï) (+á¶œ-congË¡ (wk-Â·á¶œ Ï))))))
wkUsage Ï (Emptyrecâ‚˜ Î³â–¸t Î´â–¸A) =
  sub (Emptyrecâ‚˜ (wkUsage Ï Î³â–¸t) (wkUsage Ï Î´â–¸A)) (â‰¤á¶œ-reflexive (wk-Â·á¶œ Ï))
wkUsage Ï starâ‚˜ = subst (Î» Î³ â†’ Î³ â–¸[ _ ] star) (PE.sym (wk-ğŸ˜á¶œ Ï)) starâ‚˜
wkUsage Ï (sub Î³â–¸t x) = sub (wkUsage Ï Î³â–¸t) (wk-â‰¤á¶œ Ï x)

------------------------------------------------------------------------
-- The function wkConâ‚˜â»Â¹

-- A left inverse of wkConâ‚˜Â Ï.

wkConâ‚˜â»Â¹ : Wk m n â†’ Conâ‚˜ m â†’ Conâ‚˜ n
wkConâ‚˜â»Â¹ id       Î³       = Î³
wkConâ‚˜â»Â¹ (step Ï) (Î³ âˆ™ _) = wkConâ‚˜â»Â¹ Ï Î³
wkConâ‚˜â»Â¹ (lift Ï) (Î³ âˆ™ p) = wkConâ‚˜â»Â¹ Ï Î³ âˆ™ p

-- The function wkConâ‚˜â»Â¹Â Ï is a left inverse of wkConâ‚˜Â Ï.

wkConâ‚˜â»Â¹-wkConâ‚˜ : (Ï : Wk m n) â†’ wkConâ‚˜â»Â¹ Ï (wkConâ‚˜ Ï Î³) â‰¡ Î³
wkConâ‚˜â»Â¹-wkConâ‚˜             id       = refl
wkConâ‚˜â»Â¹-wkConâ‚˜             (step Ï) = wkConâ‚˜â»Â¹-wkConâ‚˜ Ï
wkConâ‚˜â»Â¹-wkConâ‚˜ {Î³ = _ âˆ™ _} (lift Ï) = cong (_âˆ™ _) (wkConâ‚˜â»Â¹-wkConâ‚˜ Ï)

-- The function wkConâ‚˜â»Â¹Â Ï is monotone.

wkConâ‚˜â»Â¹-monotone : (Ï : Wk m n) â†’ Î³ â‰¤á¶œ Î´ â†’ wkConâ‚˜â»Â¹ Ï Î³ â‰¤á¶œ wkConâ‚˜â»Â¹ Ï Î´
wkConâ‚˜â»Â¹-monotone id leq =
  leq
wkConâ‚˜â»Â¹-monotone {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} (step Ï) (leq âˆ™ _) =
  wkConâ‚˜â»Â¹-monotone Ï leq
wkConâ‚˜â»Â¹-monotone {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} (lift Ï) (leqâ‚ âˆ™ leqâ‚‚) =
  wkConâ‚˜â»Â¹-monotone Ï leqâ‚ âˆ™ leqâ‚‚

-- The function wkConâ‚˜â»Â¹Â Ï maps ğŸ˜á¶œ to ğŸ˜á¶œ.

wkConâ‚˜â»Â¹-ğŸ˜á¶œ : (Ï : Wk m n) â†’ wkConâ‚˜â»Â¹ Ï ğŸ˜á¶œ â‰ˆá¶œ ğŸ˜á¶œ
wkConâ‚˜â»Â¹-ğŸ˜á¶œ id       = â‰ˆá¶œ-refl
wkConâ‚˜â»Â¹-ğŸ˜á¶œ (step Ï) = wkConâ‚˜â»Â¹-ğŸ˜á¶œ Ï
wkConâ‚˜â»Â¹-ğŸ˜á¶œ (lift Ï) = wkConâ‚˜â»Â¹-ğŸ˜á¶œ Ï âˆ™ â‰ˆ-refl

-- The function wkConâ‚˜â»Â¹Â Ï commutes with _+á¶œ_.

wkConâ‚˜â»Â¹-+á¶œ :
  (Ï : Wk m n) â†’ wkConâ‚˜â»Â¹ Ï (Î³ +á¶œ Î´) â‰ˆá¶œ wkConâ‚˜â»Â¹ Ï Î³ +á¶œ wkConâ‚˜â»Â¹ Ï Î´
wkConâ‚˜â»Â¹-+á¶œ                         id       = â‰ˆá¶œ-refl
wkConâ‚˜â»Â¹-+á¶œ {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} (step Ï) = wkConâ‚˜â»Â¹-+á¶œ Ï
wkConâ‚˜â»Â¹-+á¶œ {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} (lift Ï) = wkConâ‚˜â»Â¹-+á¶œ Ï âˆ™ â‰ˆ-refl

-- The function wkConâ‚˜â»Â¹Â Ï commutes with _âˆ§á¶œ_.

wkConâ‚˜â»Â¹-âˆ§á¶œ :
  (Ï : Wk m n) â†’ wkConâ‚˜â»Â¹ Ï (Î³ âˆ§á¶œ Î´) â‰ˆá¶œ wkConâ‚˜â»Â¹ Ï Î³ âˆ§á¶œ wkConâ‚˜â»Â¹ Ï Î´
wkConâ‚˜â»Â¹-âˆ§á¶œ                         id       = â‰ˆá¶œ-refl
wkConâ‚˜â»Â¹-âˆ§á¶œ {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} (step Ï) = wkConâ‚˜â»Â¹-âˆ§á¶œ Ï
wkConâ‚˜â»Â¹-âˆ§á¶œ {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} (lift Ï) = wkConâ‚˜â»Â¹-âˆ§á¶œ Ï âˆ™ â‰ˆ-refl

-- The function wkConâ‚˜â»Â¹Â Ï commutes with pÂ Â·á¶œ_.

wkConâ‚˜â»Â¹-Â·á¶œ :
  (Ï : Wk m n) â†’ wkConâ‚˜â»Â¹ Ï (p Â·á¶œ Î³) â‰ˆá¶œ p Â·á¶œ wkConâ‚˜â»Â¹ Ï Î³
wkConâ‚˜â»Â¹-Â·á¶œ             id       = â‰ˆá¶œ-refl
wkConâ‚˜â»Â¹-Â·á¶œ {Î³ = _ âˆ™ _} (step Ï) = wkConâ‚˜â»Â¹-Â·á¶œ Ï
wkConâ‚˜â»Â¹-Â·á¶œ {Î³ = _ âˆ™ _} (lift Ï) = wkConâ‚˜â»Â¹-Â·á¶œ Ï âˆ™ â‰ˆ-refl

-- The function wkConâ‚˜â»Â¹Â Ï commutes with _âŠ›á¶œ_â–·Â r.

wkConâ‚˜â»Â¹-âŠ›á¶œ :
  (Ï : Wk m n) â†’
  wkConâ‚˜â»Â¹ Ï (Î³ âŠ›á¶œ Î´ â–· r) â‰ˆá¶œ wkConâ‚˜â»Â¹ Ï Î³ âŠ›á¶œ wkConâ‚˜â»Â¹ Ï Î´ â–· r
wkConâ‚˜â»Â¹-âŠ›á¶œ                         id       = â‰ˆá¶œ-refl
wkConâ‚˜â»Â¹-âŠ›á¶œ {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} (step Ï) = wkConâ‚˜â»Â¹-âŠ›á¶œ Ï
wkConâ‚˜â»Â¹-âŠ›á¶œ {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} (lift Ï) = wkConâ‚˜â»Â¹-âŠ›á¶œ Ï âˆ™ â‰ˆ-refl

-- The function wkConâ‚˜â»Â¹Â Ï "commutes" in a certain sense with _,_â‰”_.

wkConâ‚˜â»Â¹-,â‰” :
  (Ï : Wk m n) â†’ wkConâ‚˜â»Â¹ Ï (Î³ , wkVar Ï x â‰” p) â‰ˆá¶œ wkConâ‚˜â»Â¹ Ï Î³ , x â‰” p
wkConâ‚˜â»Â¹-,â‰”                        id       = â‰ˆá¶œ-refl
wkConâ‚˜â»Â¹-,â‰” {Î³ = _ âˆ™ _}            (step Ï) = wkConâ‚˜â»Â¹-,â‰” Ï
wkConâ‚˜â»Â¹-,â‰” {Î³ = _ âˆ™ _} {x = x0}   (lift Ï) = â‰ˆá¶œ-refl
wkConâ‚˜â»Â¹-,â‰” {Î³ = _ âˆ™ _} {x = _ +1} (lift Ï) = wkConâ‚˜â»Â¹-,â‰” Ï âˆ™ â‰ˆ-refl

------------------------------------------------------------------------
-- Inversion lemmas

-- A kind of inversion lemma for the usage relation and weakening.

wkUsageâ»Â¹ : Î³ â–¸[ mâ€² ] wk Ï t â†’ wkConâ‚˜â»Â¹ Ï Î³ â–¸[ mâ€² ] t
wkUsageâ»Â¹ â–¸t = wkUsageâ»Â¹â€² â–¸t refl
  where
  open module R {n} =
    Tools.Reasoning.PartialOrder (â‰¤á¶œ-poset {n = n})

  wkUsageâ»Â¹â€² :
    Î³ â–¸[ mâ€² ] tâ€² â†’ wk Ï t â‰¡ tâ€² â†’ wkConâ‚˜â»Â¹ Ï Î³ â–¸[ mâ€² ] t
  wkUsageâ»Â¹â€² {Ï = Ï} = Î» where
      Uâ‚˜ eq â†’
        case wk-U eq of Î» {
          refl â†’
        sub Uâ‚˜ (â‰¤á¶œ-reflexive (wkConâ‚˜â»Â¹-ğŸ˜á¶œ Ï)) }
      â„•â‚˜ eq â†’
        case wk-â„• eq of Î» {
          refl â†’
        sub â„•â‚˜ (â‰¤á¶œ-reflexive (wkConâ‚˜â»Â¹-ğŸ˜á¶œ Ï)) }
      Emptyâ‚˜ eq â†’
        case wk-Empty eq of Î» {
          refl â†’
        sub Emptyâ‚˜ (â‰¤á¶œ-reflexive (wkConâ‚˜â»Â¹-ğŸ˜á¶œ Ï)) }
      Unitâ‚˜ eq â†’
        case wk-Unit eq of Î» {
          refl â†’
        sub Unitâ‚˜ (â‰¤á¶œ-reflexive (wkConâ‚˜â»Â¹-ğŸ˜á¶œ Ï)) }
      (Î Î£â‚˜ â–¸A â–¸B) eq â†’
        case wk-Î Î£ eq of Î» {
          (_ , _ , refl , refl , refl) â†’
        case wkUsageâ»Â¹ â–¸A of Î» {
          â–¸A â†’
        case wkUsageâ»Â¹ â–¸B of Î» {
          â–¸B â†’
        sub (Î Î£â‚˜ â–¸A â–¸B) (â‰¤á¶œ-reflexive (wkConâ‚˜â»Â¹-+á¶œ Ï)) }}}
      (var {m = m}) eq â†’
        case wk-var eq of Î» {
          (x , refl , refl) â†’
        sub var (begin
          wkConâ‚˜â»Â¹ Ï (ğŸ˜á¶œ , wkVar Ï x â‰” âŒœ m âŒ)  â‰ˆâŸ¨ wkConâ‚˜â»Â¹-,â‰” Ï âŸ©
          wkConâ‚˜â»Â¹ Ï ğŸ˜á¶œ , x â‰” âŒœ m âŒ            â‰ˆâŸ¨ update-congË¡ (wkConâ‚˜â»Â¹-ğŸ˜á¶œ Ï) âŸ©
          ğŸ˜á¶œ , x â‰” âŒœ m âŒ                       âˆ) }
      (lamâ‚˜ â–¸t) eq â†’
        case wk-lam eq of Î» {
          (_ , refl , refl) â†’
        lamâ‚˜ (wkUsageâ»Â¹ â–¸t) }
      (_âˆ˜â‚˜_ {Î³ = Î³} {Î´ = Î´} {p = p} â–¸t â–¸u) eq â†’
        case wk-âˆ˜ eq of Î» {
          (_ , _ , refl , refl , refl) â†’
        sub (wkUsageâ»Â¹ â–¸t âˆ˜â‚˜ wkUsageâ»Â¹ â–¸u) (begin
          wkConâ‚˜â»Â¹ Ï (Î³ +á¶œ p Â·á¶œ Î´)             â‰ˆâŸ¨ wkConâ‚˜â»Â¹-+á¶œ Ï âŸ©
          wkConâ‚˜â»Â¹ Ï Î³ +á¶œ wkConâ‚˜â»Â¹ Ï (p Â·á¶œ Î´)  â‰ˆâŸ¨ +á¶œ-congË¡ (wkConâ‚˜â»Â¹-Â·á¶œ Ï) âŸ©
          wkConâ‚˜â»Â¹ Ï Î³ +á¶œ p Â·á¶œ wkConâ‚˜â»Â¹ Ï Î´    âˆ) }
      (prodáµ£â‚˜ {Î³ = Î³} {p = p} {Î´ = Î´} â–¸t â–¸u) eq â†’
        case wk-prod eq of Î» {
          (_ , _ , refl , refl , refl) â†’
        sub (prodáµ£â‚˜ (wkUsageâ»Â¹ â–¸t) (wkUsageâ»Â¹ â–¸u)) (begin
          wkConâ‚˜â»Â¹ Ï (p Â·á¶œ Î³ +á¶œ Î´)             â‰ˆâŸ¨ wkConâ‚˜â»Â¹-+á¶œ Ï âŸ©
          wkConâ‚˜â»Â¹ Ï (p Â·á¶œ Î³) +á¶œ wkConâ‚˜â»Â¹ Ï Î´  â‰ˆâŸ¨ +á¶œ-congÊ³ (wkConâ‚˜â»Â¹-Â·á¶œ Ï) âŸ©
          p Â·á¶œ wkConâ‚˜â»Â¹ Ï Î³ +á¶œ wkConâ‚˜â»Â¹ Ï Î´    âˆ) }
      (prodâ‚šâ‚˜ {Î³ = Î³} {p = p} {Î´ = Î´} â–¸t â–¸u) eq â†’
        case wk-prod eq of Î» {
          (_ , _ , refl , refl , refl) â†’
        sub (prodâ‚šâ‚˜ (wkUsageâ»Â¹ â–¸t) (wkUsageâ»Â¹ â–¸u)) (begin
          wkConâ‚˜â»Â¹ Ï (p Â·á¶œ Î³ âˆ§á¶œ Î´)             â‰ˆâŸ¨ wkConâ‚˜â»Â¹-âˆ§á¶œ Ï âŸ©
          wkConâ‚˜â»Â¹ Ï (p Â·á¶œ Î³) âˆ§á¶œ wkConâ‚˜â»Â¹ Ï Î´  â‰ˆâŸ¨ âˆ§á¶œ-congÊ³ (wkConâ‚˜â»Â¹-Â·á¶œ Ï) âŸ©
          p Â·á¶œ wkConâ‚˜â»Â¹ Ï Î³ âˆ§á¶œ wkConâ‚˜â»Â¹ Ï Î´    âˆ) }
      (fstâ‚˜ m â–¸t refl ok) eq â†’
        case wk-fst eq of Î» {
          (_ , refl , refl) â†’
        fstâ‚˜ m (wkUsageâ»Â¹ â–¸t) refl ok }
      (sndâ‚˜ â–¸t) eq â†’
        case wk-snd eq of Î» {
          (_ , refl , refl) â†’
        sndâ‚˜ (wkUsageâ»Â¹ â–¸t) }
      (prodrecâ‚˜ {Î³ = Î³} {r = r} {Î´ = Î´} â–¸t â–¸u â–¸A ok) eq â†’
        case wk-prodrec eq of Î» {
          (_ , _ , _ , refl , refl , refl , refl) â†’
        sub
          (prodrecâ‚˜ (wkUsageâ»Â¹ â–¸t) (wkUsageâ»Â¹ â–¸u)
             (wkUsageâ»Â¹ â–¸A) ok)
          (begin
             wkConâ‚˜â»Â¹ Ï (r Â·á¶œ Î³ +á¶œ Î´)             â‰ˆâŸ¨ wkConâ‚˜â»Â¹-+á¶œ Ï âŸ©
             wkConâ‚˜â»Â¹ Ï (r Â·á¶œ Î³) +á¶œ wkConâ‚˜â»Â¹ Ï Î´  â‰ˆâŸ¨ +á¶œ-congÊ³ (wkConâ‚˜â»Â¹-Â·á¶œ Ï) âŸ©
             r Â·á¶œ wkConâ‚˜â»Â¹ Ï Î³ +á¶œ wkConâ‚˜â»Â¹ Ï Î´    âˆ) }
      zeroâ‚˜ eq â†’
        case wk-zero eq of Î» {
          refl â†’
        sub zeroâ‚˜ (â‰¤á¶œ-reflexive (wkConâ‚˜â»Â¹-ğŸ˜á¶œ Ï)) }
      (sucâ‚˜ â–¸t) eq â†’
        case wk-suc eq of Î» {
          (_ , refl , refl) â†’
        sucâ‚˜ (wkUsageâ»Â¹ â–¸t) }
      (natrecâ‚˜ {Î³ = Î³} {Î´ = Î´} {p = p} {r = r} {Î· = Î·} â–¸t â–¸u â–¸v â–¸A) eq â†’
        case wk-natrec eq of Î» {
          (_ , _ , _ , _ , refl , refl , refl , refl , refl) â†’
        sub
          (natrecâ‚˜ (wkUsageâ»Â¹ â–¸t) (wkUsageâ»Â¹ â–¸u)
             (wkUsageâ»Â¹ â–¸v) (wkUsageâ»Â¹ â–¸A))
          (begin
             wkConâ‚˜â»Â¹ Ï ((Î³ âˆ§á¶œ Î·) âŠ›á¶œ Î´ +á¶œ p Â·á¶œ Î· â–· r)             â‰ˆâŸ¨ wkConâ‚˜â»Â¹-âŠ›á¶œ Ï âŸ©

             wkConâ‚˜â»Â¹ Ï (Î³ âˆ§á¶œ Î·) âŠ›á¶œ wkConâ‚˜â»Â¹ Ï (Î´ +á¶œ p Â·á¶œ Î·) â–· r  â‰ˆâŸ¨ âŠ›áµ£á¶œ-cong (wkConâ‚˜â»Â¹-âˆ§á¶œ Ï) (wkConâ‚˜â»Â¹-+á¶œ Ï) âŸ©

             (wkConâ‚˜â»Â¹ Ï Î³ âˆ§á¶œ wkConâ‚˜â»Â¹ Ï Î·) âŠ›á¶œ
               wkConâ‚˜â»Â¹ Ï Î´ +á¶œ wkConâ‚˜â»Â¹ Ï (p Â·á¶œ Î·) â–· r            â‰ˆâŸ¨ âŠ›áµ£á¶œ-congË¡ (+á¶œ-congË¡ (wkConâ‚˜â»Â¹-Â·á¶œ Ï)) âŸ©

             (wkConâ‚˜â»Â¹ Ï Î³ âˆ§á¶œ wkConâ‚˜â»Â¹ Ï Î·) âŠ›á¶œ
               wkConâ‚˜â»Â¹ Ï Î´ +á¶œ p Â·á¶œ wkConâ‚˜â»Â¹ Ï Î· â–· r              âˆ) }
      (Emptyrecâ‚˜ â–¸t â–¸A) eq â†’
        case wk-Emptyrec eq of Î» {
          (_ , _ , refl , refl , refl) â†’
        sub (Emptyrecâ‚˜ (wkUsageâ»Â¹ â–¸t) (wkUsageâ»Â¹ â–¸A))
          (â‰¤á¶œ-reflexive (wkConâ‚˜â»Â¹-Â·á¶œ Ï)) }
      starâ‚˜ eq â†’
        case wk-star eq of Î» {
          refl â†’
        sub starâ‚˜ (â‰¤á¶œ-reflexive (wkConâ‚˜â»Â¹-ğŸ˜á¶œ Ï)) }
      (sub â–¸t leq) refl â†’
        sub (wkUsageâ»Â¹ â–¸t) (wkConâ‚˜â»Â¹-monotone Ï leq)

-- An inversion lemma for the usage relation and weakening.

wkUsageâ»Â¹â€² : wkConâ‚˜ Ï Î³ â–¸[ mâ€² ] wk Ï t â†’ Î³ â–¸[ mâ€² ] t
wkUsageâ»Â¹â€² {Ï = Ï} {Î³ = Î³} {mâ€² = mâ€²} {t = t} =
  wkConâ‚˜ Ï Î³ â–¸[ mâ€² ] wk Ï t          â†’âŸ¨ wkUsageâ»Â¹ âŸ©
  wkConâ‚˜â»Â¹ Ï (wkConâ‚˜ Ï Î³) â–¸[ mâ€² ] t  â†’âŸ¨ subst (_â–¸[ _ ] _) (wkConâ‚˜â»Â¹-wkConâ‚˜ Ï) âŸ©
  Î³ â–¸[ mâ€² ] t                        â–¡

-- An inversion lemma for wkConâ‚˜ and ğŸ˜á¶œ.

wkConâ‚˜-ğŸ˜ : wkConâ‚˜ Ï Î³ â‰¤á¶œ ğŸ˜á¶œ â†’ Î³ â‰¤á¶œ ğŸ˜á¶œ
wkConâ‚˜-ğŸ˜ {Ï = Ï} {Î³ = Î³} =
  wkConâ‚˜ Ï Î³ â‰¤á¶œ ğŸ˜á¶œ                          â†’âŸ¨ wkConâ‚˜â»Â¹-monotone Ï âŸ©
  wkConâ‚˜â»Â¹ Ï (wkConâ‚˜ Ï Î³) â‰¤á¶œ wkConâ‚˜â»Â¹ Ï ğŸ˜á¶œ  â†’âŸ¨ substâ‚‚ _â‰¤á¶œ_ (wkConâ‚˜â»Â¹-wkConâ‚˜ Ï) (â‰ˆá¶œâ†’â‰¡ (wkConâ‚˜â»Â¹-ğŸ˜á¶œ Ï)) âŸ©
  Î³ â‰¤á¶œ ğŸ˜á¶œ                                   â–¡

-- An inversion lemma for wkConâ‚˜, wkVar and _,_â‰”_.

wkConâ‚˜-,-wkVar-â‰” :
  (x : Fin n) â†’
  wkConâ‚˜ Ï Î³ â‰¤á¶œ Î´ , wkVar Ï x â‰” p â†’
  âˆƒâ‚‚ Î» Î´â€² pâ€² â†’ Î³ â‰¤á¶œ Î´â€² , x â‰” pâ€² Ã— wkConâ‚˜ Ï Î´â€² â‰¤á¶œ Î´ Ã— pâ€² â‰¤ p
wkConâ‚˜-,-wkVar-â‰” {Ï = id} _ leq =
  _ , _ , leq , â‰¤á¶œ-refl , â‰¤-refl
wkConâ‚˜-,-wkVar-â‰” {Ï = step _} {Î´ = _ âˆ™ _} _ (leqâ‚ âˆ™ leqâ‚‚) =
  case wkConâ‚˜-,-wkVar-â‰” _ leqâ‚ of Î» {
    (_ , _ , leqâ‚ , leqâ‚ƒ , leqâ‚„) â†’
  _ , _ , leqâ‚ , leqâ‚ƒ âˆ™ leqâ‚‚ , leqâ‚„ }
wkConâ‚˜-,-wkVar-â‰” {Ï = lift _} {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} x0 (leqâ‚ âˆ™ leqâ‚‚) =
  _ âˆ™ _ , _ , â‰¤á¶œ-refl , leqâ‚ âˆ™ â‰¤-refl , leqâ‚‚
wkConâ‚˜-,-wkVar-â‰”
  {Ï = lift Ï} {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} (x +1) (leqâ‚ âˆ™ leqâ‚‚) =
  case wkConâ‚˜-,-wkVar-â‰” x leqâ‚ of Î» {
    (_ , _ , leqâ‚ , leqâ‚ƒ , leqâ‚„) â†’
  _ âˆ™ _ , _ , leqâ‚ âˆ™ leqâ‚‚ , leqâ‚ƒ âˆ™ â‰¤-refl , leqâ‚„ }

-- The lemmas in the following anonymous module are defined under the
-- assumption that the zero is well-behaved.

module _
  (ğŸ˜-well-behaved : Has-well-behaved-zero semiring-with-meet)
  where

  open WBğŸ˜ semiring-with-meet-and-star ğŸ˜-well-behaved

  -- An inversion lemma for wkConâ‚˜ and _+á¶œ_.

  wkConâ‚˜-+á¶œ :
    âˆ€ Ï â†’ wkConâ‚˜ Ï Î³ â‰¤á¶œ Î´ +á¶œ Î· â†’
    âˆƒâ‚‚ Î» Î´â€² Î·â€² â†’ Î³ â‰¤á¶œ Î´â€² +á¶œ Î·â€² Ã— wkConâ‚˜ Ï Î´â€² â‰¤á¶œ Î´ Ã— wkConâ‚˜ Ï Î·â€² â‰¤á¶œ Î·
  wkConâ‚˜-+á¶œ id leq =
    _ , _ , leq , â‰¤á¶œ-refl , â‰¤á¶œ-refl
  wkConâ‚˜-+á¶œ {Î´ = _ âˆ™ _} {Î· = _ âˆ™ _} (step _) (leqâ‚ âˆ™ leqâ‚‚) =
    case wkConâ‚˜-+á¶œ _ leqâ‚ of Î» {
      (_ , _ , leqâ‚ , leqâ‚ƒ , leqâ‚„) â†’
    _ , _ , leqâ‚ ,
    leqâ‚ƒ âˆ™ â‰¤-reflexive (PE.sym (+-positiveË¡ (ğŸ˜â‰® leqâ‚‚))) ,
    leqâ‚„ âˆ™ â‰¤-reflexive (PE.sym (+-positiveÊ³ (ğŸ˜â‰® leqâ‚‚))) }
  wkConâ‚˜-+á¶œ
    {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} {Î· = _ âˆ™ _} (lift Ï) (leqâ‚ âˆ™ leqâ‚‚) =
    case wkConâ‚˜-+á¶œ Ï leqâ‚ of Î» {
      (_ , _ , leqâ‚ , leqâ‚ƒ , leqâ‚„) â†’
    _ , _ , leqâ‚ âˆ™ leqâ‚‚ , leqâ‚ƒ âˆ™ â‰¤-refl , leqâ‚„ âˆ™ â‰¤-refl }

  -- An inversion lemma for wkConâ‚˜ and _âˆ§á¶œ_.

  wkConâ‚˜-âˆ§á¶œ :
    âˆ€ Ï â†’ wkConâ‚˜ Ï Î³ â‰¤á¶œ Î´ âˆ§á¶œ Î· â†’
    âˆƒâ‚‚ Î» Î´â€² Î·â€² â†’ Î³ â‰¤á¶œ Î´â€² âˆ§á¶œ Î·â€² Ã— wkConâ‚˜ Ï Î´â€² â‰¤á¶œ Î´ Ã— wkConâ‚˜ Ï Î·â€² â‰¤á¶œ Î·
  wkConâ‚˜-âˆ§á¶œ id leq =
    _ , _ , leq , â‰¤á¶œ-refl , â‰¤á¶œ-refl
  wkConâ‚˜-âˆ§á¶œ {Î´ = _ âˆ™ _} {Î· = _ âˆ™ _} (step _) (leqâ‚ âˆ™ leqâ‚‚) =
    case wkConâ‚˜-âˆ§á¶œ _ leqâ‚ of Î» {
      (_ , _ , leqâ‚ , leqâ‚ƒ , leqâ‚„) â†’
    _ , _ , leqâ‚ ,
    leqâ‚ƒ âˆ™ â‰¤-reflexive (PE.sym (âˆ§-positiveË¡ (ğŸ˜â‰® leqâ‚‚))) ,
    leqâ‚„ âˆ™ â‰¤-reflexive (PE.sym (âˆ§-positiveÊ³ (ğŸ˜â‰® leqâ‚‚))) }
  wkConâ‚˜-âˆ§á¶œ
    {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} {Î· = _ âˆ™ _} (lift Ï) (leqâ‚ âˆ™ leqâ‚‚) =
    case wkConâ‚˜-âˆ§á¶œ Ï leqâ‚ of Î» {
      (_ , _ , leqâ‚ , leqâ‚ƒ , leqâ‚„) â†’
    _ , _ , leqâ‚ âˆ™ leqâ‚‚ , leqâ‚ƒ âˆ™ â‰¤-refl , leqâ‚„ âˆ™ â‰¤-refl }

  -- An inversion lemma for wkConâ‚˜ and _Â·á¶œ_.

  wkConâ‚˜-Â·á¶œ :
    âˆ€ Ï â†’ wkConâ‚˜ Ï Î³ â‰¤á¶œ p Â·á¶œ Î´ â†’
    p â‰¡ ğŸ˜ Ã— Î³ â‰¤á¶œ ğŸ˜á¶œ âŠ
    âˆƒ Î» Î´â€² â†’ Î³ â‰¤á¶œ p Â·á¶œ Î´â€² Ã— wkConâ‚˜ Ï Î´â€² â‰¤á¶œ Î´
  wkConâ‚˜-Â·á¶œ id leq =
    injâ‚‚ (_ , leq , â‰¤á¶œ-refl)
  wkConâ‚˜-Â·á¶œ {Î³ = Î³} {Î´ = _ âˆ™ q} (step Ï) (leqâ‚ âˆ™ leqâ‚‚) =
    case wkConâ‚˜-Â·á¶œ Ï leqâ‚ of Î» where
      (injâ‚ (refl , leqâ‚))      â†’ injâ‚ (refl , leqâ‚)
      (injâ‚‚ (Î´â€² , leqâ‚ , leqâ‚ƒ)) â†’
        case zero-product (ğŸ˜â‰® leqâ‚‚) of Î» where
          (injâ‚‚ refl) â†’ injâ‚‚ (_ , leqâ‚ , leqâ‚ƒ âˆ™ â‰¤-refl)
          (injâ‚ refl) â†’ injâ‚
            ( refl
            , (begin
                 Î³        â‰¤âŸ¨ leqâ‚ âŸ©
                 ğŸ˜ Â·á¶œ Î´â€²  â‰ˆâŸ¨ Â·á¶œ-zeroË¡ _ âŸ©
                 ğŸ˜á¶œ       âˆ)
            )
    where
    open Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  wkConâ‚˜-Â·á¶œ {Î³ = Î³ âˆ™ q} {Î´ = _ âˆ™ r} (lift Ï) (leqâ‚ âˆ™ leqâ‚‚) =
    case wkConâ‚˜-Â·á¶œ Ï leqâ‚ of Î» where
      (injâ‚‚ (_ , leqâ‚ , leqâ‚ƒ)) â†’
        injâ‚‚ (_ , leqâ‚ âˆ™ leqâ‚‚ , leqâ‚ƒ âˆ™ â‰¤-refl)
      (injâ‚ (refl , leqâ‚)) â†’ injâ‚
        ( refl
        , (begin
             Î³ âˆ™ q       â‰¤âŸ¨ leqâ‚ âˆ™ leqâ‚‚ âŸ©
             ğŸ˜á¶œ âˆ™ ğŸ˜ Â· r  â‰ˆâŸ¨ â‰ˆá¶œ-refl âˆ™ Â·-zeroË¡ _ âŸ©
             ğŸ˜á¶œ          âˆ)
        )
    where
    open Tools.Reasoning.PartialOrder â‰¤á¶œ-poset

  -- An inversion lemma for wkConâ‚˜ and _âŠ›á¶œ_â–·_.

  wkConâ‚˜-âŠ›á¶œ :
    âˆ€ Ï â†’ wkConâ‚˜ Ï Î³ â‰¤á¶œ Î´ âŠ›á¶œ Î· â–· r â†’
    âˆƒâ‚‚ Î» Î´â€² Î·â€² â†’ Î³ â‰¤á¶œ Î´â€² âŠ›á¶œ Î·â€² â–· r Ã— wkConâ‚˜ Ï Î´â€² â‰¤á¶œ Î´ Ã— wkConâ‚˜ Ï Î·â€² â‰¤á¶œ Î·
  wkConâ‚˜-âŠ›á¶œ id leq =
    _ , _ , leq , â‰¤á¶œ-refl , â‰¤á¶œ-refl
  wkConâ‚˜-âŠ›á¶œ {Î´ = _ âˆ™ _} {Î· = _ âˆ™ _} (step _) (leqâ‚ âˆ™ leqâ‚‚) =
    case wkConâ‚˜-âŠ›á¶œ _ leqâ‚ of Î» {
      (_ , _ , leqâ‚ , leqâ‚ƒ , leqâ‚„) â†’
    _ , _ , leqâ‚ ,
    leqâ‚ƒ âˆ™ â‰¤-reflexive (PE.sym (âŠ›â‰ˆğŸ˜Ë¡ (ğŸ˜â‰® leqâ‚‚))) ,
    leqâ‚„ âˆ™ â‰¤-reflexive (PE.sym (âŠ›â‰ˆğŸ˜Ê³ (ğŸ˜â‰® leqâ‚‚))) }
  wkConâ‚˜-âŠ›á¶œ
    {Î³ = _ âˆ™ _} {Î´ = _ âˆ™ _} {Î· = _ âˆ™ _} (lift Ï) (leqâ‚ âˆ™ leqâ‚‚) =
    case wkConâ‚˜-âŠ›á¶œ Ï leqâ‚ of Î» {
      (_ , _ , leqâ‚ , leqâ‚ƒ , leqâ‚„) â†’
    _ , _ , leqâ‚ âˆ™ leqâ‚‚ , leqâ‚ƒ âˆ™ â‰¤-refl , leqâ‚„ âˆ™ â‰¤-refl }

  -- An inversion lemma for wkConâ‚˜ and the operation from the conclusion
  -- of the usage rule for natrec.

  wkConâ‚˜-âŠ›á¶œâ€² :
    âˆ€ Ï â†’ wkConâ‚˜ Ï Î³ â‰¤á¶œ (Î´ âˆ§á¶œ Î¸) âŠ›á¶œ Î· +á¶œ p Â·á¶œ Î¸ â–· r â†’
    p â‰¡ ğŸ˜ Ã—
    (âˆƒâ‚ƒ Î» Î´â€² Î·â€² Î¸â€² â†’
       Î³ â‰¤á¶œ (Î´â€² âˆ§á¶œ Î¸â€²) âŠ›á¶œ Î·â€² â–· r Ã—
       wkConâ‚˜ Ï Î´â€² â‰¤á¶œ Î´ Ã— wkConâ‚˜ Ï Î·â€² â‰¤á¶œ Î· Ã— wkConâ‚˜ Ï Î¸â€² â‰¤á¶œ Î¸)
      âŠ
    (âˆƒâ‚ƒ Î» Î´â€² Î·â€² Î¸â€² â†’
       Î³ â‰¤á¶œ (Î´â€² âˆ§á¶œ Î¸â€²) âŠ›á¶œ Î·â€² +á¶œ p Â·á¶œ Î¸â€² â–· r Ã—
       wkConâ‚˜ Ï Î´â€² â‰¤á¶œ Î´ Ã— wkConâ‚˜ Ï Î·â€² â‰¤á¶œ Î· Ã— wkConâ‚˜ Ï Î¸â€² â‰¤á¶œ Î¸)
  wkConâ‚˜-âŠ›á¶œâ€² id leq =
    injâ‚‚ (_ , _ , _ , leq , â‰¤á¶œ-refl , â‰¤á¶œ-refl , â‰¤á¶œ-refl)
  wkConâ‚˜-âŠ›á¶œâ€² {Î´ = _ âˆ™ _} {Î¸ = _ âˆ™ _} {Î· = Î· âˆ™ _}
    (step Ï) (leqâ‚ âˆ™ leqâ‚‚) =
    case zero-product (+-positiveÊ³ (âŠ›â‰ˆğŸ˜Ê³ (ğŸ˜â‰® leqâ‚‚))) of Î» where
      (injâ‚‚ refl) â†’
        case wkConâ‚˜-âŠ›á¶œâ€² Ï leqâ‚ of Î» where
          (injâ‚‚ (_ , _ , _ , leqâ‚ , leqâ‚ƒ , leqâ‚„ , leqâ‚…)) â†’ injâ‚‚
            (_ , _ , _ , leqâ‚ ,
             leqâ‚ƒ
               âˆ™
             â‰¤-reflexive (PE.sym (âˆ§-positiveË¡ (âŠ›â‰ˆğŸ˜Ë¡ (ğŸ˜â‰® leqâ‚‚)))) ,
             leqâ‚„
               âˆ™
             â‰¤-reflexive (PE.sym (+-positiveË¡ (âŠ›â‰ˆğŸ˜Ê³ (ğŸ˜â‰® leqâ‚‚)))) ,
             leqâ‚… âˆ™ â‰¤-refl)
          (injâ‚ (refl , _ , _ , _ , leqâ‚ , leqâ‚ƒ , leqâ‚„ , leqâ‚…)) â†’ injâ‚
            (refl , _ , _ , _ , leqâ‚ ,
             leqâ‚ƒ
               âˆ™
             â‰¤-reflexive (PE.sym (âˆ§-positiveË¡ (âŠ›â‰ˆğŸ˜Ë¡ (ğŸ˜â‰® leqâ‚‚)))) ,
             leqâ‚„
               âˆ™
             â‰¤-reflexive (PE.sym (+-positiveË¡ (âŠ›â‰ˆğŸ˜Ê³ (ğŸ˜â‰® leqâ‚‚)))) ,
             leqâ‚… âˆ™ â‰¤-refl)
      (injâ‚ refl) â†’
        case wkConâ‚˜-âŠ›á¶œâ€² Ï leqâ‚ of Î» where
          (injâ‚‚ (_ , Î·â€² , Î¸â€² , leqâ‚ , leqâ‚ƒ , leqâ‚„ , leqâ‚…)) â†’ injâ‚
            (refl , _ , _ , _ , leqâ‚ ,
             leqâ‚ƒ
               âˆ™
             â‰¤-reflexive (PE.sym (âˆ§-positiveË¡ (âŠ›â‰ˆğŸ˜Ë¡ (ğŸ˜â‰® leqâ‚‚)))) ,
             (begin
                wkConâ‚˜ Ï (Î·â€² +á¶œ ğŸ˜ Â·á¶œ Î¸â€²)  â‰¡âŸ¨ cong (wkConâ‚˜ Ï) (â‰ˆá¶œâ†’â‰¡ (+á¶œ-congË¡ (Â·á¶œ-zeroË¡ _))) âŸ©
                wkConâ‚˜ Ï (Î·â€² +á¶œ ğŸ˜á¶œ)       â‰¡âŸ¨ cong (wkConâ‚˜ Ï) (â‰ˆá¶œâ†’â‰¡ (+á¶œ-identityÊ³ _)) âŸ©
                wkConâ‚˜ Ï Î·â€²               â‰¤âŸ¨ leqâ‚„ âŸ©
                Î·                         âˆ)
               âˆ™
             â‰¤-reflexive (PE.sym (+-positiveË¡ (âŠ›â‰ˆğŸ˜Ê³ (ğŸ˜â‰® leqâ‚‚)))) ,
             leqâ‚…
               âˆ™
             â‰¤-reflexive (PE.sym (âˆ§-positiveÊ³ (âŠ›â‰ˆğŸ˜Ë¡ (ğŸ˜â‰® leqâ‚‚)))))
          (injâ‚ (_ , _ , _ , _ , leqâ‚ , leqâ‚ƒ , leqâ‚„ , leqâ‚…)) â†’ injâ‚
            (refl , _ , _ , _ , leqâ‚ ,
             leqâ‚ƒ
               âˆ™
             â‰¤-reflexive (PE.sym (âˆ§-positiveË¡ (âŠ›â‰ˆğŸ˜Ë¡ (ğŸ˜â‰® leqâ‚‚)))) ,
             leqâ‚„
               âˆ™
             â‰¤-reflexive (PE.sym (+-positiveË¡ (âŠ›â‰ˆğŸ˜Ê³ (ğŸ˜â‰® leqâ‚‚)))) ,
             leqâ‚…
               âˆ™
             â‰¤-reflexive (PE.sym (âˆ§-positiveÊ³ (âŠ›â‰ˆğŸ˜Ë¡ (ğŸ˜â‰® leqâ‚‚)))))
    where
    open Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  wkConâ‚˜-âŠ›á¶œâ€²
    {Î³ = _ âˆ™ pâ‚} {Î´ = _ âˆ™ pâ‚‚} {Î¸ = _ âˆ™ pâ‚ƒ} {Î· = _ âˆ™ pâ‚„} {r = r}
    (lift Ï) (leqâ‚ âˆ™ leqâ‚‚) =
    case wkConâ‚˜-âŠ›á¶œâ€² Ï leqâ‚ of Î» where
          (injâ‚‚ (_ , Î·â€² , Î¸â€² , leqâ‚ , leqâ‚ƒ , leqâ‚„ , leqâ‚…)) â†’ injâ‚‚
            (_ , _ , _ ,
             leqâ‚ âˆ™ leqâ‚‚ ,
             leqâ‚ƒ âˆ™ â‰¤-refl ,
             leqâ‚„ âˆ™ â‰¤-refl ,
             leqâ‚… âˆ™ â‰¤-refl)
          (injâ‚ (refl , _ , _ , _ , leqâ‚ , leqâ‚ƒ , leqâ‚„ , leqâ‚…)) â†’ injâ‚
            (refl , _ , _ , _ ,
             leqâ‚
               âˆ™
             (begin
                pâ‚                           â‰¤âŸ¨ leqâ‚‚ âŸ©
                (pâ‚‚ âˆ§ pâ‚ƒ) âŠ› pâ‚„ + ğŸ˜ Â· pâ‚ƒ â–· r  â‰¡âŸ¨ âŠ›áµ£-congË¡ (+-congË¡ (Â·-zeroË¡ _)) âŸ©
                (pâ‚‚ âˆ§ pâ‚ƒ) âŠ› pâ‚„ + ğŸ˜ â–· r       â‰¡âŸ¨ âŠ›áµ£-congË¡ (+-identityÊ³ _) âŸ©
                (pâ‚‚ âˆ§ pâ‚ƒ) âŠ› pâ‚„ â–· r           âˆ) ,
             leqâ‚ƒ âˆ™ â‰¤-refl ,
             leqâ‚„ âˆ™ â‰¤-refl ,
             leqâ‚… âˆ™ â‰¤-refl)
    where
    open Tools.Reasoning.PartialOrder â‰¤-poset
