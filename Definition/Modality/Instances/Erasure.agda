module Definition.Modality.Instances.Erasure where

open import Tools.Product
open import Tools.PropositionalEquality
open import Tools.Relation
open import Tools.Unit

-- The set of erasure annotations with ğŸ˜ corresponding to no usage
-- and Ï‰ to any usage.

data Erasure : Set where
  ğŸ˜ Ï‰ : Erasure

open import Definition.Modality.Restrictions Erasure
open import Tools.Algebra Erasure

infixl 40 _+_
infixl 40 _âˆ§_
infixl 45 _Â·_
infix  10 _â‰¤_
infix  50 _âŠ›_â–·_

---------------------------------------
-- Operations for erasure anntations --
---------------------------------------

-- Addition of erasure annotations

_+_ : Opâ‚‚ Erasure
ğŸ˜ + q = q
Ï‰ + q = Ï‰

-- Multiplication of erasure annotations

_Â·_ : Opâ‚‚ Erasure
ğŸ˜ Â· q = ğŸ˜
Ï‰ Â· q = q

-- Meet for erasure annotations coincides with addition

_âˆ§_ : Opâ‚‚ Erasure
_âˆ§_ = _+_

-- Natrec-star operators

_âŠ›_â–·_ : Opâ‚ƒ Erasure
p âŠ› q â–· r = p + q


-- Ordering relation for erasure
-- Reflexive closure of Ï‰ â‰¤ ğŸ˜

_â‰¤_ : (p q : Erasure) â†’ Set
p â‰¤ q = p â‰¡ p âˆ§ q

---------------------------------------
-- Properties of addition (and meet) --
---------------------------------------

-- Addition is commutative
-- p + q â‰¡ q + p

+-Commutative : Commutative _+_
+-Commutative ğŸ˜ ğŸ˜ = refl
+-Commutative ğŸ˜ Ï‰ = refl
+-Commutative Ï‰ ğŸ˜ = refl
+-Commutative Ï‰ Ï‰ = refl

-- Addition is associative
-- p + (q + r) â‰¡ (p + q) + r

+-Associative : Associative _+_
+-Associative ğŸ˜ q r = refl
+-Associative Ï‰ q r = refl

-- Addition is idempotent

+-Idempotent : Idempotent _+_
+-Idempotent ğŸ˜ = refl
+-Idempotent Ï‰ = refl

-- ğŸ˜ is a left identity of addition
-- ğŸ˜ + p â‰¡ p

+-LeftIdentity : LeftIdentity ğŸ˜ _+_
+-LeftIdentity p = refl

-- ğŸ˜ is a right identity of addition
-- p + ğŸ˜ â‰¡ p

+-RightIdentity : RightIdentity ğŸ˜ _+_
+-RightIdentity ğŸ˜ = refl
+-RightIdentity Ï‰ = refl

-- ğŸ˜ is an identity of addition
-- ğŸ˜ + p â‰¡ p â‰¡ p + ğŸ˜

+-Identity : Identity ğŸ˜ _+_
+-Identity = +-LeftIdentity , +-RightIdentity

----------------------------------
-- Properties of multiplication --
----------------------------------

-- Multiplication is associative
-- p Â· (q Â· r) â‰¡ (p Â· q) Â· r

Â·-Associative : Associative _Â·_
Â·-Associative ğŸ˜ q r = refl
Â·-Associative Ï‰ q r = refl

-- ğŸ˜ is a left zero for multiplication
-- ğŸ˜ Â· p â‰¡ ğŸ˜

Â·-LeftZero : LeftZero ğŸ˜ _Â·_
Â·-LeftZero p = refl

-- ğŸ˜ is a right zero for multiplication
-- p Â· ğŸ˜ â‰¡ ğŸ˜

Â·-RightZero : RightZero ğŸ˜ _Â·_
Â·-RightZero ğŸ˜ = refl
Â·-RightZero Ï‰ = refl

-- ğŸ˜ is a zero for multiplication
-- ğŸ˜ Â· p â‰¡ ğŸ˜ â‰¡ p Â· ğŸ˜

Â·-zero : Zero ğŸ˜ _Â·_
Â·-zero = Â·-LeftZero , Â·-RightZero

-- Ï‰ is a left identity for multiplication
-- Ï‰ Â· p â‰¡ p

Â·-LeftIdentity : LeftIdentity Ï‰ _Â·_
Â·-LeftIdentity p = refl

-- Ï‰ is a right identity for multiplication
-- p Â· Ï‰ â‰¡ p

Â·-RightIdentity : RightIdentity Ï‰ _Â·_
Â·-RightIdentity ğŸ˜ = refl
Â·-RightIdentity Ï‰ = refl

-- Ï‰ is an identity for multiplication
-- Ï‰ Â· p â‰¡ p â‰¡ p Â· Ï‰

Â·-Identity : Identity Ï‰ _Â·_
Â·-Identity = Â·-LeftIdentity , Â·-RightIdentity

----------------------
-- Properties of âŠ›  --
----------------------

-- p âŠ›áµ£ q is a solution to the inequality x â‰¤ q + rx
-- p âŠ›áµ£ q â‰¤ q + r Â· (p âŠ›áµ£ q)

âŠ›-ineqâ‚ : (p q r : Erasure) â†’ p âŠ› q â–· r â‰¤ q + r Â· p âŠ› q â–· r
âŠ›-ineqâ‚ ğŸ˜ ğŸ˜ ğŸ˜ = refl
âŠ›-ineqâ‚ ğŸ˜ ğŸ˜ Ï‰ = refl
âŠ›-ineqâ‚ ğŸ˜ Ï‰ r = refl
âŠ›-ineqâ‚ Ï‰ q r = refl

-- p âŠ›áµ£ q is a solution to the the inequality x â‰¤ p
-- p âŠ›áµ£ q â‰¤ p

âŠ›-ineqâ‚‚ : (p q r : Erasure) â†’ p âŠ› q â–· r â‰¤ p
âŠ›-ineqâ‚‚ ğŸ˜ ğŸ˜ r = refl
âŠ›-ineqâ‚‚ ğŸ˜ Ï‰ r = refl
âŠ›-ineqâ‚‚ Ï‰ q r = refl

-- Addition is sub-interchangeable with âŠ›áµ£
-- (p âŠ›áµ£ q) + (pâ€² âŠ›áµ£ qâ€²) â‰¤ (p + pâ€²) âŠ›áµ£ (q + qâ€²)

+-sub-interchangeable-âŠ› : (r : Erasure) â†’ _+_ SubInterchangeable (_âŠ›_â–· r) by _â‰¤_
+-sub-interchangeable-âŠ› r ğŸ˜ ğŸ˜ ğŸ˜ ğŸ˜ = refl
+-sub-interchangeable-âŠ› r ğŸ˜ ğŸ˜ ğŸ˜ Ï‰ = refl
+-sub-interchangeable-âŠ› r ğŸ˜ ğŸ˜ Ï‰ qâ€² = refl
+-sub-interchangeable-âŠ› r ğŸ˜ Ï‰ pâ€² qâ€² = refl
+-sub-interchangeable-âŠ› r Ï‰ q pâ€² qâ€² = refl

-- Multiplation right sub-distributes over âŠ›áµ£
-- (p âŠ›áµ£ pâ€²) Â· q â‰¤ (p Â· q) âŠ›áµ£ (pâ€² Â· q)

Â·-sub-distribÊ³-âŠ› : (r : Erasure) â†’ _Â·_ SubDistributesOverÊ³ (_âŠ›_â–· r) by _â‰¤_
Â·-sub-distribÊ³-âŠ› r q ğŸ˜ pâ€² = sym (+-Idempotent (pâ€² Â· q))
Â·-sub-distribÊ³-âŠ› r ğŸ˜ Ï‰ ğŸ˜ = refl
Â·-sub-distribÊ³-âŠ› r ğŸ˜ Ï‰ Ï‰ = refl
Â·-sub-distribÊ³-âŠ› r Ï‰ Ï‰ pâ€² = refl

-- âŠ›áµ£ left sub-distributes over meet
-- p âŠ›áµ£ (q âˆ§ qâ€²) â‰¤ (p âŠ›áµ£ q) âˆ§ (p âŠ›áµ£ qâ€²)

âŠ›-sub-distribË¡-âˆ§ : (r : Erasure) â†’ (_âŠ›_â–· r) SubDistributesOverË¡ _âˆ§_ by _â‰¤_
âŠ›-sub-distribË¡-âˆ§ r ğŸ˜ q qâ€² = sym (+-Idempotent (q + qâ€²))
âŠ›-sub-distribË¡-âˆ§ r Ï‰ q qâ€² = refl

-- âŠ›áµ£ left sub-distributes over meet
-- (p âˆ§ pâ€²) âŠ›áµ£ q â‰¤ (p âŠ›áµ£ q) âˆ§ (pâ€² âŠ›áµ£ q)

âŠ›-sub-distribÊ³-âˆ§ : (r : Erasure) â†’ (_âŠ›_â–· r) SubDistributesOverÊ³ _âˆ§_ by _â‰¤_
âŠ›-sub-distribÊ³-âˆ§ r q Ï‰ pâ€² = refl
âŠ›-sub-distribÊ³-âˆ§ r q ğŸ˜ Ï‰ = refl
âŠ›-sub-distribÊ³-âˆ§ r ğŸ˜ ğŸ˜ ğŸ˜ = refl
âŠ›-sub-distribÊ³-âˆ§ r Ï‰ ğŸ˜ ğŸ˜ = refl

--------------------------------------------------------------------
-- Distributive properties of addition, multiplication (and meet) --
--------------------------------------------------------------------

-- Multiplication is left distributive over addition
-- p Â· (q + r) â‰¡ (p Â· q) + (p Â· r)

Â·-distribË¡-+ : _Â·_ DistributesOverË¡ _+_
Â·-distribË¡-+ ğŸ˜ q r = refl
Â·-distribË¡-+ Ï‰ q r = refl

-- Multiplication is right distributive over addition
-- (q + r) Â· p â‰¡ (q Â· p) + (r Â· p)

Â·-distribÊ³-+ : _Â·_ DistributesOverÊ³ _+_
Â·-distribÊ³-+ p ğŸ˜ r = refl
Â·-distribÊ³-+ ğŸ˜ Ï‰ ğŸ˜ = refl
Â·-distribÊ³-+ ğŸ˜ Ï‰ Ï‰ = refl
Â·-distribÊ³-+ Ï‰ Ï‰ r = refl

-- Multiplication is distributive over addition
-- p Â· (q + r) â‰¡ (p Â· q) + (p Â· r) and (q + r) Â· p â‰¡ (q Â· p) + (r Â· p)

Â·-distrib-+ : _Â·_ DistributesOver _+_
Â·-distrib-+ = Â·-distribË¡-+ , Â·-distribÊ³-+

-- Addition is left distributive over addition
-- p + (q + r) â‰¡ (p + q) + (p + r)

+-distribË¡-+ : _+_ DistributesOverË¡ _+_
+-distribË¡-+ ğŸ˜ q r = refl
+-distribË¡-+ Ï‰ q r = refl

-- Addition is right distributive over addition
-- (q + r) + p â‰¡ (q + p) + (r + p)

+-distribÊ³-+ : _+_ DistributesOverÊ³ _+_
+-distribÊ³-+ p Ï‰ r = refl
+-distribÊ³-+ ğŸ˜ ğŸ˜ r = refl
+-distribÊ³-+ Ï‰ ğŸ˜ ğŸ˜ = refl
+-distribÊ³-+ Ï‰ ğŸ˜ Ï‰ = refl

-- Addition is distributive over addition
-- p + (q + r) â‰¡ (p + q) + (p + r) and (q + r) + p â‰¡ (q + p) + (r + p)

+-distrib-+ : _+_ DistributesOver _+_
+-distrib-+ = +-distribË¡-+ , +-distribÊ³-+

-----------------------------------------------------
-- Addition (and meet) form the following algebras --
-----------------------------------------------------

-- Addition forms a magma

+-Magma : IsMagma _+_
+-Magma = record
  { isEquivalence = isEquivalence
  ; âˆ™-cong        = congâ‚‚ _+_
  }

-- Addition forms a semigroup

+-Semigroup : IsSemigroup _+_
+-Semigroup = record
  { isMagma = +-Magma
  ; assoc   = +-Associative
  }

-- Addition forms a monoid for ğŸ˜ as identity

+-Monoid : IsMonoid _+_ ğŸ˜
+-Monoid = record
  { isSemigroup = +-Semigroup
  ; identity    = +-Identity
  }

-- Addition forms a commutative monoid with ğŸ˜ as identity

+-CommutativeMonoid : IsCommutativeMonoid _+_ ğŸ˜
+-CommutativeMonoid = record
  { isMonoid = +-Monoid
  ; comm     = +-Commutative
  }

-- Addition forms a band

+-Band : IsBand _+_
+-Band = record
  { isSemigroup = +-Semigroup
  ; idem        = +-Idempotent
  }

-- Addition forms a semilattice

+-Semilattice : IsSemilattice _+_
+-Semilattice = record
  { isBand = +-Band
  ; comm   = +-Commutative
  }

-------------------------------------------------
-- Multiplication forms the following algebras --
-------------------------------------------------

-- Multiplication forms a magma

Â·-Magma : IsMagma _Â·_
Â·-Magma = record
  { isEquivalence = isEquivalence
  ; âˆ™-cong        = congâ‚‚ _Â·_
  }

-- Multiplication forms a semigroup

Â·-Semigroup : IsSemigroup _Â·_
Â·-Semigroup = record
  { isMagma = Â·-Magma
  ; assoc   = Â·-Associative
  }

-- Multiplication forms a monoid with Ï‰ as identity

Â·-Monoid : IsMonoid _Â·_ Ï‰
Â·-Monoid = record
  { isSemigroup = Â·-Semigroup
  ; identity    = Â·-Identity
  }

-------------------------------------------------
-- Addition and Multiplication form a semiring --
-------------------------------------------------

+-Â·-SemiringWithoutAnnihilatingZero : IsSemiringWithoutAnnihilatingZero _+_ _Â·_ ğŸ˜ Ï‰
+-Â·-SemiringWithoutAnnihilatingZero = record
  { +-isCommutativeMonoid = +-CommutativeMonoid
  ; *-isMonoid = Â·-Monoid
  ; distrib = Â·-distrib-+
  }

+-Â·-Semiring : IsSemiring _+_ _Â·_ ğŸ˜ Ï‰
+-Â·-Semiring = record
  { isSemiringWithoutAnnihilatingZero = +-Â·-SemiringWithoutAnnihilatingZero
  ; zero = Â·-zero
  }

----------------------------
-- A specific restriction --
----------------------------

-- The restriction that one of the prodrec quantities (the one that is
-- not tied to one of the Î£-type's quantities) must be Ï‰. Other
-- restrictions are inherited.

prodrec-only-for-Ï‰ : Restrictions â†’ Restrictions
prodrec-only-for-Ï‰ restrictions = record restrictions
  { Prodrec      = Î» r p q â†’ Prodrec r p q Ã— r â‰¡ Ï‰
  }
  where
  open Restrictions restrictions
