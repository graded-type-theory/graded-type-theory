{-# OPTIONS --without-K --safe #-}

open import Tools.Relation
open import Definition.Modality

module Definition.Modality.Substitution.Properties {a â„“â€²}
  {Mâ€² : Setoid a â„“â€²} (ğ•„ : Modality Mâ€²)
  where

open Modality ğ•„
open Setoid Mâ€² renaming (Carrier to M)

open import Definition.Modality.Context ğ•„
open import Definition.Modality.Context.Properties ğ•„
open import Definition.Modality.Properties ğ•„
open import Definition.Modality.Substitution ğ•„
open import Definition.Modality.Usage ğ•„
open import Definition.Modality.Usage.Properties ğ•„
open import Definition.Modality.Usage.Weakening ğ•„
open import Definition.Typed Mâ€² using (_âŠ¢_âˆ·_; _âŠ¢Ë¢_âˆ·_; _,_)
open import Definition.Untyped M as U renaming (_[_,_] to _[_,,_])

open import Tools.Fin
open import Tools.Nat hiding (_+_)
open import Tools.Product
open import Tools.PropositionalEquality as PE

private
  variable
    â„“ m n : Nat
    Î³ Î´ Î· : Conâ‚˜ n
    t u uâ€² : Term n
    Ïƒ : Subst m n
    p q : M

----------------------
-- Properties of *> --
----------------------

-- Modality substitution application distributes over addition.
-- Î¨ *> (Î³ +á¶œ Î´) â‰¡ Î¨ *> Î³ +á¶œ Î¨ *> Î´.
-- Proof by induciton on Î¨ using identiy, commutativity and associtivity of addition
-- and distributivity of multiplication over addition.

*>-distrib-+á¶œ : (Î¨ : Substâ‚˜ m n) (Î³ Î´ : Conâ‚˜ n) â†’ Î¨ *> (Î³ +á¶œ Î´) â‰ˆá¶œ Î¨ *> Î³ +á¶œ Î¨ *> Î´
*>-distrib-+á¶œ []       Îµ       Îµ      = â‰ˆá¶œ-sym (+á¶œ-identityË¡ ğŸ˜á¶œ)
*>-distrib-+á¶œ (Î¨ âŠ™ Î·) (Î³ âˆ™ p) (Î´ âˆ™ q) = begin
  (Î¨ âŠ™ Î·) *> ((Î³ âˆ™ p) +á¶œ (Î´ âˆ™ q))
    â‰ˆâŸ¨ +á¶œ-cong (Â·á¶œ-distribÊ³-+á¶œ p q Î·) (*>-distrib-+á¶œ Î¨ Î³ Î´) âŸ©
  (p Â·á¶œ Î· +á¶œ q Â·á¶œ Î·) +á¶œ Î¨ *> Î³ +á¶œ Î¨ *> Î´
    â‰ˆâŸ¨ +á¶œ-congË¡ (+á¶œ-comm (Î¨ *> Î³) (Î¨ *> Î´)) âŸ©
  (p Â·á¶œ Î· +á¶œ q Â·á¶œ Î·) +á¶œ Î¨ *> Î´ +á¶œ Î¨ *> Î³
    â‰ˆâŸ¨ +á¶œ-assoc (p Â·á¶œ Î·) (q Â·á¶œ Î·) (Î¨ *> Î´ +á¶œ Î¨ *> Î³) âŸ©
  p Â·á¶œ Î· +á¶œ q Â·á¶œ Î· +á¶œ Î¨ *> Î´ +á¶œ Î¨ *> Î³
    â‰ˆâŸ¨ +á¶œ-comm (p Â·á¶œ Î·) (q Â·á¶œ Î· +á¶œ Î¨ *> Î´ +á¶œ Î¨ *> Î³) âŸ©
  (q Â·á¶œ Î· +á¶œ Î¨ *> Î´ +á¶œ Î¨ *> Î³) +á¶œ p Â·á¶œ Î·
    â‰ˆâŸ¨ +á¶œ-assoc _ _ _ âŸ©
  q Â·á¶œ Î· +á¶œ (Î¨ *> Î´ +á¶œ Î¨ *> Î³) +á¶œ p Â·á¶œ Î·
    â‰ˆâŸ¨ +á¶œ-congË¡ (+á¶œ-assoc (Î¨ *> Î´) (Î¨ *> Î³) (p Â·á¶œ Î·)) âŸ©
  q Â·á¶œ Î· +á¶œ Î¨ *> Î´ +á¶œ Î¨ *> Î³ +á¶œ p Â·á¶œ Î·
    â‰ˆË˜âŸ¨ +á¶œ-assoc _ _ _ âŸ©
  (q Â·á¶œ Î· +á¶œ Î¨ *> Î´) +á¶œ Î¨ *> Î³ +á¶œ p Â·á¶œ Î·
    â‰ˆâŸ¨ +á¶œ-congË¡ (+á¶œ-comm (Î¨ *> Î³) (p Â·á¶œ Î·)) âŸ©
  (q Â·á¶œ Î· +á¶œ Î¨ *> Î´) +á¶œ p Â·á¶œ Î· +á¶œ Î¨ *> Î³
    â‰ˆâŸ¨ +á¶œ-comm _ _ âŸ©
  ((p Â·á¶œ Î· +á¶œ Î¨ *> Î³) +á¶œ q Â·á¶œ Î· +á¶œ Î¨ *> Î´) âˆ
  where open import Tools.Reasoning.Equivalence Conâ‚˜-setoid

-- Modality substitution application distributes over context scaling.
-- Î¨ *> (pÎ³) â‰¡ p Â·á¶œ (Î¨ *> Î³).
-- Proof by induction on Î¨ using zero and associtivity of multiplication,
-- and distributivity of multiplication over addition.

*>-distrib-Â·á¶œ : (Î¨ : Substâ‚˜ m n) (p : M) (Î³ : Conâ‚˜ n)
              â†’ Î¨ *> (p Â·á¶œ Î³) â‰ˆá¶œ p Â·á¶œ (Î¨ *> Î³)
*>-distrib-Â·á¶œ [] p Îµ = â‰ˆá¶œ-sym (Â·á¶œ-zeroÊ³ p)
*>-distrib-Â·á¶œ (Î¨ âŠ™ Î´) p (Î³ âˆ™ q) = begin
  (p Â· q) Â·á¶œ Î´ +á¶œ Î¨ *> (p Â·á¶œ Î³)  â‰ˆâŸ¨ +á¶œ-cong (Â·á¶œ-assoc p q Î´) (*>-distrib-Â·á¶œ Î¨ p Î³) âŸ©
  p Â·á¶œ (q Â·á¶œ Î´) +á¶œ p Â·á¶œ (Î¨ *> Î³) â‰ˆË˜âŸ¨ Â·á¶œ-distribË¡-+á¶œ p (q Â·á¶œ Î´) (Î¨ *> Î³) âŸ©
  p Â·á¶œ (q Â·á¶œ Î´ +á¶œ Î¨ *> Î³)        âˆ
  where open import Tools.Reasoning.Equivalence Conâ‚˜-setoid

-- Modality substitution application is linear, i.e. distributes over addition and scaling
-- Î¨ *> (pÎ³ +á¶œ qÎ´) â‰¡ p Â·á¶œ (Î¨ *> Î³) +á¶œ q Â·á¶œ (Î¨ *> Î´)

-- Modality substitution application is linear, i.e. distributes over addition and scaling.
-- Î¨ *> (pÎ³ +á¶œ qÎ´) â‰¡ p Â·á¶œ (Î¨ *> Î³) +á¶œ q Â·á¶œ (Î¨ *> Î´).
-- Follows from the distributivity over addition and multiplication.

*>-linear : (Î¨ : Substâ‚˜ m n) (p q : M) (Î³ Î´ : Conâ‚˜ n)
          â†’ Î¨ *> (p Â·á¶œ Î³ +á¶œ q Â·á¶œ Î´) â‰ˆá¶œ p Â·á¶œ Î¨ *> Î³ +á¶œ q Â·á¶œ Î¨ *> Î´
*>-linear Î¨ p q Î³ Î´ = begin
  Î¨ *> (p Â·á¶œ Î³ +á¶œ q Â·á¶œ Î´)        â‰ˆâŸ¨ *>-distrib-+á¶œ Î¨ (p Â·á¶œ Î³) (q Â·á¶œ Î´) âŸ©
  Î¨ *> (p Â·á¶œ Î³) +á¶œ Î¨ *> (q Â·á¶œ Î´) â‰ˆâŸ¨ +á¶œ-cong (*>-distrib-Â·á¶œ Î¨ p Î³) (*>-distrib-Â·á¶œ Î¨ q Î´) âŸ©
  (p Â·á¶œ Î¨ *> Î³ +á¶œ q Â·á¶œ Î¨ *> Î´)   âˆ
  where open import Tools.Reasoning.Equivalence Conâ‚˜-setoid

*>-sub-distrib-âˆ§á¶œ : (Î¨ : Substâ‚˜ m n) (Î³ Î´ : Conâ‚˜ n) â†’ Î¨ *> (Î³ âˆ§á¶œ Î´) â‰¤á¶œ Î¨ *> Î³ âˆ§á¶œ Î¨ *> Î´
*>-sub-distrib-âˆ§á¶œ [] Îµ Îµ = â‰¤á¶œ-reflexive (â‰ˆá¶œ-sym (âˆ§á¶œ-idem ğŸ˜á¶œ))
*>-sub-distrib-âˆ§á¶œ (Î¨ âŠ™ Î·) (Î³ âˆ™ p) (Î´ âˆ™ q) = begin
  (Î¨ âŠ™ Î·) *> ((Î³ âˆ™ p) âˆ§á¶œ (Î´ âˆ™ q)) â‰¡âŸ¨âŸ©
  (Î¨ âŠ™ Î·) *> (Î³ âˆ§á¶œ Î´ âˆ™ p âˆ§ q)     â‰¡âŸ¨âŸ©
  (p âˆ§ q) Â·á¶œ Î· +á¶œ Î¨ *> (Î³ âˆ§á¶œ Î´)
    â‰¤âŸ¨ +á¶œ-monotoneÊ³ (*>-sub-distrib-âˆ§á¶œ Î¨ Î³ Î´) âŸ©
  (p âˆ§ q) Â·á¶œ Î· +á¶œ (Î¨Î³ âˆ§á¶œ Î¨Î´)
    â‰ˆâŸ¨ +á¶œ-congÊ³ (Â·á¶œ-distribÊ³-âˆ§á¶œ p q Î·) âŸ©
  (pÎ· âˆ§á¶œ qÎ·) +á¶œ (Î¨Î³ âˆ§á¶œ Î¨Î´)
    â‰ˆâŸ¨ +á¶œ-distribÊ³-âˆ§á¶œ ((Î¨ *> Î³) âˆ§á¶œ (Î¨ *> Î´)) (p Â·á¶œ Î·) (q Â·á¶œ Î·) âŸ©
  (pÎ· +á¶œ (Î¨Î³ âˆ§á¶œ Î¨Î´)) âˆ§á¶œ (qÎ· +á¶œ (Î¨Î³ âˆ§á¶œ Î¨Î´))
    â‰ˆâŸ¨ âˆ§á¶œ-cong (+á¶œ-distribË¡-âˆ§á¶œ pÎ· Î¨Î³ Î¨Î´) (+á¶œ-distribË¡-âˆ§á¶œ qÎ· Î¨Î³ Î¨Î´) âŸ©
  ((pÎ· +á¶œ Î¨Î³) âˆ§á¶œ (pÎ· +á¶œ Î¨Î´)) âˆ§á¶œ ((qÎ· +á¶œ Î¨Î³) âˆ§á¶œ (qÎ· +á¶œ Î¨Î´))
    â‰¤âŸ¨ âˆ§á¶œ-monotone (âˆ§á¶œ-decreasingË¡ (pÎ· +á¶œ Î¨Î³) (pÎ· +á¶œ Î¨Î´)) (âˆ§á¶œ-decreasingÊ³ (qÎ· +á¶œ Î¨Î³) (qÎ· +á¶œ Î¨Î´)) âŸ©
  (pÎ· +á¶œ Î¨Î³) âˆ§á¶œ (qÎ· +á¶œ Î¨Î´) â‰¡âŸ¨âŸ©
  (Î¨ âŠ™ Î·) *> (Î³ âˆ™ p) âˆ§á¶œ (Î¨ âŠ™ Î·) *> (Î´ âˆ™ q) âˆ
  where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  Î¨Î³ = Î¨ *> Î³
  Î¨Î´ = Î¨ *> Î´
  pÎ· = p Â·á¶œ Î·
  qÎ· = q Â·á¶œ Î·

-- Modality substitution application sub-distributes over the two first arguments of âŠ›á¶œ
-- Î¨ *> Î³ âŠ›á¶œ Î´ â–· r â‰¤ (Î¨ *> Î³) âŠ› (Î¨ *> Î´) â–· r
-- Proof by induction on Î¨ using sub-distributivity and interchange properties of âŠ›á¶œ

*>-sub-distrib-âŠ›á¶œ : (Î¨ : Substâ‚˜ m n) (Î³ Î´ : Conâ‚˜ n) (r : M)
                   â†’ Î¨ *> (Î³ âŠ›á¶œ Î´ â–· r) â‰¤á¶œ (Î¨ *> Î³) âŠ›á¶œ (Î¨ *> Î´) â–· r
*>-sub-distrib-âŠ›á¶œ [] Îµ Îµ r = â‰¤á¶œ-reflexive (â‰ˆá¶œ-sym (âŠ›á¶œ-idem-ğŸ˜á¶œ r))
*>-sub-distrib-âŠ›á¶œ (Î¨ âŠ™ Î·) (Î³ âˆ™ p) (Î´ âˆ™ q) r = begin
  (Î¨ âŠ™ Î·) *> ((Î³ âˆ™ p) âŠ›á¶œ (Î´ âˆ™ q) â–· r)
      â‰¡âŸ¨âŸ©
  (Î¨ âŠ™ Î·) *> (Î³ âŠ›á¶œ Î´ â–· r âˆ™ p âŠ› q â–· r)
      â‰¡âŸ¨âŸ©
  p âŠ› q â–· r Â·á¶œ Î· +á¶œ Î¨ *> (Î³ âŠ›á¶œ Î´ â–· r)
      â‰¤âŸ¨ +á¶œ-monotone (Â·á¶œ-sub-distribÊ³-âŠ› p q r Î·) (*>-sub-distrib-âŠ›á¶œ Î¨ Î³ Î´ r) âŸ©
  (p Â·á¶œ Î·) âŠ›á¶œ (q Â·á¶œ Î·) â–· r +á¶œ (Î¨ *> Î³) âŠ›á¶œ (Î¨ *> Î´) â–· r
      â‰¤âŸ¨ +á¶œ-sub-interchangable-âŠ›á¶œ r (p Â·á¶œ Î·) (q Â·á¶œ Î·) (Î¨ *> Î³) (Î¨ *> Î´) âŸ©
  (p Â·á¶œ Î· +á¶œ Î¨ *> Î³) âŠ›á¶œ (q Â·á¶œ Î· +á¶œ Î¨ *> Î´) â–· r
      â‰¡âŸ¨âŸ©
  ((Î¨ âŠ™ Î·) *> (Î³ âˆ™ p)) âŠ›á¶œ ((Î¨ âŠ™ Î·) *> (Î´ âˆ™ q)) â–· r âˆ
  where open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset

--- The zero-context is a right zero to modality substitution application.
-- Î¨ *> ğŸ˜á¶œ â‰¡ ğŸ˜á¶œ.
-- Proof by induction on Î¨ using zero of multiplication and identity of addition.

*>-zeroÊ³ : (Î¨ : Substâ‚˜ m n) â†’ Î¨ *> ğŸ˜á¶œ â‰ˆá¶œ ğŸ˜á¶œ
*>-zeroÊ³ []      = â‰ˆá¶œ-refl
*>-zeroÊ³ (Î¨ âŠ™ Î³) = begin
  (Î¨ âŠ™ Î³) *> ğŸ˜á¶œ       â‰¡âŸ¨âŸ©
  ğŸ˜ Â·á¶œ Î³ +á¶œ (Î¨ *> ğŸ˜á¶œ) â‰ˆâŸ¨ +á¶œ-cong (Â·á¶œ-zeroË¡ Î³) (*>-zeroÊ³ Î¨) âŸ©
  ğŸ˜á¶œ +á¶œ ğŸ˜á¶œ            â‰ˆâŸ¨ +á¶œ-identityË¡ ğŸ˜á¶œ âŸ©
  ğŸ˜á¶œ                  âˆ
  where open import Tools.Reasoning.Equivalence Conâ‚˜-setoid

-- Modality substitution application is a monotone function.
-- If Î³ â‰¤á¶œ Î´, then Î¨ *> Î³ â‰¤á¶œ Î¨ *> Î´.
-- Proof by induction on Î¨ using monotonicity of addition and multiplication.

*>-monotone : {Î³ Î´ : Conâ‚˜ n} (Î¨ : Substâ‚˜ m n) â†’ Î³ â‰¤á¶œ Î´ â†’ Î¨ *> Î³ â‰¤á¶œ Î¨ *> Î´
*>-monotone {Î³ = Îµ} {Îµ} [] Î³â‰¤Î´ = â‰¤á¶œ-refl
*>-monotone {Î³ = Î³ âˆ™ p} {Î´ âˆ™ q} (Î¨ âŠ™ Î·) (Î³â‰¤Î´ âˆ™ pâ‰¤q) =
  +á¶œ-monotone (Â·á¶œ-monotoneË¡ pâ‰¤q) (*>-monotone Î¨ Î³â‰¤Î´)

-- Matrix/vector multiplication is associative.
-- (Î¨ <*> Î¦) *> Î³ â‰¡ Î¨ *> (Î¦ *> Î³).
-- Proof by induction on Î³ using linearity of matrix multiplication.

<*>-*>-assoc : {â„“ m n : Nat} (Î¨ : Substâ‚˜ m n) (Î¦ : Substâ‚˜ n â„“) (Î³ : Conâ‚˜ â„“)
             â†’ (Î¨ <*> Î¦) *> Î³ â‰ˆá¶œ Î¨ *> (Î¦ *> Î³)
<*>-*>-assoc Î¨ [] Îµ = â‰ˆá¶œ-sym (*>-zeroÊ³ Î¨)
<*>-*>-assoc Î¨ (Î¦ âŠ™ Î´) (Î³ âˆ™ p) = begin
  p Â·á¶œ (Î¨ *> Î´) +á¶œ (Î¨ <*> Î¦) *> Î³ â‰ˆâŸ¨ +á¶œ-cong (â‰ˆá¶œ-sym (*>-distrib-Â·á¶œ Î¨ p Î´)) (<*>-*>-assoc Î¨ Î¦ Î³) âŸ©
  Î¨ *> (p Â·á¶œ Î´) +á¶œ Î¨ *> (Î¦ *> Î³)  â‰ˆË˜âŸ¨ *>-distrib-+á¶œ Î¨ (p Â·á¶œ Î´) (Î¦ *> Î³) âŸ©
  Î¨ *> (p Â·á¶œ Î´ +á¶œ Î¦ *> Î³)         âˆ
  where open import Tools.Reasoning.Equivalence Conâ‚˜-setoid

------------------------------------------
-- Properties of specific substitutions --
------------------------------------------

-- Application of a shifted substitution.
-- wk1Substâ‚˜ Î¨ *> Î³ â‰¡ (Î¨ *> Î³) âˆ™ ğŸ˜.
-- Proof by induction on Î³ using identity of addition and zero of multiplication

wk1Substâ‚˜-app : (Î¨ : Substâ‚˜ m n) (Î³ : Conâ‚˜ n) â†’ wk1Substâ‚˜ Î¨ *> Î³ â‰ˆá¶œ (Î¨ *> Î³) âˆ™ ğŸ˜
wk1Substâ‚˜-app [] Îµ            = â‰ˆá¶œ-refl
wk1Substâ‚˜-app (Î¨ âŠ™ Î´) (Î³ âˆ™ p) = begin
  wk1Substâ‚˜ (Î¨ âŠ™ Î´) *> (Î³ âˆ™ p) â‰¡âŸ¨âŸ©
  ((p Â·á¶œ Î´) âˆ™ (p Â· ğŸ˜)) +á¶œ wk1Substâ‚˜ Î¨ *> Î³
      â‰ˆâŸ¨ +á¶œ-cong (â‰ˆá¶œ-refl âˆ™ (Â·-zeroÊ³ p)) (wk1Substâ‚˜-app Î¨ Î³) âŸ©
  ((p Â·á¶œ Î´) âˆ™ ğŸ˜) +á¶œ ((Î¨ *> Î³) âˆ™ ğŸ˜)
      â‰¡âŸ¨âŸ©
  (p Â·á¶œ Î´) +á¶œ (Î¨ *> Î³) âˆ™ (ğŸ˜ + ğŸ˜)
     â‰ˆâŸ¨ â‰ˆá¶œ-refl âˆ™ (+-identityË¡ ğŸ˜) âŸ©
  ((Î¨ âŠ™ Î´) *> (Î³ âˆ™ p)) âˆ™ ğŸ˜         âˆ
  where open import Tools.Reasoning.Equivalence Conâ‚˜-setoid


-- Application of a lifted substitution.
-- liftSubstâ‚˜ Î¨ *> (Î³ âˆ™ p) â‰¡ (Î¨ *> Î³) âˆ™ p.
-- Proof by induction on Î³ using lemma on application of a shifted substitution.

liftSubstâ‚˜-app : (Î¨ : Substâ‚˜ m n) (Î³ : Conâ‚˜ n) (p : M)
               â†’ liftSubstâ‚˜ Î¨ *> (Î³ âˆ™ p) â‰ˆá¶œ (Î¨ *> Î³) âˆ™ p
liftSubstâ‚˜-app [] Îµ p = begin
  liftSubstâ‚˜ [] *> (Îµ âˆ™ p)   â‰¡âŸ¨âŸ©
  ([] âŠ™ (ğŸ˜á¶œ âˆ™ ğŸ™)) *> (Îµ âˆ™ p) â‰¡âŸ¨âŸ©
  p Â·á¶œ (ğŸ˜á¶œ âˆ™ ğŸ™) +á¶œ ğŸ˜á¶œ         â‰¡âŸ¨âŸ©
  ((p Â·á¶œ ğŸ˜á¶œ) âˆ™ (p Â· ğŸ™)) +á¶œ ğŸ˜á¶œ â‰ˆâŸ¨ +á¶œ-identityÊ³ _ âŸ©
  (p Â·á¶œ ğŸ˜á¶œ) âˆ™ (p Â· ğŸ™)        â‰ˆâŸ¨ (Â·á¶œ-zeroÊ³ p) âˆ™ (Â·-identityÊ³ p) âŸ©
  ğŸ˜á¶œ âˆ™ p                     â‰¡âŸ¨âŸ©
  ([] *> Îµ) âˆ™ p âˆ
  where open import Tools.Reasoning.Equivalence Conâ‚˜-setoid

liftSubstâ‚˜-app (Î¨ âŠ™ Î·) Î³ p = begin
  liftSubstâ‚˜ (Î¨ âŠ™ Î·) *> (Î³ âˆ™ p)
     â‰¡âŸ¨âŸ©
  (wk1Substâ‚˜ (Î¨ âŠ™ Î·) âŠ™ (ğŸ˜á¶œ âˆ™ ğŸ™)) *> (Î³ âˆ™ p)
     â‰¡âŸ¨âŸ©
  p Â·á¶œ (ğŸ˜á¶œ âˆ™ ğŸ™) +á¶œ (wk1Substâ‚˜ (Î¨ âŠ™ Î·) *> Î³)
     â‰ˆâŸ¨ +á¶œ-cong ((Â·á¶œ-zeroÊ³ p) âˆ™ (Â·-identityÊ³ p)) (wk1Substâ‚˜-app (Î¨ âŠ™ Î·) Î³) âŸ©
  (ğŸ˜á¶œ âˆ™ p) +á¶œ (((Î¨ âŠ™ Î·) *> Î³) âˆ™ ğŸ˜)
     â‰ˆâŸ¨ (+á¶œ-identityË¡ ((Î¨ âŠ™ Î·) *> Î³)) âˆ™ (+-identityÊ³ p) âŸ©
  ((Î¨ âŠ™ Î·) *> Î³) âˆ™ p âˆ
  where open import Tools.Reasoning.Equivalence Conâ‚˜-setoid

-- The identity matrix is a left identity to substitution application.
-- idSubstâ‚˜ *> Î³ â‰¡ Î³.
-- Proof by identity of addition, multiplication and matrix multiplication,
-- zero of multiplication and lemma on the application of shifted substitution matrices.

*>-identityË¡ : (Î³ : Conâ‚˜ n) â†’ idSubstâ‚˜ *> Î³ â‰ˆá¶œ Î³
*>-identityË¡ Îµ       = â‰ˆá¶œ-refl
*>-identityË¡ (Î³ âˆ™ p) = begin
  idSubstâ‚˜ *> (Î³ âˆ™ p) â‰¡âŸ¨âŸ©
  ((p Â·á¶œ ğŸ˜á¶œ) âˆ™ (p Â· ğŸ™)) +á¶œ (wk1Substâ‚˜ idSubstâ‚˜ *> Î³)
    â‰ˆâŸ¨ +á¶œ-cong ((Â·á¶œ-zeroÊ³ p) âˆ™ (Â·-identityÊ³ p)) (wk1Substâ‚˜-app idSubstâ‚˜ Î³) âŸ©
  ((ğŸ˜á¶œ âˆ™ p) +á¶œ ((idSubstâ‚˜ *> Î³) âˆ™ ğŸ˜))
    â‰ˆâŸ¨ (+á¶œ-identityË¡ _) âˆ™ (+-identityÊ³ p) âŸ©
  (idSubstâ‚˜ *> Î³) âˆ™ p
    â‰ˆâŸ¨ (*>-identityË¡ Î³) âˆ™ â‰ˆ-refl âŸ©
  Î³ âˆ™ p âˆ
  where open import Tools.Reasoning.Equivalence Conâ‚˜-setoid

-------------------------------
-- Well-formed substitutions --
-------------------------------

-- Substitting a single (well-used) variable is a well-formed substitution
-- If Î³ â–¸ u, then sgSubstâ‚˜ Î³ â–¶ sgSubst u
-- Proof by cases
-- Case x0 uses identity of addition, multiplication and matrix mutiplication.
-- Case x +1 uses identity of addition and matrix multiplication and zero of multiplicaiton.

wf-sgSubstâ‚˜ : Î³ â–¸ u â†’ sgSubstâ‚˜ Î³ â–¶ sgSubst u
wf-sgSubstâ‚˜ {Î³ = Î³} Î³â–¸u x0 = sub Î³â–¸u eq
  where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  eq = begin
    ğŸ™ Â·á¶œ Î³ +á¶œ idSubstâ‚˜ *> ğŸ˜á¶œ â‰ˆâŸ¨ +á¶œ-cong (Â·á¶œ-identityË¡ Î³) (*>-identityË¡ ğŸ˜á¶œ) âŸ©
    Î³ +á¶œ ğŸ˜á¶œ                  â‰ˆâŸ¨ +á¶œ-identityÊ³ Î³ âŸ©
    Î³                        âˆ
wf-sgSubstâ‚˜ {Î³ = Î³} Î³â–¸u (x +1) = sub var eq
  where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  eq = begin
    ğŸ˜ Â·á¶œ Î³ +á¶œ idSubstâ‚˜ *> (ğŸ˜á¶œ , x â‰” ğŸ™) â‰ˆâŸ¨ +á¶œ-cong (Â·á¶œ-zeroË¡ Î³) (*>-identityË¡ _) âŸ©
    ğŸ˜á¶œ +á¶œ (ğŸ˜á¶œ , x â‰” ğŸ™)                 â‰ˆâŸ¨ +á¶œ-identityË¡ _ âŸ©
    ğŸ˜á¶œ , x â‰” ğŸ™                         âˆ

-- Shifting a well-formed substitution is well-formed.
-- If Î¨ â–¶ Ïƒ, then wk1Substâ‚˜ Î¨ â–¶ wk1Subst Ïƒ.
-- Proof using lemmata on the application of a shifted substitution matrix
-- and shifted modality context.

wf-wk1Substâ‚˜ : (Î¨ : Substâ‚˜ m n) (Ïƒ : Subst m n)
             â†’ Î¨ â–¶ Ïƒ â†’ wk1Substâ‚˜ Î¨ â–¶ wk1Subst Ïƒ
wf-wk1Substâ‚˜ Î¨ Ïƒ Î¨â–¶Ïƒ x =
  sub (wkUsage (step id) (Î¨â–¶Ïƒ x)) (â‰¤á¶œ-reflexive (wk1Substâ‚˜-app Î¨ _))

-- Lifting a well-formed substitution is well-formed
-- If Î¨ â–¶ Ïƒ, then liftSubstâ‚˜ Î¨ â–¶ liftSubst Ïƒ
-- Proof by cases
-- Case x0 uses identity of addition and multiplication and zero of matrix multiplication.
-- Case x +1 uses identity of addition and zero of multiplication.

wf-liftSubstâ‚˜ : {Î¨ : Substâ‚˜ m n} â†’ Î¨ â–¶ Ïƒ â†’ liftSubstâ‚˜ Î¨ â–¶ liftSubst Ïƒ
wf-liftSubstâ‚˜ {Î¨ = Î¨} Î¨â–¶Ïƒ x0 = sub var eq
  where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  eq = begin
    ((ğŸ™ Â·á¶œ ğŸ˜á¶œ) âˆ™ (ğŸ™ Â· ğŸ™)) +á¶œ wk1Substâ‚˜ Î¨ *> ğŸ˜á¶œ
                    â‰ˆâŸ¨ +á¶œ-cong ((Â·á¶œ-zeroÊ³ ğŸ™) âˆ™ (Â·-identityË¡ ğŸ™)) (*>-zeroÊ³ (wk1Substâ‚˜ Î¨)) âŸ©
    (ğŸ˜á¶œ âˆ™ ğŸ™) +á¶œ ğŸ˜á¶œ   â‰ˆâŸ¨ +á¶œ-identityÊ³ _ âŸ©
    ğŸ˜á¶œ âˆ™ ğŸ™          â‰¡âŸ¨âŸ©
    ğŸ˜á¶œ , x0 â‰” ğŸ™     âˆ

wf-liftSubstâ‚˜ {Î¨ = Î¨} Î¨â–¶Ïƒ (x +1) = sub (wf-wk1Substâ‚˜ Î¨ _ Î¨â–¶Ïƒ x) eq
  where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  eq = begin
    (ğŸ˜ Â·á¶œ ğŸ˜á¶œ âˆ™ ğŸ˜ Â· ğŸ™) +á¶œ wk1Substâ‚˜ Î¨ *> (ğŸ˜á¶œ , x â‰” ğŸ™)
      â‰ˆâŸ¨ +á¶œ-congÊ³ ((Â·á¶œ-zeroÊ³ ğŸ˜) âˆ™ (Â·-zeroË¡ ğŸ™)) âŸ©
    ğŸ˜á¶œ +á¶œ wk1Substâ‚˜ Î¨ *> (ğŸ˜á¶œ , x â‰” ğŸ™)
      â‰ˆâŸ¨ +á¶œ-identityË¡ _ âŸ©
    wk1Substâ‚˜ Î¨ *> (ğŸ˜á¶œ , x â‰” ğŸ™) âˆ

-- Extending a well-formed substitution with a well-used term gives a well-formed substitution.
-- If Î¨ â–¶ Ïƒ and Î³ â–¸ t, then (Î¨ âˆ™ Î³) â–¶ consSubst Ïƒ t.
-- Proof by cases.
-- Case x0 uses identity of addition, multiplication and zero of matrix multiplication
-- Case x +1 uses identity of addition and zero of multiplication

wf-consSubstâ‚˜ : {Î¨ : Substâ‚˜ m n} {Î³ : Conâ‚˜ m} â†’ Î¨ â–¶ Ïƒ â†’ Î³ â–¸ t â†’ Î¨ âŠ™ Î³ â–¶ consSubst Ïƒ t
wf-consSubstâ‚˜ {Î¨ = Î¨} {Î³ = Î³} Î¨â–¶Ïƒ Î³â–¸t x0 = sub Î³â–¸t eq
  where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  eq = begin
       ğŸ™ Â·á¶œ Î³ +á¶œ Î¨ *> ğŸ˜á¶œ â‰ˆâŸ¨ +á¶œ-cong (Â·á¶œ-identityË¡ Î³) (*>-zeroÊ³ Î¨) âŸ©
       Î³ +á¶œ ğŸ˜á¶œ           â‰ˆâŸ¨ +á¶œ-identityÊ³ _ âŸ©
       Î³                 âˆ
wf-consSubstâ‚˜ {Î¨ = Î¨} {Î³ = Î³} Î¨â–¶Ïƒ Î³â–¸t (x +1) = sub (Î¨â–¶Ïƒ x) eq
  where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  eq = begin
         ğŸ˜ Â·á¶œ Î³ +á¶œ Î¨ *> (ğŸ˜á¶œ , x â‰” ğŸ™) â‰ˆâŸ¨ +á¶œ-congÊ³ (Â·á¶œ-zeroË¡ _) âŸ©
         ğŸ˜á¶œ +á¶œ Î¨ *> (ğŸ˜á¶œ , x â‰” ğŸ™)     â‰ˆâŸ¨ +á¶œ-identityË¡ _ âŸ©
         Î¨ *> (ğŸ˜á¶œ , x â‰” ğŸ™)           âˆ

-- The tail of a well-formed substitution is a well-formed substitution.
-- If (Î¨ âŠ™ Î³) â–¶ Ïƒ then Î¨ â–¶ tail Ïƒ

wf-tailSubstâ‚˜ : {Î¨ : Substâ‚˜ m n} â†’ (Î¨ âŠ™ Î³) â–¶ Ïƒ â†’ Î¨ â–¶ tail Ïƒ
wf-tailSubstâ‚˜ Î¨â–¶Ïƒ x =
  sub (Î¨â–¶Ïƒ (x +1))
      (â‰¤á¶œ-reflexive (â‰ˆá¶œ-sym (â‰ˆá¶œ-trans (+á¶œ-congÊ³ (Â·á¶œ-zeroË¡ _)) (+á¶œ-identityË¡ _))))

---------------------------------------
-- Substitution lemma for modalities --
---------------------------------------

-- Substitution lemma.
-- If Î¨ â–¶ Ïƒ and Î³ â–¸ t, then Î¨ *> Î³ â–¸ t[Ïƒ].
-- Proof by induction on Î³ â–¸ t using linearity of matrix multiplication
-- and well-formedness of lifted substitution matrices.

substâ‚˜-lemma : (Î¨ : Substâ‚˜ m n) (Ïƒ : Subst m n) â†’ Î¨ â–¶ Ïƒ â†’ Î³ â–¸ t â†’ substâ‚˜ Î¨ Î³ â–¸ U.subst Ïƒ t
substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Uâ‚˜     = sub Uâ‚˜ (â‰¤á¶œ-reflexive (*>-zeroÊ³ Î¨))
substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ â„•â‚˜     = sub â„•â‚˜ (â‰¤á¶œ-reflexive (*>-zeroÊ³ Î¨))
substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Emptyâ‚˜ = sub Emptyâ‚˜ (â‰¤á¶œ-reflexive (*>-zeroÊ³ Î¨))
substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Unitâ‚˜  = sub Unitâ‚˜ (â‰¤á¶œ-reflexive (*>-zeroÊ³ Î¨))

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (Î â‚˜ Î³â–¸F Î´â–¸G) =
  sub (Î â‚˜ Î³â–¸Fâ€² Î´â–¸Gâ€³) (â‰¤á¶œ-reflexive (*>-distrib-+á¶œ Î¨ _ _))
  where
  Î³â–¸Fâ€² = substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸F
  Î´â–¸Gâ€² = substâ‚˜-lemma (liftSubstâ‚˜ Î¨) (liftSubst Ïƒ) (wf-liftSubstâ‚˜ Î¨â–¶Ïƒ) Î´â–¸G
  Î´â–¸Gâ€³ = sub Î´â–¸Gâ€² (â‰¤á¶œ-reflexive (â‰ˆá¶œ-sym (liftSubstâ‚˜-app Î¨ _ _)))

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (Î£â‚˜ Î³â–¸F Î´â–¸G) =
  sub (Î£â‚˜ Î³â–¸Fâ€² Î´â–¸Gâ€³) (â‰¤á¶œ-reflexive (*>-distrib-+á¶œ Î¨ _ _))
  where
  Î³â–¸Fâ€² = substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸F
  Î´â–¸Gâ€² = substâ‚˜-lemma (liftSubstâ‚˜ Î¨) (liftSubst Ïƒ) (wf-liftSubstâ‚˜ Î¨â–¶Ïƒ) Î´â–¸G
  Î´â–¸Gâ€³ = sub Î´â–¸Gâ€² (â‰¤á¶œ-reflexive (â‰ˆá¶œ-sym (liftSubstâ‚˜-app Î¨ _ _)))

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (var {x}) = Î¨â–¶Ïƒ x

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (lamâ‚˜ Î³â–¸t) =
  lamâ‚˜ (sub (substâ‚˜-lemma (liftSubstâ‚˜ Î¨) (liftSubst Ïƒ) (wf-liftSubstâ‚˜ Î¨â–¶Ïƒ) Î³â–¸t)
            (â‰¤á¶œ-reflexive (â‰ˆá¶œ-sym (liftSubstâ‚˜-app Î¨ _ _))))

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (_âˆ˜â‚˜_ {Î³} {Î´ = Î´} {p = p} Î³â–¸t Î´â–¸u) =
  sub ((substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸t) âˆ˜â‚˜ (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î´â–¸u)) eq
  where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  eq = begin
    Î¨ *> (Î³ +á¶œ p Â·á¶œ Î´)      â‰ˆâŸ¨ *>-distrib-+á¶œ Î¨ Î³ (p Â·á¶œ Î´) âŸ©
    Î¨ *> Î³ +á¶œ Î¨ *> (p Â·á¶œ Î´) â‰ˆâŸ¨ +á¶œ-congË¡ (*>-distrib-Â·á¶œ Î¨ p Î´) âŸ©
    Î¨ *> Î³ +á¶œ p Â·á¶œ (Î¨ *> Î´) âˆ

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (prodáµ£â‚˜ {Î³ = Î³} {Î´ = Î´} Î³â–¸t Î´â–¸u) =
  sub (prodáµ£â‚˜ (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸t) (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î´â–¸u))
      (â‰¤á¶œ-reflexive (*>-distrib-+á¶œ Î¨ Î³ Î´))

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (prodâ‚šâ‚˜ Î³â–¸t Î³â–¸u) =
  prodâ‚šâ‚˜ (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸t) (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸u)

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (fstâ‚˜ Î³â–¸t) = fstâ‚˜ (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸t)

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (sndâ‚˜ Î³â–¸t) = sndâ‚˜ (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸t)

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (prodrecâ‚˜ {Î³ = Î³} {Î´ = Î´} {p} {Î· = Î·} {q = q} Î³â–¸t Î´â–¸u Î·â–¸A P) =
  sub (prodrecâ‚˜ (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸t)
                (sub Î¨Î´â–¸Ïƒu eqâ‚)
                (sub Î¨Î·â–¸ÏƒA (â‰¤á¶œ-reflexive (â‰ˆá¶œ-sym (liftSubstâ‚˜-app Î¨ Î· q)) ))
                P)
      eqâ‚‚
  where
  Î¨Î´â–¸Ïƒu = substâ‚˜-lemma (liftSubstâ‚˜ (liftSubstâ‚˜ Î¨)) (liftSubstn Ïƒ 2)
                       (wf-liftSubstâ‚˜ (wf-liftSubstâ‚˜ Î¨â–¶Ïƒ)) Î´â–¸u
  Î¨Î·â–¸ÏƒA = substâ‚˜-lemma (liftSubstâ‚˜ Î¨) (liftSubst Ïƒ) (wf-liftSubstâ‚˜ Î¨â–¶Ïƒ) Î·â–¸A
  eqâ‚ = begin
    Î¨ *> Î´ âˆ™ p âˆ™ p
      â‰ˆË˜âŸ¨ +á¶œ-identityË¡ _ âˆ™ +-identityÊ³ p âˆ™ â‰ˆ-refl âŸ©
    (ğŸ˜á¶œ +á¶œ Î¨ *> Î´) âˆ™ (p + ğŸ˜) âˆ™ p
      â‰ˆË˜âŸ¨ +á¶œ-cong (Â·á¶œ-zeroÊ³ p âˆ™ Â·-identityÊ³ p) (wk1Substâ‚˜-app Î¨ Î´) âˆ™ â‰ˆ-refl âŸ©
    (p Â·á¶œ ğŸ˜á¶œ âˆ™ p Â· ğŸ™) +á¶œ wk1Substâ‚˜ Î¨ *> Î´ âˆ™ p
      â‰ˆË˜âŸ¨ liftSubstâ‚˜-app (liftSubstâ‚˜ Î¨) (Î´ âˆ™ p) p âŸ©
    liftSubstâ‚˜ (liftSubstâ‚˜ Î¨) *> (Î´ âˆ™ p âˆ™ p) âˆ
    where open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  eqâ‚‚ = begin
    Î¨ *> (p Â·á¶œ Î³ +á¶œ Î´)
      â‰ˆâŸ¨ *>-distrib-+á¶œ Î¨ (p Â·á¶œ Î³) Î´ âŸ©
    Î¨ *> (p Â·á¶œ Î³) +á¶œ Î¨ *> Î´
      â‰ˆâŸ¨ +á¶œ-congÊ³ (*>-distrib-Â·á¶œ Î¨ p Î³) âŸ©
    p Â·á¶œ Î¨ *> Î³ +á¶œ Î¨ *> Î´ âˆ
    where open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset


substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ zeroâ‚˜ = sub zeroâ‚˜ (â‰¤á¶œ-reflexive (*>-zeroÊ³ Î¨))

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (sucâ‚˜ Î³â–¸t) = sucâ‚˜ (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸t)

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (natrecâ‚˜ {Î³ = Î³} {Î´ = Î´} {p} {r} {Î· = Î·} {Î¸} {q} Î³â–¸z Î´â–¸s Î·â–¸n Î¸â–¸A) =
  sub (natrecâ‚˜ Î³â–¸zâ€² Î´â–¸sâ€³ Î·â–¸nâ€² Î¸â–¸Aâ€²) le
  where
  Î³â–¸zâ€² = substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸z
  Î´â–¸sâ€² = substâ‚˜-lemma (liftSubstâ‚˜ (liftSubstâ‚˜ Î¨)) (liftSubst (liftSubst Ïƒ))
                      (wf-liftSubstâ‚˜ (wf-liftSubstâ‚˜ Î¨â–¶Ïƒ)) Î´â–¸s
  Î·â–¸nâ€² = substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î·â–¸n
  Î¸â–¸Aâ€² = sub (substâ‚˜-lemma (liftSubstâ‚˜ Î¨) (liftSubst Ïƒ) (wf-liftSubstâ‚˜ Î¨â–¶Ïƒ) Î¸â–¸A)
             (â‰¤á¶œ-reflexive (â‰ˆá¶œ-sym (liftSubstâ‚˜-app Î¨ Î¸ q)))
  Î´â–¸sâ€³ = sub Î´â–¸sâ€² (begin
    (Î¨ *> Î´) âˆ™ p âˆ™ r
        â‰ˆË˜âŸ¨ +á¶œ-identityË¡ _ âˆ™ +-identityÊ³ p âˆ™ â‰ˆ-refl âŸ©
    (ğŸ˜á¶œ +á¶œ Î¨ *> Î´) âˆ™ (p + ğŸ˜) âˆ™ r
        â‰ˆË˜âŸ¨ (+á¶œ-cong (Â·á¶œ-zeroÊ³ p âˆ™ Â·-identityÊ³ p) (wk1Substâ‚˜-app Î¨ Î´)) âˆ™ â‰ˆ-refl âŸ©
    (p Â·á¶œ ğŸ˜á¶œ âˆ™ p Â· ğŸ™) +á¶œ wk1Substâ‚˜ Î¨ *> Î´ âˆ™ r
        â‰ˆË˜âŸ¨ liftSubstâ‚˜-app (liftSubstâ‚˜ Î¨) (Î´ âˆ™ p) r âŸ©
    liftSubstâ‚˜ (liftSubstâ‚˜ Î¨) *> (Î´ âˆ™ p âˆ™ r) âˆ)
    where open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  le = begin
    Î¨ *> ((Î³ âˆ§á¶œ Î·) âŠ›á¶œ (Î´ +á¶œ p Â·á¶œ Î·) â–· r)
         â‰¤âŸ¨ *>-sub-distrib-âŠ›á¶œ Î¨ _ _ r âŸ©
    (Î¨ *> (Î³ âˆ§á¶œ Î·)) âŠ›á¶œ (Î¨ *> (Î´ +á¶œ p Â·á¶œ Î·)) â–· r
         â‰ˆâŸ¨ âŠ›áµ£á¶œ-congË¡ (*>-distrib-+á¶œ Î¨ Î´ (p Â·á¶œ Î·)) âŸ©
    (Î¨ *> (Î³ âˆ§á¶œ Î·)) âŠ›á¶œ (Î¨ *> Î´ +á¶œ Î¨ *> (p Â·á¶œ Î·)) â–· r
         â‰¤âŸ¨ âŠ›á¶œ-monotoneÊ³ (*>-sub-distrib-âˆ§á¶œ Î¨ Î³ Î·) âŸ©
    (Î¨ *> Î³ âˆ§á¶œ Î¨ *> Î·) âŠ›á¶œ (Î¨ *> Î´ +á¶œ Î¨ *> (p Â·á¶œ Î·)) â–· r
         â‰ˆâŸ¨ âŠ›áµ£á¶œ-congË¡ (+á¶œ-congË¡ (*>-distrib-Â·á¶œ Î¨ p Î·)) âŸ©
    (Î¨ *> Î³ âˆ§á¶œ Î¨ *> Î·) âŠ›á¶œ (Î¨ *> Î´ +á¶œ p Â·á¶œ Î¨ *> Î·) â–· r âˆ
    where open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (Emptyrecâ‚˜ Î³â–¸t Î´â–¸A) =
  sub (Emptyrecâ‚˜ (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸t) (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î´â–¸A))
      (â‰¤á¶œ-reflexive (*>-distrib-Â·á¶œ Î¨ _ _))

substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ starâ‚˜ = sub starâ‚˜ (â‰¤á¶œ-reflexive (*>-zeroÊ³ Î¨))
substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (sub Î³â–¸t x) =
  sub (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ Î³â–¸t) (*>-monotone Î¨ x)


-- Special case of substitution lemma for single substitutions.
-- If Î³ âˆ™ p â–¸ t and Î´ â–¸ u, then (Î³ +á¶œ pÎ´) â–¸ t[u].
-- Follows from the substitution lemma.

sgSubstâ‚˜-lemma : Î³ âˆ™ p â–¸ t â†’ Î´ â–¸ u â†’ (Î³ +á¶œ p Â·á¶œ Î´) â–¸ t [ u ]
sgSubstâ‚˜-lemma {Î³ = Î³} {p} {Î´ = Î´} Î³â–¸t Î´â–¸u = sub
  (substâ‚˜-lemma (sgSubstâ‚˜ _) (sgSubst _) (wf-sgSubstâ‚˜ Î´â–¸u) Î³â–¸t)
  eq
 where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  eq = begin
    Î³ +á¶œ p Â·á¶œ Î´               â‰ˆË˜âŸ¨ +á¶œ-congÊ³ (*>-identityË¡ Î³) âŸ©
    idSubstâ‚˜ *> Î³ +á¶œ p Â·á¶œ Î´   â‰ˆË˜âŸ¨  +á¶œ-comm _ _  âŸ©
    (idSubstâ‚˜ âŠ™ Î´) *> (Î³ âˆ™ p) âˆ

-- Special case of substitution lemma for double substitutions.
-- If Î³ âˆ™ q âˆ™ p â–¸ t and Î´ â–¸ u and Î· â–¸ uâ€², then (Î³ +á¶œ pÎ´ +á¶œ qÎ·) â–¸ t[u][uâ€²].
-- Follows from the substitution lemma.

doubleSubstâ‚˜-lemma : Î³ âˆ™ q âˆ™ p â–¸ t â†’ Î´ â–¸ u â†’ Î· â–¸ uâ€²
                   â†’ (Î³ +á¶œ p Â·á¶œ Î´ +á¶œ q Â·á¶œ Î·) â–¸ t [ uâ€² ,, u ]
doubleSubstâ‚˜-lemma {Î³ = Î³} {q} {p} {Î´ = Î´} {Î· = Î·} Î³â–¸t Î´â–¸u Î·â–¸uâ€² = sub
  (substâ‚˜-lemma (consSubstâ‚˜ (sgSubstâ‚˜ _) _) _
                (wf-consSubstâ‚˜ (wf-sgSubstâ‚˜ Î·â–¸uâ€²) Î´â–¸u) Î³â–¸t)
  eq
  where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset
  eq = begin
   Î³ +á¶œ p Â·á¶œ Î´ +á¶œ q Â·á¶œ Î·   â‰ˆâŸ¨ +á¶œ-comm Î³ ((p Â·á¶œ Î´) +á¶œ (q Â·á¶œ Î·)) âŸ©
   (p Â·á¶œ Î´ +á¶œ q Â·á¶œ Î·) +á¶œ Î³ â‰ˆâŸ¨ +á¶œ-assoc (p Â·á¶œ Î´) (q Â·á¶œ Î·) Î³ âŸ©
   p Â·á¶œ Î´ +á¶œ q Â·á¶œ Î· +á¶œ Î³   â‰ˆË˜âŸ¨ +á¶œ-congË¡ (+á¶œ-congË¡ (*>-identityË¡ Î³)) âŸ©
   p Â·á¶œ Î´ +á¶œ q Â·á¶œ Î· +á¶œ idSubstâ‚˜ *> Î³ âˆ

-------------------------------------
-- Substitution matrix inference --
-------------------------------------

-- Column i of an inferred matrix is the inferred context of Ïƒ xáµ¢.
-- âˆ¥ Ïƒ âˆ¥ *> ğ•–áµ¢ â‰¡ âŒˆ Ïƒ xáµ¢ âŒ‰.
-- Proof by induction on (the width of) substitution matrices.

substâ‚˜-calc-col : (Ïƒ : Subst m n) (x : Fin n)
                â†’ âˆ¥ Ïƒ âˆ¥ *> (ğŸ˜á¶œ , x â‰” ğŸ™) â‰ˆá¶œ âŒˆ Ïƒ x âŒ‰
substâ‚˜-calc-col Ïƒ x0 = begin
  âˆ¥ Ïƒ âˆ¥ *> (ğŸ˜á¶œ , x0 â‰” ğŸ™)            â‰¡âŸ¨âŸ©
  âˆ¥ Ïƒ âˆ¥ *> (ğŸ˜á¶œ âˆ™ ğŸ™)                 â‰¡âŸ¨âŸ©
  ğŸ™ Â·á¶œ âŒˆ Ïƒ x0 âŒ‰ +á¶œ âˆ¥ tail Ïƒ âˆ¥ *> ğŸ˜á¶œ â‰ˆâŸ¨ +á¶œ-cong (Â·á¶œ-identityË¡ _) (*>-zeroÊ³ âˆ¥ tail Ïƒ âˆ¥) âŸ©
  âŒˆ Ïƒ x0 âŒ‰ +á¶œ ğŸ˜á¶œ                    â‰ˆâŸ¨ +á¶œ-identityÊ³ _ âŸ©
  âŒˆ Ïƒ x0 âŒ‰                          âˆ
  where open import Tools.Reasoning.Equivalence Conâ‚˜-setoid
substâ‚˜-calc-col Ïƒ (x +1) = begin
  âˆ¥ Ïƒ âˆ¥ *> (ğŸ˜á¶œ , x +1 â‰” ğŸ™)                    â‰¡âŸ¨âŸ©
  âˆ¥ Ïƒ âˆ¥ *> ((ğŸ˜á¶œ , x â‰” ğŸ™) âˆ™ ğŸ˜)                 â‰¡âŸ¨âŸ©
  ğŸ˜ Â·á¶œ âŒˆ Ïƒ x0 âŒ‰ +á¶œ âˆ¥ tail Ïƒ âˆ¥ *> (ğŸ˜á¶œ , x â‰” ğŸ™) â‰ˆâŸ¨ +á¶œ-cong (Â·á¶œ-zeroË¡ _) (substâ‚˜-calc-col (tail Ïƒ) x) âŸ©
  ğŸ˜á¶œ +á¶œ âŒˆ tail Ïƒ x âŒ‰                          â‰ˆâŸ¨ +á¶œ-identityË¡ _ âŸ©
  âŒˆ Ïƒ (x +1) âŒ‰                                âˆ
  where open import Tools.Reasoning.Equivalence Conâ‚˜-setoid


-- An infered substitution matrix is well-formed if
-- all substituted terms are well-resourced.
-- If âˆ€ x. Î³ â–¸ Ïƒ x then âˆ¥ Ïƒ âˆ¥ â–¶ Ïƒ.
-- Proof by the corresponding property for modality contexts applied to each column.

substâ‚˜-calc-correct : {Î“ : Con Term m} (Ïƒ : Subst m n)
                    â†’ (âˆ€ x â†’ âˆƒ Î» Î³ â†’ Î³ â–¸ Ïƒ x) â†’ âˆ¥ Ïƒ âˆ¥ â–¶ Ïƒ
substâ‚˜-calc-correct Ïƒ prop x with prop x
... | Î³ , Î³â–¸Ïƒx =
  sub (usage-inf Î³â–¸Ïƒx) (â‰¤á¶œ-reflexive (substâ‚˜-calc-col Ïƒ x))

subst-calc-correctâ€² : {Î¨ : Substâ‚˜ m n} â†’ Î¨ â–¶ Ïƒ â†’ âˆ¥ Ïƒ âˆ¥ â–¶ Ïƒ
subst-calc-correctâ€² {Ïƒ = Ïƒ} {Î¨ = Î¨ âŠ™ Î³} Î¨â–¶Ïƒ x0 =
  sub (usage-inf (Î¨â–¶Ïƒ x0))
      (â‰¤á¶œ-reflexive (â‰ˆá¶œ-trans (+á¶œ-cong (Â·á¶œ-identityË¡ _) (*>-zeroÊ³ âˆ¥ tail Ïƒ âˆ¥))
                              (+á¶œ-identityÊ³ _)))
subst-calc-correctâ€² {Î¨ = Î¨ âŠ™ Î³} Î¨â–¶Ïƒ (x +1) =
  sub (subst-calc-correctâ€² {Î¨ = Î¨} (wf-tailSubstâ‚˜ Î¨â–¶Ïƒ) x)
      (â‰¤á¶œ-reflexive (â‰ˆá¶œ-trans (+á¶œ-congÊ³ (Â·á¶œ-zeroË¡ _)) (+á¶œ-identityË¡ _)))

-- Each column of a calculated substitution matrix is an upper bound on valid contexts.
-- If Î³ â–¸ Ïƒ xáµ¢ then Î³ â‰¤á¶œ âˆ¥ Ïƒ âˆ¥ *> ğ•–áµ¢.
-- Proof using the corresponding property for modality contexts applied to each column.

substâ‚˜-calc-upper-bound : {Î³ : Conâ‚˜ m} â†’ (Ïƒ : Subst m n) â†’ (x : Fin n)
                        â†’ Î³ â–¸ Ïƒ x â†’ Î³ â‰¤á¶œ âˆ¥ Ïƒ âˆ¥ *> (ğŸ˜á¶œ , x â‰” ğŸ™)
substâ‚˜-calc-upper-bound Ïƒ x Î³â–¸Ïƒx =
  â‰¤á¶œ-trans (usage-upper-bound Î³â–¸Ïƒx)
           (â‰¤á¶œ-reflexive (â‰ˆá¶œ-sym (substâ‚˜-calc-col Ïƒ x)))

--------------------------------------------------
-- Well-formedness of substitution compositions --
--------------------------------------------------

-- Composition of well-formed substitutions are well-formed.
-- If Î¨ â–¶ Ïƒ and Î¦ â–¶ Ïƒâ€² then (Î¨ <*> Î¦) â–¶ (Ïƒ â‚›â€¢â‚› Ïƒâ€²).
-- Proof using the substitution lemma and associtivity of matrix/vector pultiplication.

wf-compSubst : {Î¨ : Substâ‚˜ m â„“} {Î¦ : Substâ‚˜ â„“ n} {Ïƒ : Subst m â„“} {Ïƒâ€² : Subst â„“ n}
             â†’ Î¨ â–¶ Ïƒ â†’ Î¦ â–¶ Ïƒâ€² â†’ (Î¨ <*> Î¦) â–¶ (Ïƒ â‚›â€¢â‚› Ïƒâ€²)
wf-compSubst {Î¨ = Î¨} {Î¦ = Î¦} {Ïƒ = Ïƒ} {Ïƒâ€² = Ïƒâ€²} Î¨â–¶Ïƒ Î¦â–¶Ïƒâ€² x =
  sub (substâ‚˜-lemma Î¨ Ïƒ Î¨â–¶Ïƒ (Î¦â–¶Ïƒâ€² x))
      (â‰¤á¶œ-reflexive (<*>-*>-assoc Î¨ Î¦ (ğŸ˜á¶œ , x â‰” ğŸ™)))
