{-# OPTIONS --without-K --safe #-}

module Definition.Modality.Erasure where

open import Tools.Product
open import Tools.PropositionalEquality

-- The set of erasure annotations with ğŸ˜ corresponding to no usage
-- and Ï‰ to any usage.

data Erasure : Set where
  ğŸ˜ Ï‰ : Erasure

open import Definition.Modality Erasure _â‰¡_ public
open import Tools.Algebra {A = Erasure} _â‰¡_
open import Tools.Nat hiding (_+_)

-- Addition of erasure annotations

_+_ : Opâ‚‚ Erasure
x + ğŸ˜ = x
x + Ï‰ = Ï‰

-- Multiplication of erasure annotations

_Â·_ : Opâ‚‚ Erasure
x Â· ğŸ˜ = ğŸ˜
x Â· Ï‰ = x

-- Meet for erasure annotations coincides with addition

_âˆ§_ : Opâ‚‚ Erasure
_âˆ§_ = _+_

-- Natrec recurrence function

nr : Opâ‚ƒ Erasure
nr ğŸ˜ q ğŸ˜ = q
nr ğŸ˜ ğŸ˜ Ï‰ = ğŸ˜
nr ğŸ˜ Ï‰ Ï‰ = Ï‰
nr Ï‰ q r = Ï‰

-- Iteratively defined natrec recurrence function

nrâ¿ : Nat â†’ Opâ‚ƒ Erasure
nrâ¿ Nat.zero p q r = ğŸ˜
nrâ¿ (1+ n) p q r = p âˆ§ (q + (r Â· (nrâ¿ n p q r)))

-- Ordering relation for erasures
-- Reflexive closure of Ï‰ â‰¤ ğŸ˜

_â‰¤_ : (p q : Erasure) â†’ Set
p â‰¤ q = p â‰¡ p âˆ§ q

---------------------------------------
-- Properties of addition (and meet) --
---------------------------------------

-- Addition is commutative
-- p + q â‰¡ q + p

+-Commutative : Commutative _+_
+-Commutative ğŸ˜ ğŸ˜ = refl
+-Commutative ğŸ˜ Ï‰ = refl
+-Commutative Ï‰ ğŸ˜ = refl
+-Commutative Ï‰ Ï‰ = refl

-- Addition is associative
-- p + (q + r) â‰¡ (p + q) + r

+-Associative : Associative _+_
+-Associative p q ğŸ˜ = refl
+-Associative p q Ï‰ = refl

-- Addition is idempotent

+-Idempotent : Idempotent _+_
+-Idempotent ğŸ˜ = refl
+-Idempotent Ï‰ = refl

-- ğŸ˜ is a left identity of addition
-- ğŸ˜ + p â‰¡ p

+-LeftIdentity : LeftIdentity ğŸ˜ _+_
+-LeftIdentity ğŸ˜ = refl
+-LeftIdentity Ï‰ = refl

-- ğŸ˜ is a right identity of addition
-- p + ğŸ˜ â‰¡ p

+-RightIdentity : RightIdentity ğŸ˜ _+_
+-RightIdentity x = refl

-- ğŸ˜ is an identity of addition
-- ğŸ˜ + p â‰¡ p â‰¡ p + ğŸ˜

+-Identity : Identity ğŸ˜ _+_
+-Identity = +-LeftIdentity , +-RightIdentity

-- Addition is positive
-- If ğŸ˜ â‰¤ p + q then ğŸ˜ â‰¤ p and ğŸ˜ â‰¤ q

+-positive : (p q : Erasure) â†’ ğŸ˜ â‰¤ (p + q) â†’ ğŸ˜ â‰¤ p Ã— ğŸ˜ â‰¤ q
+-positive ğŸ˜ ğŸ˜ refl = refl , refl
+-positive ğŸ˜ Ï‰ ()
+-positive Ï‰ ğŸ˜ ()
+-positive Ï‰ Ï‰ ()

----------------------------------
-- Properties of multiplication --
----------------------------------

-- Multiplication is associative
-- p Â· (q Â· r) â‰¡ (p Â· q) Â· r

Â·-Associative : Associative _Â·_
Â·-Associative x y ğŸ˜ = refl
Â·-Associative x y Ï‰ = refl

-- ğŸ˜ is a left zero for multiplication
-- ğŸ˜ Â· p â‰¡ ğŸ˜

Â·-LeftZero : LeftZero ğŸ˜ _Â·_
Â·-LeftZero ğŸ˜ = refl
Â·-LeftZero Ï‰ = refl

-- ğŸ˜ is a right zero for multiplication
-- p Â· ğŸ˜ â‰¡ ğŸ˜

Â·-RightZero : RightZero ğŸ˜ _Â·_
Â·-RightZero x = refl

-- ğŸ˜ is a zero for multiplication
-- ğŸ˜ Â· p â‰¡ ğŸ˜ â‰¡ p Â· ğŸ˜

Â·-zero : Zero ğŸ˜ _Â·_
Â·-zero = Â·-LeftZero , Â·-RightZero

-- Ï‰ is a left identity for multiplication
-- Ï‰ Â· p â‰¡ p

Â·-LeftIdentity : LeftIdentity Ï‰ _Â·_
Â·-LeftIdentity ğŸ˜ = refl
Â·-LeftIdentity Ï‰ = refl

-- Ï‰ is a right identity for multiplication
-- p Â· Ï‰ â‰¡ p

Â·-RightIdentity : RightIdentity Ï‰ _Â·_
Â·-RightIdentity x = refl

-- Ï‰ is an identity for multiplication
-- Ï‰ Â· p â‰¡ p â‰¡ p Â· Ï‰

Â·-Identity : Identity Ï‰ _Â·_
Â·-Identity = Â·-LeftIdentity , Â·-RightIdentity

----------------------------------------------
-- Properties of natrec recurrence function --
----------------------------------------------

-- nr iteration reaches a fixpoint after one iteration
-- nrâ¿ 1 p q r â‰¡ nrâ¿ 0 p q r

nr-fixâ‚ : (p q r : Erasure) â†’ (p âˆ§ (q + (r Â· (p âˆ§ q)))) â‰¡ (p âˆ§ q)
nr-fixâ‚ ğŸ˜ ğŸ˜ r = refl
nr-fixâ‚ Ï‰ ğŸ˜ ğŸ˜ = refl
nr-fixâ‚ Ï‰ ğŸ˜ Ï‰ = refl
nr-fixâ‚ p Ï‰ ğŸ˜ = refl
nr-fixâ‚ p Ï‰ Ï‰ = refl

-- nr coincides with nrâ¿ at the fixpoint, i.e. with nr in the modality ringoid.
-- nr p q r â‰¡ nrâ¿ 1 p q r

nr-correct : (p q r : Erasure) â†’ nr p q r â‰¡ nrâ¿ 1 p q r
nr-correct ğŸ˜ ğŸ˜ ğŸ˜ = refl
nr-correct ğŸ˜ Ï‰ ğŸ˜ = refl
nr-correct ğŸ˜ ğŸ˜ Ï‰ = refl
nr-correct ğŸ˜ Ï‰ Ï‰ = refl
nr-correct Ï‰ ğŸ˜ r = refl
nr-correct Ï‰ Ï‰ r = refl


--------------------------------------------------------------------
-- Distributive properties of addition, multiplication (and meet) --
--------------------------------------------------------------------

-- Multiplication is left distributive over addition
-- p Â· (q + r) â‰¡ (p Â· q) + (p Â· r)

Â·-distribË¡-+ : _Â·_ DistributesOverË¡ _+_
Â·-distribË¡-+ p q ğŸ˜ = refl
Â·-distribË¡-+ Ï‰ q Ï‰ = refl
Â·-distribË¡-+ ğŸ˜ ğŸ˜ Ï‰ = refl
Â·-distribË¡-+ ğŸ˜ Ï‰ Ï‰ = refl

-- Multiplication is right distributive over addition
-- (q + r) Â· p â‰¡ (q Â· p) + (r Â· p)

Â·-distribÊ³-+ : _Â·_ DistributesOverÊ³ _+_
Â·-distribÊ³-+ ğŸ˜ q r = refl
Â·-distribÊ³-+ Ï‰ q r = refl

-- Multiplication is distributive over addition
-- p Â· (q + r) â‰¡ (p Â· q) + (p Â· r) and (q + r) Â· p â‰¡ (q Â· p) + (r Â· p)

Â·-distrib-+ : _Â·_ DistributesOver _+_
Â·-distrib-+ = Â·-distribË¡-+ , Â·-distribÊ³-+

-- Addition is left distributive over addition
-- p + (q + r) â‰¡ (p + q) + (p + r)

+-distribË¡-+ : _+_ DistributesOverË¡ _+_
+-distribË¡-+ p q Ï‰ = refl
+-distribË¡-+ ğŸ˜ q ğŸ˜ = refl
+-distribË¡-+ Ï‰ ğŸ˜ ğŸ˜ = refl
+-distribË¡-+ Ï‰ Ï‰ ğŸ˜ = refl

-- Addition is right distributive over addition
-- (q + r) + p â‰¡ (q + p) + (r + p)

+-distribÊ³-+ : _+_ DistributesOverÊ³ _+_
+-distribÊ³-+ ğŸ˜ q r = refl
+-distribÊ³-+ Ï‰ q r = refl

-- Addition is distributive over addition
-- p + (q + r) â‰¡ (p + q) + (p + r) and (q + r) + p â‰¡ (q + p) + (r + p)

+-distrib-+ : _+_ DistributesOver _+_
+-distrib-+ = +-distribË¡-+ , +-distribÊ³-+

-----------------------------------------------------
-- Addition (and meet) form the following algebras --
-----------------------------------------------------

-- Addition forms a magma

+-Magma : IsMagma _+_
+-Magma = record
  { isEquivalence = isEquivalence
  ; âˆ™-cong        = congâ‚‚ _+_
  }

-- Addition forms a semigroup

+-Semigroup : IsSemigroup _+_
+-Semigroup = record
  { isMagma = +-Magma
  ; assoc   = +-Associative
  }

-- Addition forms a monoid for ğŸ˜ as identity

+-Monoid : IsMonoid _+_ ğŸ˜
+-Monoid = record
  { isSemigroup = +-Semigroup
  ; identity    = +-Identity
  }

-- Addition forms a commutative monoid with ğŸ˜ as identity

+-CommutativeMonoid : IsCommutativeMonoid _+_ ğŸ˜
+-CommutativeMonoid = record
  { isMonoid = +-Monoid
  ; comm     = +-Commutative
  }

-- Addition forms a band

+-Band : IsBand _+_
+-Band = record
  { isSemigroup = +-Semigroup
  ; idem        = +-Idempotent
  }

-- Addition forms a semilattice

+-Semilattice : IsSemilattice _+_
+-Semilattice = record
  { isBand = +-Band
  ; comm   = +-Commutative
  }

-------------------------------------------------
-- Multiplication forms the following algebras --
-------------------------------------------------

-- Multiplication forms a magma

Â·-Magma : IsMagma _Â·_
Â·-Magma = record
  { isEquivalence = isEquivalence
  ; âˆ™-cong        = congâ‚‚ _Â·_
  }

-- Multiplication forms a semigroup

Â·-Semigroup : IsSemigroup _Â·_
Â·-Semigroup = record
  { isMagma = Â·-Magma
  ; assoc   = Â·-Associative
  }

-- Multiplication forms a monoid with Ï‰ as identity

Â·-Monoid : IsMonoid _Â·_ Ï‰
Â·-Monoid = record
  { isSemigroup = Â·-Semigroup
  ; identity    = Â·-Identity
  }

-- Erasures form a modality

ErasureModality : Modality
ErasureModality = record
  { _+_                 = _+_
  ; _Â·_                 = _Â·_
  ; _âˆ§_                 = _âˆ§_
  ; nrâ¿                 = nrâ¿
  ; ğŸ˜                   = ğŸ˜
  ; ğŸ™                   = Ï‰
  ; +-CommutativeMonoid = +-CommutativeMonoid
  ; Â·-Monoid            = Â·-Monoid
  ; âˆ§-Semilattice       = +-Semilattice
  ; Â·-zero              = Â·-zero
  ; +-positive          = +-positive
  ; nrâ¿-rec             = Î» n p q r â†’ refl
  ; nrâ¿-0               = Î» p q r â†’ refl
  ; nrâ¿-fix             = 1 , nr-fixâ‚
  ; Â·-distrib-+         = Â·-distrib-+
  ; Â·-distrib-âˆ§         = Â·-distrib-+
  ; +-distrib-âˆ§         = +-distrib-+
  ; â‰ˆ-equivalence       = isEquivalence
  }
