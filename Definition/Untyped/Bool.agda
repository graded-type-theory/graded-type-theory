------------------------------------------------------------------------
-- Booleans, defined using other types
------------------------------------------------------------------------

-- Typing rules for the term formers defined in this module can be
-- found in Definition.Typed.Consequences.DerivedRules.Bool, and usage
-- rules can be found in Graded.Derived.Bool.

import Definition.Untyped
import Graded.Modality
import Graded.Modality.Dedicated-nr

module Definition.Untyped.Bool
  {a} {M : Set a}
  (open Definition.Untyped M)
  (open Graded.Modality M)
  (ğ•„ : Modality)
  (open Graded.Modality.Dedicated-nr ğ•„)
  -- One can define the booleans using either weak or strong Î£ and
  -- unit types.
  (s : Strength)
  -- It is assumed that there is a dedicated nr function.
  â¦ƒ has-nr : Dedicated-nr â¦„
  where

private
  open module M = Modality ğ•„ using (ğŸ˜; ğŸ™; Ï‰; _+_; _Â·_; _âˆ§_)

open import Definition.Untyped.Nat ğ•„
open import Definition.Untyped.Properties M
open import Definition.Untyped.Sigma ğ•„
open import Definition.Untyped.Unit ğ•„

open import Graded.Modality.Dedicated-nr.Instance
open import Graded.Modality.Nr-instances
open import Graded.Modality.Properties ğ•„
open import Graded.Mode ğ•„

open import Tools.Empty
open import Tools.Fin
open import Tools.Function
open import Tools.Nat as N hiding (_+_)
open import Tools.Product
open import Tools.PropositionalEquality
open import Tools.Reasoning.PropositionalEquality
open import Tools.Unit

private variable
  k kâ‚ kâ‚‚ n : Nat
  A t u v w : Term _
  Ïƒ         : Subst _ _
  Ï         : Wk _ _
  p         : M

------------------------------------------------------------------------
-- An Agda sketch of the implementation

private module Sketch where

  prodrecâ€² :
    âˆ€ {a p q} {A : Set a} {P : A â†’ Set p}
    (Q : Î£ A P â†’ Set q) â†’ âˆ€ x â†’ (âˆ€ x y â†’ Q (x , y)) â†’ Q x
  prodrecâ€² _ (x , y) f = f x y

  emptyrecâ€² : âˆ€ {a} (A : Set a) â†’ âŠ¥ â†’ A
  emptyrecâ€² _ ()

  unitrecâ€² : âˆ€ {p} (P : âŠ¤ â†’ Set p) â†’ âˆ€ x â†’ P tt â†’ P x
  unitrecâ€² _ _ x = x

  natcaseâ€² :
    âˆ€ {p} (P : Nat â†’ Set p) â†’ P 0 â†’ (âˆ€ n â†’ P (1+ n)) â†’ âˆ€ n â†’ P n
  natcaseâ€² _ z _ 0      = z
  natcaseâ€² _ _ s (1+ n) = s n

  strict-constâ€² : âˆ€ {a} (A : Set a) â†’ A â†’ Nat â†’ A
  strict-constâ€² _ x 0      = x
  strict-constâ€² A x (1+ n) = strict-constâ€² A x n

  OK : Nat â†’ Set
  OK =
    natcaseâ€² (Î» _ â†’ Set) âŠ¤
      (natcaseâ€² (Î» _ â†’ Set) âŠ¤ (Î» _ â†’ âŠ¥))

  Bool : Set
  Bool = Î£ Nat OK

  true : Bool
  true = 1 , tt

  false : Bool
  false = 0 , tt

  Target : âˆ€ {p} â†’ (Bool â†’ Set p) â†’ (n : Nat) â†’ OK n â†’ Set p
  Target P n ok = P (n , ok)

  boolrec : âˆ€ {p} (P : Bool â†’ Set p) â†’ P true â†’ P false â†’ âˆ€ b â†’ P b
  boolrec P t f b =
    prodrecâ€² P b
      (Î» n ok â†’
         natcaseâ€²
           (Î» n â†’ (ok : OK n) â†’ Target P n ok)
           (Î» ok â†’ unitrecâ€² (Î» ok â†’ Target P 0 ok) ok f)
           (Î» n â†’
              natcaseâ€² (Î» n â†’ (ok : OK (1+ n)) â†’ Target P (1+ n) ok)
                (Î» ok â†’ unitrecâ€² (Î» ok â†’ Target P 1 ok) ok t)
                (Î» n ok â†’
                   emptyrecâ€² (Target P (2+ n) ok)
                     (strict-constâ€² âŠ¥ ok n))
                n)
           n ok)

------------------------------------------------------------------------
-- Some grades

opaque

  -- A grade used in the implementation of OK.

  OKáµ : M
  OKáµ = nr ğŸ˜ ğŸ˜ ğŸ˜ ğŸ˜ ğŸ™

opaque

  -- A grade used in the implementation of Bool.

  Booláµ : M
  Booláµ = nr OKáµ ğŸ˜ ğŸ˜ ğŸ˜ ğŸ™

opaque

  -- A grade that is used in the implementation of boolrec.

  boolrecáµ-er : M
  boolrecáµ-er = case s of Î» where
    ğ•¨ â†’ ğŸ™
    ğ•¤ â†’ ğŸ˜

opaque

  -- A grade that is used in the implementation of boolrec.

  boolrecáµ-Î  : M
  boolrecáµ-Î  = nr ğŸ˜ ğŸ™ boolrecáµ-er ğŸ˜ ğŸ˜

opaque

  -- A grade that is used in the implementation of boolrec.

  boolrecáµ-ncâ‚ : M
  boolrecáµ-ncâ‚ = nr ğŸ˜ ğŸ™ ğŸ˜ ğŸ˜ boolrecáµ-er

opaque

  -- A grade that is used in the implementation of boolrec.

  boolrecáµ-ncâ‚‚ : M
  boolrecáµ-ncâ‚‚ = nr boolrecáµ-ncâ‚ ğŸ˜ ğŸ˜ ğŸ˜ ğŸ™

opaque

  -- A grade that is used in the implementation of boolrec.

  boolrecáµ-ncâ‚ƒ : M
  boolrecáµ-ncâ‚ƒ = âŒœ âŒ boolrecáµ-Î  âŒŸ âŒ Â· Booláµ

opaque

  -- A grade that is used in the implementation of boolrec.

  boolrecáµ-pr : M
  boolrecáµ-pr = nr boolrecáµ-ncâ‚‚ ğŸ˜ ğŸ˜ ğŸ˜ ğŸ™ âˆ§ boolrecáµ-Î 

------------------------------------------------------------------------
-- Some lemmas about the grades

opaque
  unfolding OKáµ

  -- If the dedicated nr function satisfies Linearity-like-nr-for-ğŸ˜,
  -- then OKáµ is equal to ğŸ˜Â âˆ§Â ğŸ™.

  OKáµâ‰¡ :
    Has-nr.Linearity-like-nr-for-ğŸ˜ has-dedicated-nr â†’
    OKáµ â‰¡ ğŸ˜ âˆ§ ğŸ™
  OKáµâ‰¡ hyp =
    nr ğŸ˜ ğŸ˜ ğŸ˜ ğŸ˜ ğŸ™                 â‰¡âŸ¨ hyp âŸ©
    ((ğŸ™ âˆ§ ğŸ˜) Â· ğŸ™ + ğŸ˜) âˆ§ (ğŸ™ + ğŸ˜)  â‰¡âŸ¨ congâ‚‚ _âˆ§_ (M.+-identityÊ³ _) (M.+-identityÊ³ _) âŸ©
    ((ğŸ™ âˆ§ ğŸ˜) Â· ğŸ™) âˆ§ ğŸ™            â‰¡âŸ¨ cong (flip _âˆ§_ _) $ M.Â·-identityÊ³ _ âŸ©
    (ğŸ™ âˆ§ ğŸ˜) âˆ§ ğŸ™                  â‰¡âŸ¨ cong (flip _âˆ§_ _) $ M.âˆ§-comm _ _ âŸ©
    (ğŸ˜ âˆ§ ğŸ™) âˆ§ ğŸ™                  â‰¡âŸ¨ M.âˆ§-assoc _ _ _ âŸ©
    ğŸ˜ âˆ§ (ğŸ™ âˆ§ ğŸ™)                  â‰¡âŸ¨ cong (_âˆ§_ _) $ M.âˆ§-idem _ âŸ©
    ğŸ˜ âˆ§ ğŸ™                        âˆ
    where
    open Tools.Reasoning.PropositionalEquality

opaque
  unfolding Booláµ

  -- If the dedicated nr function satisfies Linearity-like-nr-for-ğŸ˜,
  -- then Booláµ is equal to ğŸ˜Â âˆ§Â ğŸ™.

  Booláµâ‰¡ :
    Has-nr.Linearity-like-nr-for-ğŸ˜ has-dedicated-nr â†’
    Booláµ â‰¡ ğŸ˜ âˆ§ ğŸ™
  Booláµâ‰¡ hyp =
    nr OKáµ ğŸ˜ ğŸ˜ ğŸ˜ ğŸ™                 â‰¡âŸ¨ hyp âŸ©
    ((ğŸ™ âˆ§ OKáµ) Â· ğŸ™ + ğŸ˜) âˆ§ (ğŸ™ + ğŸ˜)  â‰¡âŸ¨ congâ‚‚ _âˆ§_ (M.+-identityÊ³ _) (M.+-identityÊ³ _) âŸ©
    ((ğŸ™ âˆ§ OKáµ) Â· ğŸ™) âˆ§ ğŸ™            â‰¡âŸ¨ cong (flip _âˆ§_ _) $ M.Â·-identityÊ³ _ âŸ©
    (ğŸ™ âˆ§ OKáµ) âˆ§ ğŸ™                  â‰¡âŸ¨ cong (flip _âˆ§_ _) $ M.âˆ§-comm _ _ âŸ©
    (OKáµ âˆ§ ğŸ™) âˆ§ ğŸ™                  â‰¡âŸ¨ M.âˆ§-assoc _ _ _ âŸ©
    OKáµ âˆ§ (ğŸ™ âˆ§ ğŸ™)                  â‰¡âŸ¨ cong (_âˆ§_ _) $ M.âˆ§-idem _ âŸ©
    OKáµ âˆ§ ğŸ™                        â‰¡âŸ¨ cong (_âˆ§ _) $ OKáµâ‰¡ hyp âŸ©
    (ğŸ˜ âˆ§ ğŸ™) âˆ§ ğŸ™                    â‰¡âŸ¨ M.âˆ§-assoc _ _ _ âŸ©
    ğŸ˜ âˆ§ (ğŸ™ âˆ§ ğŸ™)                    â‰¡âŸ¨ cong (_âˆ§_ _) $ M.âˆ§-idem _ âŸ©
    ğŸ˜ âˆ§ ğŸ™                          âˆ
    where
    open Tools.Reasoning.PropositionalEquality

opaque
  unfolding boolrecáµ-Î 

  -- If the dedicated nr function satisfies Linearity-like-nr-for-ğŸ™,
  -- then boolrecáµ-Î  is equal to boolrecáµ-er.

  boolrecáµ-Î â‰¡ :
    Has-nr.Linearity-like-nr-for-ğŸ™ has-dedicated-nr â†’
    boolrecáµ-Î  â‰¡ boolrecáµ-er
  boolrecáµ-Î â‰¡ hyp =
    nr ğŸ˜ ğŸ™ boolrecáµ-er ğŸ˜ ğŸ˜             â‰¡âŸ¨ hyp âŸ©
    (ğŸ™ + ğŸ˜) Â· ğŸ˜ + Ï‰ Â· ğŸ˜ + boolrecáµ-er  â‰¡âŸ¨ trans (congâ‚‚ _+_ (M.Â·-zeroÊ³ _) (cong (flip _+_ _) $ M.Â·-zeroÊ³ _)) $
                                          trans (M.+-identityË¡ _) $
                                          M.+-identityË¡ _ âŸ©
    boolrecáµ-er                        âˆ
    where
    open Tools.Reasoning.PropositionalEquality

opaque
  unfolding boolrecáµ-er boolrecáµ-Î 

  -- If s is ğ•¤, then boolrecáµ-Î  is equal toÂ ğŸ˜.

  boolrecáµ-Î â‰¡ğŸ˜ : s â‰¡ ğ•¤ â†’ boolrecáµ-Î  â‰¡ ğŸ˜
  boolrecáµ-Î â‰¡ğŸ˜ refl =
    nr ğŸ˜ ğŸ™ ğŸ˜ ğŸ˜ ğŸ˜  â‰¡âŸ¨ nr-ğŸ˜ âŸ©
    ğŸ˜             âˆ
    where
    open Tools.Reasoning.PropositionalEquality

opaque
  unfolding boolrecáµ-ncâ‚

  -- If the dedicated nr function satisfies Linearity-like-nr-for-ğŸ™,
  -- then boolrecáµ-ncâ‚ is equal to boolrecáµ-er.

  boolrecáµ-ncâ‚â‰¡ :
    Has-nr.Linearity-like-nr-for-ğŸ™ has-dedicated-nr â†’
    boolrecáµ-ncâ‚ â‰¡ boolrecáµ-er
  boolrecáµ-ncâ‚â‰¡ hyp =
    nr ğŸ˜ ğŸ™ ğŸ˜ ğŸ˜ boolrecáµ-er             â‰¡âŸ¨ hyp âŸ©
    (ğŸ™ + ğŸ˜) Â· boolrecáµ-er + Ï‰ Â· ğŸ˜ + ğŸ˜  â‰¡âŸ¨ congâ‚‚ _+_ (cong (flip _Â·_ _) $ M.+-identityÊ³ _) (M.+-identityÊ³ _) âŸ©
    ğŸ™ Â· boolrecáµ-er + Ï‰ Â· ğŸ˜            â‰¡âŸ¨ congâ‚‚ _+_ (M.Â·-identityË¡ _) (M.Â·-zeroÊ³ _) âŸ©
    boolrecáµ-er + ğŸ˜                    â‰¡âŸ¨ M.+-identityÊ³ _ âŸ©
    boolrecáµ-er                        âˆ
    where
    open Tools.Reasoning.PropositionalEquality

private opaque

  -- A lemma used below.

  â‰¡boolrecáµ-erâˆ§ğŸ™ :
    ((ğŸ™ âˆ§ boolrecáµ-er) Â· ğŸ™ + ğŸ˜) âˆ§ (ğŸ™ + ğŸ˜) â‰¡ boolrecáµ-er âˆ§ ğŸ™
  â‰¡boolrecáµ-erâˆ§ğŸ™ =
    ((ğŸ™ âˆ§ boolrecáµ-er) Â· ğŸ™ + ğŸ˜) âˆ§ (ğŸ™ + ğŸ˜)  â‰¡âŸ¨ congâ‚‚ _âˆ§_ (M.+-identityÊ³ _) (M.+-identityÊ³ _) âŸ©
    ((ğŸ™ âˆ§ boolrecáµ-er) Â· ğŸ™) âˆ§ ğŸ™            â‰¡âŸ¨ cong (flip _âˆ§_ _) $ M.Â·-identityÊ³ _ âŸ©
    (ğŸ™ âˆ§ boolrecáµ-er) âˆ§ ğŸ™                  â‰¡âŸ¨ cong (flip _âˆ§_ _) $ M.âˆ§-comm _ _ âŸ©
    (boolrecáµ-er âˆ§ ğŸ™) âˆ§ ğŸ™                  â‰¡âŸ¨ M.âˆ§-assoc _ _ _ âŸ©
    boolrecáµ-er âˆ§ (ğŸ™ âˆ§ ğŸ™)                  â‰¡âŸ¨ cong (_âˆ§_ _) $ M.âˆ§-idem _ âŸ©
    boolrecáµ-er âˆ§ ğŸ™                        âˆ
    where
    open Tools.Reasoning.PropositionalEquality

opaque
  unfolding boolrecáµ-ncâ‚‚

  -- If the dedicated nr function satisfies Linearity-like-nr-for-ğŸ˜
  -- and Linearity-like-nr-for-ğŸ™, then boolrecáµ-ncâ‚‚ is equal to
  -- boolrecáµ-erÂ âˆ§Â ğŸ™.

  boolrecáµ-ncâ‚‚â‰¡ :
    Has-nr.Linearity-like-nr-for-ğŸ˜ has-dedicated-nr â†’
    Has-nr.Linearity-like-nr-for-ğŸ™ has-dedicated-nr â†’
    boolrecáµ-ncâ‚‚ â‰¡ boolrecáµ-er âˆ§ ğŸ™
  boolrecáµ-ncâ‚‚â‰¡ hypâ‚ hypâ‚‚ =
    nr boolrecáµ-ncâ‚ ğŸ˜ ğŸ˜ ğŸ˜ ğŸ™                â‰¡âŸ¨ cong (Î» p â†’ nr p _ _ _ _) $ boolrecáµ-ncâ‚â‰¡ hypâ‚‚ âŸ©
    nr boolrecáµ-er ğŸ˜ ğŸ˜ ğŸ˜ ğŸ™                 â‰¡âŸ¨ hypâ‚ âŸ©
    ((ğŸ™ âˆ§ boolrecáµ-er) Â· ğŸ™ + ğŸ˜) âˆ§ (ğŸ™ + ğŸ˜)  â‰¡âŸ¨ â‰¡boolrecáµ-erâˆ§ğŸ™ âŸ©
    boolrecáµ-er âˆ§ ğŸ™                        âˆ
    where
    open Tools.Reasoning.PropositionalEquality

opaque
  unfolding boolrecáµ-er boolrecáµ-pr

  -- If the dedicated nr function satisfies Linearity-like-nr-for-ğŸ˜
  -- and Linearity-like-nr-for-ğŸ™, then boolrecáµ-pr is equal to
  -- boolrecáµ-erÂ âˆ§Â ğŸ™.

  boolrecáµ-prâ‰¡ :
    Has-nr.Linearity-like-nr-for-ğŸ˜ has-dedicated-nr â†’
    Has-nr.Linearity-like-nr-for-ğŸ™ has-dedicated-nr â†’
    boolrecáµ-pr â‰¡ boolrecáµ-er âˆ§ ğŸ™
  boolrecáµ-prâ‰¡ hypâ‚ hypâ‚‚ =
    nr boolrecáµ-ncâ‚‚ ğŸ˜ ğŸ˜ ğŸ˜ ğŸ™ âˆ§ boolrecáµ-Î                          â‰¡âŸ¨ congâ‚‚ _âˆ§_
                                                                      (cong (Î» p â†’ nr p _ _ _ _) $ boolrecáµ-ncâ‚‚â‰¡ hypâ‚ hypâ‚‚)
                                                                      (boolrecáµ-Î â‰¡ hypâ‚‚) âŸ©
    nr (boolrecáµ-er âˆ§ ğŸ™) ğŸ˜ ğŸ˜ ğŸ˜ ğŸ™ âˆ§ boolrecáµ-er                   â‰¡âŸ¨ cong (flip _âˆ§_ _) hypâ‚ âŸ©
    (((ğŸ™ âˆ§ (boolrecáµ-er âˆ§ ğŸ™)) Â· ğŸ™ + ğŸ˜) âˆ§ (ğŸ™ + ğŸ˜)) âˆ§ boolrecáµ-er  â‰¡âŸ¨ cong (flip _âˆ§_ _) $ cong (flip _âˆ§_ _) $ cong (flip _+_ _) $ cong (flip _Â·_ _) $
                                                                    trans (cong (_âˆ§_ _) $ M.âˆ§-comm _ _) $
                                                                    trans (sym $ M.âˆ§-assoc _ _ _) $
                                                                    cong (flip _âˆ§_ _) $ M.âˆ§-idem _ âŸ©
    (((ğŸ™ âˆ§ boolrecáµ-er) Â· ğŸ™ + ğŸ˜) âˆ§ (ğŸ™ + ğŸ˜)) âˆ§ boolrecáµ-er        â‰¡âŸ¨ cong (flip _âˆ§_ _) â‰¡boolrecáµ-erâˆ§ğŸ™ âŸ©
    (boolrecáµ-er âˆ§ ğŸ™) âˆ§ boolrecáµ-er                              â‰¡âŸ¨ trans (M.âˆ§-comm _ _) $
                                                                    trans (sym $ M.âˆ§-assoc _ _ _) $
                                                                    cong (flip _âˆ§_ _) $ M.âˆ§-idem _ âŸ©
    boolrecáµ-er âˆ§ ğŸ™                                              âˆ
    where
    open Tools.Reasoning.PropositionalEquality

opaque
  unfolding boolrecáµ-pr

  -- If the modality's zero is well-behaved, then boolrecáµ-pr is
  -- non-zero.

  boolrecáµ-prâ‰¢ğŸ˜ :
    â¦ƒ ğŸ˜-well-behaved : Has-well-behaved-zero M.semiring-with-meet â¦„ â†’
    boolrecáµ-pr â‰¢ ğŸ˜
  boolrecáµ-prâ‰¢ğŸ˜ =
    nr boolrecáµ-ncâ‚‚ ğŸ˜ ğŸ˜ ğŸ˜ ğŸ™ âˆ§ boolrecáµ-Î  â‰¡ ğŸ˜  â†’âŸ¨ âˆ§-positiveË¡ âŸ©
    nr boolrecáµ-ncâ‚‚ ğŸ˜ ğŸ˜ ğŸ˜ ğŸ™ â‰¡ ğŸ˜               â†’âŸ¨ projâ‚‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ nr-positive âŸ©
    ğŸ™ â‰¡ ğŸ˜                                     â†’âŸ¨ non-trivial âŸ©
    âŠ¥                                         â–¡

------------------------------------------------------------------------
-- Term formers

opaque

  -- A definition that is used in the implementation of Bool.

  OK : Term n â†’ Term n
  OK t =
    natcase OKáµ ğŸ˜ (U 0) (Unit s 0)
      (natcase ğŸ˜ ğŸ˜ (U 0) (Unit s 0) Empty (var x0)) t

opaque

  -- A type of booleans.

  Bool : Term n
  Bool = Î£âŸ¨ s âŸ© ğŸ™ , Booláµ â–· â„• â–¹ OK (var x0)

opaque

  -- The constructor true.

  true : Term n
  true = prod s ğŸ™ (suc zero) (star s 0)

opaque

  -- The constructor false.

  false : Term n
  false = prod s ğŸ™ zero (star s 0)

opaque

  -- A definition that is used in the implementation of boolrec.

  Target :
    âˆ€ k â†’ Term (1+ n) â†’ Term (k N.+ n) â†’ Term (k N.+ n) â†’ Term (k N.+ n)
  Target k A t u = A [ k ][ prod s ğŸ™ t u ]â†‘

opaque

  -- An eliminator for Bool.

  boolrec : M â†’ Term (1+ n) â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  boolrec p A t u v =
    prodrecâŸ¨ s âŸ© boolrecáµ-pr ğŸ™ p A v
      (natcase boolrecáµ-ncâ‚‚ (boolrecáµ-ncâ‚ƒ + p)
         (Î  boolrecáµ-Î  , p â–· OK (var x0) â–¹ Target 4 A (var x1) (var x0))
         (lam boolrecáµ-Î  $
          unitrecâŸ¨ s âŸ© 0 boolrecáµ-Î  p (Target 4 A zero (var x0))
            (var x0) (wk[ 3 ]â€² u))
         (natcase boolrecáµ-ncâ‚ (boolrecáµ-ncâ‚ƒ + p)
            (Î  boolrecáµ-Î  , p â–· OK (suc (var x0)) â–¹
             Target 5 A (suc (var x1)) (var x0))
            (lam boolrecáµ-Î  $
             unitrecâŸ¨ s âŸ© 0 boolrecáµ-Î  p
               (Target 5 A (suc zero) (var x0)) (var x0) (wk[ 4 ]â€² t))
            (lam boolrecáµ-Î  $
             emptyrec boolrecáµ-er
               (Target 5 A (suc (suc (var x1))) (var x0))
               (strict-const Empty (var x0) (var x1)))
            (var x0))
         (var x1) âˆ˜âŸ¨ boolrecáµ-Î  âŸ©
       var x0)

------------------------------------------------------------------------
-- An unfolding lemma

opaque
  unfolding Target

  -- An unfolding lemma for Target.

  Targetâ‰¡ : Target k A t u â‰¡ A [ k ][ prod s ğŸ™ t u ]â†‘
  Targetâ‰¡ = refl

------------------------------------------------------------------------
-- Substitution lemmas

opaque
  unfolding OK

  -- A substitution lemma for OK.

  OK-[] : OK t [ Ïƒ ] â‰¡ OK (t [ Ïƒ ])
  OK-[] =
    trans natcase-[] $
    cong (flip (natcase _ _ _ _) _) natcase-[]

opaque
  unfolding Bool

  -- A substitution lemma for Bool.

  Bool-[] : Bool [ Ïƒ ] â‰¡ Bool
  Bool-[] {Ïƒ} =
    (Î£âŸ¨ s âŸ© ğŸ™ , Booláµ â–· â„• â–¹ OK (var x0)) [ Ïƒ ]    â‰¡âŸ¨âŸ©
    Î£âŸ¨ s âŸ© ğŸ™ , Booláµ â–· â„• â–¹ (OK (var x0) [ Ïƒ â‡‘ ])  â‰¡âŸ¨ cong (Î£âŸ¨_âŸ©_,_â–·_â–¹_ _ _ _ _) OK-[] âŸ©
    Î£âŸ¨ s âŸ© ğŸ™ , Booláµ â–· â„• â–¹ OK (var x0)            âˆ

opaque
  unfolding true

  -- A substitution lemma for true.

  true-[] : true [ Ïƒ ] â‰¡ true
  true-[] = refl

opaque
  unfolding false

  -- A substitution lemma for false.

  false-[] : false [ Ïƒ ] â‰¡ false
  false-[] = refl

opaque
  unfolding Target

  -- A substitution lemma for Target.

  Target-[â‡‘] :
    Target k A t u [ Ïƒ â‡‘[ k ] ] â‰¡
    Target k (A [ Ïƒ â‡‘ ]) (t [ Ïƒ â‡‘[ k ] ]) (u [ Ïƒ â‡‘[ k ] ])
  Target-[â‡‘] {A} = [][]â†‘-commutes A

opaque
  unfolding Target

  -- A substitution lemma for Target.

  Target-+-[â‡‘] :
    âˆ€ j {t u} â†’
    let cast =
          substâ‚‚ Subst (sym $ +-assoc j kâ‚‚ n) (sym $ +-assoc j kâ‚ n)
    in
    (âˆ€ x â†’ wk[ kâ‚ ] (var x) [ Ïƒ ] â‰¡ wk[ kâ‚‚ ] (var x)) â†’
    Target (j N.+ kâ‚) A t u [ cast (Ïƒ â‡‘[ j ]) ] â‰¡
    Target (j N.+ kâ‚‚) A (t [ cast (Ïƒ â‡‘[ j ]) ]) (u [ cast (Ïƒ â‡‘[ j ]) ])
  Target-+-[â‡‘] {A} _ = [][]â†‘-commutes-+ A

opaque
  unfolding Target

  -- A substitution lemma for Target.

  Target-[â‚€â‡‘] :
    âˆ€ j {t u} â†’
    let cast =
          substâ‚‚ Subst (sym $ +-assoc j k n) (sym $ +-assoc j (1+ k) n)
    in
    Target (j N.+ 1+ k) A t u [ cast (sgSubst v â‡‘[ j ]) ] â‰¡
    Target (j N.+ k) A (t [ cast (sgSubst v â‡‘[ j ]) ])
      (u [ cast (sgSubst v â‡‘[ j ]) ])
  Target-[â‚€â‡‘] {A} _ = [][]â†‘-[â‚€â‡‘] _ A

opaque
  unfolding Target

  -- A substitution lemma for Target.

  Target-[â†‘â‡‘] :
    âˆ€ j {t u} â†’
    let cast =
          substâ‚‚ Subst (sym $ +-assoc j (1+ k) n)
            (sym $ +-assoc j (1+ k) n)
    in
    Target (j N.+ 1+ k) A t u
      [ cast (consSubst (wk1Subst idSubst) v â‡‘[ j ]) ] â‰¡
    Target (j N.+ 1+ k) A
      (t [ cast (consSubst (wk1Subst idSubst) v â‡‘[ j ]) ])
      (u [ cast (consSubst (wk1Subst idSubst) v â‡‘[ j ]) ])
  Target-[â†‘â‡‘] {A} _ = [][]â†‘-[â†‘â‡‘] _ A

opaque
  unfolding Target

  -- A substitution lemma for Target.

  Target-[,â‡‘] :
    âˆ€ j {t u} â†’
    let cast =
          substâ‚‚ Subst (sym $ +-assoc j k n) (sym $ +-assoc j (2+ k) n)
    in
    Target (j N.+ 2+ k) A t u
      [ cast (consSubst (sgSubst v) w â‡‘[ j ]) ] â‰¡
    Target (j N.+ k) A
      (t [ cast (consSubst (sgSubst v) w â‡‘[ j ]) ])
      (u [ cast (consSubst (sgSubst v) w â‡‘[ j ]) ])
  Target-[,â‡‘] {A} _ = [][]â†‘-[,â‡‘] _ A

opaque
  unfolding boolrec

  -- A substitution lemma for boolrec.

  boolrec-[] :
    boolrec p A t u v [ Ïƒ ] â‰¡
    boolrec p (A [ Ïƒ â‡‘ ]) (t [ Ïƒ ]) (u [ Ïƒ ]) (v [ Ïƒ ])
  boolrec-[] {p} {A} {t} {u} {v} {Ïƒ} =
    prodrecâŸ¨ s âŸ© boolrecáµ-pr ğŸ™ p A v
      (natcase boolrecáµ-ncâ‚‚ (boolrecáµ-ncâ‚ƒ + p)
         (Î  boolrecáµ-Î  , p â–· OK (var x0) â–¹ Target 4 A (var x1) (var x0))
         (lam boolrecáµ-Î  $
          unitrecâŸ¨ s âŸ© 0 boolrecáµ-Î  p (Target 4 A zero (var x0))
            (var x0) (wk[ 3 ]â€² u))
         (natcase boolrecáµ-ncâ‚ (boolrecáµ-ncâ‚ƒ + p)
            (Î  boolrecáµ-Î  , p â–· OK (suc (var x0)) â–¹
             Target 5 A (suc (var x1)) (var x0))
            (lam boolrecáµ-Î  $
             unitrecâŸ¨ s âŸ© 0 boolrecáµ-Î  p
               (Target 5 A (suc zero) (var x0)) (var x0) (wk[ 4 ]â€² t))
            (lam boolrecáµ-Î  $
             emptyrec boolrecáµ-er
               (Target 5 A (suc (suc (var x1))) (var x0))
               (strict-const Empty (var x0) (var x1)))
            (var x0))
         (var x1) âˆ˜âŸ¨ boolrecáµ-Î  âŸ©
       var x0)
      [ Ïƒ ]                                                               â‰¡âŸ¨ trans prodrecâŸ¨âŸ©-[] $
                                                                             cong (prodrecâŸ¨ _ âŸ© _ _ _ _ _) $
                                                                             cong (flip _âˆ˜âŸ¨ boolrecáµ-Î  âŸ©_ _) $
                                                                             trans natcase-[] $
                                                                             congâ‚„ (natcase _ _)
                                                                               (congâ‚‚ (Î _,_â–·_â–¹_ _ _) OK-[] refl)
                                                                               (cong (lam _) unitrecâŸ¨âŸ©-[])
                                                                               (trans natcase-[] $
                                                                                congâ‚„ (natcase _ _)
                                                                                  (congâ‚‚ (Î _,_â–·_â–¹_ _ _) OK-[] refl)
                                                                                  (cong (lam _) unitrecâŸ¨âŸ©-[])
                                                                                  (cong (lam _) $
                                                                                   cong (emptyrec _ _) strict-const-[])
                                                                                  refl)
                                                                               refl âŸ©
    prodrecâŸ¨ s âŸ© boolrecáµ-pr ğŸ™ p (A [ Ïƒ â‡‘ ]) (v [ Ïƒ ])
      (natcase boolrecáµ-ncâ‚‚ (boolrecáµ-ncâ‚ƒ + p)
         (Î  boolrecáµ-Î  , p â–· OK (var x0) â–¹
          (Target 4 A (var x1) (var x0) [ Ïƒ â‡‘[ 4 ] ]))
         (lam boolrecáµ-Î  $
          unitrecâŸ¨ s âŸ© 0 boolrecáµ-Î  p
            (Target 4 A zero (var x0) [ Ïƒ â‡‘[ 4 ] ]) (var x0)
            (wk[ 3 ]â€² u [ Ïƒ â‡‘[ 3 ] ]))
         (natcase boolrecáµ-ncâ‚ (boolrecáµ-ncâ‚ƒ + p)
            (Î  boolrecáµ-Î  , p â–· OK (suc (var x0)) â–¹
             (Target 5 A (suc (var x1)) (var x0) [ Ïƒ â‡‘[ 5 ] ]))
            (lam boolrecáµ-Î  $
             unitrecâŸ¨ s âŸ© 0 boolrecáµ-Î  p
               (Target 5 A (suc zero) (var x0) [ Ïƒ â‡‘[ 5 ] ]) (var x0)
               (wk[ 4 ]â€² t [ Ïƒ â‡‘[ 4 ] ]))
            (lam boolrecáµ-Î  $
             emptyrec boolrecáµ-er
               (Target 5 A (suc (suc (var x1))) (var x0) [ Ïƒ â‡‘[ 5 ] ])
               (strict-const Empty (var x0) (var x1)))
            (var x0))
         (var x1) âˆ˜âŸ¨ boolrecáµ-Î  âŸ©
       var x0)                                                            â‰¡âŸ¨ cong (prodrecâŸ¨ _ âŸ© _ _ _ _ _) $
                                                                             cong (flip (_âˆ˜âŸ¨ boolrecáµ-Î  âŸ©_) _) $
                                                                             congâ‚„ (natcase _ _)
                                                                               (cong (Î _,_â–·_â–¹_ _ _ _) Target-[â‡‘])
                                                                               (cong (lam _) $
                                                                                congâ‚ƒ (unitrecâŸ¨ _ âŸ© _ _ _)
                                                                                  Target-[â‡‘] refl (wk[]â€²-[â‡‘] u))
                                                                               (congâ‚„ (natcase _ _)
                                                                                  (cong (Î _,_â–·_â–¹_ _ _ _) Target-[â‡‘])
                                                                                  (cong (lam _) $
                                                                                   congâ‚ƒ (unitrecâŸ¨ _ âŸ© _ _ _)
                                                                                     Target-[â‡‘] refl (wk[]â€²-[â‡‘] t))
                                                                                  (cong (lam _) $
                                                                                   congâ‚‚ (emptyrec _) Target-[â‡‘] refl)
                                                                                  refl)
                                                                               refl âŸ©
    prodrecâŸ¨ s âŸ© boolrecáµ-pr ğŸ™ p (A [ Ïƒ â‡‘ ]) (v [ Ïƒ ])
      (natcase boolrecáµ-ncâ‚‚ (boolrecáµ-ncâ‚ƒ + p)
         (Î  boolrecáµ-Î  , p â–· OK (var x0) â–¹
          Target 4 (A [ Ïƒ â‡‘ ]) (var x1) (var x0))
         (lam boolrecáµ-Î  $
          unitrecâŸ¨ s âŸ© 0 boolrecáµ-Î  p
            (Target 4 (A [ Ïƒ â‡‘ ]) zero (var x0)) (var x0)
            (wk[ 3 ]â€² (u [ Ïƒ ])))
         (natcase boolrecáµ-ncâ‚ (boolrecáµ-ncâ‚ƒ + p)
            (Î  boolrecáµ-Î  , p â–· OK (suc (var x0)) â–¹
             Target 5 (A [ Ïƒ â‡‘ ]) (suc (var x1)) (var x0))
            (lam boolrecáµ-Î  $
             unitrecâŸ¨ s âŸ© 0 boolrecáµ-Î  p
               (Target 5 (A [ Ïƒ â‡‘ ]) (suc zero) (var x0)) (var x0)
               (wk[ 4 ]â€² (t [ Ïƒ ])))
            (lam boolrecáµ-Î  $
             emptyrec boolrecáµ-er
               (Target 5 (A [ Ïƒ â‡‘ ]) (suc (suc (var x1))) (var x0))
               (strict-const Empty (var x0) (var x1)))
            (var x0))
         (var x1) âˆ˜âŸ¨ boolrecáµ-Î  âŸ©
       var x0)                                                            âˆ

------------------------------------------------------------------------
-- Weakening lemmas

opaque

  -- A weakening lemma for OK.

  wk-OK : wk Ï (OK t) â‰¡ OK (wk Ï t)
  wk-OK {Ï} {t} =
    wk Ï (OK t)           â‰¡âŸ¨ wkâ‰¡subst _ _ âŸ©
    OK t [ toSubst Ï ]    â‰¡âŸ¨ OK-[] âŸ©
    OK (t [ toSubst Ï ])  â‰¡Ë˜âŸ¨ cong OK $ wkâ‰¡subst _ _ âŸ©
    OK (wk Ï t)           âˆ

opaque

  -- A weakening lemma for Bool.

  wk-Bool : wk Ï Bool â‰¡ Bool
  wk-Bool {Ï} =
    wk Ï Bool           â‰¡âŸ¨ wkâ‰¡subst _ _ âŸ©
    Bool [ toSubst Ï ]  â‰¡âŸ¨ Bool-[] âŸ©
    Bool                âˆ

opaque

  -- A weakening lemma for true.

  wk-true : wk Ï true â‰¡ true
  wk-true {Ï} =
    wk Ï true           â‰¡âŸ¨ wkâ‰¡subst _ _ âŸ©
    true [ toSubst Ï ]  â‰¡âŸ¨ true-[] âŸ©
    true                âˆ

opaque

  -- A weakening lemma for false.

  wk-false : wk Ï false â‰¡ false
  wk-false {Ï} =
    wk Ï false           â‰¡âŸ¨ wkâ‰¡subst _ _ âŸ©
    false [ toSubst Ï ]  â‰¡âŸ¨ false-[] âŸ©
    false                âˆ

opaque

  -- A weakening lemma for Target.

  wk-liftn-Target :
    wk (liftn Ï k) (Target k A t u) â‰¡
    Target k (wk (lift Ï) A) (wk (liftn Ï k) t) (wk (liftn Ï k) u)
  wk-liftn-Target {Ï} {k} {A} {t} {u} =
    wk (liftn Ï k) (Target k A t u)                                 â‰¡âŸ¨ wk-liftn k âŸ©

    Target k A t u [ toSubst Ï â‡‘[ k ] ]                             â‰¡âŸ¨ Target-[â‡‘] âŸ©

    Target k (A [ toSubst Ï â‡‘ ]) (t [ toSubst Ï â‡‘[ k ] ])
      (u [ toSubst Ï â‡‘[ k ] ])                                      â‰¡Ë˜âŸ¨ congâ‚ƒ (Target _) (wk-liftn 1) (wk-liftn k) (wk-liftn k) âŸ©

    Target k (wk (lift Ï) A) (wk (liftn Ï k) t) (wk (liftn Ï k) u)  âˆ

opaque
  unfolding Target

  -- Another weakening lemma for Target.

  Target-wk[]â€² :
    Target k A (wk[ k ]â€² t) (wk[ k ]â€² u) â‰¡
    wk[ k ]â€² (A [ prod s ğŸ™ t u ]â‚€)
  Target-wk[]â€² {k} {A} {t} {u} =
    A [ k ][ prod s ğŸ™ (wk[ k ]â€² t) (wk[ k ]â€² u) ]â†‘  â‰¡âŸ¨âŸ©
    A [ k ][ wk[ k ]â€² (prod s ğŸ™ t u) ]â†‘             â‰¡âŸ¨ [][wk[]â€²]â†‘ A âŸ©
    wk[ k ]â€² (A [ prod s ğŸ™ t u ]â‚€)                  âˆ

opaque

  -- A weakening lemma for boolrec.

  wk-boolrec :
    wk Ï (boolrec p A t u v) â‰¡
    boolrec p (wk (lift Ï) A) (wk Ï t) (wk Ï u) (wk Ï v)
  wk-boolrec {Ï} {p} {A} {t} {u} {v} =
    wk Ï (boolrec p A t u v)                                           â‰¡âŸ¨ wk-liftn 0 âŸ©

    boolrec p A t u v [ toSubst Ï ]                                    â‰¡âŸ¨ boolrec-[] âŸ©

    boolrec p (A [ toSubst Ï â‡‘ ]) (t [ toSubst Ï ]) (u [ toSubst Ï ])
      (v [ toSubst Ï ])                                                â‰¡Ë˜âŸ¨ congâ‚„ (boolrec _)
                                                                             (wk-liftn 1) (wk-liftn 0) (wk-liftn 0) (wk-liftn 0) âŸ©
    boolrec p (wk (lift Ï) A) (wk Ï t) (wk Ï u) (wk Ï v)               âˆ
