------------------------------------------------------------------------
-- Prodrec for strong Î£-types and projections for all Î£-types
------------------------------------------------------------------------

{-# OPTIONS --hidden-argument-puns #-}

-- These definitions are part of an investigation of to what degree
-- weak Î£-types can emulate strong Î£-types, and vice versa. This
-- investigation was prompted by a question asked by an anonymous
-- reviewer. See also Definition.Typed.Consequences.DerivedRules.Sigma
-- and Graded.Derived.Sigma.

open import Graded.Modality

module Definition.Untyped.Sigma
  {a} {M : Set a}
  (ğ•„ : Modality M)
  where

open Modality ğ•„

open import Definition.Untyped M
open import Definition.Untyped.Properties M

open import Tools.Fin
open import Tools.Function
open import Tools.Nat using (Nat; 1+; 2+)
open import Tools.PropositionalEquality
open import Tools.Reasoning.PropositionalEquality

private variable
  n     : Nat
  A B t : Term _
  Ïƒ     : Subst _ _
  s     : Strength
  p q   : M

-- A definition of prodrec for strong Î£-types.

prodrecË¢ : M â†’ Term n â†’ Term (2+ n) â†’ Term n
prodrecË¢ p t u = u [ fst p t , snd p t ]

-- The projections are defined using some extra quantities râ€² andÂ qâ€².

module FstÊ·-sndÊ· (râ€² qâ€² : M) where

  -- The first projection.

  fstÊ· : M â†’ Term n â†’ Term n â†’ Term n
  fstÊ· p A t = prodrec râ€² p qâ€² (wk1 A) t (var x1)

  -- The second projection.

  sndÊ· : M â†’ M â†’ Term n â†’ Term (1+ n) â†’ Term n â†’ Term n
  sndÊ· p q A B t =
    prodrec râ€² p q (B [ fstÊ· p (wk1 A) (var x0) ]â†‘) t (var x0)

  opaque

    -- A substitution lemma for fstÊ·.

    fstÊ·-[] : âˆ€ A t â†’ fstÊ· p A t [ Ïƒ ] â‰¡ fstÊ· p (A [ Ïƒ ]) (t [ Ïƒ ])
    fstÊ·-[] {p} {Ïƒ} A t =
      fstÊ· p A t [ Ïƒ ]                                            â‰¡âŸ¨âŸ©
      prodrec râ€² p qâ€² (wk1 A [ liftSubst Ïƒ ]) (t [ Ïƒ ]) (var x1)  â‰¡âŸ¨ cong (Î» A â†’ prodrec _ _ _ A _ _) (wk1-liftSubst A) âŸ©
      prodrec râ€² p qâ€² (wk1 (A [ Ïƒ ])) (t [ Ïƒ ]) (var x1)          â‰¡âŸ¨âŸ©
      fstÊ· p (A [ Ïƒ ]) (t [ Ïƒ ])                                  âˆ

  opaque

    -- A substitution lemma for sndÊ·.

    sndÊ·-[] :
      âˆ€ B t â†’
      sndÊ· p q A B t [ Ïƒ ] â‰¡
      sndÊ· p q (A [ Ïƒ ]) (B [ liftSubst Ïƒ ]) (t [ Ïƒ ])
    sndÊ·-[] {p} {q} {A} {Ïƒ} B t =
      sndÊ· p q A B t [ Ïƒ ]                                                â‰¡âŸ¨âŸ©

      prodrec râ€² p q
        (B [ fstÊ· p (wk1 A) (var x0) ]â†‘ [ liftSubst Ïƒ ])
        (t [ Ïƒ ]) (var x0)                                                â‰¡âŸ¨ cong (Î» B â†’ prodrec _ _ _ B _ _) $
                                                                             singleSubstLiftâ†‘ _ B _ âŸ©
      prodrec râ€² p q
        (B [ liftSubst Ïƒ ] [ fstÊ· p (wk1 A) (var x0) [ liftSubst Ïƒ ] ]â†‘)
        (t [ Ïƒ ]) (var x0)                                                â‰¡âŸ¨ cong (Î» B â†’ prodrec _ _ _ B _ _) $
                                                                             cong (Î» t â†’ B [ liftSubst Ïƒ ] [ t ]â†‘) $
                                                                             fstÊ·-[] (wk1 A) (var x0) âŸ©
      prodrec râ€² p q
        (B [ liftSubst Ïƒ ] [ fstÊ· p (wk1 A [ liftSubst Ïƒ ]) (var x0) ]â†‘)
        (t [ Ïƒ ]) (var x0)                                                â‰¡âŸ¨ cong (Î» B â†’ prodrec _ _ _ B _ _) $
                                                                             cong (Î» A â†’ B [ _ ] [ fstÊ· _ A _ ]â†‘) $
                                                                             wk1-liftSubst A âŸ©
      prodrec râ€² p q
        (B [ liftSubst Ïƒ ] [ fstÊ· p (wk1 (A [ Ïƒ ])) (var x0) ]â†‘)
        (t [ Ïƒ ]) (var x0)                                                â‰¡âŸ¨âŸ©

      sndÊ· p q (A [ Ïƒ ]) (B [ liftSubst Ïƒ ]) (t [ Ïƒ ])                    âˆ

-- The quantities "p" and "q" are instantiated based on an analysis
-- performed in Graded.Derived.Sigma.

open FstÊ·-sndÊ· (ğŸ˜ âˆ§ ğŸ™) ğŸ˜ public

opaque

  -- A variant of fst for all kinds of Î£-types.

  fstâŸ¨_âŸ© : Strength â†’ M â†’ Term n â†’ Term n â†’ Term n
  fstâŸ¨ ğ•¤ âŸ© p _ t = fst p t
  fstâŸ¨ ğ•¨ âŸ© p A t = fstÊ· p A t

opaque

  -- A variant of snd for all kinds of Î£-types.

  sndâŸ¨_âŸ© : Strength â†’ M â†’ M â†’ Term n â†’ Term (1+ n) â†’ Term n â†’ Term n
  sndâŸ¨ ğ•¤ âŸ© p _ _ _ t = snd p t
  sndâŸ¨ ğ•¨ âŸ© p q A B t = sndÊ· p q A B t

opaque
  unfolding fstâŸ¨_âŸ©

  -- A substitution lemma for fstâŸ¨_âŸ©.

  fstâŸ¨âŸ©-[] : fstâŸ¨ s âŸ© p A t [ Ïƒ ] â‰¡ fstâŸ¨ s âŸ© p (A [ Ïƒ ]) (t [ Ïƒ ])
  fstâŸ¨âŸ©-[] {s = ğ•¤}         = refl
  fstâŸ¨âŸ©-[] {s = ğ•¨} {A} {t} = fstÊ·-[] A t

opaque
  unfolding sndâŸ¨_âŸ©

  -- A substitution lemma for sndâŸ¨_âŸ©.

  sndâŸ¨âŸ©-[] :
    sndâŸ¨ s âŸ© p q A B t [ Ïƒ ] â‰¡
    sndâŸ¨ s âŸ© p q (A [ Ïƒ ]) (B [ liftSubst Ïƒ ]) (t [ Ïƒ ])
  sndâŸ¨âŸ©-[] {s = ğ•¤}         = refl
  sndâŸ¨âŸ©-[] {s = ğ•¨} {B} {t} = sndÊ·-[] B t
