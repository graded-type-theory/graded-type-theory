------------------------------------------------------------------------
-- The type constructor Erased
------------------------------------------------------------------------

open import Graded.Modality
open import Definition.Untyped.NotParametrised

module Definition.Untyped.Erased
  {a} {M : Set a}
  (ğ•„ : Modality M)
  (s : Strength)
  where

open Modality ğ•„

open import Definition.Untyped M as U
import Definition.Untyped.Erased.Eta ğ•„ as Eta
open import Definition.Untyped.Identity ğ•„
open import Definition.Untyped.Properties M
open import Definition.Untyped.Sigma ğ•„
open import Definition.Untyped.Unit ğ•„

import Definition.Untyped.Erased.No-eta ğ•„ as NoEta

open import Tools.Fin
open import Tools.Function
open import Tools.Nat
open import Tools.Product
open import Tools.PropositionalEquality as PE hiding (subst; cong)
open import Tools.Reasoning.PropositionalEquality

private variable
  n           : Nat
  A B t u v w : Term _
  Ïƒ           : Subst _ _
  p           : M

-- The type constructor Erased.

Erased : Term n â†’ Term n
Erased A = Î£âŸ¨ s âŸ© ğŸ˜ , ğŸ˜ â–· A â–¹ Unit s zeroáµ˜

-- The constructor [_].

[_] : Term n â†’ Term n
[ t ] = prod s ğŸ˜ t (star s zeroáµ˜)

opaque

  -- The "projection" erased.

  erased : Term n â†’ Term n â†’ Term n
  erased A t = case s of Î» where
    ğ•¤ â†’ Eta.erased t
    ğ•¨ â†’ NoEta.erased A t

opaque
  unfolding erased

  -- A substitution lemma for erased.

  erased-[] : erased A t U.[ Ïƒ ] â‰¡ erased (A U.[ Ïƒ ]) (t U.[ Ïƒ ])
  erased-[] {A} {t} = case singleton s of Î» where
    (ğ•¤ , refl) â†’ refl
    (ğ•¨ , refl) â†’ NoEta.erased-[] A t

opaque

  -- A grade that is used in the implementation of erasedrec.

  is-ğ•¨ : M
  is-ğ•¨ = case PE.singleton s of Î» where
    (ğ•¨ , _) â†’ ğŸ™
    (ğ•¤ , _) â†’ ğŸ˜

opaque

  -- An eliminator for Erased.

  erasedrec : M â†’ Term (1+ n) â†’ Term (1+ n) â†’ Term n â†’ Term n
  erasedrec p B t u =
    prodrecâŸ¨ s âŸ© is-ğ•¨ ğŸ˜ p B u
      (unitrecâŸ¨ s âŸ© ğŸ™ p zeroáµ˜ (B [ 3 ][ prod s ğŸ˜ (var x2) (var x0) ]â†‘)
         (var x0) (wk1 t))

opaque
  unfolding erasedrec

  -- A substitution lemma for erasedrec.

  erasedrec-[] :
    erasedrec p B t u U.[ Ïƒ ] â‰¡
    erasedrec p (B U.[ liftSubst Ïƒ ]) (t U.[ liftSubst Ïƒ ]) (u U.[ Ïƒ ])
  erasedrec-[] {p} {B} {t} {u} {Ïƒ} =
    prodrecâŸ¨ s âŸ© is-ğ•¨ ğŸ˜ p B u
      (unitrecâŸ¨ s âŸ© ğŸ™ p zeroáµ˜ (B [ 3 ][ prod s ğŸ˜ (var x2) (var x0) ]â†‘)
         (var x0) (wk1 t))
      U.[ Ïƒ ]                                                        â‰¡âŸ¨ prodrecâŸ¨âŸ©-[] âŸ©

    prodrecâŸ¨ s âŸ© is-ğ•¨ ğŸ˜ p (B U.[ liftSubst Ïƒ ]) (u U.[ Ïƒ ])
      (unitrecâŸ¨ s âŸ© ğŸ™ p zeroáµ˜ (B [ 3 ][ prod s ğŸ˜ (var x2) (var x0) ]â†‘)
         (var x0) (wk1 t)
         U.[ liftSubstn Ïƒ 2 ])                                       â‰¡âŸ¨ PE.cong (prodrecâŸ¨_âŸ© _ _ _ _ _ _)
                                                                        unitrecâŸ¨âŸ©-[] âŸ©
    prodrecâŸ¨ s âŸ© is-ğ•¨ ğŸ˜ p (B U.[ liftSubst Ïƒ ]) (u U.[ Ïƒ ])
      (unitrecâŸ¨ s âŸ© ğŸ™ p zeroáµ˜
         (B [ 3 ][ prod s ğŸ˜ (var x2) (var x0) ]â†‘
            U.[ liftSubstn Ïƒ 3 ])
         (var x0) (wk1 t U.[ liftSubstn Ïƒ 2 ]))                      â‰¡âŸ¨ PE.cong (prodrecâŸ¨_âŸ© _ _ _ _ _ _) $
                                                                        PE.congâ‚ƒ (unitrecâŸ¨_âŸ© _ _ _ _)
                                                                          (PE.trans (substCompEq B) $
                                                                           PE.trans (flip substVar-to-subst B Î»
                                                                                       { x0     â†’ PE.refl
                                                                                       ; (_ +1) â†’ PE.sym $ wk1-[][]â†‘ 3
                                                                                       }) $
                                                                           PE.sym $ substCompEq B)
                                                                          PE.refl
                                                                          (wk1-liftSubst t) âŸ©
    prodrecâŸ¨ s âŸ© is-ğ•¨ ğŸ˜ p (B U.[ liftSubst Ïƒ ]) (u U.[ Ïƒ ])
      (unitrecâŸ¨ s âŸ© ğŸ™ p zeroáµ˜
         (B U.[ liftSubst Ïƒ ] [ 3 ][ prod s ğŸ˜ (var x2) (var x0) ]â†‘)
         (var x0) (wk1 (t U.[ liftSubst Ïƒ ])))                       âˆ

opaque

  -- A propositional Î·-rule for Erased.

  Erased-Î· : Term n â†’ Term n â†’ Term n
  Erased-Î· A t =
    erasedrec ğŸ™
      (Id (Erased (wk1 A)) [ erased (wk1 A) (var x0) ] (var x0))
      rfl t

opaque
  unfolding Erased-Î·

  -- A substitution lemma for Erased-Î·.

  Erased-Î·-[] :
    Erased-Î· A t U.[ Ïƒ ] â‰¡ Erased-Î· (A U.[ Ïƒ ]) (t U.[ Ïƒ ])
  Erased-Î·-[] {A} {t} {Ïƒ} =
    erasedrec ğŸ™
      (Id (Erased (wk1 A)) [ erased (wk1 A) (var x0) ] (var x0))
      rfl t U.[ Ïƒ ]                                               â‰¡âŸ¨ erasedrec-[] âŸ©

    erasedrec ğŸ™
      (Id (Erased (wk1 A U.[ liftSubst Ïƒ ]))
         [ erased (wk1 A) (var x0) U.[ liftSubst Ïƒ ] ] (var x0))
      rfl (t U.[ Ïƒ ])                                             â‰¡âŸ¨ congâ‚ƒ (erasedrec _)
                                                                       (congâ‚ƒ Id refl (PE.cong [_] erased-[]) refl)
                                                                       refl
                                                                       refl âŸ©
    erasedrec ğŸ™
      (Id (Erased (wk1 A U.[ liftSubst Ïƒ ]))
         [ erased (wk1 A U.[ liftSubst Ïƒ ]) (var x0) ] (var x0))
      rfl (t U.[ Ïƒ ])                                             â‰¡âŸ¨ PE.cong (Î» A â†’ erasedrec _ (Id (Erased A) [ erased A _ ] _) _ _) $
                                                                     wk1-liftSubst A âŸ©
    erasedrec ğŸ™
      (Id (Erased (wk1 (A U.[ Ïƒ ])))
         [ erased (wk1 (A U.[ Ïƒ ])) (var x0) ] (var x0))
      rfl (t U.[ Ïƒ ])                                             âˆ

opaque

  -- A map function for Erased.

  mapá´± : Term n â†’ Term (1+ n) â†’ Term n â†’ Term n
  mapá´± A t u = [ t [ erased A u ]â‚€ ]

opaque
  unfolding mapá´±

  -- A substitution lemma for mapá´±.

  mapá´±-[] :
    mapá´± A t u U.[ Ïƒ ] â‰¡
    mapá´± (A U.[ Ïƒ ]) (t U.[ Ïƒ â‡‘ ]) (u U.[ Ïƒ ])
  mapá´±-[] {A} {t} {u} {Ïƒ} =
    [ t U.[ erased A u ]â‚€ U.[ Ïƒ ] ]                        â‰¡âŸ¨ PE.cong ([_]) $ singleSubstLift t _ âŸ©
    [ t U.[ Ïƒ â‡‘ ] U.[ erased A u U.[ Ïƒ ] ]â‚€ ]              â‰¡âŸ¨ PE.cong ([_] âˆ˜â†’ t U.[ Ïƒ â‡‘ ] U.[_]â‚€) erased-[] âŸ©
    [ t U.[ Ïƒ â‡‘ ] U.[ erased (A U.[ Ïƒ ]) (u U.[ Ïƒ ]) ]â‚€ ]  âˆ

opaque

  -- Substitutivity.
  --
  -- This variant of subst is an alternative to substÂ ğŸ˜.

  substáµ‰ :
    Term n â†’ Term (1+ n) â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  substáµ‰ A B t u v w =
    subst ğŸ˜ (Erased A) (B [ erased (wk1 A) (var x0) ]â†‘) [ t ] [ u ]
      ([]-cong s A t u v) w

opaque
  unfolding substáµ‰

  -- A substitution lemma for substáµ‰.

  substáµ‰-[] :
    substáµ‰ A B t u v w U.[ Ïƒ ] â‰¡
    substáµ‰ (A U.[ Ïƒ ]) (B U.[ liftSubst Ïƒ ]) (t U.[ Ïƒ ]) (u U.[ Ïƒ ])
      (v U.[ Ïƒ ]) (w U.[ Ïƒ ])
  substáµ‰-[] {A} {B} {t} {u} {v} {w} {Ïƒ} =
    subst ğŸ˜ (Erased A) (B [ erased (wk1 A) (var x0) ]â†‘) [ t ] [ u ]
      ([]-cong s A t u v) w U.[ Ïƒ ]                                       â‰¡âŸ¨ subst-[] âŸ©

    subst ğŸ˜ (Erased A U.[ Ïƒ ])
      (B [ erased (wk1 A) (var x0) ]â†‘ U.[ liftSubst Ïƒ ]) ([ t ] U.[ Ïƒ ])
      ([ u ] U.[ Ïƒ ]) ([]-cong s A t u v U.[ Ïƒ ]) (w U.[ Ïƒ ])             â‰¡âŸ¨ congâ‚… (subst _ _) lemma refl refl refl refl âŸ©

    subst ğŸ˜ (Erased (A U.[ Ïƒ ]))
      (B U.[ liftSubst Ïƒ ] [ erased (wk1 (A U.[ Ïƒ ])) (var x0) ]â†‘)
      [ t U.[ Ïƒ ] ] [ u U.[ Ïƒ ] ]
      ([]-cong s (A U.[ Ïƒ ]) (t U.[ Ïƒ ]) (u U.[ Ïƒ ]) (v U.[ Ïƒ ]))
      (w U.[ Ïƒ ])                                                         âˆ
    where
    lemma :
      B [ erased (wk1 A) (var x0) ]â†‘ U.[ liftSubst Ïƒ ] â‰¡
      B U.[ liftSubst Ïƒ ] [ erased (wk1 (A U.[ Ïƒ ])) (var x0) ]â†‘
    lemma =
      B [ erased (wk1 A) (var x0) ]â†‘ U.[ liftSubst Ïƒ ]                    â‰¡âŸ¨ singleSubstLiftâ†‘ _ B _ âŸ©
      B U.[ liftSubst Ïƒ ] [ erased (wk1 A) (var x0) U.[ liftSubst Ïƒ ] ]â†‘  â‰¡âŸ¨ PE.cong (B U.[ _ ] [_]â†‘) erased-[] âŸ©
      B U.[ liftSubst Ïƒ ] [ erased (wk1 A U.[ liftSubst Ïƒ ]) (var x0) ]â†‘  â‰¡âŸ¨ PE.cong (Î» A â†’ B U.[ _ ] [ erased A _ ]â†‘) $ wk1-liftSubst A âŸ©
      B U.[ liftSubst Ïƒ ] [ erased (wk1 (A U.[ Ïƒ ])) (var x0) ]â†‘          âˆ

opaque

  -- An alternative to JÂ ğŸ˜Â ğŸ˜.

  Jáµ‰ : Term n â†’ Term n â†’ Term (2+ n) â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  Jáµ‰ {n} A t B u v w =
    substáµ‰ Singleton
      (B U.[ consSubst
               (consSubst (wk1Subst idSubst)
                  (fstâŸ¨ s âŸ© ğŸ˜ (wk1 A) (var x0)))
               (sndâŸ¨ s âŸ© ğŸ˜ ğŸ˜ (wk1 A) (Id (wkâ‚‚ A) (wkâ‚‚ t) (var x0))
                  (var x0)) ])
      (prod s ğŸ˜ t rfl)
      (prod s ğŸ˜ v w)
      (J ğŸ˜ (ğŸ˜ âˆ§ ğŸ™) A t
         (Id (wkâ‚‚ Singleton) (wkâ‚‚ (prod s ğŸ˜ t rfl))
            (prod s ğŸ˜ (var x1) (var x0)))
         rfl v w)
      u
    where
    Singleton : Term n
    Singleton = Î£âŸ¨ s âŸ© ğŸ˜ , ğŸ˜ â–· A â–¹ Id (wk1 A) (wk1 t) (var x0)

opaque
  unfolding Jáµ‰

  -- A substitution lemma forÂ Jáµ‰.

  Jáµ‰-[] :
    Jáµ‰ A t B u v w U.[ Ïƒ ] â‰¡
    Jáµ‰ (A U.[ Ïƒ ]) (t U.[ Ïƒ ]) (B U.[ liftSubstn Ïƒ 2 ]) (u U.[ Ïƒ ])
      (v U.[ Ïƒ ]) (w U.[ Ïƒ ])
  Jáµ‰-[] {A} {t} {B} {u} {v} {w} {Ïƒ} =
    case
      PE.cong (Î£âŸ¨_âŸ©_,_â–·_â–¹_ s ğŸ˜ ğŸ˜ (A U.[ Ïƒ ]))
        {x = Id (wk1 A) (wk1 t) (var x0) U.[ _ ]} $
      congâ‚ƒ Id
        (wk1-liftSubst A)
        (wk1-liftSubst t)
        refl
    of Î»
      lemma â†’
    substáµ‰
      (Î£âŸ¨ s âŸ© ğŸ˜ , ğŸ˜ â–· A â–¹ Id (wk1 A) (wk1 t) (var x0))
      (B U.[ consSubst
               (consSubst (wk1Subst idSubst)
                  (fstâŸ¨ s âŸ© ğŸ˜ (wk1 A) (var x0)))
               (sndâŸ¨ s âŸ© ğŸ˜ ğŸ˜ (wk1 A) (Id (wkâ‚‚ A) (wkâ‚‚ t) (var x0))
                  (var x0)) ])
      (prod s ğŸ˜ t rfl)
      (prod s ğŸ˜ v w)
      (J ğŸ˜ (ğŸ˜ âˆ§ ğŸ™) A t
         (Id (wkâ‚‚ $ Î£âŸ¨ s âŸ© ğŸ˜ , ğŸ˜ â–· A â–¹ Id (wk1 A) (wk1 t) (var x0))
            (wkâ‚‚ (prod s ğŸ˜ t rfl)) (prod s ğŸ˜ (var x1) (var x0)))
         rfl v w)
      u U.[ Ïƒ ]                                                          â‰¡âŸ¨ substáµ‰-[] âŸ©

    substáµ‰
      (Î£âŸ¨ s âŸ© ğŸ˜ , ğŸ˜ â–· A U.[ Ïƒ ] â–¹
       Id (wk1 A U.[ liftSubst Ïƒ ]) (wk1 t U.[ liftSubst Ïƒ ]) (var x0))
      (B U.[ consSubst
               (consSubst (wk1Subst idSubst)
                  (fstâŸ¨ s âŸ© ğŸ˜ (wk1 A) (var x0)))
               (sndâŸ¨ s âŸ© ğŸ˜ ğŸ˜ (wk1 A) (Id (wkâ‚‚ A) (wkâ‚‚ t) (var x0))
                  (var x0)) ]
         U.[ liftSubst Ïƒ ])
      (prod s ğŸ˜ (t U.[ Ïƒ ]) rfl)
      (prod s ğŸ˜ (v U.[ Ïƒ ]) (w U.[ Ïƒ ]))
      (J ğŸ˜ (ğŸ˜ âˆ§ ğŸ™) (A U.[ Ïƒ ]) (t U.[ Ïƒ ])
         (Id
            (wkâ‚‚ (Î£âŸ¨ s âŸ© ğŸ˜ , ğŸ˜ â–· A â–¹ Id (wk1 A) (wk1 t) (var x0))
               U.[ liftSubstn Ïƒ 2 ])
            (wkâ‚‚ (prod s ğŸ˜ t rfl) U.[ liftSubstn Ïƒ 2 ])
            (prod s ğŸ˜ (var x1) (var x0)))
         rfl (v U.[ Ïƒ ]) (w U.[ Ïƒ ]))
      (u U.[ Ïƒ ])                                                         â‰¡âŸ¨ congâ‚† substáµ‰ lemma
                                                                               (
      B U.[ consSubst
              (consSubst (wk1Subst idSubst)
                 (fstâŸ¨ s âŸ© ğŸ˜ (wk1 A) (var x0)))
              (sndâŸ¨ s âŸ© ğŸ˜ ğŸ˜ (wk1 A) (Id (wkâ‚‚ A) (wkâ‚‚ t) (var x0))
                 (var x0)) ]
        U.[ liftSubst Ïƒ ]                                                       â‰¡âŸ¨ substCompEq B âŸ©

      B U.[ liftSubst Ïƒ â‚›â€¢â‚›
            consSubst
              (consSubst (wk1Subst idSubst)
                 (fstâŸ¨ s âŸ© ğŸ˜ (wk1 A) (var x0)))
              (sndâŸ¨ s âŸ© ğŸ˜ ğŸ˜ (wk1 A) (Id (wkâ‚‚ A) (wkâ‚‚ t) (var x0))
                 (var x0)) ]                                                    â‰¡âŸ¨ (flip substVar-to-subst B Î» {
                                                                                      x0 â†’
        sndâŸ¨ s âŸ© ğŸ˜ ğŸ˜ (wk1 A) (Id (wkâ‚‚ A) (wkâ‚‚ t) (var x0)) (var x0)
          U.[ liftSubst Ïƒ ]                                                             â‰¡âŸ¨ sndâŸ¨âŸ©-[] âŸ©

        sndâŸ¨ s âŸ© ğŸ˜ ğŸ˜ (wk1 A U.[ liftSubst Ïƒ ])
          (Id (wkâ‚‚ A U.[ liftSubstn Ïƒ 2 ]) (wkâ‚‚ t U.[ liftSubstn Ïƒ 2 ])
             (var x0))
          (var x0)                                                                      â‰¡âŸ¨ congâ‚ƒ (sndâŸ¨ _ âŸ© _ _)
                                                                                             (wk1-liftSubst A)
                                                                                             (congâ‚ƒ Id (wk[]â€²-[â‡‘] A) (wk[]â€²-[â‡‘] t) refl)
                                                                                             refl âŸ©
        sndâŸ¨ s âŸ© ğŸ˜ ğŸ˜ (wk1 (A U.[ Ïƒ ]))
          (Id (wkâ‚‚ (A U.[ Ïƒ ])) (wkâ‚‚ (t U.[ Ïƒ ])) (var x0)) (var x0)                    âˆ;
                                                                                      (x0 +1) â†’
        fstâŸ¨ s âŸ© ğŸ˜ (wk1 A) (var x0) U.[ liftSubst Ïƒ ]                                   â‰¡âŸ¨ fstâŸ¨âŸ©-[] âŸ©
        fstâŸ¨ s âŸ© ğŸ˜ (wk1 A U.[ liftSubst Ïƒ ]) (var x0)                                   â‰¡âŸ¨ congâ‚‚ (fstâŸ¨ _ âŸ© _) (wk1-liftSubst A) refl âŸ©
        fstâŸ¨ s âŸ© ğŸ˜ (wk1 (A U.[ Ïƒ ])) (var x0)                                           âˆ;
                                                                                      (x +1 +1) â†’
        wk1 (Ïƒ x)                                                                       â‰¡âŸ¨ wk1-tailId _ âŸ©
        Ïƒ x U.[ wk1Subst idSubst ]                                                      âˆ }) âŸ©

      B U.[ consSubst
              (consSubst (wk1Subst idSubst â‚›â€¢â‚› Ïƒ)
                 (fstâŸ¨ s âŸ© ğŸ˜ (wk1 (A U.[ Ïƒ ])) (var x0)))
              (sndâŸ¨ s âŸ© ğŸ˜ ğŸ˜ (wk1 (A U.[ Ïƒ ]))
                 (Id (wkâ‚‚ (A U.[ Ïƒ ])) (wkâ‚‚ (t U.[ Ïƒ ])) (var x0))
                 (var x0)) ]                                                    â‰¡Ë˜âŸ¨ doubleSubstCompâ€² B âŸ©

      B U.[ liftSubstn Ïƒ 2 ]
        U.[ consSubst
              (consSubst (wk1Subst idSubst)
                 (fstâŸ¨ s âŸ© ğŸ˜ (wk1 (A U.[ Ïƒ ])) (var x0)))
              (sndâŸ¨ s âŸ© ğŸ˜ ğŸ˜ (wk1 (A U.[ Ïƒ ]))
                 (Id (wkâ‚‚ (A U.[ Ïƒ ])) (wkâ‚‚ (t U.[ Ïƒ ])) (var x0))
                 (var x0)) ]                                                    âˆ)
                                                                               refl refl
                                                                               (congâ‚„ (J ğŸ˜ (ğŸ˜ âˆ§ ğŸ™) (A U.[ Ïƒ ]) (t U.[ Ïƒ ]))
                                                                                  (congâ‚ƒ Id
                                                                                     (trans
                                                                                        (wk[]â€²-[â‡‘]
                                                                                           (Î£âŸ¨ _ âŸ© _ , _ â–· A â–¹ Id (wk1 A) (wk1 t) (var x0))) $
                                                                                      PE.cong wkâ‚‚ lemma)
                                                                                     (congâ‚‚ (prod s ğŸ˜) (wk[]â€²-[â‡‘] t) refl)
                                                                                     refl)
                                                                                  refl refl refl)
                                                                               refl âŸ©
    substáµ‰
      (Î£âŸ¨ s âŸ© ğŸ˜ , ğŸ˜ â–· A U.[ Ïƒ ] â–¹
       Id (wk1 (A U.[ Ïƒ ])) (wk1 (t U.[ Ïƒ ])) (var x0))
      (B U.[ liftSubstn Ïƒ 2 ]
         U.[ consSubst
               (consSubst (wk1Subst idSubst)
                  (fstâŸ¨ s âŸ© ğŸ˜ (wk1 (A U.[ Ïƒ ])) (var x0)))
               (sndâŸ¨ s âŸ© ğŸ˜ ğŸ˜ (wk1 (A U.[ Ïƒ ]))
                  (Id (wkâ‚‚ (A U.[ Ïƒ ])) (wkâ‚‚ (t U.[ Ïƒ ])) (var x0))
                  (var x0)) ])
      (prod s ğŸ˜ (t U.[ Ïƒ ]) rfl)
      (prod s ğŸ˜ (v U.[ Ïƒ ]) (w U.[ Ïƒ ]))
      (J ğŸ˜ (ğŸ˜ âˆ§ ğŸ™) (A U.[ Ïƒ ]) (t U.[ Ïƒ ])
         (Id
            (wkâ‚‚ $
             Î£âŸ¨ s âŸ© ğŸ˜ , ğŸ˜ â–· A U.[ Ïƒ ] â–¹
             Id (wk1 (A U.[ Ïƒ ])) (wk1 (t U.[ Ïƒ ])) (var x0))
            (wkâ‚‚ (prod s ğŸ˜ (t U.[ Ïƒ ]) rfl))
            (prod s ğŸ˜ (var x1) (var x0)))
         rfl (v U.[ Ïƒ ]) (w U.[ Ïƒ ]))
      (u U.[ Ïƒ ])                                                         âˆ
