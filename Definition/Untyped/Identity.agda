------------------------------------------------------------------------
-- Definitions related to Id
------------------------------------------------------------------------

-- Typing rules for the terms given in this module can be found in
-- Definition.Typed.Properties.Admissible.Identity.

open import Graded.Modality

module Definition.Untyped.Identity
  {a} {M : Set a}
  (ğ•„ : Modality M)
  where

open Modality ğ•„

open import Definition.Untyped M
open import Definition.Untyped.Properties M

open import Tools.Fin
open import Tools.Function
open import Tools.Nat
open import Tools.PropositionalEquality as PE
  hiding (cong; congâ‚‚; subst)
open import Tools.Reasoning.PropositionalEquality

private variable
  n                                              : Nat
  A Aâ‚ Aâ‚‚ B eq eqâ‚ eqâ‚‚ t tâ‚ tâ‚‚ u uâ‚ uâ‚‚ v w wâ‚ wâ‚‚ : Term _
  Ïƒ                                              : Subst _ _
  Ï                                              : Wk _ _
  l lâ‚ lâ‚‚                                        : Universe-level
  p pâ€² q qâ€²                                      : M

opaque

  -- Substitutivity.

  subst :
    M â†’
    Term n â†’ Term (1+ n) â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  subst p A B t u v w =
    J p ğŸ˜ A t (wk1 B) w u v

opaque
  unfolding subst

  -- A substitution lemma for subst.

  subst-[] :
    subst p A B t u v w [ Ïƒ ] â‰¡
    subst p (A [ Ïƒ ]) (B [ liftSubst Ïƒ ]) (t [ Ïƒ ]) (u [ Ïƒ ]) (v [ Ïƒ ])
      (w [ Ïƒ ])
  subst-[] {B} =
    congâ‚„ (J _ _ _ _) (wk1-liftSubst B) refl refl refl

opaque

  -- A cast lemma.

  cast : Universe-level â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  cast l A B t u =
    subst ğŸ™ (U l) (var x0) A B t u

opaque
  unfolding cast

  -- A substitution lemma for cast.

  cast-[] :
    cast l A B t u [ Ïƒ ] â‰¡
    cast l (A [ Ïƒ ]) (B [ Ïƒ ]) (t [ Ïƒ ]) (u [ Ïƒ ])
  cast-[] {l} {A} {B} {t} {u} {Ïƒ} =
    subst ğŸ™ (U l) (var x0) A B t u [ Ïƒ ]                            â‰¡âŸ¨ subst-[] âŸ©
    subst ğŸ™ (U l) (var x0) (A [ Ïƒ ]) (B [ Ïƒ ]) (t [ Ïƒ ]) (u [ Ïƒ ])  âˆ

opaque

  -- A weakening lemma for cast.

  wk-cast :
    wk Ï (cast l A B t u) â‰¡
    cast l (wk Ï A) (wk Ï B) (wk Ï t) (wk Ï u)
  wk-cast {Ï} {l} {A} {B} {t} {u} =
    wk Ï (cast l A B t u)                                         â‰¡âŸ¨ wkâ‰¡subst _ _ âŸ©

    cast l A B t u [ toSubst Ï ]                                  â‰¡âŸ¨ cast-[] âŸ©

    cast l (A [ toSubst Ï ]) (B [ toSubst Ï ]) (t [ toSubst Ï ])
      (u [ toSubst Ï ])                                           â‰¡Ë˜âŸ¨ congâ‚„ (cast _) (wkâ‰¡subst _ _) (wkâ‰¡subst _ _) (wkâ‰¡subst _ _) (wkâ‰¡subst _ _) âŸ©

    cast l (wk Ï A) (wk Ï B) (wk Ï t) (wk Ï u)                    âˆ

opaque

  -- Symmetry.

  symmetry :
    Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  symmetry A t u eq =
    subst Ï‰ A (Id (wk1 A) (var x0) (wk1 t)) t u eq rfl

opaque
  unfolding symmetry

  -- A substitution lemma for symmetry.

  symmetry-[] :
    symmetry A t u eq [ Ïƒ ] â‰¡
    symmetry (A [ Ïƒ ]) (t [ Ïƒ ]) (u [ Ïƒ ]) (eq [ Ïƒ ])
  symmetry-[] {A} {t} {u} {eq} {Ïƒ} =
    subst Ï‰ A (Id (wk1 A) (var x0) (wk1 t)) t u eq rfl [ Ïƒ ]         â‰¡âŸ¨ subst-[] âŸ©

    subst Ï‰ (A [ Ïƒ ])
      (Id (wk1 A [ liftSubst Ïƒ ]) (var x0) (wk1 t [ liftSubst Ïƒ ]))
      (t [ Ïƒ ]) (u [ Ïƒ ]) (eq [ Ïƒ ]) rfl                             â‰¡âŸ¨ congâ‚… (subst _ _)
                                                                          (congâ‚ƒ Id (wk1-liftSubst A) refl (wk1-liftSubst t))
                                                                          refl refl refl refl âŸ©
    subst Ï‰ (A [ Ïƒ ])
      (Id (wk1 (A [ Ïƒ ])) (var x0) (wk1 (t [ Ïƒ ])))
      (t [ Ïƒ ]) (u [ Ïƒ ]) (eq [ Ïƒ ]) rfl                             âˆ

opaque

  -- Transitivity.

  transitivity :
    Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  transitivity A t u v eqâ‚ eqâ‚‚ =
    subst Ï‰ A (Id (wk1 A) (wk1 t) (var x0)) u v eqâ‚‚ eqâ‚

opaque
  unfolding transitivity

  -- A substitution lemma for transitivity.

  transitivity-[] :
    transitivity A t u v eqâ‚ eqâ‚‚ [ Ïƒ ] â‰¡
    transitivity (A [ Ïƒ ]) (t [ Ïƒ ]) (u [ Ïƒ ]) (v [ Ïƒ ]) (eqâ‚ [ Ïƒ ])
      (eqâ‚‚ [ Ïƒ ])
  transitivity-[] {A} {t} {u} {v} {eqâ‚} {eqâ‚‚} {Ïƒ} =
    subst Ï‰ A (Id (wk1 A) (wk1 t) (var x0)) u v eqâ‚‚ eqâ‚ [ Ïƒ ]        â‰¡âŸ¨ subst-[] âŸ©

    subst Ï‰ (A [ Ïƒ ])
      (Id (wk1 A [ liftSubst Ïƒ ]) (wk1 t [ liftSubst Ïƒ ]) (var x0))
      (u [ Ïƒ ]) (v [ Ïƒ ]) (eqâ‚‚ [ Ïƒ ]) (eqâ‚ [ Ïƒ ])                    â‰¡âŸ¨ congâ‚… (subst _ _)
                                                                          (congâ‚ƒ Id (wk1-liftSubst A) (wk1-liftSubst t) refl)
                                                                          refl refl refl refl âŸ©
    subst Ï‰ (A [ Ïƒ ]) (Id (wk1 (A [ Ïƒ ])) (wk1 (t [ Ïƒ ])) (var x0))
      (u [ Ïƒ ]) (v [ Ïƒ ]) (eqâ‚‚ [ Ïƒ ]) (eqâ‚ [ Ïƒ ])                    âˆ

opaque

  -- A simplification lemma for transitivity and symmetry.

  transitivity-symmetryË¡ :
    Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  transitivity-symmetryË¡ A t u eq =
    J Ï‰ Ï‰ A t
      (Id (Id (wk2 A) (var x1) (var x1))
         (transitivity (wk2 A) (var x1) (wk2 t) (var x1)
            (symmetry (wk2 A) (wk2 t) (var x1) (var x0))
            (var x0))
         rfl)
      rfl u eq

opaque

  -- Congruence.

  cong :
    M â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term (1+ n) â†’ Term n â†’
    Term n
  cong p A t u B v w =
    subst p A (Id (wk1 B) (wk1 (v [ t ]â‚€)) v) t u w rfl

opaque
  unfolding cong

  -- A substitution lemma for cong.

  cong-[] :
    cong p A t u B v w [ Ïƒ ] â‰¡
    cong p (A [ Ïƒ ]) (t [ Ïƒ ]) (u [ Ïƒ ]) (B [ Ïƒ ]) (v [ liftSubst Ïƒ ])
      (w [ Ïƒ ])
  cong-[] {p} {A} {t} {u} {B} {v} {w} {Ïƒ} =
    subst p A (Id (wk1 B) (wk1 (v [ t ]â‚€)) v) t u w rfl [ Ïƒ ]        â‰¡âŸ¨ subst-[] âŸ©

    subst p (A [ Ïƒ ])
      (Id (wk1 B [ liftSubst Ïƒ ]) (wk1 (v [ t ]â‚€) [ liftSubst Ïƒ ])
         (v [ liftSubst Ïƒ ]))
      (t [ Ïƒ ]) (u [ Ïƒ ]) (w [ Ïƒ ]) rfl                              â‰¡âŸ¨ congâ‚… (subst _ _)
                                                                          (congâ‚ƒ Id
                                                                             (wk1-liftSubst B)
                                                                             (
      wk1 (v [ t ]â‚€) [ liftSubst Ïƒ ]                                          â‰¡âŸ¨ wk1-liftSubst (v [ _ ]â‚€) âŸ©
      wk1 (v [ t ]â‚€ [ Ïƒ ])                                                    â‰¡âŸ¨ PE.cong wk1 $ singleSubstLift v _ âŸ©
      wk1 (v [ liftSubst Ïƒ ] [ t [ Ïƒ ] ]â‚€)                                    âˆ)
                                                                             refl)
                                                                          refl refl refl refl âŸ©
    subst p (A [ Ïƒ ])
      (Id (wk1 (B [ Ïƒ ])) (wk1 (v [ liftSubst Ïƒ ] [ t [ Ïƒ ] ]â‚€))
         (v [ liftSubst Ïƒ ]))
      (t [ Ïƒ ]) (u [ Ïƒ ]) (w [ Ïƒ ]) rfl                              âˆ

opaque

  -- Binary congruence.

  congâ‚‚ :
    M â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’
    Term (2+ n) â†’ Term n â†’ Term n â†’ Term n
  congâ‚‚ p Aâ‚ tâ‚ uâ‚ Aâ‚‚ tâ‚‚ uâ‚‚ B v wâ‚ wâ‚‚ =
    transitivity B (v [ tâ‚ , tâ‚‚ ]â‚â‚€) (v [ uâ‚ , tâ‚‚ ]â‚â‚€) (v [ uâ‚ , uâ‚‚ ]â‚â‚€)
      (cong p Aâ‚ tâ‚ uâ‚ B (v [ sgSubst (wk1 tâ‚‚) ]) wâ‚)
      (cong p Aâ‚‚ tâ‚‚ uâ‚‚ B (v [ sgSubst uâ‚ â‡‘ ]) wâ‚‚)

opaque
  unfolding congâ‚‚

  -- A substitution lemma for congâ‚‚.

  congâ‚‚-[] :
    congâ‚‚ p Aâ‚ tâ‚ uâ‚ Aâ‚‚ tâ‚‚ uâ‚‚ B v wâ‚ wâ‚‚ [ Ïƒ ] â‰¡
    congâ‚‚ p (Aâ‚ [ Ïƒ ]) (tâ‚ [ Ïƒ ]) (uâ‚ [ Ïƒ ]) (Aâ‚‚ [ Ïƒ ]) (tâ‚‚ [ Ïƒ ])
      (uâ‚‚ [ Ïƒ ]) (B [ Ïƒ ]) (v [ Ïƒ â‡‘[ 2 ] ]) (wâ‚ [ Ïƒ ]) (wâ‚‚ [ Ïƒ ])
  congâ‚‚-[] {p} {Aâ‚} {tâ‚} {uâ‚} {Aâ‚‚} {tâ‚‚} {uâ‚‚} {B} {v} {wâ‚} {wâ‚‚} {Ïƒ} =
    transitivity B (v [ tâ‚ , tâ‚‚ ]â‚â‚€) (v [ uâ‚ , tâ‚‚ ]â‚â‚€) (v [ uâ‚ , uâ‚‚ ]â‚â‚€)
      (cong p Aâ‚ tâ‚ uâ‚ B (v [ sgSubst (wk1 tâ‚‚) ]) wâ‚)
      (cong p Aâ‚‚ tâ‚‚ uâ‚‚ B (v [ sgSubst uâ‚ â‡‘ ]) wâ‚‚)
      [ Ïƒ ]                                                               â‰¡âŸ¨ transitivity-[] âŸ©

    transitivity (B [ Ïƒ ]) (v [ tâ‚ , tâ‚‚ ]â‚â‚€ [ Ïƒ ])
      (v [ uâ‚ , tâ‚‚ ]â‚â‚€ [ Ïƒ ]) (v [ uâ‚ , uâ‚‚ ]â‚â‚€ [ Ïƒ ])
      (cong p Aâ‚ tâ‚ uâ‚ B (v [ sgSubst (wk1 tâ‚‚) ]) wâ‚ [ Ïƒ ])
      (cong p Aâ‚‚ tâ‚‚ uâ‚‚ B (v [ sgSubst uâ‚ â‡‘ ]) wâ‚‚ [ Ïƒ ])                   â‰¡âŸ¨ PE.congâ‚‚ (transitivity _ _ _ _)
                                                                               cong-[] cong-[] âŸ©
    transitivity (B [ Ïƒ ]) (v [ tâ‚ , tâ‚‚ ]â‚â‚€ [ Ïƒ ])
      (v [ uâ‚ , tâ‚‚ ]â‚â‚€ [ Ïƒ ]) (v [ uâ‚ , uâ‚‚ ]â‚â‚€ [ Ïƒ ])
      (cong p (Aâ‚ [ Ïƒ ]) (tâ‚ [ Ïƒ ]) (uâ‚ [ Ïƒ ]) (B [ Ïƒ ])
         (v [ sgSubst (wk1 tâ‚‚) ] [ Ïƒ â‡‘ ]) (wâ‚ [ Ïƒ ]))
      (cong p (Aâ‚‚ [ Ïƒ ]) (tâ‚‚ [ Ïƒ ]) (uâ‚‚ [ Ïƒ ]) (B [ Ïƒ ])
         (v [ sgSubst uâ‚ â‡‘ ] [ Ïƒ â‡‘ ]) (wâ‚‚ [ Ïƒ ]))                         â‰¡âŸ¨ congâ‚… (transitivity _)
                                                                               ([,]-[]-commute v)
                                                                               ([,]-[]-commute v)
                                                                               ([,]-[]-commute v)
                                                                               (PE.cong (flip (cong _ _ _ _ _) _) $
                                                                                trans (singleSubstLift v _) $
                                                                                PE.cong (v [ _ â‡‘[ 2 ] ] [_]â‚€) $
                                                                                wk1-liftSubst tâ‚‚)
                                                                               (PE.cong (flip (cong _ _ _ _ _) _) $
                                                                                trans (substCompEq v) $
                                                                                trans
                                                                                  (flip substVar-to-subst v Î» x â†’
                                                                                   trans (substCompLift x) $
                                                                                   trans
                                                                                     (flip substVar-lift x Î» where
                                                                                        x0 â†’ refl
                                                                                        (x +1) â†’ sym $ wk1-sgSubst _ _) $
                                                                                   sym (substCompLift x)) $
                                                                                sym $ substCompEq v) âŸ©
    transitivity (B [ Ïƒ ])
      (v [ Ïƒ â‡‘[ 2 ] ] [ tâ‚ [ Ïƒ ] , tâ‚‚ [ Ïƒ ] ]â‚â‚€)
      (v [ Ïƒ â‡‘[ 2 ] ] [ uâ‚ [ Ïƒ ] , tâ‚‚ [ Ïƒ ] ]â‚â‚€)
      (v [ Ïƒ â‡‘[ 2 ] ] [ uâ‚ [ Ïƒ ] , uâ‚‚ [ Ïƒ ] ]â‚â‚€)
      (cong p (Aâ‚ [ Ïƒ ]) (tâ‚ [ Ïƒ ]) (uâ‚ [ Ïƒ ]) (B [ Ïƒ ])
         (v [ Ïƒ â‡‘[ 2 ] ] [ wk1 (tâ‚‚ [ Ïƒ ]) ]â‚€) (wâ‚ [ Ïƒ ]))
      (cong p (Aâ‚‚ [ Ïƒ ]) (tâ‚‚ [ Ïƒ ]) (uâ‚‚ [ Ïƒ ]) (B [ Ïƒ ])
         (v [ Ïƒ â‡‘[ 2 ] ] [ sgSubst (uâ‚ [ Ïƒ ]) â‡‘ ]) (wâ‚‚ [ Ïƒ ]))            âˆ

opaque

  -- If two functions are equal, then they are pointwise equal.

  pointwise-equality :
    M â†’ M â†’ Term n â†’ Term (1+ n) â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’
    Term n
  pointwise-equality p q A B t u v w =
    cong Ï‰ (Î  p , q â–· A â–¹ B) t u (B [ w ]â‚€) (var x0 âˆ˜âŸ¨ p âŸ© wk1 w) v

opaque
  unfolding pointwise-equality

  -- A substitution lemma for pointwise-equality.

  pointwise-equality-[] :
    pointwise-equality p q A B t u v w [ Ïƒ ] â‰¡
    pointwise-equality p q (A [ Ïƒ ]) (B [ liftSubst Ïƒ ]) (t [ Ïƒ ])
      (u [ Ïƒ ]) (v [ Ïƒ ]) (w [ Ïƒ ])
  pointwise-equality-[] {p} {q} {A} {B} {t} {u} {v} {w} {Ïƒ} =
    cong Ï‰ (Î  p , q â–· A â–¹ B) t u (B [ w ]â‚€) (var x0 âˆ˜âŸ¨ p âŸ© wk1 w) v
      [ Ïƒ ]                                                               â‰¡âŸ¨ cong-[] âŸ©

    cong Ï‰ (Î  p , q â–· A [ Ïƒ ] â–¹ (B [ liftSubst Ïƒ ])) (t [ Ïƒ ]) (u [ Ïƒ ])
      (B [ w ]â‚€ [ Ïƒ ]) (var x0 âˆ˜âŸ¨ p âŸ© wk1 w [ liftSubst Ïƒ ]) (v [ Ïƒ ])    â‰¡âŸ¨ congâ‚ƒ (cong _ _ _ _)
                                                                               (singleSubstLift B _)
                                                                               (PE.cong (_âˆ˜âŸ¨_âŸ©_ _ _) $ wk1-liftSubst w)
                                                                               refl âŸ©
    cong Ï‰ (Î  p , q â–· A [ Ïƒ ] â–¹ (B [ liftSubst Ïƒ ])) (t [ Ïƒ ]) (u [ Ïƒ ])
      (B [ liftSubst Ïƒ ] [ w [ Ïƒ ] ]â‚€) (var x0 âˆ˜âŸ¨ p âŸ© wk1 (w [ Ïƒ ]))
      (v [ Ïƒ ])                                                           âˆ

opaque

  -- Uniqueness of identity proofs (UIP)

  uip : M â†’ M â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  uip p q A t u eqâ‚ eqâ‚‚ =
    transitivity
      (Id A t u)
      eqâ‚
      (transitivity A t u u eqâ‚‚
         (transitivity A u t u (symmetry A t u eqâ‚) eqâ‚))
      eqâ‚‚
      (J Ï‰ Ï‰ A t
         (Id
            (Id (wk2 A) (wk2 t) (var x1))
            (var x0)
            (transitivity (wk2 A) (wk2 t) (wk2 u) (var x1) (wk2 eqâ‚‚)
               (transitivity (wk2 A) (wk2 u) (wk2 t) (var x1)
                  (symmetry (wk2 A) (wk2 t) (wk2 u) (wk2 eqâ‚))
                  (var x0))))
         (K Ï‰ A t (Id (Id (wk1 A) (wk1 t) (wk1 t)) rfl (var x0)) rfl
            (transitivity A t u t eqâ‚‚
               (transitivity A u t t (symmetry A t u eqâ‚) rfl)))
         u eqâ‚)
      (cong Ï‰ (Id A u u) (transitivity A u t u (symmetry A t u eqâ‚) eqâ‚)
         rfl (Id A t u)
         (transitivity (wk1 A) (wk1 t) (wk1 u) (wk1 u) (wk1 eqâ‚‚)
            (var x0))
         (transitivity-symmetryË¡ A t u eqâ‚))

opaque

  -- A certain formulation of function extensionality.

  Funext : M â†’ M â†’ M â†’ M â†’ Universe-level â†’ Universe-level â†’ Term n
  Funext p q pâ€² qâ€² lâ‚ lâ‚‚ =
    Î  p , q â–· U lâ‚ â–¹
    Î  pâ€² , qâ€² â–· (Î  p , q â–· var x0 â–¹ U lâ‚‚) â–¹
    let Î -type = Î  p , q â–· var x1 â–¹ (var x1 âˆ˜âŸ¨ p âŸ© var x0) in
    Î  pâ€² , qâ€² â–· Î -type â–¹
    Î  pâ€² , qâ€² â–· wk1 Î -type â–¹
    Î  pâ€² , qâ€² â–·
      (Î  p , q â–· var x3 â–¹
       Id (var x3 âˆ˜âŸ¨ p âŸ© var x0)
         (var x2 âˆ˜âŸ¨ p âŸ© var x0)
         (var x1 âˆ˜âŸ¨ p âŸ© var x0)) â–¹
    Id (wk[ 3 ]â€² Î -type) (var x2) (var x1)

opaque
  unfolding Funext

  -- A substitution lemma for Funext.

  Funext-[] : Funext p q pâ€² qâ€² lâ‚ lâ‚‚ [ Ïƒ ] â‰¡ Funext p q pâ€² qâ€² lâ‚ lâ‚‚
  Funext-[] = refl

opaque

  -- A variant of function extensionality that works in the presence
  -- of equality reflection (see
  -- Definition.Typed.Properties.Admissible.Identity.âŠ¢funext).

  funext : M â†’ M â†’ Term n
  funext p pâ€² = lam p $ lam pâ€² $ lam pâ€² $ lam pâ€² $ lam pâ€² rfl

opaque
  unfolding funext

  -- A substitution lemma for funext.

  funext-[] : funext p pâ€² [ Ïƒ ] â‰¡ funext p pâ€²
  funext-[] = refl
