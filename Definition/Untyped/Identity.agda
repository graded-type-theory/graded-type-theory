------------------------------------------------------------------------
-- Definitions related to Id
------------------------------------------------------------------------

-- Typing rules for the terms given in this module can be found in
-- Definition.Typed.Properties.Admissible.Identity.

open import Graded.Modality

module Definition.Untyped.Identity
  {a} {M : Set a}
  (ğ•„ : Modality M)
  where

open Modality ğ•„

open import Definition.Untyped M
open import Definition.Untyped.Properties M

open import Tools.Fin
open import Tools.Function
open import Tools.Nat
open import Tools.PropositionalEquality as PE hiding (cong; subst)
open import Tools.Reasoning.PropositionalEquality

private variable
  n                      : Nat
  A B eq eqâ‚ eqâ‚‚ t u v w : Term _
  Ïƒ                      : Subst _ _
  l                      : Universe-level
  p q                    : M

opaque

  -- Substitutivity.

  subst :
    M â†’
    Term n â†’ Term (1+ n) â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  subst p A B t u v w =
    J p ğŸ˜ A t (wk1 B) w u v

opaque
  unfolding subst

  -- A substitution lemma for subst.

  subst-[] :
    subst p A B t u v w [ Ïƒ ] â‰¡
    subst p (A [ Ïƒ ]) (B [ liftSubst Ïƒ ]) (t [ Ïƒ ]) (u [ Ïƒ ]) (v [ Ïƒ ])
      (w [ Ïƒ ])
  subst-[] {B} =
    congâ‚„ (J _ _ _ _) (wk1-liftSubst B) refl refl refl

opaque

  -- A cast lemma.

  cast : Universe-level â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  cast l A B t u =
    subst ğŸ™ (U l) (var x0) A B t u

opaque
  unfolding cast

  -- A substitution lemma for cast.

  cast-[] :
    cast l A B t u [ Ïƒ ] â‰¡
    cast l (A [ Ïƒ ]) (B [ Ïƒ ]) (t [ Ïƒ ]) (u [ Ïƒ ])
  cast-[] {l} {A} {B} {t} {u} {Ïƒ} =
    subst ğŸ™ (U l) (var x0) A B t u [ Ïƒ ]                            â‰¡âŸ¨ subst-[] âŸ©
    subst ğŸ™ (U l) (var x0) (A [ Ïƒ ]) (B [ Ïƒ ]) (t [ Ïƒ ]) (u [ Ïƒ ])  âˆ

opaque

  -- Symmetry.

  symmetry :
    Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  symmetry A t u eq =
    subst Ï‰ A (Id (wk1 A) (var x0) (wk1 t)) t u eq rfl

opaque
  unfolding symmetry

  -- A substitution lemma for symmetry.

  symmetry-[] :
    symmetry A t u eq [ Ïƒ ] â‰¡
    symmetry (A [ Ïƒ ]) (t [ Ïƒ ]) (u [ Ïƒ ]) (eq [ Ïƒ ])
  symmetry-[] {A} {t} {u} {eq} {Ïƒ} =
    subst Ï‰ A (Id (wk1 A) (var x0) (wk1 t)) t u eq rfl [ Ïƒ ]         â‰¡âŸ¨ subst-[] âŸ©

    subst Ï‰ (A [ Ïƒ ])
      (Id (wk1 A [ liftSubst Ïƒ ]) (var x0) (wk1 t [ liftSubst Ïƒ ]))
      (t [ Ïƒ ]) (u [ Ïƒ ]) (eq [ Ïƒ ]) rfl                             â‰¡âŸ¨ congâ‚… (subst _ _)
                                                                          (congâ‚ƒ Id (wk1-liftSubst A) refl (wk1-liftSubst t))
                                                                          refl refl refl refl âŸ©
    subst Ï‰ (A [ Ïƒ ])
      (Id (wk1 (A [ Ïƒ ])) (var x0) (wk1 (t [ Ïƒ ])))
      (t [ Ïƒ ]) (u [ Ïƒ ]) (eq [ Ïƒ ]) rfl                             âˆ

opaque

  -- Transitivity.

  transitivity :
    Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  transitivity A t u v eqâ‚ eqâ‚‚ =
    subst Ï‰ A (Id (wk1 A) (wk1 t) (var x0)) u v eqâ‚‚ eqâ‚

opaque
  unfolding transitivity

  -- A substitution lemma for transitivity.

  transitivity-[] :
    transitivity A t u v eqâ‚ eqâ‚‚ [ Ïƒ ] â‰¡
    transitivity (A [ Ïƒ ]) (t [ Ïƒ ]) (u [ Ïƒ ]) (v [ Ïƒ ]) (eqâ‚ [ Ïƒ ])
      (eqâ‚‚ [ Ïƒ ])
  transitivity-[] {A} {t} {u} {v} {eqâ‚} {eqâ‚‚} {Ïƒ} =
    subst Ï‰ A (Id (wk1 A) (wk1 t) (var x0)) u v eqâ‚‚ eqâ‚ [ Ïƒ ]        â‰¡âŸ¨ subst-[] âŸ©

    subst Ï‰ (A [ Ïƒ ])
      (Id (wk1 A [ liftSubst Ïƒ ]) (wk1 t [ liftSubst Ïƒ ]) (var x0))
      (u [ Ïƒ ]) (v [ Ïƒ ]) (eqâ‚‚ [ Ïƒ ]) (eqâ‚ [ Ïƒ ])                    â‰¡âŸ¨ congâ‚… (subst _ _)
                                                                          (congâ‚ƒ Id (wk1-liftSubst A) (wk1-liftSubst t) refl)
                                                                          refl refl refl refl âŸ©
    subst Ï‰ (A [ Ïƒ ]) (Id (wk1 (A [ Ïƒ ])) (wk1 (t [ Ïƒ ])) (var x0))
      (u [ Ïƒ ]) (v [ Ïƒ ]) (eqâ‚‚ [ Ïƒ ]) (eqâ‚ [ Ïƒ ])                    âˆ

opaque

  -- A simplification lemma for transitivity and symmetry.

  transitivity-symmetryË¡ :
    Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  transitivity-symmetryË¡ A t u eq =
    J Ï‰ Ï‰ A t
      (Id (Id (wk2 A) (var x1) (var x1))
         (transitivity (wk2 A) (var x1) (wk2 t) (var x1)
            (symmetry (wk2 A) (wk2 t) (var x1) (var x0))
            (var x0))
         rfl)
      rfl u eq

opaque

  -- Congruence.

  cong :
    M â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term (1+ n) â†’ Term n â†’
    Term n
  cong p A t u B v w =
    subst p A (Id (wk1 B) (wk1 (v [ t ]â‚€)) v) t u w rfl

opaque
  unfolding cong

  -- A substitution lemma for cong.

  cong-[] :
    cong p A t u B v w [ Ïƒ ] â‰¡
    cong p (A [ Ïƒ ]) (t [ Ïƒ ]) (u [ Ïƒ ]) (B [ Ïƒ ]) (v [ liftSubst Ïƒ ])
      (w [ Ïƒ ])
  cong-[] {p} {A} {t} {u} {B} {v} {w} {Ïƒ} =
    subst p A (Id (wk1 B) (wk1 (v [ t ]â‚€)) v) t u w rfl [ Ïƒ ]        â‰¡âŸ¨ subst-[] âŸ©

    subst p (A [ Ïƒ ])
      (Id (wk1 B [ liftSubst Ïƒ ]) (wk1 (v [ t ]â‚€) [ liftSubst Ïƒ ])
         (v [ liftSubst Ïƒ ]))
      (t [ Ïƒ ]) (u [ Ïƒ ]) (w [ Ïƒ ]) rfl                              â‰¡âŸ¨ congâ‚… (subst _ _)
                                                                          (congâ‚ƒ Id
                                                                             (wk1-liftSubst B)
                                                                             (
      wk1 (v [ t ]â‚€) [ liftSubst Ïƒ ]                                          â‰¡âŸ¨ wk1-liftSubst (v [ _ ]â‚€) âŸ©
      wk1 (v [ t ]â‚€ [ Ïƒ ])                                                    â‰¡âŸ¨ PE.cong wk1 $ singleSubstLift v _ âŸ©
      wk1 (v [ liftSubst Ïƒ ] [ t [ Ïƒ ] ]â‚€)                                    âˆ)
                                                                             refl)
                                                                          refl refl refl refl âŸ©
    subst p (A [ Ïƒ ])
      (Id (wk1 (B [ Ïƒ ])) (wk1 (v [ liftSubst Ïƒ ] [ t [ Ïƒ ] ]â‚€))
         (v [ liftSubst Ïƒ ]))
      (t [ Ïƒ ]) (u [ Ïƒ ]) (w [ Ïƒ ]) rfl                              âˆ

opaque

  -- If two functions are equal, then they are pointwise equal.

  pointwise-equality :
    M â†’ M â†’ Term n â†’ Term (1+ n) â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’
    Term n
  pointwise-equality p q A B t u v w =
    cong Ï‰ (Î  p , q â–· A â–¹ B) t u (B [ w ]â‚€) (var x0 âˆ˜âŸ¨ p âŸ© wk1 w) v

opaque
  unfolding pointwise-equality

  -- A substitution lemma for pointwise-equality.

  pointwise-equality-[] :
    pointwise-equality p q A B t u v w [ Ïƒ ] â‰¡
    pointwise-equality p q (A [ Ïƒ ]) (B [ liftSubst Ïƒ ]) (t [ Ïƒ ])
      (u [ Ïƒ ]) (v [ Ïƒ ]) (w [ Ïƒ ])
  pointwise-equality-[] {p} {q} {A} {B} {t} {u} {v} {w} {Ïƒ} =
    cong Ï‰ (Î  p , q â–· A â–¹ B) t u (B [ w ]â‚€) (var x0 âˆ˜âŸ¨ p âŸ© wk1 w) v
      [ Ïƒ ]                                                               â‰¡âŸ¨ cong-[] âŸ©

    cong Ï‰ (Î  p , q â–· A [ Ïƒ ] â–¹ (B [ liftSubst Ïƒ ])) (t [ Ïƒ ]) (u [ Ïƒ ])
      (B [ w ]â‚€ [ Ïƒ ]) (var x0 âˆ˜âŸ¨ p âŸ© wk1 w [ liftSubst Ïƒ ]) (v [ Ïƒ ])    â‰¡âŸ¨ congâ‚ƒ (cong _ _ _ _)
                                                                               (singleSubstLift B _)
                                                                               (PE.cong (_âˆ˜âŸ¨_âŸ©_ _ _) $ wk1-liftSubst w)
                                                                               refl âŸ©
    cong Ï‰ (Î  p , q â–· A [ Ïƒ ] â–¹ (B [ liftSubst Ïƒ ])) (t [ Ïƒ ]) (u [ Ïƒ ])
      (B [ liftSubst Ïƒ ] [ w [ Ïƒ ] ]â‚€) (var x0 âˆ˜âŸ¨ p âŸ© wk1 (w [ Ïƒ ]))
      (v [ Ïƒ ])                                                           âˆ

opaque

  -- Uniqueness of identity proofs (UIP)

  uip : M â†’ M â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n â†’ Term n
  uip p q A t u eqâ‚ eqâ‚‚ =
    transitivity
      (Id A t u)
      eqâ‚
      (transitivity A t u u eqâ‚‚
         (transitivity A u t u (symmetry A t u eqâ‚) eqâ‚))
      eqâ‚‚
      (J Ï‰ Ï‰ A t
         (Id
            (Id (wk2 A) (wk2 t) (var x1))
            (var x0)
            (transitivity (wk2 A) (wk2 t) (wk2 u) (var x1) (wk2 eqâ‚‚)
               (transitivity (wk2 A) (wk2 u) (wk2 t) (var x1)
                  (symmetry (wk2 A) (wk2 t) (wk2 u) (wk2 eqâ‚))
                  (var x0))))
         (K Ï‰ A t (Id (Id (wk1 A) (wk1 t) (wk1 t)) rfl (var x0)) rfl
            (transitivity A t u t eqâ‚‚
               (transitivity A u t t (symmetry A t u eqâ‚) rfl)))
         u eqâ‚)
      (cong Ï‰ (Id A u u) (transitivity A u t u (symmetry A t u eqâ‚) eqâ‚)
         rfl (Id A t u)
         (transitivity (wk1 A) (wk1 t) (wk1 u) (wk1 u) (wk1 eqâ‚‚)
            (var x0))
         (transitivity-symmetryË¡ A t u eqâ‚))
