------------------------------------------------------------------------
-- Inversion lemmas
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.Typed.Inversion
  {â„“} {M : Set â„“}
  {ğ•„ : Modality M}
  (R : Type-restrictions ğ•„)
  where

open Type-restrictions R

open import Definition.Typed R
import Definition.Typed.Inversion.Primitive R as I
open import Definition.Typed.Properties.Well-formed R
open import Definition.Typed.Substitution.Primitive R
open import Definition.Typed.Syntactic R
open import Definition.Typed.Well-formed R

open import Definition.Untyped M
import Definition.Untyped.Erased ğ•„ as Erased

open import Tools.Fin
open import Tools.Function
open import Tools.Product

open I public

private variable
  x               : Fin _
  Î“               : Con Term _
  A B C l t u v w : Term _
  b               : BinderMode
  s               : Strength
  p q qâ€² r        : M

------------------------------------------------------------------------
-- Inversion for variables

opaque

  -- Inversion for var.

  inversion-var : Î“ âŠ¢ var x âˆ· A â†’ âˆƒ Î» B â†’ x âˆ· B âˆˆ Î“ Ã— Î“ âŠ¢ A â‰¡ B
  inversion-var âŠ¢x@(var _ xâˆˆ) =
    _ , xâˆˆ , refl (syntacticTerm âŠ¢x)
  inversion-var (conv âŠ¢var eq) =
    let a , b , c = inversion-var âŠ¢var in
    a , b , trans (sym eq) c

------------------------------------------------------------------------
-- Inversion for Lift

opaque

  -- Inversion for lower.

  inversion-lower : Î“ âŠ¢ lower t âˆ· A â†’ âˆƒâ‚‚ Î» l B â†’ Î“ âŠ¢ t âˆ· Lift l B Ã— Î“ âŠ¢ A â‰¡ B
  inversion-lower (conv x xâ‚) =
    let _ , _ , âŠ¢t , Aâ‰¡B = inversion-lower x
    in _ , _ , âŠ¢t , trans (sym xâ‚) Aâ‰¡B
  inversion-lower (lowerâ±¼ x) = _ , _ , x , refl (inversion-Lift (syntacticTerm x) .projâ‚‚)

------------------------------------------------------------------------
-- Inversion for Unit

opaque

  -- If a term has type UnitÂ sÂ l, then Unit-allowedÂ s holds.

  âŠ¢âˆ·Unitâ†’Unit-allowed : Î“ âŠ¢ t âˆ· Unit s l â†’ Unit-allowed s
  âŠ¢âˆ·Unitâ†’Unit-allowed {Î“} {t} {s} {l} =
    Î“ âŠ¢ t âˆ· Unit s l  â†’âŸ¨ syntacticTerm âŸ©
    Î“ âŠ¢ Unit s l      â†’âŸ¨ inversion-Unit-allowed âŸ©
    Unit-allowed s    â–¡

opaque

  -- Inversion for unitrec.

  inversion-unitrec :
    Î“ âŠ¢ unitrec p q l A t u âˆ· B â†’
    (Î“ âˆ™ UnitÊ· l âŠ¢ A) Ã—
    Î“ âŠ¢ t âˆ· UnitÊ· l Ã—
    Î“ âŠ¢ u âˆ· A [ starÊ· l ]â‚€ Ã—
    Î“ âŠ¢ B â‰¡ A [ t ]â‚€
  inversion-unitrec (unitrecâ±¼ âŠ¢l âŠ¢A âŠ¢t âŠ¢u _) =
    âŠ¢A , âŠ¢t , âŠ¢u , refl (substType âŠ¢A âŠ¢t)
  inversion-unitrec (conv âŠ¢ur eq) =
    let a , b , c , d = inversion-unitrec âŠ¢ur
    in  a , b , c , trans (sym eq) d

------------------------------------------------------------------------
-- Inversion for Î  and Î£

opaque

  -- If a term has type Î Î£âŸ¨Â bÂ âŸ©Â pÂ ,Â qÂ â–·Â AÂ â–¹Â B, then Î Î£-allowedÂ bÂ pÂ q
  -- holds.

  âŠ¢âˆ·Î Î£â†’Î Î£-allowed :
    Î“ âŠ¢ t âˆ· Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B â†’ Î Î£-allowed b p q
  âŠ¢âˆ·Î Î£â†’Î Î£-allowed {Î“} {t} {b} {p} {q} {A} {B} =
    Î“ âŠ¢ t âˆ· Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B  â†’âŸ¨ syntacticTerm âŸ©
    Î“ âŠ¢ Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B      â†’âŸ¨ projâ‚‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ inversion-Î Î£ âŸ©
    Î Î£-allowed b p q               â–¡

opaque

  -- Inversion for lam.

  inversion-lam :
    Î“ âŠ¢ lam p t âˆ· A â†’
    âˆƒâ‚ƒ Î» B C q â†’
      (Î“ âŠ¢ B) Ã— Î“ âˆ™ B âŠ¢ t âˆ· C Ã—
      Î“ âŠ¢ A â‰¡ Î  p , q â–· B â–¹ C Ã—
      Î -allowed p q
  inversion-lam (lamâ±¼ _ âŠ¢t ok) =
    let âŠ¢B = syntacticTerm âŠ¢t in
    _ , _ , _ , âŠ¢âˆ™â†’âŠ¢ (wf âŠ¢B) , âŠ¢t , refl (Î Î£â±¼ âŠ¢B ok) , ok
  inversion-lam (conv âŠ¢lam eq) =
    let a , b , c , d , e , f , g = inversion-lam âŠ¢lam in
    a , b , c , d , e , trans (sym eq) f , g

opaque

  -- Inversion for _âˆ˜âŸ¨_âŸ©_.

  inversion-app :
    Î“ âŠ¢ t âˆ˜âŸ¨ p âŸ© u âˆ· A â†’
    âˆƒâ‚ƒ Î» B C q â†’ Î“ âŠ¢ t âˆ· Î  p , q â–· B â–¹ C Ã— Î“ âŠ¢ u âˆ· B Ã— Î“ âŠ¢ A â‰¡ C [ u ]â‚€
  inversion-app (âŠ¢t âˆ˜â±¼ âŠ¢u) =
    _ , _ , _ , âŠ¢t , âŠ¢u , refl (substTypeÎ  (syntacticTerm âŠ¢t) âŠ¢u)
  inversion-app (conv âŠ¢app eq) =
    let a , b , c , d , e , f = inversion-app âŠ¢app in
    a , b , c , d , e , trans (sym eq) f

opaque

  -- Inversion for snd.

  inversion-snd :
    Î“ âŠ¢ snd p t âˆ· A â†’
    âˆƒâ‚ƒ Î» B C q â†’
      (Î“ âŠ¢ B) Ã— (Î“ âˆ™ B âŠ¢ C) Ã—
      Î“ âŠ¢ t âˆ· Î£Ë¢ p , q â–· B â–¹ C Ã—
      Î“ âŠ¢ A â‰¡ C [ fst p t ]â‚€
  inversion-snd (sndâ±¼ âŠ¢C âŠ¢t) =
    _ , _ , _ , âŠ¢âˆ™â†’âŠ¢ (wf âŠ¢C) , âŠ¢C , âŠ¢t ,
    refl (substType âŠ¢C (fstâ±¼ âŠ¢C âŠ¢t))
  inversion-snd (conv âŠ¢snd eq) =
    let a , b , c , d , e , f , g = inversion-snd âŠ¢snd in
    a , b , c , d , e , f , trans (sym eq) g

opaque

  -- Inversion for prodrec.

  inversion-prodrec :
    Î“ âŠ¢ prodrec r p qâ€² A t u âˆ· B â†’
    âˆƒâ‚ƒ Î» C D q â†’
      (Î“ âŠ¢ C) Ã— (Î“ âˆ™ C âŠ¢ D) Ã—
      (Î“ âˆ™ Î£Ê· p , q â–· C â–¹ D âŠ¢ A) Ã—
      Î“ âŠ¢ t âˆ· Î£Ê· p , q â–· C â–¹ D Ã—
      Î“ âˆ™ C âˆ™ D âŠ¢ u âˆ· A [ prodÊ· p (var x1) (var x0) ]â†‘Â² Ã—
      Î“ âŠ¢ B â‰¡ A [ t ]â‚€
  inversion-prodrec (prodrecâ±¼ âŠ¢A âŠ¢t âŠ¢u _) =
    let âŠ¢D = âŠ¢âˆ™â†’âŠ¢ (wfTerm âŠ¢u) in
    _ , _ , _ , âŠ¢âˆ™â†’âŠ¢ (wf âŠ¢D) , âŠ¢D , âŠ¢A , âŠ¢t , âŠ¢u ,
    refl (substType âŠ¢A âŠ¢t)
  inversion-prodrec (conv âŠ¢pr eq) =
    let a , b , c , d , e , f , g , h , i = inversion-prodrec âŠ¢pr in
    a , b , c , d , e , f , g , h , trans (sym eq) i

------------------------------------------------------------------------
-- Inversion forÂ â„•

opaque

  -- Inversion for natrec.

  inversion-natrec :
    Î“ âŠ¢ natrec p q r A t u v âˆ· B â†’
    (Î“ âˆ™ â„• âŠ¢ A) Ã—
    Î“ âŠ¢ t âˆ· A [ zero ]â‚€ Ã—
    Î“ âˆ™ â„• âˆ™ A âŠ¢ u âˆ· A [ suc (var x1) ]â†‘Â² Ã—
    Î“ âŠ¢ v âˆ· â„• Ã—
    Î“ âŠ¢ B â‰¡ A [ v ]â‚€
  inversion-natrec (natrecâ±¼ âŠ¢t âŠ¢u âŠ¢v) =
    let âŠ¢A = âŠ¢âˆ™â†’âŠ¢ (wfTerm âŠ¢u) in
    âŠ¢A , âŠ¢t , âŠ¢u , âŠ¢v , refl (substType âŠ¢A âŠ¢v)
  inversion-natrec (conv âŠ¢nr eq) =
    let a , b , c , d , e = inversion-natrec âŠ¢nr in
    a , b , c , d , trans (sym eq) e

------------------------------------------------------------------------
-- Inversion for Id

opaque

  -- Inversion for Id.

  inversion-Id-U :
    Î“ âŠ¢ Id A t u âˆ· B â†’
    âˆƒ Î» l â†’ Î“ âŠ¢ A âˆ· U l Ã— Î“ âŠ¢ t âˆ· A Ã— Î“ âŠ¢ u âˆ· A Ã— Î“ âŠ¢ B â‰¡ U l
  inversion-Id-U = Î» where
    (Idâ±¼ âŠ¢A âŠ¢t âŠ¢u) â†’ _ , âŠ¢A , âŠ¢t , âŠ¢u , refl (Uâ±¼ (inversion-U-Level (wf-âŠ¢âˆ· âŠ¢A)))
    (conv âŠ¢Id Câ‰¡B) â†’
      case inversion-Id-U âŠ¢Id of Î» {
        (_ , âŠ¢A , âŠ¢t , âŠ¢u , Câ‰¡U) â†’
      _ , âŠ¢A , âŠ¢t , âŠ¢u , trans (sym Câ‰¡B) Câ‰¡U }

opaque

  -- Inversion for rfl.

  inversion-rfl :
    Î“ âŠ¢ rfl âˆ· A â†’
    âˆƒâ‚‚ Î» B t â†’ (Î“ âŠ¢ B) Ã— Î“ âŠ¢ t âˆ· B Ã— Î“ âŠ¢ A â‰¡ Id B t t
  inversion-rfl = Î» where
    âŠ¢rfl@(rflâ±¼ âŠ¢t)  â†’
      _ , _ , syntacticTerm âŠ¢t , âŠ¢t , refl (syntacticTerm âŠ¢rfl)
    (conv âŠ¢rfl eq) â†’
      let a , b , c , d , e = inversion-rfl âŠ¢rfl in
      a , b , c , d , trans (sym eq) e

opaque

  -- Inversion forÂ J.

  inversion-J :
    Î“ âŠ¢ J p q A t B u v w âˆ· C â†’
    (Î“ âŠ¢ A) Ã—
    Î“ âŠ¢ t âˆ· A Ã—
    (Î“ âˆ™ A âˆ™ Id (wk1 A) (wk1 t) (var x0) âŠ¢ B) Ã—
    Î“ âŠ¢ u âˆ· B [ t , rfl ]â‚â‚€ Ã—
    Î“ âŠ¢ v âˆ· A Ã—
    Î“ âŠ¢ w âˆ· Id A t v Ã—
    Î“ âŠ¢ C â‰¡ B [ v , w ]â‚â‚€
  inversion-J = Î» where
    âŠ¢J@(Jâ±¼ âŠ¢t âŠ¢B âŠ¢u âŠ¢v âŠ¢w) â†’
      âŠ¢âˆ™â†’âŠ¢ (wf (âŠ¢âˆ™â†’âŠ¢ (wf âŠ¢B))) , âŠ¢t , âŠ¢B , âŠ¢u , âŠ¢v , âŠ¢w ,
      refl (syntacticTerm âŠ¢J)
    (conv âŠ¢J eq) â†’
      let a , b , c , d , e , f , g = inversion-J âŠ¢J in
      a , b , c , d , e , f , trans (sym eq) g

opaque

  -- Inversion forÂ K.

  inversion-K :
    Î“ âŠ¢ K p A t B u v âˆ· C â†’
    (Î“ âŠ¢ A) Ã—
    Î“ âŠ¢ t âˆ· A Ã—
    (Î“ âˆ™ Id A t t âŠ¢ B) Ã—
    Î“ âŠ¢ u âˆ· B [ rfl ]â‚€ Ã—
    Î“ âŠ¢ v âˆ· Id A t t Ã—
    K-allowed Ã—
    Î“ âŠ¢ C â‰¡ B [ v ]â‚€
  inversion-K = Î» where
    âŠ¢K@(Kâ±¼ âŠ¢B âŠ¢u âŠ¢v ok) â†’
        let âŠ¢A , âŠ¢t , _ = inversion-Id (âŠ¢âˆ™â†’âŠ¢ (wf âŠ¢B)) in
        âŠ¢A , âŠ¢t , âŠ¢B , âŠ¢u , âŠ¢v , ok
      , refl (syntacticTerm âŠ¢K)
    (conv âŠ¢K eq) â†’
      let a , b , c , d , e , f , g = inversion-K âŠ¢K in
      a , b , c , d , e , f , trans (sym eq) g

opaque

  -- Inversion for []-cong.

  inversion-[]-cong :
    Î“ âŠ¢ []-cong s A t u v âˆ· B â†’
    let open Erased s in
      (Î“ âŠ¢ A) Ã—
      Î“ âŠ¢ t âˆ· A Ã—
      Î“ âŠ¢ u âˆ· A Ã—
      Î“ âŠ¢ v âˆ· Id A t u Ã—
      []-cong-allowed s Ã—
      Î“ âŠ¢ B â‰¡ Id (Erased A) ([ t ]) ([ u ])
  inversion-[]-cong = Î» where
    âŠ¢[]-cong@([]-congâ±¼ _ âŠ¢t âŠ¢u âŠ¢v ok) â†’
        syntacticTerm âŠ¢t , âŠ¢t , âŠ¢u , âŠ¢v , ok
      , refl (syntacticTerm âŠ¢[]-cong)
    (conv âŠ¢bc eq) â†’
      let a , b , c , d , e , f = inversion-[]-cong âŠ¢bc in
      a , b , c , d , e , trans (sym eq) f
