------------------------------------------------------------------------
-- Equality lemmata.
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.Typed.Consequences.Equality
  {a} {M : Set a}
  {ğ•„ : Modality M}
  (R : Type-restrictions ğ•„)
  where

open Type-restrictions R

open import Definition.Untyped M
open import Definition.Untyped.Lift ğ•„
open import Definition.Untyped.Neutral M type-variant
open import Definition.Untyped.Properties M
open import Definition.Untyped.Whnf M type-variant
open import Definition.Typed R
open import Definition.Typed.Properties R
open import Definition.Typed.EqRelInstance R
open import Definition.Typed.Inversion R
open import Definition.Typed.Syntactic R
open import Definition.LogicalRelation R
open import Definition.LogicalRelation.Hidden R
open import Definition.LogicalRelation.Fundamental.Reducibility R
open import Definition.LogicalRelation.Substitution.Introductions R

open import Tools.Fin
open import Tools.Function
open import Tools.Nat using (1+)
open import Tools.Product as Î£
import Tools.PropositionalEquality as PE
open import Tools.Relation

private
  variable
    âˆ‡         : DCon (Term 0) _
    Î“         : Con Term _
    A B C t u : Term _
    V         : Set a
    b         : BinderMode
    p q       : M
    s         : Strength
    l         : Universe-level

opaque

  -- If the WHNF A is judgmentally equal to UÂ l, then A is
  -- propositionally equal to UÂ l (given a certain assumption).

  Uâ‰¡A :
    â¦ƒ ok : No-equality-reflection or-empty Î“ â¦„ â†’
    âˆ‡ Â» Î“ âŠ¢ U l â‰¡ A â†’ Whnf âˆ‡ A â†’ A PE.â‰¡ U l
  Uâ‰¡A {Î“} {âˆ‡} {l} {A} Uâ‰¡A A-whnf =    $âŸ¨ Uâ‰¡A âŸ©
    âˆ‡ Â» Î“ âŠ¢ U l â‰¡ A                   â†’âŸ¨ reducible-âŠ©â‰¡ âŸ©
    (âˆƒ Î» lâ€² â†’ âˆ‡ Â» Î“ âŠ©âŸ¨ lâ€² âŸ© U l â‰¡ A)  â†’âŸ¨ projâ‚‚ âˆ˜â†’ âŠ©Uâ‰¡â‡” .projâ‚ âˆ˜â†’ projâ‚‚ âŸ©
    âˆ‡ Â» Î“ âŠ¢ A â‡’* U l                  â†’âŸ¨ flip whnfRed* A-whnf âŸ©
    A PE.â‰¡ U l                        â–¡

opaque

  -- If equality reflection is allowed, then there is a WHNF A that is
  -- judgementally equal to UÂ l but not propositionally equal to UÂ l
  -- (given a certain assumption).

  whnfâ‰¢U :
    Equality-reflection â†’
    UnitÊ·-allowed â†’
    Â» âˆ‡ â†’
    âˆƒâ‚‚ Î» (Î“ : Con Term 1) (A : Term 1) â†’
      âˆ‡ Â» Î“ âŠ¢ U l â‰¡ A Ã— Whnf âˆ‡ A Ã— A PE.â‰¢ U l
  whnfâ‰¢U {l} okâ‚ okâ‚‚ Â»âˆ‡ =
    Îµ âˆ™ Id (U (1+ l)) (U l) (UnitÊ· (1+ l)) ,
    UnitÊ· (1+ l) ,
    univ
      (equality-reflectionâ€² okâ‚ $
       varâ‚€ (Idâ±¼â€² (Uâ±¼ (Îµ Â»âˆ‡)) (Unitâ±¼ (Îµ Â»âˆ‡) okâ‚‚))) ,
    Unitâ‚™ ,
    (Î» ())

opaque

  -- If the WHNF A is judgmentally equal toÂ â„•, then A is
  -- propositionally equal toÂ â„• (given a certain assumption).

  â„•â‰¡A :
    â¦ƒ ok : No-equality-reflection or-empty Î“ â¦„ â†’
    âˆ‡ Â» Î“ âŠ¢ â„• â‰¡ A â†’ Whnf âˆ‡ A â†’ A PE.â‰¡ â„•
  â„•â‰¡A {Î“} {âˆ‡} {A} â„•â‰¡A A-whnf =
                    $âŸ¨ â„•â‰¡A âŸ©
    âˆ‡ Â» Î“ âŠ¢ â„• â‰¡ A   â†’âŸ¨ âŠ©â„•â‰¡â‡” .projâ‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ reducible-âŠ©â‰¡ âŸ©
    âˆ‡ Â» Î“ âŠ©â„• â„• â‰¡ A  â‰¡âŸ¨ PE.refl âŸ©â†’
    âˆ‡ Â» Î“ âŠ¢ A â‡’* â„•  â†’âŸ¨ flip whnfRed* A-whnf âŸ©
    A PE.â‰¡ â„•        â–¡

opaque

  -- If equality reflection is allowed, then there is a WHNF A that is
  -- judgementally equal to â„• but not propositionally equal toÂ â„•.

  whnfâ‰¢â„• :
    Equality-reflection â†’
    Â» âˆ‡ â†’
    âˆƒâ‚‚ Î» (Î“ : Con Term 1) (A : Term 1) â†’
      âˆ‡ Â» Î“ âŠ¢ â„• â‰¡ A Ã— Whnf âˆ‡ A Ã— A PE.â‰¢ â„•
  whnfâ‰¢â„• ok Â»âˆ‡ =
    Îµ âˆ™ Id (U 0) â„• Empty ,
    Empty ,
    univ
      (equality-reflectionâ€² ok $
       varâ‚€ (Idâ±¼â€² (â„•â±¼ (Îµ Â»âˆ‡)) (Emptyâ±¼ (Îµ Â»âˆ‡)))) ,
    Emptyâ‚™ ,
    (Î» ())

opaque

  -- If the WHNF A is judgmentally equal to Empty, then A is
  -- propositionally equal to Empty (given a certain assumption).

  Emptyâ‰¡A :
    â¦ƒ ok : No-equality-reflection or-empty Î“ â¦„ â†’
    âˆ‡ Â» Î“ âŠ¢ Empty â‰¡ A â†’ Whnf âˆ‡ A â†’ A PE.â‰¡ Empty
  Emptyâ‰¡A {Î“} {âˆ‡} {A} Emptyâ‰¡A A-whnf =
                            $âŸ¨ Emptyâ‰¡A âŸ©
    âˆ‡ Â» Î“ âŠ¢ Empty â‰¡ A       â†’âŸ¨ âŠ©Emptyâ‰¡â‡” .projâ‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ reducible-âŠ©â‰¡ âŸ©
    âˆ‡ Â» Î“ âŠ©Empty Empty â‰¡ A  â‰¡âŸ¨ PE.refl âŸ©â†’
    âˆ‡ Â» Î“ âŠ¢ A â‡’* Empty      â†’âŸ¨ flip whnfRed* A-whnf âŸ©
    A PE.â‰¡ Empty            â–¡

opaque

  -- If equality reflection is allowed, then there is a WHNF A that is
  -- judgementally equal to Empty but not propositionally equal
  -- toÂ Empty.

  whnfâ‰¢Empty :
    Equality-reflection â†’
    Â» âˆ‡ â†’
    âˆƒâ‚‚ Î» (Î“ : Con Term 1) (A : Term 1) â†’
      âˆ‡ Â» Î“ âŠ¢ Empty â‰¡ A Ã— Whnf âˆ‡ A Ã— A PE.â‰¢ Empty
  whnfâ‰¢Empty ok Â»âˆ‡ =
    Îµ âˆ™ Id (U 0) Empty â„• ,
    â„• ,
    univ
      (equality-reflectionâ€² ok $
       varâ‚€ (Idâ±¼â€² (Emptyâ±¼ (Îµ Â»âˆ‡)) (â„•â±¼ (Îµ Â»âˆ‡)))) ,
    â„•â‚™ ,
    (Î» ())

opaque

  -- If the WHNF A is judgmentally equal to UnitÂ sÂ l, then A is
  -- propositionally equal to UnitÂ sÂ l (given a certain assumption).

  Unitâ‰¡A :
    â¦ƒ ok : No-equality-reflection or-empty Î“ â¦„ â†’
    âˆ‡ Â» Î“ âŠ¢ Unit s l â‰¡ A â†’ Whnf âˆ‡ A â†’ A PE.â‰¡ Unit s l
  Unitâ‰¡A {Î“} {âˆ‡} {s} {l} {A} Unitâ‰¡A A-whnf =
                                           $âŸ¨ Unitâ‰¡A âŸ©
    âˆ‡ Â» Î“ âŠ¢ Unit s l â‰¡ A                   â†’âŸ¨ reducible-âŠ©â‰¡ âŸ©
    (âˆƒ Î» lâ€² â†’ âˆ‡ Â» Î“ âŠ©âŸ¨ lâ€² âŸ© Unit s l â‰¡ A)  â†’âŸ¨ projâ‚‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ âŠ©Unitâ‰¡â‡” .projâ‚ âˆ˜â†’ projâ‚‚ âŸ©
    âˆ‡ Â» Î“ âŠ¢ A â‡’* Unit s l                  â†’âŸ¨ flip whnfRed* A-whnf âŸ©
    A PE.â‰¡ Unit s l                        â–¡

opaque

  -- If equality reflection is allowed, then there is a WHNF A that is
  -- judgementally equal to a unit type but not propositionally equal
  -- to any unit type (given a certain assumption).

  whnfâ‰¢Unit :
    Equality-reflection â†’
    Unit-allowed s â†’
    Â» âˆ‡ â†’
    âˆƒâ‚‚ Î» (Î“ : Con Term 1) (A : Term 1) â†’
      âˆ‡ Â» Î“ âŠ¢ Unit s l â‰¡ A Ã— Whnf âˆ‡ A Ã—
      Â¬ âˆƒâ‚‚ Î» s l â†’ A PE.â‰¡ Unit s l
  whnfâ‰¢Unit {s} {l} okâ‚ okâ‚‚ Â»âˆ‡ =
    Îµ âˆ™ Id (U l) (Unit s l) (Id (Unit s l) (star s l) (star s l)) ,
    Id (Unit s l) (star s l) (star s l) ,
    univ
      (equality-reflectionâ€² okâ‚ $ varâ‚€ $
       let âŠ¢Îµ = Îµ Â»âˆ‡ in
       Idâ±¼â€² (Unitâ±¼ âŠ¢Îµ okâ‚‚) $ Idâ±¼ (Unitâ±¼ âŠ¢Îµ okâ‚‚) (starâ±¼ âŠ¢Îµ okâ‚‚) (starâ±¼ âŠ¢Îµ okâ‚‚)) ,
    Idâ‚™ ,
    (Î» ())

opaque

  -- If the neutral term B is judgmentally equal to the WHNFÂ A, then A
  -- is neutral (given a certain assumption).

  neâ‰¡A :
    â¦ƒ ok : No-equality-reflection or-empty Î“ â¦„ â†’
    Neutral No-equality-reflection âˆ‡ B â†’
    âˆ‡ Â» Î“ âŠ¢ B â‰¡ A â†’
    Whnf âˆ‡ A â†’
    Neutral No-equality-reflection âˆ‡ A
  neâ‰¡A {Î“} {âˆ‡} {B} {A} B-ne Bâ‰¡A A-whnf = $âŸ¨ Bâ‰¡A âŸ©
    âˆ‡ Â» Î“ âŠ¢ B â‰¡ A                                  â†’âŸ¨ reducible-âŠ©â‰¡ âŸ©
    (âˆƒ Î» l â†’ âˆ‡ Â» Î“ âŠ©âŸ¨ l âŸ© B â‰¡ A)                   â†’âŸ¨ Î£.map idá¶  (Î£.map idá¶  projâ‚) âˆ˜â†’ âŠ©neâ‰¡â‡” B-ne .projâ‚ âˆ˜â†’ projâ‚‚ âŸ©
    (âˆƒ Î» C â†’ Neutral No-equality-reflection âˆ‡ C Ã—
             âˆ‡ Â» Î“ âŠ¢ A â‡’* C)                       â†’âŸ¨ (Î» (_ , C-ne , Aâ‡’*C) â†’
                                                      PE.subst (Neutral _ âˆ‡) (PE.sym $ whnfRed* Aâ‡’*C A-whnf) C-ne) âŸ©
    Neutral No-equality-reflection âˆ‡ A             â–¡

opaque

  -- If equality reflection is allowed, then there is a WHNF B that is
  -- judgementally equal (as a type) to a neutral term A but not
  -- propositionally equal toÂ A.

  whnfâ‰¢ne :
    Equality-reflection â†’
    Â» âˆ‡ â†’
    âˆƒâ‚ƒ Î» (Î“ : Con Term 2) (A B : Term 2) â†’
      âˆ‡ Â» Î“ âŠ¢ A â‰¡ B Ã— Neutralâº âˆ‡ A Ã— Whnf âˆ‡ B Ã— A PE.â‰¢ B
  whnfâ‰¢ne ok Â»âˆ‡ =
    Îµ âˆ™ U 0 âˆ™ Id (U 0) (var x0) Empty ,
    var x1 ,
    Empty ,
    univ
      (equality-reflectionâ€² ok $
       varâ‚€ (Idâ±¼â€² (varâ‚€ (Uâ±¼ (Îµ Â»âˆ‡))) (Emptyâ±¼ (âˆ™ Uâ±¼ (Îµ Â»âˆ‡))))) ,
    varâº _ ,
    Emptyâ‚™ ,
    (Î» ())

opaque

  -- If a WHNF C is definitionally equal to Î Î£âŸ¨Â bÂ âŸ©Â pÂ ,Â qÂ â–·Â AÂ â–¹Â B,
  -- then C has the shape Î Î£âŸ¨Â bÂ âŸ©Â pÂ ,Â qÂ â–·Â _Â â–¹Â _ (given a certain
  -- assumption).

  Î Î£â‰¡Whnf :
    â¦ƒ ok : No-equality-reflection or-empty Î“ â¦„ â†’
    âˆ‡ Â» Î“ âŠ¢ Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B â‰¡ C â†’ Whnf âˆ‡ C â†’
    âˆƒâ‚‚ Î» Aâ€² Bâ€² â†’ C PE.â‰¡ Î Î£âŸ¨ b âŸ© p , q â–· Aâ€² â–¹ Bâ€²
  Î Î£â‰¡Whnf {Î“} {âˆ‡} {b} {p} {q} {A} {B} {C} Î Î£â‰¡C C-whnf =  $âŸ¨ Î Î£â‰¡C âŸ©
    âˆ‡ Â» Î“ âŠ¢ Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B â‰¡ C                    â†’âŸ¨ reducible-âŠ©â‰¡ âŸ©
    (âˆƒ Î» l â†’ âˆ‡ Â» Î“ âŠ©âŸ¨ l âŸ© Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B â‰¡ C)     â†’âŸ¨ Î£.map idá¶  (Î£.map idá¶  projâ‚) âˆ˜â†’ projâ‚‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ âŠ©Î Î£â‰¡â‡” .projâ‚ âˆ˜â†’ projâ‚‚ âŸ©
    (âˆƒâ‚‚ Î» Aâ€² Bâ€² â†’ âˆ‡ Â» Î“ âŠ¢ C â‡’* Î Î£âŸ¨ b âŸ© p , q â–· Aâ€² â–¹ Bâ€²)  â†’âŸ¨ Î£.map idá¶  $ Î£.map idá¶  (flip whnfRed* C-whnf) âŸ©
    (âˆƒâ‚‚ Î» Aâ€² Bâ€² â†’ C PE.â‰¡ Î Î£âŸ¨ b âŸ© p , q â–· Aâ€² â–¹ Bâ€²)        â–¡

opaque

  -- If equality reflection is allowed, then there is a WHNF C that is
  -- judgementally equal to Î Î£âŸ¨Â bÂ âŸ©Â pÂ ,Â qÂ â–·Â AÂ â–¹Â B but not
  -- propositionally equal to any Î - or Î£-type (given a certain
  -- assumption).

  whnfâ‰¢Î Î£ :
    Equality-reflection â†’
    Î Î£-allowed b p q â†’
    Â» âˆ‡ â†’
    âˆƒâ‚„ Î» (Î“ : Con Term 1) A B C â†’
      âˆ‡ Â» Î“ âŠ¢ Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B â‰¡ C Ã— Whnf âˆ‡ C Ã—
      Â¬ âˆƒâ‚… Î» b p q A B â†’ C PE.â‰¡ Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B
  whnfâ‰¢Î Î£ {b} {p} {q} okâ‚ okâ‚‚ Â»âˆ‡ =
    Îµ âˆ™ Id (U 0) (Î Î£âŸ¨ b âŸ© p , q â–· â„• â–¹ â„•) â„• ,
    â„• , â„• , â„• ,
    univ
      (equality-reflectionâ€² okâ‚ $
       let âŠ¢Îµ = Îµ Â»âˆ‡ in
       varâ‚€ (Idâ±¼â€² (Î Î£â±¼ (â„•â±¼ âŠ¢Îµ) (â„•â±¼ (âˆ™ â„•â±¼ âŠ¢Îµ)) okâ‚‚) (â„•â±¼ âŠ¢Îµ))) ,
    â„•â‚™ ,
    (Î» ())

opaque

  -- If a WHNF A is definitionally equal to Î Â pÂ ,Â qÂ â–·Â BÂ â–¹Â C, then A
  -- has the shape Î Â pÂ ,Â qÂ â–·Â _Â â–¹Â _ (given a certain assumption).

  Î â‰¡A :
    â¦ƒ ok : No-equality-reflection or-empty Î“ â¦„ â†’
    âˆ‡ Â» Î“ âŠ¢ Î  p , q â–· B â–¹ C â‰¡ A â†’ Whnf âˆ‡ A â†’
    âˆƒâ‚‚ Î» Bâ€² Câ€² â†’ A PE.â‰¡ Î  p , q â–· Bâ€² â–¹ Câ€²
  Î â‰¡A = Î Î£â‰¡Whnf

opaque

  -- If a WHNF A is definitionally equal to Î£âŸ¨Â sÂ âŸ©Â pÂ ,Â qÂ â–·Â BÂ â–¹Â C, then
  -- A has the shape Î£âŸ¨Â sÂ âŸ©Â pÂ ,Â qÂ â–·Â _Â â–¹Â _ (given a certain
  -- assumption).

  Î£â‰¡A :
    â¦ƒ ok : No-equality-reflection or-empty Î“ â¦„ â†’
    âˆ‡ Â» Î“ âŠ¢ Î£âŸ¨ s âŸ© p , q â–· B â–¹ C â‰¡ A â†’ Whnf âˆ‡ A â†’
    âˆƒâ‚‚ Î» Bâ€² Câ€² â†’ A PE.â‰¡ Î£âŸ¨ s âŸ© p , q â–· Bâ€² â–¹ Câ€²
  Î£â‰¡A = Î Î£â‰¡Whnf

opaque

  -- If the WHNF B is judgmentally equal to IdÂ AÂ tÂ u, then there are
  -- Aâ€², tâ€² and uâ€² such that B is propositionally equal to IdÂ Aâ€²Â tâ€²Â uâ€²
  -- (given a certain assumption).

  Idâ‰¡Whnf :
    â¦ƒ ok : No-equality-reflection or-empty Î“ â¦„ â†’
    âˆ‡ Â» Î“ âŠ¢ Id A t u â‰¡ B â†’ Whnf âˆ‡ B â†’
    âˆƒâ‚ƒ Î» Aâ€² tâ€² uâ€² â†’ B PE.â‰¡ Id Aâ€² tâ€² uâ€²
  Idâ‰¡Whnf {Î“} {âˆ‡} {A} {t} {u} {B} Idâ‰¡B B-whnf =
                                                $âŸ¨ Idâ‰¡B âŸ©
    âˆ‡ Â» Î“ âŠ¢ Id A t u â‰¡ B                        â†’âŸ¨ reducible-âŠ©â‰¡ âŸ©
    (âˆƒ Î» l â†’ âˆ‡ Â» Î“ âŠ©âŸ¨ l âŸ© Id A t u â‰¡ B)         â†’âŸ¨ Î£.map idá¶  (Î£.map idá¶  (Î£.map idá¶  projâ‚)) âˆ˜â†’ projâ‚‚ âˆ˜â†’ âŠ©Idâ‰¡â‡” .projâ‚ âˆ˜â†’ projâ‚‚ âŸ©
    (âˆƒâ‚ƒ Î» Aâ€² tâ€² uâ€² â†’ âˆ‡ Â» Î“ âŠ¢ B â‡’* Id Aâ€² tâ€² uâ€²)  â†’âŸ¨ Î£.map idá¶  $ Î£.map idá¶  $ Î£.map idá¶  (flip whnfRed* B-whnf) âŸ©
    (âˆƒâ‚ƒ Î» Aâ€² tâ€² uâ€² â†’ B PE.â‰¡ Id Aâ€² tâ€² uâ€²)        â–¡

opaque

  -- If equality reflection is allowed, then there is a WHNF B that is
  -- judgementally equal to IdÂ AÂ tÂ u but not propositionally equal to
  -- any identity type.

  whnfâ‰¢Id :
    Equality-reflection â†’
    Â» âˆ‡ â†’
    âˆƒâ‚… Î» (Î“ : Con Term 1) A t u B â†’
      âˆ‡ Â» Î“ âŠ¢ Id A t u â‰¡ B Ã— Whnf âˆ‡ B Ã—
      Â¬ âˆƒâ‚ƒ Î» A t u â†’ B PE.â‰¡ Id A t u
  whnfâ‰¢Id ok Â»âˆ‡ =
    Îµ âˆ™ Id (U 0) (Id â„• zero zero) â„• ,
    â„• , zero , zero , â„• ,
    univ
      (equality-reflectionâ€² ok $
       let âŠ¢Îµ = Îµ Â»âˆ‡ in
       varâ‚€ (Idâ±¼â€² (Idâ±¼ (â„•â±¼ âŠ¢Îµ) (zeroâ±¼ âŠ¢Îµ) (zeroâ±¼ âŠ¢Îµ)) (â„•â±¼ âŠ¢Îµ))) ,
    â„•â‚™ ,
    (Î» ())

opaque

  -- If t is definitionally equal to rfl, then t reduces to rfl (given
  -- a certain assumption).

  rfl-norm :
    â¦ƒ ok : No-equality-reflection or-empty Î“ â¦„ â†’
    âˆ‡ Â» Î“ âŠ¢ t â‰¡ rfl âˆ· A â†’ âˆ‡ Â» Î“ âŠ¢ t â‡’* rfl âˆ· A
  rfl-norm tâ‰¡rfl =
    case inversion-rfl (syntacticEqTerm tâ‰¡rfl .projâ‚‚ .projâ‚‚) of Î»
      (_ , _ , _ , _ , Aâ‰¡Id) â†’
    case âŠ©â‰¡âˆ·Idâ‡” .projâ‚ $ projâ‚‚ $ reducible-âŠ©â‰¡âˆ· $
         conv tâ‰¡rfl Aâ‰¡Id of Î»
      (tâ€² , _ , tâ‡’*u , rflâ‡’*v , _ , _ , uâˆ¼v) â†’
    case whnfRed*Term rflâ‡’*v rflâ‚™ of Î» {
      PE.refl â†’
    case uâˆ¼v of Î» where
      (rflâ‚Œ _) â†’
        conv* tâ‡’*u (sym Aâ‰¡Id)
      (ne _ () _) }

opaque

  -- If equality reflection is allowed, then there is a term that is
  -- judgementally equal to rfl but that does not reduce to rfl.

  rfl-not-norm :
    Equality-reflection â†’
    Â» âˆ‡ â†’
    âˆƒâ‚ƒ Î» (Î“ : Con Term 2) A t â†’
      âˆ‡ Â» Î“ âŠ¢ t â‰¡ rfl âˆ· A Ã— Â¬ âˆƒâ‚‚ Î» Î” B â†’ âˆ‡ Â» Î” âŠ¢ t â‡’* rfl âˆ· B
  rfl-not-norm ok Â»âˆ‡ =
    let âŠ¢Id = Idâ±¼â€² (zeroâ±¼ (Îµ Â»âˆ‡)) (zeroâ±¼ (Îµ Â»âˆ‡)) in
    Îµ âˆ™ Id â„• zero zero âˆ™ Id (Id â„• zero zero) (var x0) rfl ,
    Id â„• zero zero , var x1 ,
    (equality-reflectionâ€² ok $
     varâ‚€ (Idâ±¼â€² (varâ‚€ âŠ¢Id) (rflâ±¼ (zeroâ±¼ (âˆ™ âŠ¢Id))))) ,
    (Î» { (_ , _ , x1â‡’ â‡¨ _) â†’ neRedTerm x1â‡’ (varâº _) })

opaque

  -- If the WHNF t is judgmentally equal to rfl, then t is
  -- propositionally equal to rfl (given a certain assumption).

  whnfâ‰¡rfl :
    â¦ƒ ok : No-equality-reflection or-empty Î“ â¦„ â†’
    âˆ‡ Â» Î“ âŠ¢ t â‰¡ rfl âˆ· A â†’ Whnf âˆ‡ t â†’ t PE.â‰¡ rfl
  whnfâ‰¡rfl = whnfRed*Term âˆ˜â†’ rfl-norm

opaque

  -- If equality reflection is allowed, then there is a WHNF t that is
  -- judgementally equal to rfl but not propositionally equal to rfl.

  whnfâ‰¢rfl :
    Equality-reflection â†’
    Â» âˆ‡ â†’
    âˆƒâ‚ƒ Î» (Î“ : Con Term 2) A t â†’
      âˆ‡ Â» Î“ âŠ¢ t â‰¡ rfl âˆ· A Ã— Whnf âˆ‡ t Ã— t PE.â‰¢ rfl
  whnfâ‰¢rfl ok Â»âˆ‡ =
    let âŠ¢Id = Idâ±¼â€² (zeroâ±¼ (Îµ Â»âˆ‡)) (zeroâ±¼ (Îµ Â»âˆ‡)) in
    Îµ âˆ™ Id â„• zero zero âˆ™ Id (Id â„• zero zero) (var x0) rfl ,
    Id â„• zero zero , var x1 ,
    (equality-reflectionâ€² ok $
     varâ‚€ (Idâ±¼â€² (varâ‚€ âŠ¢Id) (rflâ±¼ (zeroâ±¼ (âˆ™ âŠ¢Id))))) ,
    ne (varâº _) ,
    (Î» ())
