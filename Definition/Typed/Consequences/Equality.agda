------------------------------------------------------------------------
-- Equality lemmata.
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.Typed.Consequences.Equality
  {a} {M : Set a}
  {ğ•„ : Modality M}
  (R : Type-restrictions ğ•„)
  where

open Type-restrictions R

open import Definition.Untyped M
open import Definition.Untyped.Neutral M type-variant
open import Definition.Typed R
open import Definition.Typed.Properties R
open import Definition.Typed.EqRelInstance R
open import Definition.Typed.Inversion R
open import Definition.Typed.Syntactic R
open import Definition.LogicalRelation R
open import Definition.LogicalRelation.Hidden R
open import Definition.LogicalRelation.Fundamental.Reducibility R
open import Definition.LogicalRelation.Substitution.Introductions R

open import Tools.Function
open import Tools.Nat using (Nat)
open import Tools.Product as Î£
import Tools.PropositionalEquality as PE

private
  variable
    n         : Nat
    Î“         : Con Term n
    A B C t u : Term _
    b         : BinderMode
    p q       : M
    s         : Strength
    l         : Universe-level

opaque

  -- If the WHNF A is judgmentally equal to UÂ l, then A is
  -- propositionally equal to UÂ l.

  Uâ‰¡A : Î“ âŠ¢ U l â‰¡ A â†’ Whnf A â†’ A PE.â‰¡ U l
  Uâ‰¡A {Î“} {l} {A} Uâ‰¡A A-whnf =    $âŸ¨ Uâ‰¡A âŸ©
    Î“ âŠ¢ U l â‰¡ A                   â†’âŸ¨ reducible-âŠ©â‰¡ âŸ©
    (âˆƒ Î» lâ€² â†’ Î“ âŠ©âŸ¨ lâ€² âŸ© U l â‰¡ A)  â†’âŸ¨ projâ‚‚ âˆ˜â†’ âŠ©Uâ‰¡â‡” .projâ‚ âˆ˜â†’ projâ‚‚ âŸ©
    Î“ âŠ¢ A â‡’* U l                  â†’âŸ¨ flip whnfRed* A-whnf âŸ©
    A PE.â‰¡ U l                    â–¡

opaque

  -- If the WHNF A is judgmentally equal toÂ â„•, then A is
  -- propositionally equal toÂ â„•.

  â„•â‰¡A : Î“ âŠ¢ â„• â‰¡ A â†’ Whnf A â†’ A PE.â‰¡ â„•
  â„•â‰¡A {Î“} {A} â„•â‰¡A A-whnf =
                $âŸ¨ â„•â‰¡A âŸ©
    Î“ âŠ¢ â„• â‰¡ A   â†’âŸ¨ âŠ©â„•â‰¡â‡” .projâ‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ reducible-âŠ©â‰¡ âŸ©
    Î“ âŠ©â„• â„• â‰¡ A  â‰¡âŸ¨ PE.refl âŸ©â†’
    Î“ âŠ¢ A â‡’* â„•  â†’âŸ¨ flip whnfRed* A-whnf âŸ©
    A PE.â‰¡ â„•    â–¡

opaque

  -- If the WHNF A is judgmentally equal to Empty, then A is
  -- propositionally equal to Empty.

  Emptyâ‰¡A : Î“ âŠ¢ Empty â‰¡ A â†’ Whnf A â†’ A PE.â‰¡ Empty
  Emptyâ‰¡A {Î“} {A} Emptyâ‰¡A A-whnf =
                        $âŸ¨ Emptyâ‰¡A âŸ©
    Î“ âŠ¢ Empty â‰¡ A       â†’âŸ¨ âŠ©Emptyâ‰¡â‡” .projâ‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ reducible-âŠ©â‰¡ âŸ©
    Î“ âŠ©Empty Empty â‰¡ A  â‰¡âŸ¨ PE.refl âŸ©â†’
    Î“ âŠ¢ A â‡’* Empty      â†’âŸ¨ flip whnfRed* A-whnf âŸ©
    A PE.â‰¡ Empty        â–¡

opaque

  -- If the WHNF A is judgmentally equal to UnitÂ sÂ l, then A is
  -- propositionally equal to UnitÂ sÂ l.

  Unitâ‰¡A : Î“ âŠ¢ Unit s l â‰¡ A â†’ Whnf A â†’ A PE.â‰¡ Unit s l
  Unitâ‰¡A {Î“} {s} {l} {A} Unitâ‰¡A A-whnf =
                                       $âŸ¨ Unitâ‰¡A âŸ©
    Î“ âŠ¢ Unit s l â‰¡ A                   â†’âŸ¨ reducible-âŠ©â‰¡ âŸ©
    (âˆƒ Î» lâ€² â†’ Î“ âŠ©âŸ¨ lâ€² âŸ© Unit s l â‰¡ A)  â†’âŸ¨ projâ‚‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ âŠ©Unitâ‰¡â‡” .projâ‚ âˆ˜â†’ projâ‚‚ âŸ©
    Î“ âŠ¢ A â‡’* Unit s l                  â†’âŸ¨ flip whnfRed* A-whnf âŸ©
    A PE.â‰¡ Unit s l                    â–¡

opaque

  -- If the neutral term B is judgmentally equal to the WHNFÂ A, then A
  -- is neutral.

  neâ‰¡A : Neutral B â†’ Î“ âŠ¢ B â‰¡ A â†’ Whnf A â†’ Neutral A
  neâ‰¡A {B} {Î“} {A} B-ne Bâ‰¡A A-whnf =  $âŸ¨ Bâ‰¡A âŸ©
    Î“ âŠ¢ B â‰¡ A                         â†’âŸ¨ reducible-âŠ©â‰¡ âŸ©
    (âˆƒ Î» l â†’ Î“ âŠ©âŸ¨ l âŸ© B â‰¡ A)          â†’âŸ¨ Î£.map idá¶  (Î£.map idá¶  projâ‚) âˆ˜â†’ âŠ©neâ‰¡â‡” B-ne .projâ‚ âˆ˜â†’ projâ‚‚ âŸ©
    (âˆƒ Î» C â†’ Neutral C Ã— Î“ âŠ¢ A â‡’* C)  â†’âŸ¨ (Î» (_ , C-ne , Aâ‡’*C) â†’
                                            PE.subst Neutral (PE.sym $ whnfRed* Aâ‡’*C A-whnf) C-ne) âŸ©
    Neutral A                         â–¡

opaque

  -- If a WHNF C is definitionally equal to Î Î£âŸ¨Â bÂ âŸ©Â pÂ ,Â qÂ â–·Â AÂ â–¹Â B, then
  -- C has the shape Î Î£âŸ¨Â bÂ âŸ©Â pÂ ,Â qÂ â–·Â _Â â–¹Â _.

  Î Î£â‰¡Whnf :
    Î“ âŠ¢ Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B â‰¡ C â†’ Whnf C â†’
    âˆƒâ‚‚ Î» Aâ€² Bâ€² â†’ C PE.â‰¡ Î Î£âŸ¨ b âŸ© p , q â–· Aâ€² â–¹ Bâ€²
  Î Î£â‰¡Whnf {Î“} {b} {p} {q} {A} {B} {C} Î Î£â‰¡C C-whnf =  $âŸ¨ Î Î£â‰¡C âŸ©
    Î“ âŠ¢ Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B â‰¡ C                    â†’âŸ¨ reducible-âŠ©â‰¡ âŸ©
    (âˆƒ Î» l â†’ Î“ âŠ©âŸ¨ l âŸ© Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B â‰¡ C)     â†’âŸ¨ Î£.map idá¶  (Î£.map idá¶  projâ‚) âˆ˜â†’ projâ‚‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ âŠ©Î Î£â‰¡â‡” .projâ‚ âˆ˜â†’ projâ‚‚ âŸ©
    (âˆƒâ‚‚ Î» Aâ€² Bâ€² â†’ Î“ âŠ¢ C â‡’* Î Î£âŸ¨ b âŸ© p , q â–· Aâ€² â–¹ Bâ€²)  â†’âŸ¨ Î£.map idá¶  $ Î£.map idá¶  (flip whnfRed* C-whnf) âŸ©
    (âˆƒâ‚‚ Î» Aâ€² Bâ€² â†’ C PE.â‰¡ Î Î£âŸ¨ b âŸ© p , q â–· Aâ€² â–¹ Bâ€²)    â–¡

opaque

  -- If a WHNF A is definitionally equal to Î Â pÂ ,Â qÂ â–·Â BÂ â–¹Â C, then A
  -- has the shape Î Â pÂ ,Â qÂ â–·Â _Â â–¹Â _.

  Î â‰¡A :
    Î“ âŠ¢ Î  p , q â–· B â–¹ C â‰¡ A â†’ Whnf A â†’
    âˆƒâ‚‚ Î» Bâ€² Câ€² â†’ A PE.â‰¡ Î  p , q â–· Bâ€² â–¹ Câ€²
  Î â‰¡A = Î Î£â‰¡Whnf

opaque

  -- If a WHNF A is definitionally equal to Î£âŸ¨Â sÂ âŸ©Â pÂ ,Â qÂ â–·Â BÂ â–¹Â C, then
  -- A has the shape Î£âŸ¨Â sÂ âŸ©Â pÂ ,Â qÂ â–·Â _Â â–¹Â _.

  Î£â‰¡A :
    Î“ âŠ¢ Î£âŸ¨ s âŸ© p , q â–· B â–¹ C â‰¡ A â†’ Whnf A â†’
    âˆƒâ‚‚ Î» Bâ€² Câ€² â†’ A PE.â‰¡ Î£âŸ¨ s âŸ© p , q â–· Bâ€² â–¹ Câ€²
  Î£â‰¡A = Î Î£â‰¡Whnf

opaque

  -- If the WHNF B is judgmentally equal to IdÂ AÂ tÂ u, then there are
  -- Aâ€², tâ€² and uâ€² such that B is propositionally equal to
  -- IdÂ Aâ€²Â tâ€²Â uâ€².

  Idâ‰¡Whnf :
    Î“ âŠ¢ Id A t u â‰¡ B â†’ Whnf B â†’
    âˆƒâ‚ƒ Î» Aâ€² tâ€² uâ€² â†’ B PE.â‰¡ Id Aâ€² tâ€² uâ€²
  Idâ‰¡Whnf {Î“} {A} {t} {u} {B} Idâ‰¡B B-whnf =
                                            $âŸ¨ Idâ‰¡B âŸ©
    Î“ âŠ¢ Id A t u â‰¡ B                        â†’âŸ¨ reducible-âŠ©â‰¡ âŸ©
    (âˆƒ Î» l â†’ Î“ âŠ©âŸ¨ l âŸ© Id A t u â‰¡ B)         â†’âŸ¨ Î£.map idá¶  (Î£.map idá¶  (Î£.map idá¶  projâ‚)) âˆ˜â†’ projâ‚‚ âˆ˜â†’ âŠ©Idâ‰¡â‡” .projâ‚ âˆ˜â†’ projâ‚‚ âŸ©
    (âˆƒâ‚ƒ Î» Aâ€² tâ€² uâ€² â†’ Î“ âŠ¢ B â‡’* Id Aâ€² tâ€² uâ€²)  â†’âŸ¨ Î£.map idá¶  $ Î£.map idá¶  $ Î£.map idá¶  (flip whnfRed* B-whnf) âŸ©
    (âˆƒâ‚ƒ Î» Aâ€² tâ€² uâ€² â†’ B PE.â‰¡ Id Aâ€² tâ€² uâ€²)    â–¡

opaque

  -- If t is definitionally equal to rfl, then t reduces to rfl.

  rfl-norm : Î“ âŠ¢ t â‰¡ rfl âˆ· A â†’ Î“ âŠ¢ t â‡’* rfl âˆ· A
  rfl-norm tâ‰¡rfl =
    case inversion-rfl (syntacticEqTerm tâ‰¡rfl .projâ‚‚ .projâ‚‚) of Î»
      (_ , _ , _ , _ , Aâ‰¡Id) â†’
    case âŠ©â‰¡âˆ·Idâ‡” .projâ‚ $ projâ‚‚ $ reducible-âŠ©â‰¡âˆ· $ conv tâ‰¡rfl Aâ‰¡Id of Î»
      (tâ€² , _ , tâ‡’*u , rflâ‡’*v , _ , _ , uâˆ¼v) â†’
    case whnfRed*Term rflâ‡’*v rflâ‚™ of Î» {
      PE.refl â†’
    case uâˆ¼v of Î» where
      (rflâ‚Œ _) â†’
        conv* tâ‡’*u (sym Aâ‰¡Id)
      (ne _ () _) }

opaque

  -- If the WHNF t is judgmentally equal to rfl, then t is
  -- propositionally equal to rfl.

  whnfâ‰¡rfl : Î“ âŠ¢ t â‰¡ rfl âˆ· A â†’ Whnf t â†’ t PE.â‰¡ rfl
  whnfâ‰¡rfl = whnfRed*Term âˆ˜â†’ rfl-norm
