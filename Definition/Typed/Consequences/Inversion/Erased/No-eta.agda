------------------------------------------------------------------------
-- Some inversion lemmas related to typing and the weak variant of
-- Erased
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
import Graded.Modality

module Definition.Typed.Consequences.Inversion.Erased.No-eta
  {a} {M : Set a}
  (open Graded.Modality M)
  {ğ•„ : Modality}
  (R : Type-restrictions ğ•„)
  where

open Modality ğ•„
open Type-restrictions R

open import Definition.Typed R
open import Definition.Typed.Inversion R
open import Definition.Typed.Properties R
open import Definition.Typed.Substitution R
open import Definition.Typed.Consequences.Inequality R
open import Definition.Typed.Consequences.Injectivity R

open import Definition.Untyped M as U
open import Definition.Untyped.Erased ğ•„ ğ•¨ hiding (erased)
open import Definition.Untyped.Erased.No-eta ğ•„
open import Definition.Untyped.Properties M
open import Definition.Untyped.Sigma ğ•„

open import Tools.Empty
open import Tools.Function
open import Tools.Product
import Tools.PropositionalEquality as PE
open import Tools.Relation

open import Definition.Typed.Consequences.Inversion.Erased R ğ•¨ public

opaque

  -- If Erased is allowed, then a certain form of inversion for erased
  -- does not hold.

  Â¬-inversion-erasedâ€² :
    ErasedÊ·-allowed â†’
    Â¬ (âˆ€ {n} {Î“ : Con Term n} {t A : Term n} â†’
       Î“ âŠ¢ erased A t âˆ· A â†’
       âˆƒâ‚‚ Î» q l â†’ Î“ âŠ¢ t âˆ· Î£Ê· ğŸ˜ , q â–· A â–¹ UnitÊ· l)
  Â¬-inversion-erasedâ€² (Unit-ok , Î£Ê·-ok) inversion-erased = bad
    where
    Î“â€² : Con Term 0
    Î“â€² = Îµ

    tâ€² : Term 0
    tâ€² = prodÊ· ğŸ˜ zero zero

    Aâ€² : Term 0
    Aâ€² = â„•

    âŠ¢Î“â€²âˆ™â„• : âŠ¢ Î“â€² âˆ™ â„•
    âŠ¢Î“â€²âˆ™â„• = âˆ™ â„•â±¼ Îµ

    âŠ¢tâ€²â‚ : Î“â€² âŠ¢ tâ€² âˆ· Î£Ê· ğŸ˜ , ğŸ˜ â–· â„• â–¹ â„•
    âŠ¢tâ€²â‚ = prodâ±¼ (â„•â±¼ âŠ¢Î“â€²âˆ™â„•) (zeroâ±¼ Îµ) (zeroâ±¼ Îµ) Î£Ê·-ok

    âŠ¢erased-tâ€² : Î“â€² âŠ¢ erased Aâ€² tâ€² âˆ· Aâ€²
    âŠ¢erased-tâ€² = fstÊ·â±¼ âŠ¢tâ€²â‚

    erased-tâ€²â‰¡zero : Î“â€² âŠ¢ erased Aâ€² tâ€² â‰¡ zero âˆ· Aâ€²
    erased-tâ€²â‰¡zero = fstÊ·-Î²-â‰¡ (â„•â±¼ âŠ¢Î“â€²âˆ™â„•) (zeroâ±¼ Îµ) (zeroâ±¼ Îµ) Î£Ê·-ok

    âŠ¢tâ€²â‚‚ : âˆƒâ‚‚ Î» q l â†’ Î“â€² âŠ¢ tâ€² âˆ· Î£Ê· ğŸ˜ , q â–· Aâ€² â–¹ UnitÊ· l
    âŠ¢tâ€²â‚‚ = inversion-erased âŠ¢erased-tâ€²

    âŠ¢snd-tâ€² :
      âˆƒ Î» l â†’ Î“â€² âŠ¢ sndÊ· ğŸ˜ (âŠ¢tâ€²â‚‚ .projâ‚) Aâ€² (UnitÊ· l) tâ€² âˆ· UnitÊ· l
    âŠ¢snd-tâ€² = _ , sndÊ·â±¼ (âŠ¢tâ€²â‚‚ .projâ‚‚ .projâ‚‚)

    â„•â‰¡Unit : âˆƒ Î» l â†’ Î“â€² âŠ¢ â„• â‰¡ UnitÊ· l
    â„•â‰¡Unit =
      let l , âŠ¢snd-tâ€² = âŠ¢snd-tâ€² in
      case inversion-prodrec âŠ¢snd-tâ€² of
        Î» (F , G , _ , _ , _ , _ , âŠ¢tâ€² , âŠ¢xâ‚€ , Unitâ‰¡) â†’
      case inversion-var âŠ¢xâ‚€ of Î» {
        (Q , here , Unitâ‰¡â€²) â†’
      case inversion-prod âŠ¢tâ€² of
        Î» (Fâ€² , Gâ€² , _ , _ , _ , âŠ¢zero , âŠ¢zeroâ€² , Î£â‰¡Î£ , _) â†’
      case Î£-injectivity Î£â‰¡Î£ of
        Î» (Fâ‰¡Fâ€² , Gâ‰¡Gâ€² , _ , _ , _) â†’
      case inversion-zero âŠ¢zero of
        Î» â‰¡â„• â†’
      case inversion-zero âŠ¢zeroâ€² of
        Î» â‰¡â„•â€² â†’
      case conv âŠ¢zero (sym Fâ‰¡Fâ€²) of
        Î» âŠ¢zeroâ€³ â†’
      case substTypeEq Gâ‰¡Gâ€² (refl âŠ¢zeroâ€³)  of
        Î» Gâ‚€â‰¡Gâ€²â‚€ â†’
      let âŠ¢Ïƒ : Î“â€² âŠ¢Ë¢Ê· consSubst (sgSubst zero) zero âˆ· (Î“â€² âˆ™ F âˆ™ G)
          âŠ¢Ïƒ = â†’âŠ¢Ë¢Ê·âˆ·âˆ™
                 (â†’âŠ¢Ë¢Ê·âˆ·âˆ™ (âŠ¢Ë¢Ê·âˆ·-idSubst Îµ) $
                  PE.subst (_âŠ¢_âˆ·_ _ _) (PE.sym (subst-id F)) âŠ¢zeroâ€³)
                 (conv âŠ¢zeroâ€² (sym Gâ‚€â‰¡Gâ€²â‚€))
      in case PE.subst (_âŠ¢_â‰¡_ _ _) (wk1-tail G)
               (subst-âŠ¢â‰¡ Unitâ‰¡â€² (refl-âŠ¢Ë¢Ê·â‰¡âˆ· âŠ¢Ïƒ)) of
        Î» Unitâ‰¡â€³ â†’
      l , sym (trans Unitâ‰¡â€³ (trans Gâ‚€â‰¡Gâ€²â‚€ â‰¡â„•â€²)) }

    bad : âŠ¥
    bad = â„•â‰¢Unitâ±¼ (â„•â‰¡Unit .projâ‚‚)

opaque

  -- If Erased is allowed, then another form of inversion for erased
  -- also does not hold.

  Â¬-inversion-erased :
    ErasedÊ·-allowed â†’
    Â¬ (âˆ€ {n} {Î“ : Con Term n} {t A : Term n} â†’
       Î“ âŠ¢ erased A t âˆ· A â†’
       Î“ âŠ¢ t âˆ· Erased A)
  Â¬-inversion-erased Erased-ok inversion-erased =
    Â¬-inversion-erasedâ€² Erased-ok Î» âŠ¢erased â†’
    _ , _ , inversion-erased âŠ¢erased
