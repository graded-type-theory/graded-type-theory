{-# OPTIONS --without-K --safe #-}

open import Tools.Relation
open import Definition.Modality

module Definition.Typed.Usage {a â„“}
  {Mâ€² : Setoid a â„“} (ğ•„ : Modality Mâ€²)
  where

open Modality ğ•„
open Setoid Mâ€² renaming (Carrier to M)

open import Definition.Modality.Context ğ•„
open import Definition.Modality.Context.Properties ğ•„
open import Definition.Modality.Properties ğ•„
open import Definition.Modality.Substitution.Properties ğ•„
open import Definition.Modality.Usage ğ•„
open import Definition.Modality.Usage.Inversion ğ•„
open import Definition.Typed Mâ€²
open import Definition.Untyped M hiding (_âˆ·_)

open import Tools.Nat
open import Tools.Product
open import Tools.PropositionalEquality as PE


private
  variable
    n : Nat
    Î“ : Con Term n
    Î³ Î´ : Conâ‚˜ n
    t u A B : Term n

-- Subject reduction properties for modality usage

-- Term reduction preserves resource usage
-- If Î³ â–¸ t and Î“ âŠ¢ t â‡’ u âˆ· A, then Î³ â–¸ u

usagePresTerm : Î³ â–¸ t â†’ Î“ âŠ¢ t â‡’ u âˆ· A â†’ Î³ â–¸ u
usagePresTerm Î³â–¸t (conv tâ‡’u x) = usagePresTerm Î³â–¸t tâ‡’u
usagePresTerm Î³â–¸t (app-subst tâ‡’u x) =
  let invUsageApp Î´â–¸t Î·â–¸a Î³â‰¤Î´+pÎ· = inv-usage-app Î³â–¸t
  in  sub ((usagePresTerm Î´â–¸t tâ‡’u) âˆ˜â‚˜ Î·â–¸a) Î³â‰¤Î´+pÎ·
usagePresTerm Î³â–¸Î»ta (Î²-red x xâ‚ xâ‚‚ xâ‚ƒ xâ‚„) =
  let invUsageApp Î´â–¸Î»t Î·â–¸a Î³â‰¤Î´â€²+pÎ· = inv-usage-app Î³â–¸Î»ta
      invUsageLam Î´â–¸t Î´â€²â‰¤Î´ = inv-usage-lam Î´â–¸Î»t
  in  sub (sgSubstâ‚˜-lemma Î´â–¸t Î·â–¸a)
          (â‰¤á¶œ-trans Î³â‰¤Î´â€²+pÎ· (+á¶œ-monotone Î´â€²â‰¤Î´ (Â·á¶œ-monotoneË¡ (â‰¤-reflexive (â‰ˆ-sym xâ‚„)))))
usagePresTerm Î³â–¸t (fst-subst x xâ‚ tâ‡’u) =
  let invUsageProj ğŸ˜â–¸t Î³â‰¤ğŸ˜ = inv-usage-fst Î³â–¸t
  in  sub (fstâ‚˜ (usagePresTerm ğŸ˜â–¸t tâ‡’u)) Î³â‰¤ğŸ˜
usagePresTerm Î³â–¸t (snd-subst x xâ‚ tâ‡’u) =
  let invUsageProj ğŸ˜â–¸t Î³â‰¤ğŸ˜ = inv-usage-snd Î³â–¸t
  in  sub (sndâ‚˜ (usagePresTerm ğŸ˜â–¸t tâ‡’u)) Î³â‰¤ğŸ˜
usagePresTerm Î³â–¸tâ€² (Î£-Î²â‚ x xâ‚ xâ‚‚ xâ‚ƒ xâ‚„) =
  let invUsageProj Î´â–¸tu Î³â‰¤Î´ = inv-usage-fst Î³â–¸tâ€²
      invUsageProdâ‚š Î·â–¸t Î·â–¸u Î´â‰¤Î· = inv-usage-prodâ‚š Î´â–¸tu
  in  sub Î·â–¸t (â‰¤á¶œ-trans Î³â‰¤Î´ Î´â‰¤Î·)
usagePresTerm Î³â–¸tâ€² (Î£-Î²â‚‚ x xâ‚ xâ‚‚ xâ‚ƒ xâ‚„) =
  let invUsageProj Î´â–¸tu Î³â‰¤Î´ = inv-usage-snd Î³â–¸tâ€²
      invUsageProdâ‚š Î·â–¸t Î·â–¸u Î´â‰¤Î· = inv-usage-prodâ‚š Î´â–¸tu
  in  sub Î·â–¸u (â‰¤á¶œ-trans Î³â‰¤Î´ Î´â‰¤Î·)

usagePresTerm Î³â–¸natrec (natrec-subst x xâ‚ xâ‚‚ tâ‡’u) =
  let invUsageNatrec Î´â–¸z Î·â–¸s Î¸â–¸n Î³â‰¤X = inv-usage-natrec Î³â–¸natrec
  in  sub (natrecâ‚˜ Î´â–¸z Î·â–¸s (usagePresTerm Î¸â–¸n tâ‡’u)) Î³â‰¤X

usagePresTerm Î³â–¸natrec (natrec-zero {p = p} {r = r} x xâ‚ xâ‚‚) =
  let invUsageNatrec {Î´ = Î´} {Î¸ = Î¸} Î´â–¸z Î·â–¸s Î¸â–¸n Î³â‰¤Î³â€² = inv-usage-natrec Î³â–¸natrec
      Î¸â‰¤ğŸ˜ = inv-usage-zero Î¸â–¸n
      Î³â€²â‰¤Î´ = begin
        (Î´ âˆ§á¶œ Î¸) âŠ›á¶œ (_ +á¶œ p Â·á¶œ _) â–· r â‰¤âŸ¨ âŠ›á¶œ-ineqâ‚‚ (Î´ âˆ§á¶œ Î¸) _ r âŸ©
        Î´ âˆ§á¶œ Î¸                        â‰¤âŸ¨ âˆ§á¶œ-decreasingË¡ Î´ Î¸ âŸ©
        Î´                             âˆ
  in  sub Î´â–¸z (â‰¤á¶œ-trans Î³â‰¤Î³â€² Î³â€²â‰¤Î´)
  where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset

usagePresTerm {Î³ = Î³} Î³â–¸natrec (natrec-suc {p = p} {r = r} x xâ‚ xâ‚‚ xâ‚ƒ) =
  let invUsageNatrec {Î´ = Î´} {Î·} {Î¸} Î´â–¸z Î·â–¸s Î¸â–¸sn Î³â‰¤Î³â€² = inv-usage-natrec Î³â–¸natrec
      invUsageSuc {Î´ = Î¸â€²} Î¸â€²â–¸n Î¸â‰¤Î¸â€² = inv-usage-suc Î¸â–¸sn
      Î³â€² = (Î´ âˆ§á¶œ Î¸) âŠ›á¶œ (Î· +á¶œ p Â·á¶œ Î¸) â–· r
      Î³â‰¤Î³â€³ = begin
        Î³       â‰¤âŸ¨ Î³â‰¤Î³â€² âŸ©
        Î³â€²      â‰¤âŸ¨ âŠ›á¶œ-ineqâ‚ _ _ _ âŸ©
        (Î· +á¶œ p Â·á¶œ Î¸) +á¶œ r Â·á¶œ Î³â€²
                â‰ˆâŸ¨ +á¶œ-assoc Î· (p Â·á¶œ Î¸) (r Â·á¶œ (Î´ âˆ§á¶œ Î¸) âŠ›á¶œ (Î· +á¶œ (p Â·á¶œ Î¸)) â–· r) âŸ©
        Î· +á¶œ p Â·á¶œ Î¸ +á¶œ r Â·á¶œ Î³â€²
               â‰ˆâŸ¨ +á¶œ-congË¡ (+á¶œ-comm (p Â·á¶œ Î¸) (r Â·á¶œ (Î´ âˆ§á¶œ Î¸) âŠ›á¶œ (Î· +á¶œ (p Â·á¶œ Î¸)) â–· r)) âŸ©
        Î· +á¶œ r Â·á¶œ Î³â€² +á¶œ p Â·á¶œ Î¸
               â‰¤âŸ¨ +á¶œ-monotoneÊ³ (+á¶œ-monotoneÊ³ (Â·á¶œ-monotoneÊ³ Î¸â‰¤Î¸â€²)) âŸ©
        Î· +á¶œ r Â·á¶œ Î³â€² +á¶œ p Â·á¶œ Î¸â€² âˆ
  in  sub (doubleSubstâ‚˜-lemma Î·â–¸s (natrecâ‚˜ Î´â–¸z Î·â–¸s (sub Î¸â€²â–¸n Î¸â‰¤Î¸â€²)) Î¸â€²â–¸n) Î³â‰¤Î³â€³
  where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset

usagePresTerm Î³â–¸prodrec (prodrec-subst x xâ‚ xâ‚‚ xâ‚ƒ xâ‚„) =
  let invUsageProdrec Î´â–¸t Î·â–¸u Î³â‰¤Î³â€² = inv-usage-prodrec Î³â–¸prodrec
  in  sub (prodrecâ‚˜ (usagePresTerm Î´â–¸t xâ‚„) Î·â–¸u) Î³â‰¤Î³â€²
usagePresTerm {Î³ = Î³} Î³â–¸prodrec (prodrec-Î² {p = p} {t = t} {tâ€²} {u} x xâ‚ xâ‚‚ xâ‚ƒ xâ‚„ xâ‚…) =
  let invUsageProdrec {Î´ = Î´} {Î·} Î´â–¸t Î·â–¸u Î³â‰¤pÎ´+Î· = inv-usage-prodrec Î³â–¸prodrec
      invUsageProdáµ£ {Î´ = Î´â€²} {Î·â€²} {Î¸} Î´â€²â–¸tâ‚ Î·â€²â–¸tâ‚‚ Î³â€³â‰¡Î´â€²+Î·â€² Î³â€²â‰¤Î³â€³ = inv-usage-prodáµ£ Î´â–¸t
      le = begin
        Î³                      â‰¤âŸ¨ Î³â‰¤pÎ´+Î· âŸ©
        p Â·á¶œ Î´ +á¶œ Î·            â‰ˆâŸ¨ +á¶œ-comm (p Â·á¶œ Î´) Î· âŸ©
        Î· +á¶œ p Â·á¶œ Î´            â‰¤âŸ¨ +á¶œ-monotoneÊ³ (Â·á¶œ-monotoneÊ³ Î³â€²â‰¤Î³â€³) âŸ©
        Î· +á¶œ (p Â·á¶œ Î¸)          â‰¡âŸ¨ PE.cong (Î» Î³ â†’ Î· +á¶œ p Â·á¶œ Î³) Î³â€³â‰¡Î´â€²+Î·â€² âŸ©
        Î· +á¶œ p Â·á¶œ (Î´â€² +á¶œ Î·â€²)   â‰ˆâŸ¨ +á¶œ-congË¡ (Â·á¶œ-distribË¡-+á¶œ p Î´â€² Î·â€²) âŸ©
        Î· +á¶œ p Â·á¶œ Î´â€² +á¶œ p Â·á¶œ Î·â€² â‰ˆâŸ¨ +á¶œ-congË¡ (+á¶œ-comm (p Â·á¶œ Î´â€²) (p Â·á¶œ Î·â€²)) âŸ©
        Î· +á¶œ p Â·á¶œ Î·â€² +á¶œ p Â·á¶œ Î´â€² âˆ
  in  sub (doubleSubstâ‚˜-lemma Î·â–¸u Î·â€²â–¸tâ‚‚ Î´â€²â–¸tâ‚) le
  where
  open import Tools.Reasoning.PartialOrder â‰¤á¶œ-poset

usagePresTerm Î³â–¸et (Emptyrec-subst x tâ‡’u) =
  let invUsageEmptyrec Î´â–¸t Î³â‰¤Î´ = inv-usage-Emptyrec Î³â–¸et
  in  sub (Emptyrecâ‚˜ (usagePresTerm Î´â–¸t tâ‡’u)) Î³â‰¤Î´

-- Type reduction preserves modality usage
-- If Î³ â–¸ A and Î“ âŠ¢ A â‡’ B, then Î³ â–¸ B

usagePres : Î³ â–¸ A â†’ Î“ âŠ¢ A â‡’ B â†’ Î³ â–¸ B
usagePres Î³â–¸A (univ Aâ‡’B) = usagePresTerm Î³â–¸A Aâ‡’B

-- Term reduction closeure preserves modality usage
-- If Î³ â–¸ t and Î“ âŠ¢ t â‡’* u âˆ· A then Î³ â–¸ u

usagePres*Term : Î³ â–¸ t â†’ Î“ âŠ¢ t â‡’* u âˆ· A â†’ Î³ â–¸ u
usagePres*Term Î³â–¸t (id x) = Î³â–¸t
usagePres*Term Î³â–¸t (x â‡¨ tâ‡’u) = usagePres*Term (usagePresTerm Î³â–¸t x) tâ‡’u

-- Type reduction closeure preserves modality usage
-- If Î³ â–¸ A and Î“ âŠ¢ A â‡’* B then Î³ â–¸ B

usagePres* : Î³ â–¸ A â†’ Î“ âŠ¢ A â‡’* B â†’ Î³ â–¸ B
usagePres* Î³â–¸A (id x) = Î³â–¸A
usagePres* Î³â–¸A (x â‡¨ Aâ‡’B) = usagePres* (usagePres Î³â–¸A x) Aâ‡’B
