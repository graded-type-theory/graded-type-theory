------------------------------------------------------------------------
-- Properties of the typing and reduction relations
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.Typed.Properties
  {â„“} {M : Set â„“}
  {ğ•„ : Modality M}
  (R : Type-restrictions ğ•„)
  where

open Type-restrictions R

open import Definition.Typed.Properties.Well-formed R public

open import Definition.Untyped M
open import Definition.Untyped.Neutral M type-variant
open import Definition.Typed R

import Graded.Derived.Erased.Typed.Primitive R as Erased

open import Tools.Empty using (âŠ¥; âŠ¥-elim)
open import Tools.Fin
open import Tools.Function
open import Tools.Nat
open import Tools.Product
import Tools.PropositionalEquality as PE
open import Tools.Relation
open import Tools.Sum using (_âŠ_; injâ‚; injâ‚‚)

private
  variable
    n l lâ‚ lâ‚‚ : Nat
    Î“ : Con Term n
    A Aâ€² B Bâ€² C D E F Uâ€² : Term n
    a b t tâ€² u uâ€² v w : Term n
    p pâ€² q r : M
    s : Strength


------------------------------------------------------------------------
-- Inversion lemmas related to _âŠ¢_â‡’_âˆ·_

private opaque

  -- An inversion lemma related to _âˆ˜âŸ¨_âŸ©_.

  inv-â‡’-âˆ˜ :
    Î“ âŠ¢ t âˆ˜âŸ¨ p âŸ© u â‡’ v âˆ· A â†’
    (âˆƒâ‚‚ Î» tâ€² B â†’ Î“ âŠ¢ t â‡’ tâ€² âˆ· B Ã— v PE.â‰¡ tâ€² âˆ˜âŸ¨ p âŸ© u) âŠ
    (âˆƒ Î» tâ€² â†’ t PE.â‰¡ lam p tâ€² Ã— v PE.â‰¡ tâ€² [ u ]â‚€)
  inv-â‡’-âˆ˜ (conv d _)              = inv-â‡’-âˆ˜ d
  inv-â‡’-âˆ˜ (app-subst d _)         = injâ‚ (_ , _ , d , PE.refl)
  inv-â‡’-âˆ˜ (Î²-red _ _ _ PE.refl _) = injâ‚‚ (_ , PE.refl , PE.refl)

  -- An inversion lemma related to fst.

  inv-â‡’-fst :
    Î“ âŠ¢ fst p t â‡’ u âˆ· A â†’
    (âˆƒâ‚‚ Î» tâ€² B â†’ Î“ âŠ¢ t â‡’ tâ€² âˆ· B Ã— u PE.â‰¡ fst p tâ€²) âŠ
    (âˆƒâ‚‚ Î» tâ€² tâ€³ â†’ t PE.â‰¡ prodË¢ p tâ€² tâ€³ Ã— u PE.â‰¡ tâ€²)
  inv-â‡’-fst (conv d _)             = inv-â‡’-fst d
  inv-â‡’-fst (fst-subst _ d)        = injâ‚ (_ , _ , d , PE.refl)
  inv-â‡’-fst (Î£-Î²â‚ _ _ _ PE.refl _) = injâ‚‚ (_ , _ , PE.refl , PE.refl)

  -- An inversion lemma related to snd.

  inv-â‡’-snd :
    Î“ âŠ¢ snd p t â‡’ u âˆ· A â†’
    (âˆƒâ‚‚ Î» tâ€² B â†’ Î“ âŠ¢ t â‡’ tâ€² âˆ· B Ã— u PE.â‰¡ snd p tâ€²) âŠ
    (âˆƒâ‚‚ Î» tâ€² tâ€³ â†’ t PE.â‰¡ prodË¢ p tâ€² tâ€³ Ã— u PE.â‰¡ tâ€³)
  inv-â‡’-snd (conv d _)             = inv-â‡’-snd d
  inv-â‡’-snd (snd-subst _ d)        = injâ‚ (_ , _ , d , PE.refl)
  inv-â‡’-snd (Î£-Î²â‚‚ _ _ _ PE.refl _) = injâ‚‚ (_ , _ , PE.refl , PE.refl)

  -- An inversion lemma related to prodrec.

  inv-â‡’-prodrec :
    Î“ âŠ¢ prodrec r p q A t u â‡’ v âˆ· B â†’
    (âˆƒâ‚‚ Î» tâ€² C â†’ Î“ âŠ¢ t â‡’ tâ€² âˆ· C Ã— v PE.â‰¡ prodrec r p q A tâ€² u) âŠ
    (âˆƒâ‚‚ Î» tâ€² tâ€³ â†’ t PE.â‰¡ prodÊ· p tâ€² tâ€³ Ã— v PE.â‰¡ u [ tâ€² , tâ€³ ]â‚â‚€)
  inv-â‡’-prodrec (conv d _) =
    inv-â‡’-prodrec d
  inv-â‡’-prodrec (prodrec-subst _ _ d _) =
    injâ‚ (_ , _ , d , PE.refl)
  inv-â‡’-prodrec (prodrec-Î² _ _ _ _ PE.refl _) =
    injâ‚‚ (_ , _ , PE.refl , PE.refl)

  -- An inversion lemma related to natrec.

  inv-â‡’-natrec :
    Î“ âŠ¢ natrec p q r A t u v â‡’ w âˆ· B â†’
    (âˆƒâ‚‚ Î» vâ€² C â†’ Î“ âŠ¢ v â‡’ vâ€² âˆ· C Ã— w PE.â‰¡ natrec p q r A t u vâ€²) âŠ
    v PE.â‰¡ zero Ã— w PE.â‰¡ t âŠ
    (âˆƒ Î» vâ€² â†’ v PE.â‰¡ suc vâ€² Ã— w PE.â‰¡ u [ vâ€² , natrec p q r A t u vâ€² ]â‚â‚€)
  inv-â‡’-natrec (conv d _) =
    inv-â‡’-natrec d
  inv-â‡’-natrec (natrec-subst _ _ d) =
    injâ‚ (_ , _ , d , PE.refl)
  inv-â‡’-natrec (natrec-zero _ _) =
    injâ‚‚ (injâ‚ (PE.refl , PE.refl))
  inv-â‡’-natrec (natrec-suc _ _ _) =
    injâ‚‚ (injâ‚‚ (_ , PE.refl , PE.refl))

  -- An inversion lemma related to emptyrec.

  inv-â‡’-emptyrec :
    Î“ âŠ¢ emptyrec p A t â‡’ u âˆ· B â†’
    (âˆƒâ‚‚ Î» tâ€² C â†’ Î“ âŠ¢ t â‡’ tâ€² âˆ· C Ã— u PE.â‰¡ emptyrec p A tâ€²)
  inv-â‡’-emptyrec (conv d _) =
    inv-â‡’-emptyrec d
  inv-â‡’-emptyrec (emptyrec-subst _ d) =
    _ , _ , d , PE.refl

  -- An inversion lemma related to unitrec.

  inv-â‡’-unitrec :
    Î“ âŠ¢ unitrec l p q A t u â‡’ v âˆ· B â†’
    (âˆƒâ‚‚ Î» tâ€² C â†’ Î“ âŠ¢ t â‡’ tâ€² âˆ· C Ã— v PE.â‰¡ unitrec l p q A tâ€² u Ã—
     Â¬ UnitÊ·-Î·) âŠ
    t PE.â‰¡ starÊ· l Ã— v PE.â‰¡ u Ã— Â¬ UnitÊ·-Î· âŠ
    v PE.â‰¡ u Ã— UnitÊ·-Î·
  inv-â‡’-unitrec (conv d _) =
    inv-â‡’-unitrec d
  inv-â‡’-unitrec (unitrec-subst _ _ d _ no-Î·) =
    injâ‚ (_ , _ , d , PE.refl , no-Î·)
  inv-â‡’-unitrec (unitrec-Î² _ _ _ no-Î·) =
    injâ‚‚ (injâ‚ (PE.refl , PE.refl , no-Î·))
  inv-â‡’-unitrec (unitrec-Î²-Î· _ _ _ _ Î·) =
    injâ‚‚ (injâ‚‚ (PE.refl , Î·))

  -- An inversion lemma related toÂ J.

  inv-â‡’-J :
    Î“ âŠ¢ J p q A t B u v w â‡’ tâ€² âˆ· C â†’
    (âˆƒâ‚‚ Î» wâ€² D â†’ Î“ âŠ¢ w â‡’ wâ€² âˆ· D Ã— tâ€² PE.â‰¡ J p q A t B u v wâ€²) âŠ
    w PE.â‰¡ rfl Ã— tâ€² PE.â‰¡ u
  inv-â‡’-J (conv d _) =
    inv-â‡’-J d
  inv-â‡’-J (J-subst _ _ _ _ d) =
    injâ‚ (_ , _ , d , PE.refl)
  inv-â‡’-J (J-Î² _ _ _ _ _ _) =
    injâ‚‚ (PE.refl , PE.refl)

  -- An inversion lemma related toÂ K.

  inv-â‡’-K :
    Î“ âŠ¢ K p A t B u v â‡’ w âˆ· C â†’
    (âˆƒâ‚‚ Î» vâ€² D â†’ Î“ âŠ¢ v â‡’ vâ€² âˆ· D Ã— w PE.â‰¡ K p A t B u vâ€²) âŠ
    v PE.â‰¡ rfl Ã— w PE.â‰¡ u
  inv-â‡’-K (conv d _) =
    inv-â‡’-K d
  inv-â‡’-K (K-subst _ _ _ _ d _) =
    injâ‚ (_ , _ , d , PE.refl)
  inv-â‡’-K (K-Î² _ _ _ _) =
    injâ‚‚ (PE.refl , PE.refl)

  -- An inversion lemma related to []-cong.

  inv-â‡’-[]-cong :
    Î“ âŠ¢ []-cong s A t u v â‡’ w âˆ· C â†’
    (âˆƒâ‚‚ Î» vâ€² D â†’ Î“ âŠ¢ v â‡’ vâ€² âˆ· D Ã— w PE.â‰¡ []-cong s A t u vâ€²) âŠ
    v PE.â‰¡ rfl Ã— w PE.â‰¡ rfl
  inv-â‡’-[]-cong (conv d _) =
    inv-â‡’-[]-cong d
  inv-â‡’-[]-cong ([]-cong-subst _ _ _ d _) =
    injâ‚ (_ , _ , d , PE.refl)
  inv-â‡’-[]-cong ([]-cong-Î² _ _ _ _ _) =
    injâ‚‚ (PE.refl , PE.refl)

------------------------------------------------------------------------
-- Some lemmas related to the reduction relations

-- Reduction is a subset of conversion

subsetTerm : Î“ âŠ¢ t â‡’ u âˆ· A â†’ Î“ âŠ¢ t â‰¡ u âˆ· A
subsetTerm (natrec-subst z s nâ‡’nâ€²) =
  natrec-cong (refl (âŠ¢âˆ™â†’âŠ¢ (wfTerm s))) (refl z) (refl s)
    (subsetTerm nâ‡’nâ€²)
subsetTerm (natrec-zero z s) = natrec-zero z s
subsetTerm (natrec-suc z s n) = natrec-suc z s n
subsetTerm (emptyrec-subst A nâ‡’nâ€²) =
  emptyrec-cong (refl A) (subsetTerm nâ‡’nâ€²)
subsetTerm (app-subst tâ‡’u a) =
  app-cong (subsetTerm tâ‡’u) (refl a)
subsetTerm (Î²-red B t a pâ‰¡pâ€² ok) = Î²-red B t a pâ‰¡pâ€² ok
subsetTerm (conv tâ‡’u Aâ‰¡B) = conv (subsetTerm tâ‡’u) Aâ‰¡B
subsetTerm (fst-subst G x) = fst-cong G (subsetTerm x)
subsetTerm (snd-subst G x) = snd-cong G (subsetTerm x)
subsetTerm (prodrec-subst A u tâ‡’tâ€² ok) =
  prodrec-cong (refl A) (subsetTerm tâ‡’tâ€²) (refl u) ok
subsetTerm (prodrec-Î² A t tâ€² u eq ok) =
  prodrec-Î² A t tâ€² u eq ok
subsetTerm (Î£-Î²â‚ G x xâ‚ xâ‚‚ ok) = Î£-Î²â‚ G x xâ‚ xâ‚‚ ok
subsetTerm (Î£-Î²â‚‚ G x xâ‚ xâ‚‚ ok) = Î£-Î²â‚‚ G x xâ‚ xâ‚‚ ok
subsetTerm (J-subst âŠ¢t âŠ¢B âŠ¢u âŠ¢tâ€² vâ‡’vâ€²) =
  J-cong (refl (âŠ¢âˆ™â†’âŠ¢ (wf (âŠ¢âˆ™â†’âŠ¢ (wf âŠ¢B))))) âŠ¢t (refl âŠ¢t) (refl âŠ¢B)
    (refl âŠ¢u) (refl âŠ¢tâ€²) (subsetTerm vâ‡’vâ€²)
subsetTerm (K-subst âŠ¢A âŠ¢t âŠ¢B âŠ¢u vâ‡’vâ€² ok) =
  K-cong (refl âŠ¢A) âŠ¢t (refl âŠ¢t) (refl âŠ¢B) (refl âŠ¢u)
    (subsetTerm vâ‡’vâ€²) ok
subsetTerm ([]-cong-subst âŠ¢A âŠ¢t âŠ¢u vâ‡’vâ€² ok) =
  []-cong-cong (refl âŠ¢A) (refl âŠ¢t) (refl âŠ¢u) (subsetTerm vâ‡’vâ€²) ok
subsetTerm (J-Î² âŠ¢t _ tâ‰¡tâ€² âŠ¢B _ âŠ¢u) =
  trans (sym (J-cong (refl (âŠ¢âˆ™â†’âŠ¢ (wf (âŠ¢âˆ™â†’âŠ¢ (wf âŠ¢B))))) âŠ¢t (refl âŠ¢t)
                (refl âŠ¢B) (refl âŠ¢u) tâ‰¡tâ€² (refl (rflâ±¼ âŠ¢t))))
    (J-Î² âŠ¢t âŠ¢B âŠ¢u PE.refl)
subsetTerm (K-Î² âŠ¢t âŠ¢B âŠ¢u ok) = K-Î² âŠ¢t âŠ¢B âŠ¢u ok
subsetTerm ([]-cong-Î² âŠ¢A âŠ¢t _ tâ‰¡tâ€² ok) =
  trans
    ([]-cong-cong (refl âŠ¢A) (refl âŠ¢t)
       (sym tâ‰¡tâ€²)
       (conv (refl (rflâ±¼ âŠ¢t)) (Id-cong (refl âŠ¢A) (refl âŠ¢t) tâ‰¡tâ€²))
       ok)
    (conv ([]-cong-Î² âŠ¢t PE.refl ok)
       (Id-cong (refl (Erasedâ±¼ âŠ¢A)) (refl ([]â±¼ âŠ¢A âŠ¢t))
          ([]-congâ€² âŠ¢A tâ‰¡tâ€²)))
  where
  open Erased ([]-congâ†’Erased ok)
subsetTerm (unitrec-subst A u tâ‡’tâ€² ok no-Î·) =
  unitrec-cong (refl A) (subsetTerm tâ‡’tâ€²) (refl u) ok no-Î·
subsetTerm (unitrec-Î² A u okâ‚ okâ‚‚) = unitrec-Î² A u okâ‚ okâ‚‚
subsetTerm (unitrec-Î²-Î· A t u okâ‚ okâ‚‚) =
 unitrec-Î²-Î· A t u okâ‚ okâ‚‚

subset : Î“ âŠ¢ A â‡’ B â†’ Î“ âŠ¢ A â‰¡ B
subset (univ Aâ‡’B) = univ (subsetTerm Aâ‡’B)

subset*Term : Î“ âŠ¢ t â‡’* u âˆ· A â†’ Î“ âŠ¢ t â‰¡ u âˆ· A
subset*Term (id t) = refl t
subset*Term (tâ‡’tâ€² â‡¨ tâ‡’*u) = trans (subsetTerm tâ‡’tâ€²) (subset*Term tâ‡’*u)

subset* : Î“ âŠ¢ A â‡’* B â†’ Î“ âŠ¢ A â‰¡ B
subset* (id A) = refl A
subset* (Aâ‡’Aâ€² â‡¨ Aâ€²â‡’*B) = trans (subset Aâ‡’Aâ€²) (subset* Aâ€²â‡’*B)


-- Can extract left-part of a reduction

redFirstTerm :{Î“ : Con Term n} â†’ Î“ âŠ¢ t â‡’ u âˆ· A â†’ Î“ âŠ¢ t âˆ· A
redFirstTerm (conv tâ‡’u Aâ‰¡B) = conv (redFirstTerm tâ‡’u) Aâ‰¡B
redFirstTerm (app-subst tâ‡’u a) = (redFirstTerm tâ‡’u) âˆ˜â±¼ a
redFirstTerm (Î²-red B t a PE.refl ok) =
  conv (lamâ±¼ t ok) (Î Î£-cong (refl (âŠ¢âˆ™â†’âŠ¢ (wf B))) (refl B) ok) âˆ˜â±¼ a
redFirstTerm (natrec-subst z s nâ‡’nâ€²) = natrecâ±¼ z s (redFirstTerm nâ‡’nâ€²)
redFirstTerm (natrec-zero z s) = natrecâ±¼ z s (zeroâ±¼ (wfTerm z))
redFirstTerm (natrec-suc z s n) = natrecâ±¼ z s (sucâ±¼ n)
redFirstTerm (emptyrec-subst A nâ‡’nâ€²) = emptyrecâ±¼ A (redFirstTerm nâ‡’nâ€²)
redFirstTerm (fst-subst G x) = fstâ±¼ G (redFirstTerm x)
redFirstTerm (snd-subst G x) = sndâ±¼ G (redFirstTerm x)
redFirstTerm (prodrec-subst A u tâ‡’tâ€² ok) =
  prodrecâ±¼ A (redFirstTerm tâ‡’tâ€²) u ok
redFirstTerm (prodrec-Î² A t tâ€² u PE.refl ok) =
  let G = âŠ¢âˆ™â†’âŠ¢ (wfTerm u)
      F = âŠ¢âˆ™â†’âŠ¢ (wf G)
  in
  prodrecâ±¼ A (conv (prodâ±¼ G t tâ€² ok) (Î Î£-cong (refl F) (refl G) ok)) u
    ok
redFirstTerm (Î£-Î²â‚ G x xâ‚ PE.refl ok) =
  fstâ±¼ G
    (conv (prodâ±¼ G x xâ‚ ok) (Î Î£-cong (refl (âŠ¢âˆ™â†’âŠ¢ (wf G))) (refl G) ok))
redFirstTerm (Î£-Î²â‚‚ G x xâ‚ PE.refl ok) =
  sndâ±¼ G
    (conv (prodâ±¼ G x xâ‚ ok) (Î Î£-cong (refl (âŠ¢âˆ™â†’âŠ¢ (wf G))) (refl G) ok))
redFirstTerm (J-subst âŠ¢t âŠ¢B âŠ¢u âŠ¢tâ€² vâ‡’vâ€²) =
  Jâ±¼ âŠ¢t âŠ¢B âŠ¢u âŠ¢tâ€² (redFirstTerm vâ‡’vâ€²)
redFirstTerm (K-subst _ âŠ¢t âŠ¢B âŠ¢u vâ‡’vâ€² ok) =
  Kâ±¼ âŠ¢t âŠ¢B âŠ¢u (redFirstTerm vâ‡’vâ€²) ok
redFirstTerm ([]-cong-subst _ âŠ¢t âŠ¢u vâ‡’vâ€² ok) =
  []-congâ±¼ âŠ¢t âŠ¢u (redFirstTerm vâ‡’vâ€²) ok
redFirstTerm (J-Î² âŠ¢t âŠ¢tâ€² tâ‰¡tâ€² âŠ¢B Bâ‰¡B âŠ¢u) =
  conv (Jâ±¼ âŠ¢t âŠ¢B âŠ¢u âŠ¢tâ€²
          (conv (rflâ±¼ âŠ¢t)
             (Id-cong (refl (âŠ¢âˆ™â†’âŠ¢ (wf (âŠ¢âˆ™â†’âŠ¢ (wf âŠ¢B))))) (refl âŠ¢t)
                tâ‰¡tâ€²)))
    (sym Bâ‰¡B)
redFirstTerm (K-Î² âŠ¢t âŠ¢B âŠ¢u ok) =
  Kâ±¼ âŠ¢t âŠ¢B âŠ¢u (rflâ±¼ âŠ¢t) ok
redFirstTerm ([]-cong-Î² âŠ¢A âŠ¢t âŠ¢tâ€² tâ‰¡tâ€² ok) =
  []-congâ±¼ âŠ¢t âŠ¢tâ€² (conv (rflâ±¼ âŠ¢t) (Id-cong (refl âŠ¢A) (refl âŠ¢t) tâ‰¡tâ€²)) ok
redFirstTerm (unitrec-subst A u tâ‡’tâ€² ok _) =
  unitrecâ±¼ A (redFirstTerm tâ‡’tâ€²) u ok
redFirstTerm (unitrec-Î² A u ok _) =
  unitrecâ±¼ A (starâ±¼ (wfTerm u) ok) u ok
redFirstTerm (unitrec-Î²-Î· A t u ok _) =
  unitrecâ±¼ A t u ok

redFirst :{Î“ : Con Term n} â†’ Î“ âŠ¢ A â‡’ B â†’ Î“ âŠ¢ A
redFirst (univ Aâ‡’B) = univ (redFirstTerm Aâ‡’B)

redFirst*Term : {Î“ : Con Term n} â†’ Î“ âŠ¢ t â‡’* u âˆ· A â†’ Î“ âŠ¢ t âˆ· A
redFirst*Term (id t) = t
redFirst*Term (tâ‡’tâ€² â‡¨ tâ€²â‡’*u) = redFirstTerm tâ‡’tâ€²

redFirst* : {Î“ : Con Term n} â†’ Î“ âŠ¢ A â‡’* B â†’ Î“ âŠ¢ A
redFirst* (id A) = A
redFirst* (Aâ‡’Aâ€² â‡¨ Aâ€²â‡’*B) = redFirst Aâ‡’Aâ€²

opaque

  -- Neutral terms do not reduce.

  neRedTerm : Î“ âŠ¢ t â‡’ u âˆ· A â†’ Neutral t â†’ âŠ¥
  neRedTerm = Î» where
    (conv d _)                â†’ neRedTerm d
    (app-subst d _)           â†’ neRedTerm d âˆ˜â†’ inv-ne-âˆ˜
    (Î²-red _ _ _ _ _)         â†’ (Î» ()) âˆ˜â†’ inv-ne-âˆ˜
    (natrec-subst _ _ d)      â†’ neRedTerm d âˆ˜â†’ inv-ne-natrec
    (natrec-zero _ _)         â†’ (Î» ()) âˆ˜â†’ inv-ne-natrec
    (natrec-suc _ _ _)        â†’ (Î» ()) âˆ˜â†’ inv-ne-natrec
    (emptyrec-subst _ d)      â†’ neRedTerm d âˆ˜â†’ inv-ne-emptyrec
    (fst-subst _ d)           â†’ neRedTerm d âˆ˜â†’ inv-ne-fst
    (snd-subst _ d)           â†’ neRedTerm d âˆ˜â†’ inv-ne-snd
    (prodrec-subst _ _ d _)   â†’ neRedTerm d âˆ˜â†’ inv-ne-prodrec
    (prodrec-Î² _ _ _ _ _ _)   â†’ (Î» ()) âˆ˜â†’ inv-ne-prodrec
    (Î£-Î²â‚ _ _ _ _ _)          â†’ (Î» ()) âˆ˜â†’ inv-ne-fst
    (Î£-Î²â‚‚ _ _ _ _ _)          â†’ (Î» ()) âˆ˜â†’ inv-ne-snd
    (J-subst _ _ _ _ d)       â†’ neRedTerm d âˆ˜â†’ inv-ne-J
    (K-subst _ _ _ _ d _)     â†’ neRedTerm d âˆ˜â†’ inv-ne-K
    ([]-cong-subst _ _ _ d _) â†’ neRedTerm d âˆ˜â†’ inv-ne-[]-cong
    (J-Î² _ _ _ _ _ _)         â†’ (Î» ()) âˆ˜â†’ inv-ne-J
    (K-Î² _ _ _ _)             â†’ (Î» ()) âˆ˜â†’ inv-ne-K
    ([]-cong-Î² _ _ _ _ _)     â†’ (Î» ()) âˆ˜â†’ inv-ne-[]-cong
    (unitrec-subst _ _ d _ _) â†’ neRedTerm d âˆ˜â†’ projâ‚‚ âˆ˜â†’ inv-ne-unitrec
    (unitrec-Î² _ _ _ _)       â†’ (Î» ()) âˆ˜â†’ projâ‚‚ âˆ˜â†’ inv-ne-unitrec
    (unitrec-Î²-Î· _ _ _ _ ok)  â†’ (_$ ok) âˆ˜â†’ projâ‚ âˆ˜â†’ inv-ne-unitrec


neRed : (d : Î“ âŠ¢ A â‡’ B) (N : Neutral A) â†’ âŠ¥
neRed (univ x) N = neRedTerm x N

opaque

  -- WHNFs do not reduce.

  whnfRedTerm : Î“ âŠ¢ t â‡’ u âˆ· A â†’ Whnf t â†’ âŠ¥
  whnfRedTerm = Î» where
    (conv d _)                â†’ whnfRedTerm d
    (app-subst d _)           â†’ neRedTerm d âˆ˜â†’ inv-whnf-âˆ˜
    (Î²-red _ _ _ _ _)         â†’ (Î» ()) âˆ˜â†’ inv-whnf-âˆ˜
    (natrec-subst _ _ d)      â†’ neRedTerm d âˆ˜â†’ inv-whnf-natrec
    (natrec-zero _ _)         â†’ (Î» ()) âˆ˜â†’ inv-whnf-natrec
    (natrec-suc _ _ _)        â†’ (Î» ()) âˆ˜â†’ inv-whnf-natrec
    (emptyrec-subst _ d)      â†’ neRedTerm d âˆ˜â†’ inv-whnf-emptyrec
    (fst-subst _ d)           â†’ neRedTerm d âˆ˜â†’ inv-whnf-fst
    (snd-subst _ d)           â†’ neRedTerm d âˆ˜â†’ inv-whnf-snd
    (prodrec-subst _ _ d _)   â†’ neRedTerm d âˆ˜â†’ inv-whnf-prodrec
    (prodrec-Î² _ _ _ _ _ _)   â†’ (Î» ()) âˆ˜â†’ inv-whnf-prodrec
    (Î£-Î²â‚ _ _ _ _ _)          â†’ (Î» ()) âˆ˜â†’ inv-whnf-fst
    (Î£-Î²â‚‚ _ _ _ _ _)          â†’ (Î» ()) âˆ˜â†’ inv-whnf-snd
    (J-subst _ _ _ _ d)       â†’ neRedTerm d âˆ˜â†’ inv-whnf-J
    (K-subst _ _ _ _ d _)     â†’ neRedTerm d âˆ˜â†’ inv-whnf-K
    ([]-cong-subst _ _ _ d _) â†’ neRedTerm d âˆ˜â†’ inv-whnf-[]-cong
    (J-Î² _ _ _ _ _ _)         â†’ (Î» ()) âˆ˜â†’ inv-whnf-J
    (K-Î² _ _ _ _)             â†’ (Î» ()) âˆ˜â†’ inv-whnf-K
    ([]-cong-Î² _ _ _ _ _)     â†’ (Î» ()) âˆ˜â†’ inv-whnf-[]-cong
    (unitrec-subst _ _ d _ _) â†’ neRedTerm d âˆ˜â†’ projâ‚‚ âˆ˜â†’ inv-whnf-unitrec
    (unitrec-Î² _ _ _ _)       â†’ (Î» ()) âˆ˜â†’ projâ‚‚ âˆ˜â†’ inv-whnf-unitrec
    (unitrec-Î²-Î· _ _ _ _ ok)  â†’ (_$ ok) âˆ˜â†’ projâ‚ âˆ˜â†’ inv-whnf-unitrec

whnfRed : (d : Î“ âŠ¢ A â‡’ B) (w : Whnf A) â†’ âŠ¥
whnfRed (univ x) w = whnfRedTerm x w

whnfRed*Term : (d : Î“ âŠ¢ t â‡’* u âˆ· A) (w : Whnf t) â†’ t PE.â‰¡ u
whnfRed*Term (id _)  _ = PE.refl
whnfRed*Term (d â‡¨ _) w = âŠ¥-elim (whnfRedTerm d w)

whnfRed* : (d : Î“ âŠ¢ A â‡’* B) (w : Whnf A) â†’ A PE.â‰¡ B
whnfRed* (id x) w = PE.refl
whnfRed* (x â‡¨ d) w = âŠ¥-elim (whnfRed x w)

opaque

  -- Single-step reduction is deterministic.

  whrDetTerm : Î“ âŠ¢ t â‡’ u âˆ· A â†’ Î“ âŠ¢ t â‡’ uâ€² âˆ· Aâ€² â†’ u PE.â‰¡ uâ€²
  whrDetTerm = Î» where
    (conv d _) dâ€² â†’
      whrDetTerm d dâ€²
    (app-subst d _) dâ€² â†’
      case inv-â‡’-âˆ˜ dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , PE.refl)) â†’
          PE.cong (_âˆ˜âŸ¨ _ âŸ© _) (whrDetTerm d dâ€²)
        (injâ‚‚ (_ , PE.refl , _)) â†’ âŠ¥-elim (whnfRedTerm d lamâ‚™)
    (Î²-red _ _ _ _ _) dâ€² â†’
      case inv-â‡’-âˆ˜ dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , _))        â†’ âŠ¥-elim (whnfRedTerm dâ€² lamâ‚™)
        (injâ‚‚ (_ , PE.refl , PE.refl)) â†’ PE.refl
    (fst-subst _ d) dâ€² â†’
      case inv-â‡’-fst dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , PE.refl)) â†’
          PE.cong (fst _) (whrDetTerm d dâ€²)
        (injâ‚‚ (_ , _ , PE.refl , _)) â†’ âŠ¥-elim (whnfRedTerm d prodâ‚™)
    (Î£-Î²â‚ _ _ _ _ _) dâ€² â†’
      case inv-â‡’-fst dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , _)) â†’
          âŠ¥-elim (whnfRedTerm dâ€² prodâ‚™)
        (injâ‚‚ (_ , _ , PE.refl , PE.refl)) â†’ PE.refl
    (snd-subst _ d) dâ€² â†’
      case inv-â‡’-snd dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , PE.refl)) â†’
          PE.cong (snd _) (whrDetTerm d dâ€²)
        (injâ‚‚ (_ , _ , PE.refl , _)) â†’ âŠ¥-elim (whnfRedTerm d prodâ‚™)
    (Î£-Î²â‚‚ _ _ _ _ _) dâ€² â†’
      case inv-â‡’-snd dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , _)) â†’
          âŠ¥-elim (whnfRedTerm dâ€² prodâ‚™)
        (injâ‚‚ (_ , _ , PE.refl , PE.refl)) â†’ PE.refl
    (prodrec-subst _ _ d _) dâ€² â†’
      case inv-â‡’-prodrec dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , PE.refl)) â†’
          PE.cong (Î» t â†’ prodrec _ _ _ _ t _) (whrDetTerm d dâ€²)
        (injâ‚‚ (_ , _ , PE.refl , _)) â†’ âŠ¥-elim (whnfRedTerm d prodâ‚™)
    (prodrec-Î² _ _ _ _ _ _) dâ€² â†’
      case inv-â‡’-prodrec dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , _)) â†’
          âŠ¥-elim (whnfRedTerm dâ€² prodâ‚™)
        (injâ‚‚ (_ , _ , PE.refl , PE.refl)) â†’ PE.refl
    (natrec-subst _ _ d) dâ€² â†’
      case inv-â‡’-natrec dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , PE.refl)) â†’
          PE.cong (natrec _ _ _ _ _ _) (whrDetTerm d dâ€²)
        (injâ‚‚ (injâ‚ (PE.refl , _))) â†’ âŠ¥-elim (whnfRedTerm d zeroâ‚™)
        (injâ‚‚ (injâ‚‚ (_ , PE.refl , _))) â†’ âŠ¥-elim (whnfRedTerm d sucâ‚™)
    (natrec-zero _ _) dâ€² â†’
      case inv-â‡’-natrec dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , _))     â†’ âŠ¥-elim (whnfRedTerm dâ€² zeroâ‚™)
        (injâ‚‚ (injâ‚ (_ , PE.refl))) â†’ PE.refl
        (injâ‚‚ (injâ‚‚ (_ , () , _)))
    (natrec-suc _ _ _) dâ€² â†’
      case inv-â‡’-natrec dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , _)) â†’
          âŠ¥-elim (whnfRedTerm dâ€² sucâ‚™)
        (injâ‚‚ (injâ‚ (() , _)))
        (injâ‚‚ (injâ‚‚ (_ , PE.refl , PE.refl))) â†’ PE.refl
    (emptyrec-subst _ d) dâ€² â†’
      case inv-â‡’-emptyrec dâ€² of Î» where
        (_ , _ , dâ€² , PE.refl) â†’
          PE.cong (emptyrec _ _) (whrDetTerm d dâ€²)
    (unitrec-subst _ _ d _ no-Î·) dâ€² â†’
      case inv-â‡’-unitrec dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , PE.refl , _)) â†’
          PE.cong (Î» t â†’ unitrec _ _ _ _ t _) (whrDetTerm d dâ€²)
        (injâ‚‚ (injâ‚ (PE.refl , _))) â†’ âŠ¥-elim (whnfRedTerm d starâ‚™)
        (injâ‚‚ (injâ‚‚ (_ , Î·)))       â†’ âŠ¥-elim (no-Î· Î·)
    (unitrec-Î² _ _ _ no-Î·) dâ€² â†’
      case inv-â‡’-unitrec dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , _))         â†’ âŠ¥-elim (whnfRedTerm dâ€² starâ‚™)
        (injâ‚‚ (injâ‚ (_ , PE.refl , _))) â†’ PE.refl
        (injâ‚‚ (injâ‚‚ (_ , Î·)))           â†’ âŠ¥-elim (no-Î· Î·)
    (unitrec-Î²-Î· _ _ _ _ Î·) dâ€² â†’
      case inv-â‡’-unitrec dâ€² of Î» where
        (injâ‚ (_ , _ , _ , _ , no-Î·)) â†’ âŠ¥-elim (no-Î· Î·)
        (injâ‚‚ (injâ‚ (_ , _ , no-Î·)))  â†’ âŠ¥-elim (no-Î· Î·)
        (injâ‚‚ (injâ‚‚ (PE.refl , _)))   â†’ PE.refl
    (J-subst _ _ _ _ d) dâ€² â†’
      case inv-â‡’-J dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , PE.refl)) â†’
          PE.cong (J _ _ _ _ _ _ _) (whrDetTerm d dâ€²)
        (injâ‚‚ (PE.refl , _)) â†’ âŠ¥-elim (whnfRedTerm d rflâ‚™)
    (J-Î² _ _ _ _ _ _) dâ€² â†’
      case inv-â‡’-J dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , _)) â†’ âŠ¥-elim (whnfRedTerm dâ€² rflâ‚™)
        (injâ‚‚ (_ , PE.refl))    â†’ PE.refl
    (K-subst _ _ _ _ d _) dâ€² â†’
      case inv-â‡’-K dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , PE.refl)) â†’
          PE.cong (K _ _ _ _ _) (whrDetTerm d dâ€²)
        (injâ‚‚ (PE.refl , _)) â†’ âŠ¥-elim (whnfRedTerm d rflâ‚™)
    (K-Î² _ _ _ _) dâ€² â†’
      case inv-â‡’-K dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , _)) â†’ âŠ¥-elim (whnfRedTerm dâ€² rflâ‚™)
        (injâ‚‚ (_ , PE.refl))    â†’ PE.refl
    ([]-cong-subst _ _ _ d _) dâ€² â†’
      case inv-â‡’-[]-cong dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , PE.refl)) â†’
          PE.cong ([]-cong _ _ _ _) (whrDetTerm d dâ€²)
        (injâ‚‚ (PE.refl , _)) â†’ âŠ¥-elim (whnfRedTerm d rflâ‚™)
    ([]-cong-Î² _ _ _ _ _) dâ€² â†’
      case inv-â‡’-[]-cong dâ€² of Î» where
        (injâ‚ (_ , _ , dâ€² , _)) â†’ âŠ¥-elim (whnfRedTerm dâ€² rflâ‚™)
        (injâ‚‚ (_ , PE.refl))    â†’ PE.refl

whrDet : (d : Î“ âŠ¢ A â‡’ B) (dâ€² : Î“ âŠ¢ A â‡’ Bâ€²) â†’ B PE.â‰¡ Bâ€²
whrDet (univ x) (univ xâ‚) = whrDetTerm x xâ‚

opaque

  -- If A reduces to the WHNFÂ B, and A also reduces toÂ C, then C
  -- reduces toÂ B.

  whrDetâ†˜ : Î“ âŠ¢ A â†˜ B â†’ Î“ âŠ¢ A â‡’* C â†’ Î“ âŠ¢ C â‡’* B
  whrDetâ†˜ (Aâ‡’*B , _)      (id _)    = Aâ‡’*B
  whrDetâ†˜ (id _ , A-whnf) (Aâ‡’D â‡¨ _) =
    âŠ¥-elim (whnfRed Aâ‡’D A-whnf)
  whrDetâ†˜ (Aâ‡’D â‡¨ Dâ‡’*B , B-whnf) (Aâ‡’E â‡¨ Eâ‡’*C) =
    whrDetâ†˜ (PE.subst (_ âŠ¢_â‡’* _) (whrDet Aâ‡’D Aâ‡’E) Dâ‡’*B , B-whnf) Eâ‡’*C

opaque

  -- A variant of whrDetâ†˜.

  whrDet:â‡’*: : Whnf B â†’ Î“ âŠ¢ A :â‡’*: B â†’ Î“ âŠ¢ A :â‡’*: C â†’ Î“ âŠ¢ C :â‡’*: B
  whrDet:â‡’*: B-whnf [ _ , âŠ¢B , Aâ‡’*B ] [ _ , âŠ¢C , Aâ‡’*C ] =
    [ âŠ¢C , âŠ¢B , whrDetâ†˜ (Aâ‡’*B , B-whnf) Aâ‡’*C ]

whrDetâ†˜Term : (d : Î“ âŠ¢ t â†˜ u âˆ· A) (dâ€² : Î“ âŠ¢ t â‡’* uâ€² âˆ· A) â†’ Î“ âŠ¢ uâ€² â‡’* u âˆ· A
whrDetâ†˜Term (projâ‚ , projâ‚‚) (id x) = projâ‚
whrDetâ†˜Term (id x , projâ‚‚) (xâ‚ â‡¨ dâ€²) = âŠ¥-elim (whnfRedTerm xâ‚ projâ‚‚)
whrDetâ†˜Term (x â‡¨ projâ‚ , projâ‚‚) (xâ‚ â‡¨ dâ€²) =
  whrDetâ†˜Term (PE.subst (Î» xâ‚‚ â†’ _ âŠ¢ xâ‚‚ â†˜ _ âˆ· _) (whrDetTerm x xâ‚) (projâ‚ , projâ‚‚)) dâ€²

opaque

  -- A variant of whrDetâ†˜Term.

  whrDet:â‡’*:Term :
    Whnf u â†’ Î“ âŠ¢ t :â‡’*: u âˆ· A â†’ Î“ âŠ¢ t :â‡’*: v âˆ· A â†’ Î“ âŠ¢ v :â‡’*: u âˆ· A
  whrDet:â‡’*:Term u-whnf [ _ , âŠ¢u , tâ‡’*u ] [ _ , âŠ¢v , tâ‡’*v ] =
    [ âŠ¢v , âŠ¢u , whrDetâ†˜Term (tâ‡’*u , u-whnf) tâ‡’*v ]

whrDet*Term : (d : Î“ âŠ¢ t â†˜ u âˆ· A) (dâ€² : Î“ âŠ¢ t â†˜ uâ€² âˆ· Aâ€²) â†’ u PE.â‰¡ uâ€²
whrDet*Term (id x , projâ‚‚) (id xâ‚ , projâ‚„) = PE.refl
whrDet*Term (id x , projâ‚‚) (xâ‚ â‡¨ projâ‚ƒ , projâ‚„) = âŠ¥-elim (whnfRedTerm xâ‚ projâ‚‚)
whrDet*Term (x â‡¨ projâ‚ , projâ‚‚) (id xâ‚ , projâ‚„) = âŠ¥-elim (whnfRedTerm x projâ‚„)
whrDet*Term (x â‡¨ projâ‚ , projâ‚‚) (xâ‚ â‡¨ projâ‚ƒ , projâ‚„) =
  whrDet*Term (projâ‚ , projâ‚‚) (PE.subst (Î» xâ‚‚ â†’ _ âŠ¢ xâ‚‚ â†˜ _ âˆ· _)
                                    (whrDetTerm xâ‚ x) (projâ‚ƒ , projâ‚„))

whrDet* : (d : Î“ âŠ¢ A â†˜ B) (dâ€² : Î“ âŠ¢ A â†˜ Bâ€²) â†’ B PE.â‰¡ Bâ€²
whrDet* (id x , projâ‚‚) (id xâ‚ , projâ‚„) = PE.refl
whrDet* (id x , projâ‚‚) (xâ‚ â‡¨ projâ‚ƒ , projâ‚„) = âŠ¥-elim (whnfRed xâ‚ projâ‚‚)
whrDet* (x â‡¨ projâ‚ , projâ‚‚) (id xâ‚ , projâ‚„) = âŠ¥-elim (whnfRed x projâ‚„)
whrDet* (Aâ‡’Aâ€² â‡¨ Aâ€²â‡’*B , whnfB) (Aâ‡’Aâ€³ â‡¨ Aâ€³â‡’*Bâ€² , whnfBâ€²) =
  whrDet* (Aâ€²â‡’*B , whnfB) (PE.subst (Î» x â†’ _ âŠ¢ x â†˜ _)
                                     (whrDet Aâ‡’Aâ€³ Aâ‡’Aâ€²)
                                     (Aâ€³â‡’*Bâ€² , whnfBâ€²))

-- Reduction does not include Î·-expansion (for WHNFs) for unit types
-- with (or without) Î·-equality: if a WHNF t reduces to starÂ sÂ l (at
-- type UnitÂ sÂ l), then t is equal to starÂ sÂ l.

no-Î·-expansion-Unit :
  Whnf t â†’ Î“ âŠ¢ t â‡’* star s l âˆ· Unit s l â†’ t PE.â‰¡ star s l
no-Î·-expansion-Unit = flip whnfRed*Term

-- Reduction does not include Î·-expansion for strong Î£-types (for
-- WHNFs): if a WHNF t reduces to prodË¢Â pÂ uÂ v (at type
-- Î£Ë¢Â pâ€²Â ,Â qÂ â–·Â AÂ â–¹Â B), then t is equal to prodË¢Â pÂ uÂ v.

no-Î·-expansion-Î£Ë¢ :
  Whnf t â†’
  Î“ âŠ¢ t â‡’* prodË¢ p u v âˆ· Î£Ë¢ pâ€² , q â–· A â–¹ B â†’
  t PE.â‰¡ prodË¢ p u v
no-Î·-expansion-Î£Ë¢ = flip whnfRed*Term

-- Identity of syntactic reduction

idRed:*: : Î“ âŠ¢ A â†’ Î“ âŠ¢ A :â‡’*: A
idRed:*: A = [ A , A , id A ]

idRedTerm:*: : Î“ âŠ¢ t âˆ· A â†’ Î“ âŠ¢ t :â‡’*: t âˆ· A
idRedTerm:*: t = [ t , t , id t ]

------------------------------------------------------------------------
-- A lemma related to _âˆ·_âˆˆ_

detâˆˆ : âˆ€ {x} â†’ x âˆ· A âˆˆ Î“ â†’ x âˆ· B âˆˆ Î“ â†’ A PE.â‰¡ B
detâˆˆ here here = PE.refl
detâˆˆ (there x) (there y) = PE.cong wk1 (detâˆˆ x y)

------------------------------------------------------------------------
-- Some derived typing rules

opaque

  -- A typing rule for variableÂ 0.

  varâ‚€ : Î“ âŠ¢ A â†’ Î“ âˆ™ A âŠ¢ var x0 âˆ· wk1 A
  varâ‚€ âŠ¢A = var (âˆ™ âŠ¢A) here

opaque

  -- A typing rule for variableÂ 1.

  varâ‚ : Î“ âˆ™ A âŠ¢ B â†’ Î“ âˆ™ A âˆ™ B âŠ¢ var x1 âˆ· wk1 (wk1 A)
  varâ‚ âŠ¢B = var (âˆ™ âŠ¢B) (there here)

opaque

  -- A typing rule for variableÂ 2.

  varâ‚‚ : Î“ âˆ™ A âˆ™ B âŠ¢ C â†’ Î“ âˆ™ A âˆ™ B âˆ™ C âŠ¢ var x2 âˆ· wk1 (wk1 (wk1 A))
  varâ‚‚ âŠ¢C = var (âˆ™ âŠ¢C) (there (there here))

opaque

  -- A typing rule for variableÂ 3.

  varâ‚ƒ :
    Î“ âˆ™ A âˆ™ B âˆ™ C âŠ¢ D â†’
    Î“ âˆ™ A âˆ™ B âˆ™ C âˆ™ D âŠ¢ var x3 âˆ· wk1 (wk1 (wk1 (wk1 A)))
  varâ‚ƒ âŠ¢D = var (âˆ™ âŠ¢D) (there (there (there here)))

opaque

  -- A typing rule for variableÂ 4.

  varâ‚„ :
    Î“ âˆ™ A âˆ™ B âˆ™ C âˆ™ D âŠ¢ E â†’
    Î“ âˆ™ A âˆ™ B âˆ™ C âˆ™ D âˆ™ E âŠ¢ var x4 âˆ· wk1 (wk1 (wk1 (wk1 (wk1 A))))
  varâ‚„ âŠ¢E = var (âˆ™ âŠ¢E) (there (there (there (there here))))

opaque

  -- A typing rule for variableÂ 5.

  varâ‚… :
    Î“ âˆ™ A âˆ™ B âˆ™ C âˆ™ D âˆ™ E âŠ¢ F â†’
    Î“ âˆ™ A âˆ™ B âˆ™ C âˆ™ D âˆ™ E âˆ™ F âŠ¢ var x5 âˆ·
      wk1 (wk1 (wk1 (wk1 (wk1 (wk1 A)))))
  varâ‚… âŠ¢F = var (âˆ™ âŠ¢F) (there (there (there (there (there here)))))
