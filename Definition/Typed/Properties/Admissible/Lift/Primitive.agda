------------------------------------------------------------------------
-- Some admissible rules related to Lift
------------------------------------------------------------------------

-- Note that lemmas corresponding to the lemmas in this module, in
-- some cases with fewer arguments, can (at the time of writing) be
-- imported from Definition.Typed.Properties.Admissible.Lift.

open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.Typed.Properties.Admissible.Lift.Primitive
  {a} {M : Set a}
  {ùïÑ : Modality M}
  (R : Type-restrictions ùïÑ)
  where

open import Definition.Typed R
open import Definition.Typed.Properties.Admissible.Level.Primitive R
open import Definition.Typed.Properties.Admissible.U.Primitive R
open import Definition.Typed.Properties.Well-formed R
open import Definition.Typed.Substitution.Primitive.Primitive R

open import Definition.Untyped M
open import Definition.Untyped.Lift M
open import Definition.Untyped.Properties M

open import Tools.Function
import Tools.PropositionalEquality as PE

private variable
  Œì                                  : Con _ _
  A A‚ÇÅ A‚ÇÇ B B‚ÇÅ B‚ÇÇ l l‚ÇÅ l‚ÇÇ l‚ÇÉ t t‚ÇÅ t‚ÇÇ : Term _

------------------------------------------------------------------------
-- Some lemmas related to Lift

opaque

  -- A variant of Lift‚±º.

  Lift‚±º-comm :
    Œì ‚ä¢ l‚ÇÅ ‚à∑Level ‚Üí
    Œì ‚ä¢ l‚ÇÇ ‚à∑Level ‚Üí
    Œì ‚ä¢ A ‚à∑ U l‚ÇÅ ‚Üí
    Œì ‚ä¢ Lift l‚ÇÇ A ‚à∑ U (l‚ÇÇ sup·µò‚Çó l‚ÇÅ)
  Lift‚±º-comm ‚ä¢l‚ÇÅ ‚ä¢l‚ÇÇ ‚ä¢A =
    conv (Lift‚±º ‚ä¢l‚ÇÅ ‚ä¢l‚ÇÇ ‚ä¢A) (U-cong-‚ä¢‚â° (sup·µò‚Çó-comm ‚ä¢l‚ÇÅ ‚ä¢l‚ÇÇ))

opaque

  -- A variant of Lift-cong.

  Lift-cong-comm :
    Œì ‚ä¢ l‚ÇÅ ‚à∑Level ‚Üí
    Œì ‚ä¢ l‚ÇÇ ‚à∑Level ‚Üí
    Œì ‚ä¢ l‚ÇÇ ‚â° l‚ÇÉ ‚à∑Level ‚Üí
    Œì ‚ä¢ A‚ÇÅ ‚â° A‚ÇÇ ‚à∑ U l‚ÇÅ ‚Üí
    Œì ‚ä¢ Lift l‚ÇÇ A‚ÇÅ ‚â° Lift l‚ÇÉ A‚ÇÇ ‚à∑ U (l‚ÇÇ sup·µò‚Çó l‚ÇÅ)
  Lift-cong-comm ‚ä¢l‚ÇÅ ‚ä¢l‚ÇÇ l‚ÇÇ‚â°l‚ÇÉ A‚ÇÅ‚â°A‚ÇÇ =
    conv (Lift-cong ‚ä¢l‚ÇÅ ‚ä¢l‚ÇÇ l‚ÇÇ‚â°l‚ÇÉ A‚ÇÅ‚â°A‚ÇÇ)
      (U-cong-‚ä¢‚â° (sup·µò‚Çó-comm ‚ä¢l‚ÇÅ ‚ä¢l‚ÇÇ))

------------------------------------------------------------------------
-- Some lemmas related to lower‚ÇÄ

opaque
  unfolding lower‚ÇÄ

  -- A typing rule for lower‚ÇÄ.

  lower‚ÇÄType
    : Œì ‚ä¢ l ‚à∑Level
    ‚Üí Œì ‚àô A ‚ä¢ B
    ‚Üí Œì ‚àô Lift l A ‚ä¢ lower‚ÇÄ B
  lower‚ÇÄType ‚ä¢l ‚ä¢B =
    subst-‚ä¢ ‚ä¢B $
    ‚ä¢À¢ ∑‚à∑-[][]‚Üë (lower‚±º (var (‚àô Lift‚±º ‚ä¢l (‚ä¢‚àô‚Üí‚ä¢ (wf ‚ä¢B))) here))

opaque
  unfolding lower‚ÇÄ

  -- An equality rule for lower‚ÇÄ.

  lower‚ÇÄTypeEq
    : Œì ‚ä¢ l ‚à∑Level
    ‚Üí Œì ‚àô A ‚ä¢ B‚ÇÅ ‚â° B‚ÇÇ
    ‚Üí Œì ‚àô Lift l A ‚ä¢ lower‚ÇÄ B‚ÇÅ ‚â° lower‚ÇÄ B‚ÇÇ
  lower‚ÇÄTypeEq ‚ä¢l B‚ÇÅ‚â°B‚ÇÇ =
    subst-‚ä¢‚â° B‚ÇÅ‚â°B‚ÇÇ $ refl-‚ä¢À¢ ∑‚â°‚à∑ $
    ‚ä¢À¢ ∑‚à∑-[][]‚Üë (lower‚±º (var (‚àô Lift‚±º ‚ä¢l (‚ä¢‚àô‚Üí‚ä¢ (wfEq B‚ÇÅ‚â°B‚ÇÇ))) here))

opaque
  unfolding lower‚ÇÄ

  -- A typing rule for lower‚ÇÄ.

  lower‚ÇÄTerm :
    Œì ‚ä¢ l ‚à∑Level ‚Üí
    Œì ‚àô A ‚ä¢ t ‚à∑ B ‚Üí
    Œì ‚àô Lift l A ‚ä¢ lower‚ÇÄ t ‚à∑ lower‚ÇÄ B
  lower‚ÇÄTerm ‚ä¢l ‚ä¢t =
    subst-‚ä¢‚à∑ ‚ä¢t
      (‚ä¢À¢ ∑‚à∑-[][]‚Üë (lower‚±º (var (‚àô Lift‚±º ‚ä¢l (‚ä¢‚àô‚Üí‚ä¢ (wfTerm ‚ä¢t))) here)))

opaque
  unfolding lower‚ÇÄ

  -- An equality rule for lower‚ÇÄ.

  lower‚ÇÄTermEq :
    Œì ‚ä¢ l ‚à∑Level ‚Üí
    Œì ‚àô A ‚ä¢ t‚ÇÅ ‚â° t‚ÇÇ ‚à∑ B ‚Üí
    Œì ‚àô Lift l A ‚ä¢ lower‚ÇÄ t‚ÇÅ ‚â° lower‚ÇÄ t‚ÇÇ ‚à∑ lower‚ÇÄ B
  lower‚ÇÄTermEq ‚ä¢l t‚ÇÅ‚â°t‚ÇÇ =
    subst-‚ä¢‚â°‚à∑ t‚ÇÅ‚â°t‚ÇÇ
      (refl-‚ä¢À¢ ∑‚â°‚à∑ $ ‚ä¢À¢ ∑‚à∑-[][]‚Üë $
       lower‚±º (var (‚àô Lift‚±º ‚ä¢l (‚ä¢‚àô‚Üí‚ä¢ (wfEqTerm t‚ÇÅ‚â°t‚ÇÇ))) here))

opaque
  unfolding lower‚ÇÄ

  -- A typing rule involving lower‚ÇÄ, lift and _[_]‚ÇÄ.

  ‚ä¢lower‚ÇÄ[lift]‚ÇÄ :
    Œì ‚àô A ‚ä¢ B ‚Üí
    Œì ‚ä¢ t ‚à∑ A ‚Üí
    Œì ‚ä¢ lower‚ÇÄ B [ lift t ]‚ÇÄ
  ‚ä¢lower‚ÇÄ[lift]‚ÇÄ {B} ‚ä¢B ‚ä¢t =
    let ‚ä¢A = ‚ä¢‚àô‚Üí‚ä¢ (wf ‚ä¢B) in
    PE.subst (_‚ä¢_ _) (PE.sym ([]‚Üë-[]‚ÇÄ B)) $
    substType ‚ä¢B (lower‚±º (lift‚±º (‚ä¢zero·µò (wf ‚ä¢A)) ‚ä¢A ‚ä¢t))

opaque
  unfolding lower‚ÇÄ

  -- An equality rule involving lower‚ÇÄ, lift and _[_]‚ÇÄ.

  lower‚ÇÄ[lift]‚ÇÄ :
    Œì ‚àô A ‚ä¢ B ‚Üí
    Œì ‚ä¢ t ‚à∑ A ‚Üí
    Œì ‚ä¢ lower‚ÇÄ B [ lift t ]‚ÇÄ ‚â° B [ t ]‚ÇÄ
  lower‚ÇÄ[lift]‚ÇÄ {B} ‚ä¢B ‚ä¢t =
    let ‚ä¢A = ‚ä¢‚àô‚Üí‚ä¢ (wf ‚ä¢B) in
    PE.subst‚ÇÇ (_‚ä¢_‚â°_ _) (PE.sym ([]‚Üë-[]‚ÇÄ B)) PE.refl $
    subst-‚ä¢‚â° (refl ‚ä¢B) $
    ‚ä¢À¢ ∑‚â°‚à∑-sgSubst (lower‚±º (lift‚±º (‚ä¢zero·µò (wf ‚ä¢A)) ‚ä¢A ‚ä¢t)) ‚ä¢t
      (Lift-Œ≤ ‚ä¢A ‚ä¢t)
