------------------------------------------------------------------------
-- Admissible rules related to the unit types
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.Typed.Properties.Admissible.Unit
  {a} {M : Set a}
  {ğ•„ : Modality M}
  (TR : Type-restrictions ğ•„)
  where

open Type-restrictions TR

open import Definition.Untyped M
open import Definition.Typed TR
open import Definition.Typed.Inversion TR
open import Definition.Typed.Properties.Admissible.Equality TR
open import Definition.Typed.Properties.Admissible.Identity.Primitive TR
open import Definition.Typed.Properties.Admissible.Var TR
open import Definition.Typed.Properties.Reduction TR
open import Definition.Typed.Properties.Well-formed TR
open import Definition.Typed.Reasoning.Term TR
open import Definition.Typed.Substitution.Primitive TR
open import Definition.Typed.Syntactic TR
open import Definition.Untyped.Unit ğ•„

open import Tools.Function
open import Tools.Product
import Tools.PropositionalEquality as PE
open import Tools.Relation
open import Tools.Sum using (_âŠ_; injâ‚; injâ‚‚)

private variable
  Î“                          : Con Term _
  A Aâ‚ Aâ‚‚ t tâ€² tâ‚ tâ‚‚ u uâ‚ uâ‚‚ : Term _
  s                          : Strength
  l                          : Universe-level
  p q                        : M

------------------------------------------------------------------------
-- A definitional Î·-rule

opaque

  -- A definitional Î·-rule for unit types with Î·-equality.

  Unit-Î·-â‰¡ :
    Unit-with-Î· s â†’
    Î“ âŠ¢ t âˆ· Unit s l â†’
    Î“ âŠ¢ star s l â‰¡ t âˆ· Unit s l
  Unit-Î·-â‰¡ Î· âŠ¢t =
    Î·-unit (starâ±¼ (wfTerm âŠ¢t) (âŠ¢âˆ·Unitâ†’Unit-allowed âŠ¢t)) âŠ¢t Î·

------------------------------------------------------------------------
-- Lemmas related to unitrec

opaque

  -- A variant of unitrecâ±¼.

  unitrecâ±¼â€² :
    Î“ âˆ™ UnitÊ· l âŠ¢ A â†’
    Î“ âŠ¢ t âˆ· UnitÊ· l â†’
    Î“ âŠ¢ u âˆ· A [ starÊ· l ]â‚€ â†’
    Î“ âŠ¢ unitrec l p q A t u âˆ· A [ t ]â‚€
  unitrecâ±¼â€² âŠ¢A âŠ¢t âŠ¢u =
    unitrecâ±¼ âŠ¢A âŠ¢t âŠ¢u (âŠ¢âˆ·Unitâ†’Unit-allowed âŠ¢t)

opaque

  -- A generalisation of unitrec-cong.

  unitrec-congâ€² :
    Î“ âˆ™ UnitÊ· l âŠ¢ Aâ‚ â‰¡ Aâ‚‚ â†’
    Î“ âŠ¢ tâ‚ â‰¡ tâ‚‚ âˆ· UnitÊ· l â†’
    Î“ âŠ¢ uâ‚ â‰¡ uâ‚‚ âˆ· Aâ‚ [ starÊ· l ]â‚€ â†’
    Î“ âŠ¢ unitrec l p q Aâ‚ tâ‚ uâ‚ â‰¡ unitrec l p q Aâ‚‚ tâ‚‚ uâ‚‚ âˆ· Aâ‚ [ tâ‚ ]â‚€
  unitrec-congâ€²
    {l} {Aâ‚} {Aâ‚‚} {tâ‚} {tâ‚‚} {uâ‚} {uâ‚‚} {p} {q} Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ uâ‚â‰¡uâ‚‚ =
    case inversion-Unit $ syntacticEqTerm tâ‚â‰¡tâ‚‚ .projâ‚ of Î»
      ok â†’
    case UnitÊ·-Î·? of Î» where
      (no no-Î·) â†’
        unitrec-cong Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ uâ‚â‰¡uâ‚‚ ok no-Î·
      (yes Î·) â†’
        case syntacticEq Aâ‚â‰¡Aâ‚‚ of Î»
          (âŠ¢Aâ‚ , âŠ¢Aâ‚‚) â†’
        case syntacticEqTerm tâ‚â‰¡tâ‚‚ of Î»
          (_ , âŠ¢tâ‚ , âŠ¢tâ‚‚) â†’
        case syntacticEqTerm uâ‚â‰¡uâ‚‚ of Î»
          (_ , âŠ¢uâ‚ , âŠ¢uâ‚‚) â†’
        unitrec l p q Aâ‚ tâ‚ uâ‚  â‰¡âŸ¨ unitrec-Î²-Î· âŠ¢Aâ‚ âŠ¢tâ‚ âŠ¢uâ‚ ok Î· âŸ©âŠ¢
        uâ‚                      â‰¡âŸ¨ conv uâ‚â‰¡uâ‚‚
                                     (substTypeEq (refl âŠ¢Aâ‚) (Unit-Î·-â‰¡ (injâ‚‚ Î·) âŠ¢tâ‚)) âŸ©âŠ¢
        uâ‚‚                      â‰¡Ë˜âŸ¨ conv (unitrec-Î²-Î· âŠ¢Aâ‚‚ âŠ¢tâ‚‚ (conv âŠ¢uâ‚‚ (substTypeEq Aâ‚â‰¡Aâ‚‚ (refl (starâ±¼ (wfTerm âŠ¢tâ‚) ok)))) ok Î·)
                                      (sym (substTypeEq Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚)) âŸ©âŠ¢âˆ
        unitrec l p q Aâ‚‚ tâ‚‚ uâ‚‚  âˆ

opaque

  -- A generalisation of _âŠ¢_â‰¡_âˆ·_.unitrec-Î².

  unitrec-Î²-â‰¡ :
    Î“ âˆ™ UnitÊ· l âŠ¢ A â†’
    Î“ âŠ¢ t âˆ· A [ starÊ· l ]â‚€ â†’
    Î“ âŠ¢ unitrec l p q A (starÊ· l) t â‰¡ t âˆ· A [ starÊ· l ]â‚€
  unitrec-Î²-â‰¡ âŠ¢A âŠ¢t =
    case wf âŠ¢A of Î» {
      (âˆ™ âŠ¢Unit) â†’
    case inversion-Unit âŠ¢Unit of Î»
      Unit-ok â†’
    case UnitÊ·-Î·? of Î» where
      (yes ok) â†’
        unitrec-Î²-Î· âŠ¢A (starâ±¼ (wf âŠ¢Unit) Unit-ok) âŠ¢t Unit-ok ok
      (no not-ok) â†’
        unitrec-Î² âŠ¢A âŠ¢t Unit-ok not-ok }

opaque

  -- A generalisation of _âŠ¢_â‡’_âˆ·_.unitrec-Î².

  unitrec-Î²-â‡’ :
    Î“ âˆ™ UnitÊ· l âŠ¢ A â†’
    Î“ âŠ¢ t âˆ· A [ starÊ· l ]â‚€ â†’
    Î“ âŠ¢ unitrec l p q A (starÊ· l) t â‡’ t âˆ· A [ starÊ· l ]â‚€
  unitrec-Î²-â‡’ âŠ¢A âŠ¢t =
    case wf âŠ¢A of Î» {
      (âˆ™ âŠ¢Unit) â†’
    case inversion-Unit âŠ¢Unit of Î»
      Unit-ok â†’
    case UnitÊ·-Î·? of Î» where
      (yes ok) â†’
        unitrec-Î²-Î· âŠ¢A (starâ±¼ (wf âŠ¢Unit) Unit-ok) âŠ¢t Unit-ok ok
      (no not-ok) â†’
        unitrec-Î² âŠ¢A âŠ¢t Unit-ok not-ok }


opaque

  -- A variant of _âŠ¢_â‰¡_âˆ·_.unitrec-Î²-Î·.

  unitrec-Î²-Î·-â‰¡ :
    Î“ âˆ™ UnitÊ· l âŠ¢ A â†’
    Î“ âŠ¢ t âˆ· UnitÊ· l â†’
    Î“ âŠ¢ u âˆ· A [ starÊ· l ]â‚€ â†’
    UnitÊ·-Î· â†’
    Î“ âŠ¢ unitrec l p q A t u â‰¡ u âˆ· A [ t ]â‚€
  unitrec-Î²-Î·-â‰¡ âŠ¢A âŠ¢t âŠ¢u Î· =
    unitrec-Î²-Î· âŠ¢A âŠ¢t âŠ¢u (âŠ¢âˆ·Unitâ†’Unit-allowed âŠ¢t) Î·

opaque

  -- A variant of _âŠ¢_â‡’_âˆ·_.unitrec-Î²-Î·.

  unitrec-Î²-Î·-â‡’ :
    Î“ âˆ™ UnitÊ· l âŠ¢ A â†’
    Î“ âŠ¢ t âˆ· UnitÊ· l â†’
    Î“ âŠ¢ u âˆ· A [ starÊ· l ]â‚€ â†’
    UnitÊ·-Î· â†’
    Î“ âŠ¢ unitrec l p q A t u â‡’ u âˆ· A [ t ]â‚€
  unitrec-Î²-Î·-â‡’ âŠ¢A âŠ¢t âŠ¢u Î· =
    unitrec-Î²-Î· âŠ¢A âŠ¢t âŠ¢u (âŠ¢âˆ·Unitâ†’Unit-allowed âŠ¢t) Î·

opaque

  -- A variant of unitrec-subst

  unitrec-substâ€² :
    Î“ âˆ™ UnitÊ· l âŠ¢ A â†’
    Î“ âŠ¢ u âˆ· A [ starÊ· l ]â‚€ â†’
    Î“ âŠ¢ tâ‚ â‡’ tâ‚‚ âˆ· UnitÊ· l â†’
    Â¬ UnitÊ·-Î· â†’
    Î“ âŠ¢ unitrec l p q A tâ‚ u â‡’ unitrec l p q A tâ‚‚ u âˆ· A [ tâ‚ ]â‚€
  unitrec-substâ€² âŠ¢A âŠ¢u tâ‚â‡’tâ‚‚ =
    unitrec-subst âŠ¢A âŠ¢u tâ‚â‡’tâ‚‚ $
    inversion-Unit $ syntacticEqTerm (subsetTerm tâ‚â‡’tâ‚‚) .projâ‚

opaque

  -- A variant of unitrec-subst for _âŠ¢_â‡’*_âˆ·_.

  unitrec-subst* :
    Î“ âŠ¢ t â‡’* tâ€² âˆ· UnitÊ· l â†’
    Î“ âˆ™ UnitÊ· l âŠ¢ A â†’
    Î“ âŠ¢ u âˆ· A [ starÊ· l ]â‚€ â†’
    Â¬ UnitÊ·-Î· â†’
    Î“ âŠ¢ unitrec l p q A t u â‡’* unitrec l p q A tâ€² u âˆ· A [ t ]â‚€
  unitrec-subst* (id âŠ¢t) âŠ¢A âŠ¢u _ =
    id (unitrecâ±¼ âŠ¢A âŠ¢t âŠ¢u (âŠ¢âˆ·Unitâ†’Unit-allowed âŠ¢t))
  unitrec-subst* (tâ‡’tâ€² â‡¨ tâ€²â‡’*tâ€³) âŠ¢A âŠ¢u not-ok =
    let ok = âŠ¢âˆ·Unitâ†’Unit-allowed (redFirstTerm tâ‡’tâ€²) in
    unitrec-subst âŠ¢A âŠ¢u tâ‡’tâ€² ok not-ok â‡¨
    conv* (unitrec-subst* tâ€²â‡’*tâ€³ âŠ¢A âŠ¢u not-ok)
      (substTypeEq (refl âŠ¢A) (symâ€² (subsetTerm tâ‡’tâ€²)))

------------------------------------------------------------------------
-- Lemmas related to unitrecâŸ¨_âŸ©

opaque
  unfolding unitrecâŸ¨_âŸ©

  -- A typing rule for unitrecâŸ¨_âŸ©.

  âŠ¢unitrecâŸ¨âŸ© :
    Î“ âˆ™ Unit s l âŠ¢ A â†’
    Î“ âŠ¢ t âˆ· Unit s l â†’
    Î“ âŠ¢ u âˆ· A [ star s l ]â‚€ â†’
    Î“ âŠ¢ unitrecâŸ¨ s âŸ© l p q A t u âˆ· A [ t ]â‚€
  âŠ¢unitrecâŸ¨âŸ© {s = ğ•¨} âŠ¢A âŠ¢t âŠ¢u =
    unitrecâ±¼â€² âŠ¢A âŠ¢t âŠ¢u
  âŠ¢unitrecâŸ¨âŸ© {s = ğ•¤} âŠ¢A âŠ¢t âŠ¢u =
    conv âŠ¢u (substTypeEq (refl âŠ¢A) (Unit-Î·-â‰¡ (injâ‚ PE.refl) âŠ¢t))

opaque
  unfolding unitrecâŸ¨_âŸ©

  -- A reduction rule for unitrecâŸ¨_âŸ©.

  unitrecâŸ¨âŸ©-Î²-â‡’* :
    (s PE.â‰¡ ğ•¨ â†’ Î“ âˆ™ Unit s l âŠ¢ A) â†’
    Î“ âŠ¢ t âˆ· A [ star s l ]â‚€ â†’
    Î“ âŠ¢ unitrecâŸ¨ s âŸ© l p q A (star s l) t â‡’* t âˆ· A [ star s l ]â‚€
  unitrecâŸ¨âŸ©-Î²-â‡’* {s = ğ•¨} âŠ¢A âŠ¢t =
    redMany $ unitrec-Î²-â‡’ (âŠ¢A PE.refl) âŠ¢t
  unitrecâŸ¨âŸ©-Î²-â‡’* {s = ğ•¤} âŠ¢A âŠ¢t =
    id âŠ¢t

opaque

  -- An equality rule for unitrecâŸ¨_âŸ©.

  unitrecâŸ¨âŸ©-Î²-â‰¡ :
    (s PE.â‰¡ ğ•¨ â†’ Î“ âˆ™ Unit s l âŠ¢ A) â†’
    Î“ âŠ¢ t âˆ· A [ star s l ]â‚€ â†’
    Î“ âŠ¢ unitrecâŸ¨ s âŸ© l p q A (star s l) t â‰¡ t âˆ· A [ star s l ]â‚€
  unitrecâŸ¨âŸ©-Î²-â‰¡ âŠ¢A âŠ¢t =
    subset*Term (unitrecâŸ¨âŸ©-Î²-â‡’* âŠ¢A âŠ¢t)

opaque
  unfolding unitrecâŸ¨_âŸ©

  -- Another reduction rule for unitrecâŸ¨_âŸ©.

  unitrecâŸ¨âŸ©-subst :
    Î“ âˆ™ Unit s l âŠ¢ A â†’
    Î“ âŠ¢ u âˆ· A [ star s l ]â‚€ â†’
    Î“ âŠ¢ tâ‚ â‡’ tâ‚‚ âˆ· Unit s l â†’
    s PE.â‰¡ ğ•¤ âŠ Â¬ UnitÊ·-Î· â†’
    Î“ âŠ¢ unitrecâŸ¨ s âŸ© l p q A tâ‚ u â‡’* unitrecâŸ¨ s âŸ© l p q A tâ‚‚ u âˆ·
      A [ tâ‚ ]â‚€
  unitrecâŸ¨âŸ©-subst {s = ğ•¨} âŠ¢A âŠ¢u tâ‚â‡’tâ‚‚ (injâ‚‚ not-ok) =
    redMany $
    unitrec-subst âŠ¢A âŠ¢u tâ‚â‡’tâ‚‚
      (inversion-Unit $ syntacticEqTerm (subsetTerm tâ‚â‡’tâ‚‚) .projâ‚)
      not-ok
  unitrecâŸ¨âŸ©-subst {s = ğ•¤} {p} {q} âŠ¢A âŠ¢u tâ‚â‡’tâ‚‚ _ =
    id $
    âŠ¢unitrecâŸ¨âŸ© {p = p} {q = q} âŠ¢A
      (syntacticEqTerm (subsetTerm tâ‚â‡’tâ‚‚) .projâ‚‚ .projâ‚) âŠ¢u

opaque
  unfolding unitrecâŸ¨_âŸ©

  -- Another equality rule for unitrecâŸ¨_âŸ©.

  unitrecâŸ¨âŸ©-cong :
    Î“ âˆ™ Unit s l âŠ¢ Aâ‚ â‰¡ Aâ‚‚ â†’
    Î“ âŠ¢ tâ‚ â‰¡ tâ‚‚ âˆ· Unit s l â†’
    Î“ âŠ¢ uâ‚ â‰¡ uâ‚‚ âˆ· Aâ‚ [ star s l ]â‚€ â†’
    Î“ âŠ¢ unitrecâŸ¨ s âŸ© l p q Aâ‚ tâ‚ uâ‚ â‰¡ unitrecâŸ¨ s âŸ© l p q Aâ‚‚ tâ‚‚ uâ‚‚ âˆ·
      Aâ‚ [ tâ‚ ]â‚€
  unitrecâŸ¨âŸ©-cong {s = ğ•¨} Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ uâ‚â‰¡uâ‚‚ =
    unitrec-congâ€² Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ uâ‚â‰¡uâ‚‚
  unitrecâŸ¨âŸ©-cong {s = ğ•¤} Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ uâ‚â‰¡uâ‚‚ =
    conv uâ‚â‰¡uâ‚‚ $
    substTypeEq (refl (syntacticEq Aâ‚â‰¡Aâ‚‚ .projâ‚))
      (Unit-Î·-â‰¡ (injâ‚ PE.refl) $ syntacticEqTerm tâ‚â‰¡tâ‚‚ .projâ‚‚ .projâ‚)

------------------------------------------------------------------------
-- A lemma related to Unit-Î·

opaque
  unfolding Unit-Î·

  -- A typing rule for Unit-Î·.

  âŠ¢Unit-Î· :
    Î“ âŠ¢ t âˆ· Unit s l â†’
    Î“ âŠ¢ Unit-Î· s l p t âˆ· Id (Unit s l) (star s l) t
  âŠ¢Unit-Î· âŠ¢t =
    case syntacticTerm âŠ¢t of Î»
      âŠ¢Unit â†’
    case wf âŠ¢Unit of Î»
      âŠ¢Î“ â†’
    case inversion-Unit âŠ¢Unit of Î»
      ok â†’
    âŠ¢unitrecâŸ¨âŸ©
      (Idâ±¼â€² (starâ±¼ (âˆ™ Unitâ±¼ âŠ¢Î“ ok) ok) (varâ‚€ âŠ¢Unit))
      âŠ¢t
      (rflâ±¼ (starâ±¼ âŠ¢Î“ ok))
