------------------------------------------------------------------------
-- Admissible rules for Level
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.Typed.Properties.Admissible.Level
  {a} {M : Set a}
  {ùïÑ : Modality M}
  (R : Type-restrictions ùïÑ)
  where

open Type-restrictions R

open import Definition.Typed R
open import Definition.Typed.Inversion R
import Definition.Typed.Substitution.Primitive.Primitive R as S
open import Definition.Typed.Properties.Admissible.Equality R
open import Definition.Typed.Reasoning.Term R
open import Definition.Typed.Syntactic R

open import Definition.Untyped M
open import Definition.Untyped.Properties M

open import Tools.Fin
open import Tools.Function
open import Tools.Nat
open import Tools.Product
import Tools.PropositionalEquality as PE
open import Tools.Reasoning.PropositionalEquality

private variable
  Œì                                     : Con Term _
  A B B‚ÇÅ B‚ÇÇ l l‚ÇÅ l‚ÇÇ l‚ÇÇ‚Ä≤ t t‚ÇÅ t‚ÇÇ u u‚ÇÅ u‚ÇÇ : Term _

wf-‚ä¢‚â§ : Œì ‚ä¢ t ‚â§ u ‚à∑Level ‚Üí Œì ‚ä¢ t ‚à∑ Level √ó Œì ‚ä¢ u ‚à∑ Level
wf-‚ä¢‚â§ t‚â§u =
  let _ , ‚ä¢t‚äîu , ‚ä¢u = syntacticEqTerm t‚â§u
      ‚ä¢t , _ = inversion-max·µò ‚ä¢t‚äîu
  in ‚ä¢t , ‚ä¢u

-- The order on levels is reflexive

‚ä¢‚â§-refl : ‚àÄ {t u} ‚Üí Œì ‚ä¢ t ‚â° u ‚à∑ Level ‚Üí Œì ‚ä¢ t ‚â§ u ‚à∑Level
‚ä¢‚â§-refl t‚â°u =
  let _ , _ , ‚ä¢u = syntacticEqTerm t‚â°u
  in trans (max·µò-cong t‚â°u (refl ‚ä¢u)) (max·µò-idem ‚ä¢u)

-- The order on levels is transitive

‚ä¢‚â§-trans
  : ‚àÄ {t u v}
  ‚Üí Œì ‚ä¢ t ‚â§ u ‚à∑Level
  ‚Üí Œì ‚ä¢ u ‚â§ v ‚à∑Level
  ‚Üí Œì ‚ä¢ t ‚â§ v ‚à∑Level
‚ä¢‚â§-trans {t} {u} {v} t‚â§u u‚â§v =
  let ‚ä¢t , ‚ä¢u = wf-‚ä¢‚â§ t‚â§u
      _  , ‚ä¢v = wf-‚ä¢‚â§ u‚â§v
  in
  t max·µò v          ‚â°Àò‚ü® max·µò-cong (refl ‚ä¢t) u‚â§v ‚ü©‚ä¢
  t max·µò (u max·µò v) ‚â°Àò‚ü® max·µò-assoc ‚ä¢t ‚ä¢u ‚ä¢v ‚ü©‚ä¢
  (t max·µò u) max·µò v ‚â°‚ü® max·µò-cong t‚â§u (refl ‚ä¢v) ‚ü©‚ä¢
  u max·µò v          ‚â°‚ü® u‚â§v ‚ü©‚ä¢‚àé
  v                 ‚àé

-- The order on levels is antisymmetric

‚ä¢‚â§-antisymmetric
  : ‚àÄ {t u}
  ‚Üí Œì ‚ä¢ t ‚â§ u ‚à∑Level
  ‚Üí Œì ‚ä¢ u ‚â§ t ‚à∑Level
  ‚Üí Œì ‚ä¢ t ‚â° u ‚à∑ Level
‚ä¢‚â§-antisymmetric {t} {u} t‚â§u u‚â§t =
  let ‚ä¢t , ‚ä¢u = wf-‚ä¢‚â§ t‚â§u in
  t        ‚â°Àò‚ü® u‚â§t ‚ü©‚ä¢
  u max·µò t ‚â°‚ü® max·µò-comm ‚ä¢u ‚ä¢t ‚ü©‚ä¢
  t max·µò u ‚â°‚ü® t‚â§u ‚ü©‚ä¢‚àé
  u        ‚àé

-- A typing rule for suc·µò·µè

‚ä¢suc·µò·µè : ‚àÄ {t k} ‚Üí Œì ‚ä¢ t ‚à∑ Level ‚Üí Œì ‚ä¢ suc·µò·µè k t ‚à∑ Level
‚ä¢suc·µò·µè {k = 0} ‚ä¢t = ‚ä¢t
‚ä¢suc·µò·µè {k = 1+ k} ‚ä¢t = suc·µò‚±º (‚ä¢suc·µò·µè ‚ä¢t)

-- A variant of max·µò-sub.
--
-- This is also proved in EqualityRelation but we can't import that
-- without creating a dependency cycle...

max·µò-sub‚Ä≤
  : Œì ‚ä¢ t ‚â§ u ‚à∑Level
  ‚Üí Œì ‚ä¢ t ‚â§ suc·µò u ‚à∑Level
max·µò-sub‚Ä≤ {t} {u} t‚â§u =
  let ‚ä¢t , ‚ä¢u = wf-‚ä¢‚â§ t‚â§u in
  t max·µò suc·µò u               ‚â°Àò‚ü® max·µò-cong (refl ‚ä¢t) (trans (max·µò-suc·µò ‚ä¢t ‚ä¢u) (suc·µò-cong t‚â§u)) ‚ü©‚ä¢
  t max·µò (suc·µò t max·µò suc·µò u) ‚â°Àò‚ü® max·µò-assoc ‚ä¢t (suc·µò‚±º ‚ä¢t) (suc·µò‚±º ‚ä¢u) ‚ü©‚ä¢
  (t max·µò suc·µò t) max·µò suc·µò u ‚â°‚ü® max·µò-cong (max·µò-sub ‚ä¢t) (refl (suc·µò‚±º ‚ä¢u)) ‚ü©‚ä¢
  suc·µò t max·µò suc·µò u          ‚â°‚ü® max·µò-suc·µò ‚ä¢t ‚ä¢u ‚ü©‚ä¢
  suc·µò (t max·µò u)             ‚â°‚ü® suc·µò-cong t‚â§u ‚ü©‚ä¢‚àé
  suc·µò u                      ‚àé

-- If t ‚â§ u, then t ‚â§ suc·µò·µè k u

max·µò-sub·µè
  : ‚àÄ {t u k}
  ‚Üí Œì ‚ä¢ t ‚â§ u ‚à∑Level
  ‚Üí Œì ‚ä¢ t ‚â§ suc·µò·µè k u ‚à∑Level
max·µò-sub·µè {k = 0} t‚â§u = t‚â§u
max·µò-sub·µè {k = 1+ k} t‚â§u = max·µò-sub‚Ä≤ (max·µò-sub·µè t‚â§u)

-- If t ‚â§ u, then suc·µò t ‚â§ suc·µò u

‚â§-suc·µò
  : ‚àÄ {t u}
  ‚Üí Œì ‚ä¢ t ‚â§ u ‚à∑Level
  ‚Üí Œì ‚ä¢ suc·µò t ‚â§ suc·µò u ‚à∑Level
‚â§-suc·µò t‚â§u =
  let ‚ä¢t , ‚ä¢u = wf-‚ä¢‚â§ t‚â§u
  in trans (max·µò-suc·µò ‚ä¢t ‚ä¢u) (suc·µò-cong t‚â§u)

-- If n ‚â§ m and t ‚â§ u, then suc·µò·µè n t ‚â§ suc·µò·µè m u

‚â§-suc·µò·µè
  : ‚àÄ {t u n m}
  ‚Üí n ‚â§ m
  ‚Üí Œì ‚ä¢ t ‚â§ u ‚à∑Level
  ‚Üí Œì ‚ä¢ suc·µò·µè n t ‚â§ suc·µò·µè m u ‚à∑Level
‚â§-suc·µò·µè z‚â§n t‚â§u = max·µò-sub·µè t‚â§u
‚â§-suc·µò·µè (s‚â§s n‚â§m) t‚â§u = ‚â§-suc·µò (‚â§-suc·µò·µè n‚â§m t‚â§u)

-- A variant of max·µò-comm

max·µò-comm-assoc
  : ‚àÄ {t u v}
  ‚Üí Œì ‚ä¢ t ‚à∑ Level
  ‚Üí Œì ‚ä¢ u ‚à∑ Level
  ‚Üí Œì ‚ä¢ v ‚à∑ Level
  ‚Üí Œì ‚ä¢ t max·µò (u max·µò v) ‚â° u max·µò (t max·µò v) ‚à∑ Level
max·µò-comm-assoc ‚ä¢t ‚ä¢u ‚ä¢v =
  trans (sym‚Ä≤ (max·µò-assoc ‚ä¢t ‚ä¢u ‚ä¢v))
    (trans (max·µò-cong (max·µò-comm ‚ä¢t ‚ä¢u) (refl ‚ä¢v))
      (max·µò-assoc ‚ä¢u ‚ä¢t ‚ä¢v))
