------------------------------------------------------------------------
-- Decidability of whether terms reduce to applications of specific
-- type formers (in the absence of equality reflection, or for empty
-- contexts)
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
import Definition.Untyped
open import Graded.Modality
import Tools.PropositionalEquality as PE
open import Tools.Relation

module Definition.Typed.Decidable.Reduction
  {a} {M : Set a}
  (open Definition.Untyped M)
  {ğ•„ : Modality M}
  (R : Type-restrictions ğ•„)
  (open Type-restrictions R)
  (_â‰Ÿ_ : Decidable (PE._â‰¡_ {A = M}))
  {m} {âˆ‡ : DCon (Term 0) m}
  {n} {Î“ : Con Term n}
  â¦ƒ ok : No-equality-reflection or-empty Î“ â¦„
  where

open import Definition.Untyped.Neutral M type-variant as N
open import Definition.Untyped.Whnf M type-variant
open import Definition.Typed R
open import Definition.Typed.Properties R
open import Definition.Typed.EqRelInstance R
open import Definition.Typed.Consequences.Inequality R as I
open import Definition.Typed.Consequences.Reduction R
open import Definition.LogicalRelation R
open import Definition.LogicalRelation.Fundamental.Reducibility R
open import Definition.LogicalRelation.Properties R

open import Tools.Function
open import Tools.Product
import Tools.Relation as Dec

private
  variable
    A B : Term n
    l   : Universe-level

opaque

  -- It is decidable whether a well-formed type reduces to an
  -- application ofÂ U.

  â‡’*U? : âˆ‡ Â» Î“ âŠ¢ A â†’ Dec (âˆƒ Î» l â†’ âˆ‡ Â» Î“ âŠ¢ A â‡’* U l)
  â‡’*U? âŠ¢A =
    case whNorm âŠ¢A of Î»
      (B , B-whnf , Aâ‡’*B) â†’
    case is-U B-whnf of Î» where
      (yes (l , PE.refl)) â†’ yes (l , Aâ‡’*B)
      (no not) â†’
        no Î» (l , Aâ‡’*U) â†’
        not (_ , whrDet* (Aâ‡’*U , Uâ‚™) (Aâ‡’*B , B-whnf))
    where
    is-U : Whnf âˆ‡ B â†’ Dec (âˆƒ Î» l â†’ U l PE.â‰¡ B)
    is-U Uâ‚™        = yes (_ , PE.refl)
    is-U Î Î£â‚™       = no Î» ()
    is-U â„•â‚™        = no Î» ()
    is-U Unitâ‚™     = no Î» ()
    is-U Emptyâ‚™    = no Î» ()
    is-U Idâ‚™       = no Î» ()
    is-U lamâ‚™      = no Î» ()
    is-U zeroâ‚™     = no Î» ()
    is-U sucâ‚™      = no Î» ()
    is-U starâ‚™     = no Î» ()
    is-U prodâ‚™     = no Î» ()
    is-U rflâ‚™      = no Î» ()
    is-U (ne B-ne) = no (N.Uâ‰¢ne B-ne âˆ˜â†’ projâ‚‚)

private opaque

  -- A lemma used below.

  isÎ Î£â€² :
    âˆ‡ Â» Î“ âŠ©âŸ¨ l âŸ© A â†’ Dec (âˆƒâ‚… Î» b p q B C â†’ âˆ‡ Â» Î“ âŠ¢ A â‡’* Î Î£âŸ¨ b âŸ© p , q â–· B â–¹ C)
  isÎ Î£â€² (Uáµ£â€² _ _ Aâ‡’*U) =
    no Î» (_ , _ , _ , _ , _ , Aâ‡’*) â†’
    I.Uâ‰¢Î Î£â±¼ (trans (sym (subset* Aâ‡’*U)) (subset* Aâ‡’*))
  isÎ Î£â€² (â„•áµ£ Aâ‡’*â„•) =
    no Î» (_ , _ , _ , _ , _ , Aâ‡’*W) â†’
    I.â„•â‰¢Î Î£â±¼ (trans (sym (subset* Aâ‡’*â„•)) (subset* Aâ‡’*W))
  isÎ Î£â€² (Emptyáµ£ Aâ‡’*Empty) =
    no Î» (_ , _ , _ , _ , _ , Aâ‡’*W) â†’
    Emptyâ‰¢Î Î£â±¼ (trans (sym (subset* Aâ‡’*Empty)) (subset* Aâ‡’*W))
  isÎ Î£â€² (Unitáµ£ (Unitâ‚œ Aâ‡’*Unit _)) =
    no Î» (_ , _ , _ , _ , _ , Aâ‡’*W) â†’
    Unitâ‰¢Î Î£â±¼ (trans (sym (subset* Aâ‡’*Unit)) (subset* Aâ‡’*W))
  isÎ Î£â€² (neâ€² _ Aâ‡’*B B-ne _) =
    no Î» (_ , _ , _ , _ , _ , Aâ‡’*W) â†’
    I.Î Î£â‰¢ne B-ne (trans (sym (subset* Aâ‡’*W)) (subset* Aâ‡’*B))
  isÎ Î£â€² (Î áµ£â€² _ _ Aâ‡’*Î Î£ _ _ _ _ _) =
    yes (_ , _ , _ , _ , _ , Aâ‡’*Î Î£)
  isÎ Î£â€² (Î£áµ£â€² _ _ Aâ‡’*Î Î£ _ _ _ _ _) =
    yes (_ , _ , _ , _ , _ , Aâ‡’*Î Î£)
  isÎ Î£â€² (Idáµ£ âŠ©A) =
    no Î» (_ , _ , _ , _ , _ , Aâ‡’*Id) â†’
    I.Idâ‰¢Î Î£ $
    trans (sym (subset* (_âŠ©â‚—Id_.â‡’*Id âŠ©A))) (subset* Aâ‡’*Id)
  isÎ Î£â€² (emb â‰¤áµ˜-refl     âŠ©A) = isÎ Î£â€² âŠ©A
  isÎ Î£â€² (emb (â‰¤áµ˜-step p) âŠ©A) = isÎ Î£â€² (emb p âŠ©A)

opaque

  -- It is decidable whether a well-formed type reduces to (or does
  -- not reduce to) either a Î -type or a Î£-type.

  isÎ Î£ : âˆ‡ Â» Î“ âŠ¢ A â†’ Dec (âˆƒâ‚… Î» b p q B C â†’ âˆ‡ Â» Î“ âŠ¢ A â‡’* Î Î£âŸ¨ b âŸ© p , q â–· B â–¹ C)
  isÎ Î£ âŠ¢A = isÎ Î£â€² (reducible-âŠ© âŠ¢A .projâ‚‚)

opaque

  -- It is decidable whether a well-formed type reduces to a Î -type.

  isÎ  : âˆ‡ Â» Î“ âŠ¢ A â†’ Dec (âˆƒâ‚„ Î» p q B C â†’ âˆ‡ Â» Î“ âŠ¢ A â‡’* Î  p , q â–· B â–¹ C)
  isÎ  âŠ¢A with isÎ Î£ âŠ¢A
  â€¦ | yes (BMÎ  , rest)                   = yes rest
  â€¦ | yes (BMÎ£ _ , _ , _ , _ , _ , Aâ‡’*Î£) =
    no Î» (_ , _ , _ , _ , Aâ‡’*Î ) â†’
    Î â‰¢Î£â±¼ (trans (sym (subset* Aâ‡’*Î )) (subset* Aâ‡’*Î£))
  â€¦ | no not = no (not âˆ˜â†’ (_ ,_))

opaque

  -- It is decidable whether a well-formed type reduces to a Î£-type.

  isÎ£ : âˆ‡ Â» Î“ âŠ¢ A â†’ Dec (âˆƒâ‚… Î» s p q B C â†’ âˆ‡ Â» Î“ âŠ¢ A â‡’* Î£âŸ¨ s âŸ© p , q â–· B â–¹ C)
  isÎ£ âŠ¢A with isÎ Î£ âŠ¢A
  â€¦ | yes (BMÎ£ _ , rest)               = yes (_ , rest)
  â€¦ | yes (BMÎ  , _ , _ , _ , _ , Aâ‡’*Î ) =
    no Î» (_ , _ , _ , _ , _ , Aâ‡’*Î£) â†’
    Î â‰¢Î£â±¼ (trans (sym (subset* Aâ‡’*Î )) (subset* Aâ‡’*Î£))
  â€¦ | no not = no (not âˆ˜â†’ (_ ,_) âˆ˜â†’ projâ‚‚)

opaque

  -- It is decidable whether a well-formed type reduces to a strong
  -- Î£-type.

  isÎ£Ë¢ : âˆ‡ Â» Î“ âŠ¢ A â†’ Dec (âˆƒâ‚„ Î» p q B C â†’ âˆ‡ Â» Î“ âŠ¢ A â‡’* Î£Ë¢ p , q â–· B â–¹ C)
  isÎ£Ë¢ âŠ¢A with isÎ£ âŠ¢A
  â€¦ | yes (ğ•¤ , rest)                  = yes rest
  â€¦ | yes (ğ•¨ , _ , _ , _ , _ , Aâ‡’*Î£Ê·) =
    no Î» (_ , _ , _ , _ , Aâ‡’*Î£Ë¢) â†’
    Î£Ë¢â‰¢Î£Ê·â±¼ (trans (sym (subset* Aâ‡’*Î£Ë¢)) (subset* Aâ‡’*Î£Ê·))
  â€¦ | no not = no (not âˆ˜â†’ (_ ,_))

opaque

  -- It is decidable whether a well-formed type reduces to a weak
  -- Î£-type.

  isÎ£Ê· : âˆ‡ Â» Î“ âŠ¢ A â†’ Dec (âˆƒâ‚„ Î» p q B C â†’ âˆ‡ Â» Î“ âŠ¢ A â‡’* Î£Ê· p , q â–· B â–¹ C)
  isÎ£Ê· âŠ¢A with isÎ£ âŠ¢A
  â€¦ | yes (ğ•¨ , rest)                  = yes rest
  â€¦ | yes (ğ•¤ , _ , _ , _ , _ , Aâ‡’*Î£Ë¢) =
    no Î» (_ , _ , _ , _ , Aâ‡’*Î£Ê·) â†’
    Î£Ë¢â‰¢Î£Ê·â±¼ (trans (sym (subset* Aâ‡’*Î£Ë¢)) (subset* Aâ‡’*Î£Ê·))
  â€¦ | no not = no (not âˆ˜â†’ (_ ,_))

opaque

  -- It is decidable whether a well-formed type reduces to an identity
  -- type.

  is-Id : âˆ‡ Â» Î“ âŠ¢ A â†’ Dec (âˆƒâ‚ƒ Î» B t u â†’ âˆ‡ Â» Î“ âŠ¢ A â‡’* Id B t u)
  is-Id = helper âˆ˜â†’ projâ‚‚ âˆ˜â†’ reducible-âŠ©
    where
    helper : âˆ‡ Â» Î“ âŠ©âŸ¨ l âŸ© A â†’ Dec (âˆƒâ‚ƒ Î» B t u â†’ âˆ‡ Â» Î“ âŠ¢ A â‡’* Id B t u)
    helper (Uáµ£ âŠ©U) =
      no Î» (_ , _ , _ , Aâ‡’*Id) â†’
        Idâ‰¢U $
        trans (sym (subset* Aâ‡’*Id)) (subset* (_âŠ©â‚U_.â‡’*U âŠ©U))
    helper (â„•áµ£ Aâ‡’*â„•) =
      no Î» (_ , _ , _ , Aâ‡’*Id) â†’
        Idâ‰¢â„• (trans (sym (subset* Aâ‡’*Id)) (subset* Aâ‡’*â„•))
    helper (Emptyáµ£ Aâ‡’*Empty) =
      no Î» (_ , _ , _ , Aâ‡’*Id) â†’
        Idâ‰¢Empty (trans (sym (subset* Aâ‡’*Id)) (subset* Aâ‡’*Empty))
    helper (Unitáµ£ âŠ©Unit) =
      no Î» (_ , _ , _ , Aâ‡’*Id) â†’
        Idâ‰¢Unit $
        trans (sym (subset* Aâ‡’*Id))
          (subset* (_âŠ©UnitâŸ¨_,_âŸ©_.â‡’*-Unit âŠ©Unit))
    helper (ne âŠ©A) =
      no Î» (_ , _ , _ , Aâ‡’*Id) â†’
        I.Idâ‰¢ne neK $ trans (sym (subset* Aâ‡’*Id)) (subset* D)
      where
      open _âŠ©ne_ âŠ©A
    helper (Báµ£ (BM _ _ _) âŠ©A) =
      no Î» (_ , _ , _ , Aâ‡’*Id) â†’
        I.Idâ‰¢Î Î£ $
        trans (sym (subset* Aâ‡’*Id)) (subset* (_âŠ©â‚—BâŸ¨_âŸ©_.D âŠ©A))
    helper (Idáµ£ âŠ©A) = yes (_ , _ , _ , â‡’*Id)
      where
      open _âŠ©â‚—Id_ âŠ©A
    helper (emb â‰¤áµ˜-refl     âŠ©A) = helper âŠ©A
    helper (emb (â‰¤áµ˜-step p) âŠ©A) = helper (emb p âŠ©A)
