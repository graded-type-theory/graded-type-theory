------------------------------------------------------------------------
-- Raw terms, weakening (renaming) and substitution.
------------------------------------------------------------------------

module Definition.Untyped {a} (M : Set a) where

open import Tools.Fin
open import Tools.Function
open import Tools.Nat
open import Tools.Product
open import Tools.List
open import Tools.PropositionalEquality as PE hiding (subst)

-- Some definitions that do not depend on M are re-exported from
-- Definition.Untyped.NotParametrised.

open import Definition.Untyped.NotParametrised public

private
  variable
    n m l : Nat
    bs bsâ€² : List _
    ts tsâ€² : GenTs _ _ _

infix 30 Î Î£âŸ¨_âŸ©_,_â–·_â–¹_
infix 30 Î _,_â–·_â–¹_
infix 30 Î£_,_â–·_â–¹_
infix 30 Î£Ë¢_,_â–·_â–¹_
infix 30 Î£Ê·_,_â–·_â–¹_
infix 30 Î£âŸ¨_âŸ©_,_â–·_â–¹_
infixl 30 _âˆ˜âŸ¨_âŸ©_
infixl 30 _âˆ˜_
infix 30 âŸ¦_âŸ§_â–¹_
infixl 30 _â‚›â€¢â‚›_ _â€¢â‚›_ _â‚›â€¢_
infix 25 _[_]
infix 25 _[_]â‚€
infix 25 _[_]â†‘
infix 25 _[_,_]â‚â‚€
infix 25 _[_]â†‘Â²

-- Kinds are indexed by a list of natural numbers specifying
-- the number of sub-terms (the length of the list) and the
-- number of new variables bound by each sub-term (each element
-- in the list).

data Kind : (ns : List Nat) â†’ Set a where
  Ukind : Universe-level â†’ Kind []

  Binderkind : (b : BinderMode) (p q : M) â†’ Kind (0 âˆ· 1 âˆ· [])

  Lamkind : (p : M)   â†’ Kind (1 âˆ· [])
  Appkind : (p : M)   â†’ Kind (0 âˆ· 0 âˆ· [])

  Prodkind    : Strength â†’ (p : M) â†’ Kind (0 âˆ· 0 âˆ· [])
  Fstkind     : (p : M) â†’ Kind (0 âˆ· [])
  Sndkind     : (p : M) â†’ Kind (0 âˆ· [])
  Prodreckind : (r p q : M) â†’ Kind (1 âˆ· 0 âˆ· 2 âˆ· [])

  Natkind    : Kind []
  Zerokind   : Kind []
  Suckind    : Kind (0 âˆ· [])
  Natreckind : (p q r : M) â†’ Kind (1 âˆ· 0 âˆ· 2 âˆ· 0 âˆ· [])

  Unitkind : Strength â†’ Universe-level â†’ Kind []
  Starkind : Strength â†’ Universe-level â†’ Kind []
  Unitreckind : Universe-level â†’ (p q : M) â†’ Kind (1 âˆ· 0 âˆ· 0 âˆ· [])

  Emptykind    : Kind []
  Emptyreckind : (p : M) â†’ Kind (0 âˆ· 0 âˆ· [])

  Idkind      : Kind (0 âˆ· 0 âˆ· 0 âˆ· [])
  Reflkind    : Kind []
  Jkind       : M â†’ M â†’ Kind (0 âˆ· 0 âˆ· 2 âˆ· 0 âˆ· 0 âˆ· 0 âˆ· [])
  Kkind       : M â†’ Kind (0 âˆ· 0 âˆ· 1 âˆ· 0 âˆ· 0 âˆ· [])
  Boxcongkind : Strength â†’ Kind (0 âˆ· 0 âˆ· 0 âˆ· 0 âˆ· [])

-- The type of terms is parametrised by the number of free variables.
-- A term is either a variable (a de Bruijn index) or a generic term,
-- consisting of a kind and a list of sub-terms.
--
-- A term (gen k (nâ‚ âˆ· â€¦ âˆ· nâ‚˜)) consists of m sub-terms (possibly zero)
-- each binding náµ¢ variables.

data Term (n : Nat) : Set a where
  var : (x : Fin n) â†’ Term n
  gen : {bs : List Nat} (k : Kind bs) (ts : GenTs Term n bs) â†’ Term n

private variable
  t    : Term n
  k kâ€² : Kind _

-- The Grammar of our language.

-- We represent the expressions of our language as de Bruijn terms.
-- Variables are natural numbers interpreted as de Bruijn indices.
-- Î , lam, and natrec are binders.

-- Type constructors.
pattern U n = gen (Ukind n) []
pattern â„• = gen Natkind []
pattern Empty = gen Emptykind []
pattern Unit! = gen (Unitkind _ _) []
pattern Unit s l = gen (Unitkind s l) []
pattern UnitÊ· l = gen (Unitkind ğ•¨ l) []
pattern UnitË¢ l = gen (Unitkind ğ•¤ l) []

pattern Î Î£âŸ¨_âŸ©_,_â–·_â–¹_ b p q F G = gen (Binderkind b p q) (F âˆ·â‚œ G âˆ·â‚œ [])
pattern Î _,_â–·_â–¹_ p q F G = gen (Binderkind BMÎ  p q) (F âˆ·â‚œ G âˆ·â‚œ [])
pattern Î£Ë¢_,_â–·_â–¹_ p q F G = gen (Binderkind (BMÎ£ ğ•¤) p q) (F âˆ·â‚œ G âˆ·â‚œ [])
pattern Î£Ê·_,_â–·_â–¹_ p q F G = gen (Binderkind (BMÎ£ ğ•¨) p q) (F âˆ·â‚œ G âˆ·â‚œ [])
pattern Î£_,_â–·_â–¹_ p q F G = gen (Binderkind (BMÎ£ _) p q) (F âˆ·â‚œ G âˆ·â‚œ [])
pattern Î£âŸ¨_âŸ©_,_â–·_â–¹_ s p q F G =
  gen (Binderkind (BMÎ£ s) p q) (F âˆ·â‚œ G âˆ·â‚œ [])

pattern lam p t = gen (Lamkind p) (t âˆ·â‚œ [])
pattern _âˆ˜âŸ¨_âŸ©_ t p u = gen (Appkind p) (t âˆ·â‚œ u âˆ·â‚œ [])
pattern _âˆ˜_ t u = gen (Appkind _) (t âˆ·â‚œ u âˆ·â‚œ [])

pattern prodË¢ p t u = gen (Prodkind ğ•¤ p) (t âˆ·â‚œ u âˆ·â‚œ [])
pattern prodÊ· p t u = gen (Prodkind ğ•¨ p) (t âˆ·â‚œ u âˆ·â‚œ [])
pattern prod m p t u = gen (Prodkind m p) (t âˆ·â‚œ u âˆ·â‚œ [])
pattern prod! t u = gen (Prodkind _ _) (t âˆ·â‚œ u âˆ·â‚œ [])
pattern fst p t = gen (Fstkind p) (t âˆ·â‚œ [])
pattern snd p t = gen (Sndkind p) (t âˆ·â‚œ [])
pattern prodrec r p q A t u =
  gen (Prodreckind r p q) (A âˆ·â‚œ t âˆ·â‚œ u âˆ·â‚œ [])

pattern zero = gen Zerokind []
pattern suc t = gen Suckind (t âˆ·â‚œ [])
pattern natrec p q r A z s n =
  gen (Natreckind p q r) (A âˆ·â‚œ z âˆ·â‚œ s âˆ·â‚œ n âˆ·â‚œ [])

pattern star! = gen (Starkind _ _) []
pattern star s l = gen (Starkind s l) []
pattern starÊ· l = gen (Starkind ğ•¨ l) []
pattern starË¢ l = gen (Starkind ğ•¤ l) []
pattern unitrec l p q A t u =
  gen (Unitreckind l p q) (A âˆ·â‚œ t âˆ·â‚œ u âˆ·â‚œ [])
pattern emptyrec p A t = gen (Emptyreckind p) (A âˆ·â‚œ t âˆ·â‚œ [])

pattern Id A t u = gen Idkind (A âˆ·â‚œ t âˆ·â‚œ u âˆ·â‚œ [])
pattern rfl = gen Reflkind []
pattern J p q A t B u v w =
  gen (Jkind p q) (A âˆ·â‚œ t âˆ·â‚œ B âˆ·â‚œ u âˆ·â‚œ v âˆ·â‚œ w âˆ·â‚œ [])
pattern K p A t B u v = gen (Kkind p) (A âˆ·â‚œ t âˆ·â‚œ B âˆ·â‚œ u âˆ·â‚œ v âˆ·â‚œ [])
pattern []-cong! A t u v = gen (Boxcongkind _) (A âˆ·â‚œ t âˆ·â‚œ u âˆ·â‚œ v âˆ·â‚œ [])
pattern []-cong m A t u v = gen (Boxcongkind m) (A âˆ·â‚œ t âˆ·â‚œ u âˆ·â‚œ v âˆ·â‚œ [])
pattern []-congÊ· A t u v = gen (Boxcongkind ğ•¨) (A âˆ·â‚œ t âˆ·â‚œ u âˆ·â‚œ v âˆ·â‚œ [])
pattern []-congË¢ A t u v = gen (Boxcongkind ğ•¤) (A âˆ·â‚œ t âˆ·â‚œ u âˆ·â‚œ v âˆ·â‚œ [])


data BindingType : Set a where
  BM : BinderMode â†’ (p q : M) â†’ BindingType

pattern BÎ  p q = BM BMÎ  p q
pattern BÎ ! = BÎ  _ _
pattern BÎ£ s p q = BM (BMÎ£ s) p q
pattern BÎ£! = BÎ£ _ _ _
pattern BÎ£Ê· = BÎ£ ğ•¨ _ _
pattern BÎ£Ë¢ = BÎ£ ğ•¤ _ _

âŸ¦_âŸ§_â–¹_ : BindingType â†’ Term n â†’ Term (1+ n) â†’ Term n
âŸ¦ BM b p q âŸ§ A â–¹ B = Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B

-- Fully normalized natural numbers

data Numeral {n : Nat} : Term n â†’ Set a where
  zeroâ‚™ : Numeral zero
  sucâ‚™ : Numeral t â†’ Numeral (suc t)

-- The canonical term corresponding to the given natural number.

sucáµ : (k : Nat) â†’ Term n
sucáµ 0      = zero
sucáµ (1+ n) = suc (sucáµ n)

------------------------------------------------------------------------
-- Weakening

  -- Weakening of terms.
  -- If Î· : Î“ â‰¤ Î” and Î” âŠ¢ t : A then Î“ âŠ¢ wk Î· t : wk Î· A.

mutual
  wkGen : {m n : Nat} {bs : List Nat} (Ï : Wk m n) (c : GenTs (Term) n bs) â†’ GenTs (Term) m bs
  wkGen Ï []                 = []
  wkGen Ï (_âˆ·â‚œ_ {b = b} t c) = wk (liftn Ï b) t âˆ·â‚œ wkGen Ï c

  wk : {m n : Nat} (Ï : Wk m n) (t : Term n) â†’ Term m
  wk Ï (var x)   = var (wkVar Ï x)
  wk Ï (gen k c) = gen k (wkGen Ï c)


-- Adding one variable to the context requires wk1.
-- If Î“ âŠ¢ t : B then Î“âˆ™A âŠ¢ wk1 t : wk1 B.

wk1 : Term n â†’ Term (1+ n)
wk1 = wk (step id)

-- Two successive uses of wk1.

wk2 : Term n â†’ Term (1+ (1+ n))
wk2 = wk1 âˆ˜â†’ wk1

-- An alternative to wk2.

wkâ‚‚ : Term n â†’ Term (2+ n)
wkâ‚‚ = wk (step (step id))

-- Three successive uses of wk1.

wk3 : Term n â†’ Term (3+ n)
wk3 = wk1 âˆ˜â†’ wk2

-- An alternative to wk3.

wkâ‚ƒ : Term n â†’ Term (3+ n)
wkâ‚ƒ = wk (step (step (step id)))




------------------------------------------------------------------------
-- Substitution

-- The substitution operation tÂ [Â ÏƒÂ ] replaces the free de Bruijn indices
-- of term t by chosen terms as specified by Ïƒ.

-- The substitution Ïƒ itself is a map from FinÂ n to terms.

Subst : Nat â†’ Nat â†’ Set a
Subst m n = Fin n â†’ Term m

-- Given closed contexts âŠ¢ Î“ and âŠ¢ Î”,
-- substitutions may be typed via Î“ âŠ¢ Ïƒ : Î” meaning that
-- Î“ âŠ¢ Ïƒ(x) : (subst Ïƒ Î”)(x) for all x âˆˆ dom(Î”).
--
-- The substitution operation is then typed as follows:
-- If Î“ âŠ¢ Ïƒ : Î” and Î” âŠ¢ t : A, then Î“ âŠ¢ t [ Ïƒ ] : A [ Ïƒ ].
--
-- Although substitutions are untyped, typing helps us
-- to understand the operation on substitutions.

-- We may view Ïƒ as the finite stream Ïƒ 0, Ïƒ 1, ..., Ïƒ n

-- Extract the substitution of the first variable.
--
-- If Î“ âŠ¢ Ïƒ : Î”âˆ™A  then Î“ âŠ¢ head Ïƒ : A [ Ïƒ ].

head : Subst m (1+ n) â†’ Term m
head Ïƒ = Ïƒ x0

-- Remove the first variable instance of a substitution
-- and shift the rest to accommodate.
--
-- If Î“ âŠ¢ Ïƒ : Î”âˆ™A then Î“ âŠ¢ tail Ïƒ : Î”.

tail : Subst m (1+ n) â†’ Subst m n
tail Ïƒ x = Ïƒ (x +1)

-- Substitution of a variable.
--
-- If Î“ âŠ¢ Ïƒ : Î” then Î“ âŠ¢ substVar Ïƒ x : (subst Ïƒ Î”)(x).

substVar : (Ïƒ : Subst m n) (x : Fin n) â†’ Term m
substVar Ïƒ x = Ïƒ x

-- Identity substitution.
-- Replaces each variable by itself.
--
-- Î“ âŠ¢ idSubst : Î“.

idSubst : Subst n n
idSubst = var

-- Weaken a substitution by one.
--
-- If Î“ âŠ¢ Ïƒ : Î” then Î“âˆ™A âŠ¢ wk1Subst Ïƒ : Î”.

wk1Subst : Subst m n â†’ Subst (1+ m) n
wk1Subst Ïƒ x = wk1 (Ïƒ x)

-- An n-ary variant of wk1Subst.

wkSubst : âˆ€ k â†’ Subst m n â†’ Subst (k + m) n
wkSubst 0      = idá¶ 
wkSubst (1+ k) = wk1Subst âˆ˜â†’ wkSubst k

-- Lift a substitution.
--
-- If Î“ âŠ¢ Ïƒ : Î” then Î“âˆ™A âŠ¢ liftSubst Ïƒ : Î”âˆ™A.

liftSubst : (Ïƒ : Subst m n) â†’ Subst (1+ m) (1+ n)
liftSubst Ïƒ x0     = var x0
liftSubst Ïƒ (x +1) = wk1Subst Ïƒ x

liftSubstn : {k m : Nat} â†’ Subst k m â†’ (n : Nat) â†’ Subst (n + k) (n + m)
liftSubstn Ïƒ Nat.zero = Ïƒ
liftSubstn Ïƒ (1+ n)   = liftSubst (liftSubstn Ïƒ n)

-- A synonym of liftSubst.

_â‡‘ : Subst m n â†’ Subst (1+ m) (1+ n)
_â‡‘ = liftSubst

-- Transform a weakening into a substitution.
--
-- If Ï : Î“ â‰¤ Î” then Î“ âŠ¢ toSubst Ï : Î”.

toSubst :  Wk m n â†’ Subst m n
toSubst pr x = var (wkVar pr x)

-- Apply a substitution to a term.
--
-- If Î“ âŠ¢ Ïƒ : Î” and Î” âŠ¢ t : A then Î“ âŠ¢ t [ Ïƒ ] : A [ Ïƒ ].

mutual
  substGen : {bs : List Nat} (Ïƒ : Subst m n) (g : GenTs (Term) n bs) â†’ GenTs (Term) m bs
  substGen Ïƒ []              = []
  substGen Ïƒ (_âˆ·â‚œ_ {b} t ts) = t [ liftSubstn Ïƒ b ] âˆ·â‚œ substGen Ïƒ ts

  _[_] : (t : Term n) (Ïƒ : Subst m n) â†’ Term m
  var x [ Ïƒ ] = substVar Ïƒ x
  gen x c [ Ïƒ ] = gen x (substGen Ïƒ c)

-- Extend a substitution by adding a term as
-- the first variable substitution and shift the rest.
--
-- If Î“ âŠ¢ Ïƒ : Î” and Î“ âŠ¢ t : A [ Ïƒ ] then Î“ âŠ¢ consSubst Ïƒ t : Î”âˆ™A.

consSubst : Subst m n â†’ Term m â†’ Subst m (1+ n)
consSubst Ïƒ t  x0    = t
consSubst Ïƒ t (x +1) = Ïƒ x

-- Singleton substitution.
--
-- If Î“ âŠ¢ t : A then Î“ âŠ¢ sgSubst t : Î“âˆ™A.

sgSubst : Term n â†’ Subst n (1+ n)
sgSubst = consSubst idSubst

-- Compose two substitutions.
--
-- If Î“ âŠ¢ Ïƒ : Î” and Î” âŠ¢ Ïƒâ€² : Î¦ then Î“ âŠ¢ Ïƒ â‚›â€¢â‚› Ïƒâ€² : Î¦.

_â‚›â€¢â‚›_ : Subst l m â†’ Subst m n â†’ Subst l n
_â‚›â€¢â‚›_ Ïƒ Ïƒâ€² x = Ïƒâ€² x [ Ïƒ ]

-- Composition of weakening and substitution.
--
--  If Ï : Î“ â‰¤ Î” and Î” âŠ¢ Ïƒ : Î¦ then Î“ âŠ¢ Ï â€¢â‚› Ïƒ : Î¦.

_â€¢â‚›_ : Wk l m â†’ Subst m n â†’ Subst l n
_â€¢â‚›_ Ï Ïƒ x = wk Ï (Ïƒ x)

--  If Î“ âŠ¢ Ïƒ : Î” and Ï : Î” â‰¤ Î¦ then Î“ âŠ¢ Ïƒ â‚›â€¢ Ï : Î¦.

_â‚›â€¢_ : Subst l m â†’ Wk m n â†’ Subst l n
_â‚›â€¢_ Ïƒ Ï x = Ïƒ (wkVar Ï x)

-- Substitute the first variable of a term with an other term.
--
-- If Î“âˆ™A âŠ¢ t : B and Î“ âŠ¢ s : A then Î“ âŠ¢ t[s]â‚€ : B[s]â‚€.

_[_]â‚€ : (t : Term (1+ n)) (s : Term n) â†’ Term n
t [ s ]â‚€ = t [ sgSubst s ]

-- Substitute the first variable of a term with an other term,
-- but let the two terms share the same context.
--
-- If Î“âˆ™A âŠ¢ t : B and Î“âˆ™A âŠ¢ s : A then Î“âˆ™A âŠ¢ t[s]â†‘ : B[s]â†‘.

_[_]â†‘ : (t : Term (1+ n)) (s : Term (1+ n)) â†’ Term (1+ n)
t [ s ]â†‘ = t [ consSubst (wk1Subst idSubst) s ]


-- Substitute the first two variables of a term with other terms.
--
-- If Î“âˆ™Aâˆ™B âŠ¢ t : C, Î“ âŠ¢ s : A and Î“ âŠ¢ sâ€² : B and  then Î“ âŠ¢ t[s,sâ€²] : C[s,sâ€²]

_[_,_]â‚â‚€ : (t : Term (2+ n)) (s sâ€² : Term n) â†’ Term n
t [ s , sâ€² ]â‚â‚€ = t [ consSubst (sgSubst s) sâ€² ]

-- Substitute the first variable with a term and shift remaining variables up by one
-- If Î“ âˆ™ A âŠ¢ t : Aâ€² and Î“ âˆ™ B âˆ™ C âŠ¢ s : A then Î“ âˆ™ B âˆ™ C âŠ¢ t[s]â†‘Â² : Aâ€²

_[_]â†‘Â² : (t : Term (1+ n)) (s : Term (2+ n)) â†’ Term (2+ n)
t [ s ]â†‘Â² = t [ consSubst (wk1Subst (wk1Subst idSubst)) s ]


B-subst : (Ïƒ : Subst m n) (W : BindingType) (F : Term n) (G : Term (1+ n))
        â†’ (âŸ¦ W âŸ§ F â–¹ G) [ Ïƒ ] PE.â‰¡ âŸ¦ W âŸ§ F [ Ïƒ ] â–¹ (G [ liftSubst Ïƒ ])
B-subst Ïƒ (BÎ  p q) F G = PE.refl
B-subst Ïƒ (BÎ£ m p q) F G = PE.refl

------------------------------------------------------------------------
-- Some inversion lemmas

-- Inversion of equality for gen.

gen-congâ»Â¹ :
  gen {bs = bs} k ts â‰¡ gen {bs = bsâ€²} kâ€² tsâ€² â†’
  âˆƒ Î» (eq : bs â‰¡ bsâ€²) â†’
    PE.subst Kind eq k â‰¡ kâ€² Ã—
    PE.subst (GenTs Term _) eq ts â‰¡ tsâ€²
gen-congâ»Â¹ refl = refl , refl , refl

-- Inversion of equality for _âˆ·_.

âˆ·-congâ»Â¹ :
  âˆ€ {b} {t tâ€² : Term (b + n)} â†’
  _âˆ·â‚œ_ {A = Term} {b = b} t ts â‰¡ tâ€² âˆ·â‚œ tsâ€² â†’
  t â‰¡ tâ€² Ã— ts â‰¡ tsâ€²
âˆ·-congâ»Â¹ refl = refl , refl
