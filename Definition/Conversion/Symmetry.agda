------------------------------------------------------------------------
-- The algorithmic equality is symmetric (in the absence of equality
-- reflection)
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.Conversion.Symmetry
  {a} {M : Set a}
  {ğ•„ : Modality M}
  (R : Type-restrictions ğ•„)
  (open Type-restrictions R)
  â¦ƒ no-equality-reflection : No-equality-reflection â¦„
  where

open import Definition.Untyped M
open import Definition.Untyped.Neutral M type-variant
open import Definition.Typed R
open import Definition.Typed.EqRelInstance R using (eqRelInstance)
open import Definition.Typed.EqualityRelation.Instance R
open import Definition.Typed.Inversion R
open import Definition.Typed.Properties R
open import Definition.Typed.Stability R
open import Definition.Typed.Substitution R
open import Definition.Typed.Syntactic R
open import Definition.Typed.Weakening R as W hiding (wk)
open import Definition.Conversion R
open import Definition.Conversion.Soundness R
open import Definition.Conversion.Conversion R
open import Definition.Conversion.Stability R
open import Definition.Conversion.Whnf R
open import Definition.Typed.Consequences.Equality R
open import Definition.Typed.Consequences.Reduction R
open import Definition.Typed.Consequences.Injectivity R
open import Definition.Typed.Consequences.NeTypeEq R

open import Tools.Bool
open import Tools.Function
open import Tools.List hiding (_âˆ·_)
open import Tools.Nat
open import Tools.Product
import Tools.PropositionalEquality as PE

private
  variable
    n : Nat
    Î“ Î” : Con Term n
    d : Bool

mutual
  -- Symmetry of algorithmic equality of neutrals.
  sym~â†‘ : âˆ€ {t u A} â†’ âŠ¢ Î“ â‰¡ Î”
        â†’ Î“ âŠ¢ t ~ u â†‘ A
        â†’ âˆƒ Î» B â†’ Î“ âŠ¢ A â‰¡ B Ã— Î” âŠ¢ u ~ t â†‘ B
  sym~â†‘ Î“â‰¡Î” (var-refl x xâ‰¡y) =
    let âŠ¢A = syntacticTerm x
    in  _ , refl âŠ¢A
     ,  var-refl (PE.subst (Î» y â†’ _ âŠ¢ var y âˆ· _) xâ‰¡y (stabilityTerm Î“â‰¡Î” x))
                 (PE.sym xâ‰¡y)
  sym~â†‘ Î“â‰¡Î” (lower-cong tâ‚~tâ‚‚) =
    case sym~â†“ Î“â‰¡Î” tâ‚~tâ‚‚ of Î»
      (C , whnfB , Aâ‰¡B , tâ‚‚~tâ‚) â†’
    case Liftâ‰¡A Aâ‰¡B whnfB of Î» {
      (k , D , PE.refl) â†’
    let _ , Aâ‰¡D = Lift-injectivity Aâ‰¡B
    in _ , Aâ‰¡D , lower-cong tâ‚‚~tâ‚ }
  sym~â†‘ Î“â‰¡Î” (app-cong t~u x) =
    case contextConvSubst Î“â‰¡Î” of Î» {
      (âŠ¢Î“ , âŠ¢Î” , _) â†’
    case sym~â†“ Î“â‰¡Î” t~u of Î» {
      (B , whnfB , Aâ‰¡B , u~t) â†’
    case Î â‰¡A Aâ‰¡B whnfB of Î» {
      (Fâ€² , Gâ€² , Î Fâ€²Gâ€²â‰¡B) â†’
    case Î Î£-injectivity (PE.subst (Î» x â†’ _ âŠ¢ _ â‰¡ x) Î Fâ€²Gâ€²â‰¡B Aâ‰¡B) of Î» {
      (Fâ‰¡Fâ€² , Gâ‰¡Gâ€² , _ , _) â†’
    _ , Gâ‰¡Gâ€² (soundnessConvâ†‘Term x) ,
    app-cong (PE.subst (Î» x â†’ _ âŠ¢ _ ~ _ â†“ x) Î Fâ€²Gâ€²â‰¡B u~t)
      (convConvâ†‘Term (stabilityEq Î“â‰¡Î” Fâ‰¡Fâ€²) (symConvâ†‘Term Î“â‰¡Î” x)) }}}}
  sym~â†‘ Î“â‰¡Î” (fst-cong p~r) =
    case sym~â†“ Î“â‰¡Î” p~r of Î» (B , whnfB , Aâ‰¡B , r~p) â†’
    case Î£â‰¡A Aâ‰¡B whnfB of Î» where
      (Fâ€² , Gâ€² , PE.refl) â†’
        case Î Î£-injectivity Aâ‰¡B of Î» where
          (Fâ‰¡ , Gâ‰¡ , _ , _) â†’
            Fâ€² , Fâ‰¡ , fst-cong r~p
  sym~â†‘ Î“â‰¡Î” (snd-cong p~r) =
    case sym~â†“ Î“â‰¡Î” p~r of Î» (B , whnfB , Aâ‰¡B , r~p) â†’
    case Î£â‰¡A Aâ‰¡B whnfB of Î» where
      (Fâ€² , Gâ€² , PE.refl) â†’
        case Î Î£-injectivity Aâ‰¡B of Î» where
          (Fâ‰¡ , Gâ‰¡ , _ , _) â†’
            let fstâ‰¡ = soundness~â†‘ (fst-cong p~r) in
            _ , Gâ‰¡ fstâ‰¡ , snd-cong r~p
  sym~â†‘ Î“â‰¡Î” (natrec-cong x xâ‚ xâ‚‚ t~u) =
    let âŠ¢Î“ , âŠ¢Î” , _ = contextConvSubst Î“â‰¡Î”
        B , whnfB , Aâ‰¡B , u~t = sym~â†“ Î“â‰¡Î” t~u
        Bâ‰¡â„• = â„•â‰¡A Aâ‰¡B whnfB
        Fâ‰¡G = stabilityEq (Î“â‰¡Î” âˆ™ refl (â„•â±¼ âŠ¢Î“)) (soundnessConvâ†‘ x)
        F[0]â‰¡G[0] = substTypeEq Fâ‰¡G (refl (zeroâ±¼ âŠ¢Î”))
    in  _ , substTypeEq (soundnessConvâ†‘ x) (soundness~â†“ t~u)
    ,   natrec-cong
          (symConvâ†‘ (Î“â‰¡Î” âˆ™ refl (â„•â±¼ âŠ¢Î“)) x)
          (convConvâ†‘Term F[0]â‰¡G[0] (symConvâ†‘Term Î“â‰¡Î” xâ‚))
          (convConvâ†‘Term (sucCongâ€² Fâ‰¡G)
             (symConvâ†‘Term (Î“â‰¡Î” âˆ™ refl (â„•â±¼ âŠ¢Î“) âˆ™ soundnessConvâ†‘ x) xâ‚‚))
          (PE.subst (_âŠ¢_~_â†“_ _ _ _) Bâ‰¡â„• u~t)
  sym~â†‘ {Î“ = Î“} {Î” = Î”} Î“â‰¡Î”
    (prodrec-cong {F = F} {G = G} Câ†‘E g~h uâ†‘v) =
    case sym~â†“ Î“â‰¡Î” g~h of Î» (B , whnfB , âŠ¢Î£â‰¡B , h~g) â†’
    case Î£â‰¡A âŠ¢Î£â‰¡B whnfB of Î» where
      (Fâ€² , Gâ€² , PE.refl) â†’
        case Î Î£-injectivity-no-equality-reflection
               (stabilityEq Î“â‰¡Î” âŠ¢Î£â‰¡B) of Î» where
          (âŠ¢Fâ‰¡Fâ€² , âŠ¢Gâ‰¡Gâ€² , _ , _ , _) â†’
            let gâ‰¡h = soundness~â†“ g~h
                Câ‰¡E = soundnessConvâ†‘ Câ†‘E
                âŠ¢Î£ , _ = syntacticEqTerm gâ‰¡h
                âŠ¢F , âŠ¢G , _ = inversion-Î Î£ âŠ¢Î£
                Eâ†‘C = symConvâ†‘ (Î“â‰¡Î” âˆ™ âŠ¢Î£â‰¡B) Câ†‘E
                vâ†‘u = symConvâ†‘Term (Î“â‰¡Î” âˆ™ refl âŠ¢F âˆ™ refl âŠ¢G) uâ†‘v
                âŠ¢Î“ , âŠ¢Î” , âŠ¢idsubst = contextConvSubst Î“â‰¡Î”
                âŠ¢Fâ€² = stability Î“â‰¡Î” âŠ¢F
                âŠ¢Gâ€² = stability (Î“â‰¡Î” âˆ™ refl âŠ¢F) âŠ¢G
                âŠ¢ÏF = W.wk (stepÊ· (step id) âŠ¢Gâ€²) âŠ¢Fâ€²
                âŠ¢ÏG = W.wk (liftÊ· (step (step id)) âŠ¢ÏF) âŠ¢Gâ€²
                Câ‚Šâ‰¡Eâ‚Š = substâ†‘Â²TypeEq-prod
                          (stabilityEq (Î“â‰¡Î” âˆ™ refl âŠ¢Î£) Câ‰¡E)
            in  _ , substTypeEq Câ‰¡E gâ‰¡h
              , prodrec-cong Eâ†‘C h~g
                  (convConvâ†‘Termâ€² (refl-âˆ™ âŠ¢Fâ‰¡Fâ€² âˆ™ âŠ¢Gâ‰¡Gâ€²)
                     Câ‚Šâ‰¡Eâ‚Š vâ†‘u)
  sym~â†‘ Î“â‰¡Î” (emptyrec-cong x t~u) =
    let âŠ¢Î“ , âŠ¢Î” , _ = contextConvSubst Î“â‰¡Î”
        B , whnfB , Aâ‰¡B , u~t = sym~â†“ Î“â‰¡Î” t~u
        Bâ‰¡Empty = Emptyâ‰¡A Aâ‰¡B whnfB
        Fâ‰¡G = stabilityEq Î“â‰¡Î” (soundnessConvâ†‘ x)
    in  _ , soundnessConvâ†‘ x
    , emptyrec-cong (symConvâ†‘ Î“â‰¡Î” x)
                    (PE.subst (Î» xâ‚ â†’ _ âŠ¢ _ ~ _ â†“ xâ‚) Bâ‰¡Empty u~t)
  sym~â†‘ Î“â‰¡Î” (unitrec-cong F<>H k~l u<>v no-Î·) =
    let kâ‰¡l = soundness~âˆ· k~l
        âŠ¢Unit = projâ‚ (syntacticEqTerm kâ‰¡l)
        ok = inversion-Unit âŠ¢Unit
        Unitâ‰¡Unit = refl âŠ¢Unit
        H<>F = symConvâ†‘ (Î“â‰¡Î” âˆ™ Unitâ‰¡Unit) F<>H
        âŠ¢Î“ , âŠ¢Î” , _ = contextConvSubst Î“â‰¡Î”
        l~k = sym~âˆ· Î“â‰¡Î” k~l
        v<>u = symConvâ†‘Term Î“â‰¡Î” u<>v
        âŠ¢Fâ‰¡H = soundnessConvâ†‘ F<>H
        âŠ¢Fâ‚Šâ‰¡Hâ‚Š = substTypeEq âŠ¢Fâ‰¡H (refl (starâ±¼ âŠ¢Î“ ok))
        âŠ¢Fkâ‰¡Hl = substTypeEq âŠ¢Fâ‰¡H kâ‰¡l
        v<>uâ€² = convConvâ†‘Term (stabilityEq Î“â‰¡Î” âŠ¢Fâ‚Šâ‰¡Hâ‚Š) v<>u
    in _ , âŠ¢Fkâ‰¡Hl , unitrec-cong H<>F l~k v<>uâ€² no-Î·
  sym~â†‘ Î“â‰¡Î” (J-cong Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ Bâ‚â‰¡Bâ‚‚ uâ‚â‰¡uâ‚‚ vâ‚â‰¡vâ‚‚ wâ‚~wâ‚‚ Câ‰¡Id-tâ‚-vâ‚) =
    case sym~â†“ Î“â‰¡Î” wâ‚~wâ‚‚ of Î» {
      (_ , _ , Câ‰¡D , wâ‚‚~wâ‚) â†’
    case soundnessConvâ†‘ Aâ‚â‰¡Aâ‚‚ of Î» {
      âŠ¢Aâ‚â‰¡Aâ‚‚ â†’
    case soundnessConvâ†‘ Bâ‚â‰¡Bâ‚‚ of Î» {
      âŠ¢Bâ‚â‰¡Bâ‚‚ â†’
    case soundnessConvâ†‘Term tâ‚â‰¡tâ‚‚ of Î» {
      âŠ¢tâ‚â‰¡tâ‚‚ â†’
    case soundnessConvâ†‘Term vâ‚â‰¡vâ‚‚ of Î» {
      âŠ¢vâ‚â‰¡vâ‚‚ â†’
    case reflConEq (wfEq âŠ¢Aâ‚â‰¡Aâ‚‚) of Î» {
      Î“â‰¡Î“ â†’
      _
    , J-result-cong âŠ¢Bâ‚â‰¡Bâ‚‚ âŠ¢vâ‚â‰¡vâ‚‚ (conv (soundness~â†“ wâ‚~wâ‚‚) Câ‰¡Id-tâ‚-vâ‚)
    , J-cong (symConvâ†‘ Î“â‰¡Î” Aâ‚â‰¡Aâ‚‚)
        (convConvâ†‘Termâ€² Î“â‰¡Î” âŠ¢Aâ‚â‰¡Aâ‚‚ (symConvâ†‘Term Î“â‰¡Î“ tâ‚â‰¡tâ‚‚))
        (symConvâ†‘ (J-motive-context-cong Î“â‰¡Î” âŠ¢Aâ‚â‰¡Aâ‚‚ âŠ¢tâ‚â‰¡tâ‚‚) Bâ‚â‰¡Bâ‚‚)
        (convConvâ†‘Termâ€² Î“â‰¡Î” (J-motive-rfl-cong âŠ¢Bâ‚â‰¡Bâ‚‚ âŠ¢tâ‚â‰¡tâ‚‚)
           (symConvâ†‘Term Î“â‰¡Î“ uâ‚â‰¡uâ‚‚))
        (convConvâ†‘Termâ€² Î“â‰¡Î” âŠ¢Aâ‚â‰¡Aâ‚‚ (symConvâ†‘Term Î“â‰¡Î“ vâ‚â‰¡vâ‚‚)) wâ‚‚~wâ‚
        (stabilityEq Î“â‰¡Î” $
         trans (trans (sym Câ‰¡D) Câ‰¡Id-tâ‚-vâ‚)
           (Id-cong âŠ¢Aâ‚â‰¡Aâ‚‚ âŠ¢tâ‚â‰¡tâ‚‚ âŠ¢vâ‚â‰¡vâ‚‚)) }}}}}}
  sym~â†‘ Î“â‰¡Î” (K-cong Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ Bâ‚â‰¡Bâ‚‚ uâ‚â‰¡uâ‚‚ vâ‚~vâ‚‚ Câ‰¡Id-tâ‚-tâ‚ ok) =
    case sym~â†“ Î“â‰¡Î” vâ‚~vâ‚‚ of Î» {
      (_ , _ , Câ‰¡D , vâ‚‚~vâ‚) â†’
    case soundnessConvâ†‘ Aâ‚â‰¡Aâ‚‚ of Î» {
      âŠ¢Aâ‚â‰¡Aâ‚‚ â†’
    case soundnessConvâ†‘ Bâ‚â‰¡Bâ‚‚ of Î» {
      âŠ¢Bâ‚â‰¡Bâ‚‚ â†’
    case soundnessConvâ†‘Term tâ‚â‰¡tâ‚‚ of Î» {
      âŠ¢tâ‚â‰¡tâ‚‚ â†’
    case reflConEq (wfEq âŠ¢Aâ‚â‰¡Aâ‚‚) of Î» {
      Î“â‰¡Î“ â†’
      _
    , substTypeEq âŠ¢Bâ‚â‰¡Bâ‚‚
        (conv (soundness~â†“ vâ‚~vâ‚‚) Câ‰¡Id-tâ‚-tâ‚)
    , K-cong (symConvâ†‘ Î“â‰¡Î” Aâ‚â‰¡Aâ‚‚)
        (convConvâ†‘Termâ€² Î“â‰¡Î” âŠ¢Aâ‚â‰¡Aâ‚‚ (symConvâ†‘Term Î“â‰¡Î“ tâ‚â‰¡tâ‚‚))
        (symConvâ†‘ (K-motive-context-cong Î“â‰¡Î” âŠ¢Aâ‚â‰¡Aâ‚‚ âŠ¢tâ‚â‰¡tâ‚‚) Bâ‚â‰¡Bâ‚‚)
        (convConvâ†‘Termâ€² Î“â‰¡Î” (K-motive-rfl-cong âŠ¢Bâ‚â‰¡Bâ‚‚)
           (symConvâ†‘Term Î“â‰¡Î“ uâ‚â‰¡uâ‚‚))
        vâ‚‚~vâ‚
        (stabilityEq Î“â‰¡Î” $
         trans (trans (sym Câ‰¡D) Câ‰¡Id-tâ‚-tâ‚)
           (Id-cong âŠ¢Aâ‚â‰¡Aâ‚‚ âŠ¢tâ‚â‰¡tâ‚‚ âŠ¢tâ‚â‰¡tâ‚‚))
        ok }}}}}
  sym~â†‘ Î“â‰¡Î” ([]-cong-cong Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ uâ‚â‰¡uâ‚‚ vâ‚~vâ‚‚ Bâ‰¡Id-tâ‚-uâ‚ ok) =
    case sym~â†“ Î“â‰¡Î” vâ‚~vâ‚‚ of Î» {
      (_ , _ , Bâ‰¡C , vâ‚‚~vâ‚) â†’
    case soundnessConvâ†‘ Aâ‚â‰¡Aâ‚‚ of Î» {
      âŠ¢Aâ‚â‰¡Aâ‚‚ â†’
    case soundnessConvâ†‘Term tâ‚â‰¡tâ‚‚ of Î» {
      âŠ¢tâ‚â‰¡tâ‚‚ â†’
    case soundnessConvâ†‘Term uâ‚â‰¡uâ‚‚ of Î» {
      âŠ¢uâ‚â‰¡uâ‚‚ â†’
    case reflConEq (wfEq âŠ¢Aâ‚â‰¡Aâ‚‚) of Î» {
      Î“â‰¡Î“ â†’
    case []-congâ†’Erased ok of Î» {
      Erased-ok â†’
      _
    , Id-cong (Erased-cong Erased-ok âŠ¢Aâ‚â‰¡Aâ‚‚) ([]-congâ€² Erased-ok âŠ¢tâ‚â‰¡tâ‚‚)
        ([]-congâ€² Erased-ok âŠ¢uâ‚â‰¡uâ‚‚)
    , []-cong-cong (symConvâ†‘ Î“â‰¡Î” Aâ‚â‰¡Aâ‚‚)
        (convConvâ†‘Termâ€² Î“â‰¡Î” âŠ¢Aâ‚â‰¡Aâ‚‚ (symConvâ†‘Term Î“â‰¡Î“ tâ‚â‰¡tâ‚‚))
        (convConvâ†‘Termâ€² Î“â‰¡Î” âŠ¢Aâ‚â‰¡Aâ‚‚ (symConvâ†‘Term Î“â‰¡Î“ uâ‚â‰¡uâ‚‚))
        vâ‚‚~vâ‚
        (stabilityEq Î“â‰¡Î” $
         trans (trans (sym Bâ‰¡C) Bâ‰¡Id-tâ‚-uâ‚)
           (Id-cong âŠ¢Aâ‚â‰¡Aâ‚‚ âŠ¢tâ‚â‰¡tâ‚‚ âŠ¢uâ‚â‰¡uâ‚‚))
        ok }}}}}}

  -- Symmetry of algorithmic equality of neutrals of types in WHNF.
  sym~â†“ : âˆ€ {t u A} â†’ âŠ¢ Î“ â‰¡ Î” â†’ Î“ âŠ¢ t ~ u â†“ A
         â†’ âˆƒ Î» B â†’ Whnf B Ã— Î“ âŠ¢ A â‰¡ B Ã— Î” âŠ¢ u ~ t â†“ B
  sym~â†“ Î“â‰¡Î” ([~] Aâ‚ (D , whnfA) k~l) =
    let B , Aâ‰¡B , k~lâ€² = sym~â†‘ Î“â‰¡Î” k~l
        _ , âŠ¢B = syntacticEq Aâ‰¡B
        Bâ€² , whnfBâ€² , Dâ€² = whNorm âŠ¢B
        Aâ‰¡Bâ€² = trans (sym (subset* D)) (trans Aâ‰¡B (subset* Dâ€²))
    in  Bâ€² , whnfBâ€² , Aâ‰¡Bâ€² ,
        [~] B (stabilityRed* Î“â‰¡Î” Dâ€² , whnfBâ€²) k~lâ€²

  sym~âˆ· : âˆ€ {t u A} â†’ âŠ¢ Î“ â‰¡ Î” â†’ Î“ âŠ¢ t ~ u âˆ· A â†’ Î” âŠ¢ u ~ t âˆ· A
  sym~âˆ· Î“â‰¡Î” (â†‘ Aâ‰¡B k~l) =
    let C , Bâ‰¡C , k~lâ€² = sym~â†‘ Î“â‰¡Î” k~l
    in â†‘ (stabilityEq Î“â‰¡Î” (trans Aâ‰¡B Bâ‰¡C)) k~lâ€²

  -- Symmetry of algorithmic equality of types.
  symConvâ†‘ : âˆ€ {A B} â†’ âŠ¢ Î“ â‰¡ Î” â†’ Î“ âŠ¢ A [convâ†‘] B â†’ Î” âŠ¢ B [convâ†‘] A
  symConvâ†‘ Î“â‰¡Î” ([â†‘] Aâ€² Bâ€² D Dâ€² Aâ€²<>Bâ€²) =
    [â†‘] Bâ€² Aâ€² (stabilityRedâ†˜ Î“â‰¡Î” Dâ€²) (stabilityRedâ†˜ Î“â‰¡Î” D)
        (symConvâ†“ Î“â‰¡Î” Aâ€²<>Bâ€²)

  -- Symmetry of algorithmic equality of types in WHNF.
  symConvâ†“ : âˆ€ {A B} â†’ âŠ¢ Î“ â‰¡ Î” â†’ Î“ âŠ¢ A [convâ†“] B â†’ Î” âŠ¢ B [convâ†“] A
  symConvâ†“ Î“â‰¡Î” (Level-refl x) =
    let _ , âŠ¢Î” , _ = contextConvSubst Î“â‰¡Î”
    in  Level-refl âŠ¢Î”
  symConvâ†“ Î“â‰¡Î” (U-cong x) =
    U-cong (symConvâ†‘Term Î“â‰¡Î” x)
  symConvâ†“ Î“â‰¡Î” (Lift-cong lâ‚â‰¡lâ‚‚ Fâ‰¡H) =
    Lift-cong (symConvâ†‘Term Î“â‰¡Î” lâ‚â‰¡lâ‚‚) (symConvâ†‘ Î“â‰¡Î” Fâ‰¡H)
  symConvâ†“ Î“â‰¡Î” (â„•-refl x) =
    let _ , âŠ¢Î” , _ = contextConvSubst Î“â‰¡Î”
    in  â„•-refl âŠ¢Î”
  symConvâ†“ Î“â‰¡Î” (Empty-refl x) =
    let _ , âŠ¢Î” , _ = contextConvSubst Î“â‰¡Î”
    in  Empty-refl âŠ¢Î”
  symConvâ†“ Î“â‰¡Î” (Unit-refl x ok) =
    let _ , âŠ¢Î” , _ = contextConvSubst Î“â‰¡Î”
    in  Unit-refl âŠ¢Î” ok
  symConvâ†“ Î“â‰¡Î” (ne A~B) =
    let B , whnfB , Uâ‰¡B , B~A = sym~â†“ Î“â‰¡Î” A~B
        Bâ‰¡U = Uâ‰¡A Uâ‰¡B whnfB .projâ‚‚
    in  ne (PE.subst (Î» x â†’ _ âŠ¢ _ ~ _ â†“ x) Bâ‰¡U B~A)
  symConvâ†“ Î“â‰¡Î” (Î Î£-cong A<>B A<>Bâ‚ ok) =
    let Fâ‰¡H = soundnessConvâ†‘ A<>B
    in  Î Î£-cong (symConvâ†‘ Î“â‰¡Î” A<>B)
          (symConvâ†‘ (Î“â‰¡Î” âˆ™ Fâ‰¡H) A<>Bâ‚) ok
  symConvâ†“ Î“â‰¡Î” (Id-cong Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ uâ‚â‰¡uâ‚‚) =
    case soundnessConvâ†‘ Aâ‚â‰¡Aâ‚‚ of Î» {
      âŠ¢Aâ‚â‰¡Aâ‚‚ â†’
    case reflConEq (wfEq âŠ¢Aâ‚â‰¡Aâ‚‚) of Î» {
      Î“â‰¡Î“ â†’
    Id-cong (symConvâ†‘ Î“â‰¡Î” Aâ‚â‰¡Aâ‚‚)
      (convConvâ†‘Termâ€² Î“â‰¡Î” âŠ¢Aâ‚â‰¡Aâ‚‚ (symConvâ†‘Term Î“â‰¡Î“ tâ‚â‰¡tâ‚‚))
      (convConvâ†‘Termâ€² Î“â‰¡Î” âŠ¢Aâ‚â‰¡Aâ‚‚ (symConvâ†‘Term Î“â‰¡Î“ uâ‚â‰¡uâ‚‚)) }}

  -- Symmetry of algorithmic equality of terms.
  symConvâ†‘Term : âˆ€ {t u A} â†’ âŠ¢ Î“ â‰¡ Î” â†’ Î“ âŠ¢ t [convâ†‘] u âˆ· A â†’ Î” âŠ¢ u [convâ†‘] t âˆ· A
  symConvâ†‘Term Î“â‰¡Î” ([â†‘]â‚œ B tâ€² uâ€² D d dâ€² t<>u) =
    [â†‘]â‚œ B uâ€² tâ€² (stabilityRedâ†˜ Î“â‰¡Î” D) (stabilityRedâ†˜Term Î“â‰¡Î” dâ€²)
         (stabilityRedâ†˜Term Î“â‰¡Î” d) (symConvâ†“Term Î“â‰¡Î” t<>u)

  -- Symmetry of algorithmic equality of terms in WHNF.
  symConvâ†“Term : âˆ€ {t u A} â†’ âŠ¢ Î“ â‰¡ Î” â†’ Î“ âŠ¢ t [convâ†“] u âˆ· A â†’ Î” âŠ¢ u [convâ†“] t âˆ· A
  symConvâ†“Term Î“â‰¡Î” (Level-ins t~u) =
    Level-ins (stabilityConvâ†“Level Î“â‰¡Î” (symConvâ†“Level t~u))
  symConvâ†“Term Î“â‰¡Î” (â„•-ins t~u) =
    let B , whnfB , Aâ‰¡B , u~t = sym~â†“ Î“â‰¡Î” t~u
        Bâ‰¡â„• = â„•â‰¡A Aâ‰¡B whnfB
    in  â„•-ins (PE.subst (Î» x â†’ _ âŠ¢ _ ~ _ â†“ x) Bâ‰¡â„• u~t)
  symConvâ†“Term Î“â‰¡Î” (Empty-ins t~u) =
    let B , whnfB , Aâ‰¡B , u~t = sym~â†“ Î“â‰¡Î” t~u
        Bâ‰¡Empty = Emptyâ‰¡A Aâ‰¡B whnfB
    in  Empty-ins (PE.subst (Î» x â†’ _ âŠ¢ _ ~ _ â†“ x) Bâ‰¡Empty u~t)
  symConvâ†“Term Î“â‰¡Î” (UnitÊ·-ins ok t~u) =
    UnitÊ·-ins ok (sym~âˆ· Î“â‰¡Î” t~u)
  symConvâ†“Term Î“â‰¡Î” (Î£Ê·-ins t u t~u) =
    case sym~â†“ Î“â‰¡Î” t~u of Î» (B , whnfB , Aâ‰¡B , u~t) â†’
    case Î£â‰¡A Aâ‰¡B whnfB of Î» where
      (_ , Bâ‰¡Î£ , PE.refl) â†’
        Î£Ê·-ins (stabilityTerm Î“â‰¡Î” u) (stabilityTerm Î“â‰¡Î” t) u~t
  symConvâ†“Term Î“â‰¡Î” (ne-ins t u x t~u) =
    let B , whnfB , Aâ‰¡B , u~t = sym~â†“ Î“â‰¡Î” t~u
    in  ne-ins (stabilityTerm Î“â‰¡Î” u) (stabilityTerm Î“â‰¡Î” t) x u~t
  symConvâ†“Term Î“â‰¡Î” (univ x xâ‚ xâ‚‚) =
    univ (stabilityTerm Î“â‰¡Î” xâ‚) (stabilityTerm Î“â‰¡Î” x) (symConvâ†“ Î“â‰¡Î” xâ‚‚)
  symConvâ†“Term Î“â‰¡Î” (Lift-Î· âŠ¢t âŠ¢u wt wu lowerâ‰¡lower) =
    Lift-Î· (stabilityTerm Î“â‰¡Î” âŠ¢u) (stabilityTerm Î“â‰¡Î” âŠ¢t) wu wt (symConvâ†‘Term Î“â‰¡Î” lowerâ‰¡lower)
  symConvâ†“Term Î“â‰¡Î” (zero-refl x) =
    let _ , âŠ¢Î” , _ = contextConvSubst Î“â‰¡Î”
    in  zero-refl âŠ¢Î”
  symConvâ†“Term Î“â‰¡Î” (starÊ·-refl y ok no-Î·) =
    let âŠ¢Î“ , âŠ¢Î” , _ = contextConvSubst Î“â‰¡Î”
    in  starÊ·-refl âŠ¢Î” ok no-Î·
  symConvâ†“Term Î“â‰¡Î” (suc-cong t<>u) = suc-cong (symConvâ†‘Term Î“â‰¡Î” t<>u)
  symConvâ†“Term Î“â‰¡Î” (prod-cong xâ‚ xâ‚‚ xâ‚ƒ ok) =
    let Î”âŠ¢G = stability (Î“â‰¡Î” âˆ™ refl (âŠ¢âˆ™â†’âŠ¢ (wf xâ‚))) xâ‚
        Î”âŠ¢tâ€²â†‘t = symConvâ†‘Term Î“â‰¡Î” xâ‚‚
        Î”âŠ¢uâ€²â†‘u = symConvâ†‘Term Î“â‰¡Î” xâ‚ƒ
        Gtâ‰¡Gtâ€² = substTypeEq (refl Î”âŠ¢G)
                   (symâ€² (soundnessConvâ†‘Term Î”âŠ¢tâ€²â†‘t))
    in  prod-cong Î”âŠ¢G Î”âŠ¢tâ€²â†‘t (convConvâ†‘Term Gtâ‰¡Gtâ€² Î”âŠ¢uâ€²â†‘u) ok
  symConvâ†“Term Î“â‰¡Î” (Î·-eq xâ‚ xâ‚‚ y yâ‚ t<>u) =
    let âŠ¢F , _ , _ = inversion-Î Î£ (syntacticTerm xâ‚)
    in  Î·-eq (stabilityTerm Î“â‰¡Î” xâ‚‚) (stabilityTerm Î“â‰¡Î” xâ‚)
             yâ‚ y (symConvâ†‘Term (Î“â‰¡Î” âˆ™ refl âŠ¢F) t<>u)
  symConvâ†“Term Î“â‰¡Î” (Î£-Î· âŠ¢p âŠ¢r pProd rProd fstConv sndConv) =
    let Î”âŠ¢p = stabilityTerm Î“â‰¡Î” âŠ¢p
        Î”âŠ¢r = stabilityTerm Î“â‰¡Î” âŠ¢r
        _ , âŠ¢G , _ = inversion-Î Î£ (syntacticTerm âŠ¢p)
        Î”fstâ‰¡ = symConvâ†‘Term Î“â‰¡Î” fstConv
        Î”sndâ‰¡â‚ = symConvâ†‘Term Î“â‰¡Î” sndConv
        Î”Gfsttâ‰¡Gfstu = stabilityEq Î“â‰¡Î” (substTypeEq (refl âŠ¢G)
                                                    (soundnessConvâ†‘Term fstConv))
        Î”sndâ‰¡ = convConvâ†‘Term Î”Gfsttâ‰¡Gfstu Î”sndâ‰¡â‚
    in  Î£-Î· Î”âŠ¢r Î”âŠ¢p rProd pProd Î”fstâ‰¡ Î”sndâ‰¡
  symConvâ†“Term Î“â‰¡Î” (Î·-unit [t] [u] tUnit uUnit okâ‚ okâ‚‚) =
    let [t] = stabilityTerm Î“â‰¡Î” [t]
        [u] = stabilityTerm Î“â‰¡Î” [u]
    in  Î·-unit [u] [t] uUnit tUnit okâ‚ okâ‚‚
  symConvâ†“Term Î“â‰¡Î” (Id-ins âŠ¢vâ‚ vâ‚~vâ‚‚) =
    case sym~â†“ Î“â‰¡Î” vâ‚~vâ‚‚ of Î» {
      (_ , B-whnf , Idâ‰¡B , vâ‚‚~vâ‚) â†’
    case Idâ‰¡Whnf Idâ‰¡B B-whnf of Î» {
      (_ , _ , _ , PE.refl) â†’
    case syntacticEqTerm (soundness~â†“ vâ‚~vâ‚‚) .projâ‚‚ of Î» {
      (âŠ¢vâ‚â€² , âŠ¢vâ‚‚) â†’
    case sym (neTypeEq (ne~â†“ vâ‚~vâ‚‚ .projâ‚‚ .projâ‚) âŠ¢vâ‚ âŠ¢vâ‚â€²) of Î» {
      Idâ‰¡Id â†’
    Id-ins (stabilityTerm Î“â‰¡Î” (conv âŠ¢vâ‚‚ Idâ‰¡Id)) vâ‚‚~vâ‚ }}}}
  symConvâ†“Term Î“â‰¡Î” (rfl-refl tâ‰¡u) =
    rfl-refl (stabilityEqTerm Î“â‰¡Î” tâ‰¡u)

  -- Symmetry of algorithmic equality of levels.

  symConvâ†“Level : âˆ€ {t u} â†’ Î“ âŠ¢ t [convâ†“] u âˆ·Level â†’ Î“ âŠ¢ u [convâ†“] t âˆ·Level
  symConvâ†“Level ([â†“]Ë¡ táµ› uáµ› tâ‰¡ uâ‰¡ tâ‰¡u) =
    [â†“]Ë¡ uáµ› táµ› uâ‰¡ tâ‰¡ (sym-â‰¡áµ› tâ‰¡u)

  sym~â†“Level : âˆ€ {t u} â†’ Î“ âŠ¢ t ~ u â†“ Level â†’ Î“ âŠ¢ u ~ t â†“ Level
  sym~â†“Level t~u =
    let B , whnfB , Levelâ‰¡B , u~t = sym~â†“ (reflConEq (wfEqTerm (soundness~â†“ t~u))) t~u
    in PE.subst (_âŠ¢_~_â†“_ _ _ _) (Levelâ‰¡A Levelâ‰¡B whnfB) u~t

  sym-â‰¡â¿ : âˆ€ {t u : Term n} â†’ â‰¡â¿ Î“ t u d â†’ â‰¡â¿ Î“ t u (not d)
  sym-â‰¡â¿ (neâ‰¡ x) = neâ‰¡' (sym~â†“Level x)
  sym-â‰¡â¿ (neâ‰¡' x) = neâ‰¡ (sym~â†“Level x)

  sym-â‰¤áµƒ : âˆ€ {t u : LevelAtom Î“} â†’ â‰¤áµƒ d t u â†’ â‰¤áµƒ (not d) t u
  sym-â‰¤áµƒ zeroáµ˜â‰¤ = zeroáµ˜â‰¤
  sym-â‰¤áµƒ (neâ‰¤ x) = neâ‰¤ (sym-â‰¡â¿ x)

  sym-â‰¤âº : âˆ€ {t u : Levelâº Î“} â†’ â‰¤âº d t u â†’ â‰¤âº (not d) t u
  sym-â‰¤âº (a , b) = a , sym-â‰¤áµƒ b

  sym-â‰¤âºáµ› : âˆ€ {t} {u : Leveláµ› Î“} â†’ â‰¤âºáµ› d t u â†’ â‰¤âºáµ› (not d) t u
  sym-â‰¤âºáµ› (Any.here px) = Any.here (sym-â‰¤âº px)
  sym-â‰¤âºáµ› (Any.there x) = Any.there (sym-â‰¤âºáµ› x)

  sym-â‰¤áµ› : âˆ€ {t u : Leveláµ› Î“} â†’ â‰¤áµ› d t u â†’ â‰¤áµ› (not d) t u
  sym-â‰¤áµ› All.[] = All.[]
  sym-â‰¤áµ› (px All.âˆ· x) = sym-â‰¤âºáµ› px All.âˆ· sym-â‰¤áµ› x

  sym-â‰¡áµ› : âˆ€ {t u : Leveláµ› Î“} â†’ t â‰¡áµ› u â†’ u â‰¡áµ› t
  sym-â‰¡áµ› (tâ‰¤u , uâ‰¤t) = sym-â‰¤áµ› uâ‰¤t , sym-â‰¤áµ› tâ‰¤u

symConvâ†“Termâ€² : âˆ€ {t u A} â†’ Î“ âŠ¢ t [convâ†“] u âˆ· A â†’ Î“ âŠ¢ u [convâ†“] t âˆ· A
symConvâ†“Termâ€² tConvU =
  symConvâ†“Term (reflConEq (wfEqTerm (soundnessConvâ†“Term tConvU))) tConvU

-- Symmetry of algorithmic equality of types with preserved context.
symConv : âˆ€ {A B} â†’ Î“ âŠ¢ A [convâ†‘] B â†’ Î“ âŠ¢ B [convâ†‘] A
symConv A<>B =
  let âŠ¢Î“ = wfEq (soundnessConvâ†‘ A<>B)
  in  symConvâ†‘ (reflConEq âŠ¢Î“) A<>B

-- Symmetry of algorithmic equality of terms with preserved context.
symConvTerm : âˆ€ {t u A} â†’ Î“ âŠ¢ t [convâ†‘] u âˆ· A â†’ Î“ âŠ¢ u [convâ†‘] t âˆ· A
symConvTerm t<>u =
  let âŠ¢Î“ = wfEqTerm (soundnessConvâ†‘Term t<>u)
  in  symConvâ†‘Term (reflConEq âŠ¢Î“) t<>u
