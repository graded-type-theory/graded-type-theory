------------------------------------------------------------------------
-- The algorithmic equality is closed under weakening.
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.Conversion.Weakening
  {a} {M : Set a}
  {ğ•„ : Modality M}
  (R : Type-restrictions ğ•„)
  where

open Type-restrictions R

open import Definition.Untyped M as U hiding (wk)
open import Definition.Untyped.Neutral M type-variant
open import Definition.Untyped.Properties M
open import Definition.Typed R
open import Definition.Typed.Properties R
open import Definition.Typed.Weakening R
open import Definition.Typed.Consequences.Syntactic R
open import Definition.Conversion R
open import Definition.Conversion.Soundness R

open import Tools.Fin
open import Tools.Function
open import Tools.Nat
import Tools.PropositionalEquality as PE
open import Tools.Product

private
  variable
    m n : Nat
    Ï : Wk m n
    p r : M

mutual
  -- Weakening of algorithmic equality of neutrals.
  wk~â†‘ : âˆ€ {t u A Î“ Î”} ([Ï] : Ï âˆ· Î” âŠ‡ Î“) â†’ âŠ¢ Î”
      â†’ Î“ âŠ¢ t ~ u â†‘ A
      â†’ Î” âŠ¢ U.wk Ï t ~ U.wk Ï u â†‘ U.wk Ï A
  wk~â†‘ {Ï = Ï} [Ï] âŠ¢Î” (var-refl xâ‚ xâ‰¡y) = var-refl (wkTerm [Ï] âŠ¢Î” xâ‚) (PE.cong (wkVar Ï) xâ‰¡y)
  wk~â†‘ Ï âŠ¢Î” (app-cong {B} t~u x) =
    PE.subst (Î» x â†’ _ âŠ¢ _ ~ _ â†‘ x) (PE.sym (wk-Î² B))
             (app-cong (wk~â†“ Ï âŠ¢Î” t~u) (wkConvâ†‘Term Ï âŠ¢Î” x))
  wk~â†‘ Ï âŠ¢Î” (fst-cong p~r) =
    fst-cong (wk~â†“ Ï âŠ¢Î” p~r)
  wk~â†‘ Ï âŠ¢Î” (snd-cong {B} p~r) =
    PE.subst (Î» x â†’ _ âŠ¢ _ ~ _ â†‘ x)
             (PE.sym (wk-Î² B))
             (snd-cong (wk~â†“ Ï âŠ¢Î” p~r))
  wk~â†‘ [Ï] âŠ¢Î” (natrec-cong {Aâ‚} x xâ‚ xâ‚‚ t~u) =
    let âŠ¢Î”â„• = âŠ¢Î” âˆ™ (â„•â±¼ âŠ¢Î”)
        Î”â„•âŠ¢F = wk (lift [Ï]) âŠ¢Î”â„• (projâ‚ (syntacticEq (soundnessConvâ†‘ x)))
    in
    PE.subst (_âŠ¢_~_â†‘_ _ _ _) (PE.sym (wk-Î² Aâ‚)) $
    natrec-cong (wkConvâ†‘ (lift [Ï]) (âŠ¢Î” âˆ™ â„•â±¼ âŠ¢Î”) x)
      (PE.subst (_âŠ¢_[convâ†‘]_âˆ·_ _ _ _) (wk-Î² Aâ‚) $
       wkConvâ†‘Term [Ï] âŠ¢Î” xâ‚)
      (PE.subst (_âŠ¢_[convâ†‘]_âˆ·_ _ _ _) (wk-Î²-natrec _ Aâ‚) $
       wkConvâ†‘Term (lift (lift [Ï])) (âŠ¢Î”â„• âˆ™ Î”â„•âŠ¢F) xâ‚‚)
      (wk~â†“ [Ï] âŠ¢Î” t~u)
  wk~â†‘
    {Ï = Ï} {Î” = Î”} [Ï] âŠ¢Î”
    (prodrec-cong {C = C} {E} {g} {h} {u} {v} x g~h xâ‚) =
    let Ïg~Ïh = wk~â†“ [Ï] âŠ¢Î” g~h
        âŠ¢ÏÎ£ , _ , _ = syntacticEqTerm (soundness~â†“ Ïg~Ïh)
        âŠ¢ÏF , âŠ¢ÏG = syntacticÎ£ âŠ¢ÏÎ£
        uâ†“v = PE.subst (Î» x â†’ _ âŠ¢ U.wk (liftn Ï 2) u [convâ†‘] U.wk (liftn Ï 2) v âˆ· x)
                       (wk-Î²-prodrec Ï C)
                       (wkConvâ†‘Term (lift (lift [Ï])) (âŠ¢Î” âˆ™ âŠ¢ÏF âˆ™ âŠ¢ÏG) xâ‚)
    in  PE.subst  (Î» x â†’ _ âŠ¢ U.wk Ï (prodrec _ _ _ C g u) ~
                           U.wk Ï (prodrec _ _ _ E h v) â†‘ x)
                  (PE.sym (wk-Î² C))
                  (prodrec-cong (wkConvâ†‘ (lift [Ï]) (âŠ¢Î” âˆ™ âŠ¢ÏÎ£) x)
                     Ïg~Ïh uâ†“v)
  wk~â†‘ [Ï] âŠ¢Î” (emptyrec-cong x t~u) =
    emptyrec-cong (wkConvâ†‘ [Ï] âŠ¢Î” x) (wk~â†“ [Ï] âŠ¢Î” t~u)
  wk~â†‘ [Ï] âŠ¢Î” (unitrec-cong {Aâ‚} x xâ‚ xâ‚‚ no-Î·) =
    let k~l = wk~â†“ [Ï] âŠ¢Î” xâ‚
        âŠ¢Unit , _ = syntacticEqTerm (soundness~â†“ k~l)
        uâ†‘v = PE.subst (_âŠ¢_[convâ†‘]_âˆ·_ _ _ _)
                       (wk-Î² Aâ‚)
                       (wkConvâ†‘Term [Ï] âŠ¢Î” xâ‚‚)
    in  PE.subst (_âŠ¢_~_â†‘_ _ _ _)
                 (PE.sym (wk-Î² Aâ‚))
                 (unitrec-cong (wkConvâ†‘ (lift [Ï]) (âŠ¢Î” âˆ™ âŠ¢Unit) x)
                               k~l uâ†‘v no-Î·)
  wk~â†‘
    {Ï} {Î”} [Ï] âŠ¢Î”
    (J-cong {Aâ‚} {Bâ‚} {Bâ‚‚} Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ Bâ‚â‰¡Bâ‚‚ uâ‚â‰¡uâ‚‚ vâ‚â‰¡vâ‚‚ wâ‚~wâ‚‚ â‰¡Id) =
    case syntacticEq (soundnessConvâ†‘ Aâ‚â‰¡Aâ‚‚) .projâ‚ of Î» {
      âŠ¢Aâ‚ â†’
    case syntacticEqTerm (soundnessConvâ†‘Term tâ‚â‰¡tâ‚‚) .projâ‚‚ .projâ‚ of Î» {
      âŠ¢tâ‚ â†’
    case âŠ¢Î” âˆ™ wk [Ï] âŠ¢Î” âŠ¢Aâ‚ of Î» {
      âŠ¢Î”âˆ™wk-Ï-Aâ‚ â†’
    PE.subst (_ âŠ¢ J _ _ _ _ _ _ _ _ ~ _ â†‘_)
      (PE.sym $ wk-Î²-doubleSubst _ Bâ‚ _ _) $
    J-cong (wkConvâ†‘ [Ï] âŠ¢Î” Aâ‚â‰¡Aâ‚‚) (wkConvâ†‘Term [Ï] âŠ¢Î” tâ‚â‰¡tâ‚‚)
      (PE.subst
         (Î» Id â†’
            Î” âˆ™ U.wk Ï Aâ‚ âˆ™ Id âŠ¢
              U.wk (lift (lift Ï)) Bâ‚ [convâ†‘] U.wk (lift (lift Ï)) Bâ‚‚)
         (PE.congâ‚‚ (Î» A t â†’ Id A t (var x0))
            (PE.sym $ wk1-wkâ‰¡lift-wk1 _ _)
            (PE.sym $ wk1-wkâ‰¡lift-wk1 _ _)) $
       wkConvâ†‘ (lift (lift [Ï]))
         (âŠ¢Î”âˆ™wk-Ï-Aâ‚ âˆ™
          Idâ±¼
            (PE.substâ‚‚ (_âŠ¢_âˆ·_ _)
               (PE.sym $ lift-wk1 _ _)
               (PE.sym $ lift-wk1 _ _) $
             wkTerm (step [Ï]) âŠ¢Î”âˆ™wk-Ï-Aâ‚ âŠ¢tâ‚)
            (PE.subst (_âŠ¢_âˆ·_ _ _) (wk1-wkâ‰¡lift-wk1 _ _) $
             var âŠ¢Î”âˆ™wk-Ï-Aâ‚ here))
         Bâ‚â‰¡Bâ‚‚)
      (PE.subst (_âŠ¢_[convâ†‘]_âˆ·_ _ _ _) (wk-Î²-doubleSubst _ Bâ‚ _ _) $
       wkConvâ†‘Term [Ï] âŠ¢Î” uâ‚â‰¡uâ‚‚)
      (wkConvâ†‘Term [Ï] âŠ¢Î” vâ‚â‰¡vâ‚‚) (wk~â†“ [Ï] âŠ¢Î” wâ‚~wâ‚‚)
      (wkEq [Ï] âŠ¢Î” â‰¡Id) }}}
  wk~â†‘ [Ï] âŠ¢Î” (K-cong {Bâ‚} Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ Bâ‚â‰¡Bâ‚‚ uâ‚â‰¡uâ‚‚ vâ‚~vâ‚‚ â‰¡Id ok) =
    case syntacticEqTerm (soundnessConvâ†‘Term tâ‚â‰¡tâ‚‚) .projâ‚‚ .projâ‚ of Î» {
      âŠ¢tâ‚ â†’
    PE.subst (_ âŠ¢ K _ _ _ _ _ _ ~ _ â†‘_)
      (PE.sym $ wk-Î² Bâ‚) $
    K-cong (wkConvâ†‘ [Ï] âŠ¢Î” Aâ‚â‰¡Aâ‚‚) (wkConvâ†‘Term [Ï] âŠ¢Î” tâ‚â‰¡tâ‚‚)
      (wkConvâ†‘ (lift [Ï]) (âŠ¢Î” âˆ™ wk [Ï] âŠ¢Î” (Idâ±¼ âŠ¢tâ‚ âŠ¢tâ‚)) Bâ‚â‰¡Bâ‚‚)
      (PE.subst (_âŠ¢_[convâ†‘]_âˆ·_ _ _ _) (wk-Î² Bâ‚) $
       wkConvâ†‘Term [Ï] âŠ¢Î” uâ‚â‰¡uâ‚‚)
      (wk~â†“ [Ï] âŠ¢Î” vâ‚~vâ‚‚) (wkEq [Ï] âŠ¢Î” â‰¡Id) ok }
  wk~â†‘ [Ï] âŠ¢Î” ([]-cong-cong Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ uâ‚â‰¡uâ‚‚ vâ‚~vâ‚‚ â‰¡Id ok) =
    []-cong-cong (wkConvâ†‘ [Ï] âŠ¢Î” Aâ‚â‰¡Aâ‚‚) (wkConvâ†‘Term [Ï] âŠ¢Î” tâ‚â‰¡tâ‚‚)
      (wkConvâ†‘Term [Ï] âŠ¢Î” uâ‚â‰¡uâ‚‚) (wk~â†“ [Ï] âŠ¢Î” vâ‚~vâ‚‚) (wkEq [Ï] âŠ¢Î” â‰¡Id)
      ok

  -- Weakening of algorithmic equality of neutrals in WHNF.
  wk~â†“ : âˆ€ {t u A Î“ Î”} ([Ï] : Ï âˆ· Î” âŠ‡ Î“) â†’ âŠ¢ Î”
      â†’ Î“ âŠ¢ t ~ u â†“ A
      â†’ Î” âŠ¢ U.wk Ï t ~ U.wk Ï u â†“ U.wk Ï A
  wk~â†“ {Ï} [Ï] âŠ¢Î” ([~] Aâ‚ D k~l) =
    [~] (U.wk Ï Aâ‚) (wkRedâ†˜ [Ï] âŠ¢Î” D) (wk~â†‘ [Ï] âŠ¢Î” k~l)

  -- Weakening of algorithmic equality of types.
  wkConvâ†‘ : âˆ€ {A B Î“ Î”} ([Ï] : Ï âˆ· Î” âŠ‡ Î“) â†’ âŠ¢ Î”
          â†’ Î“ âŠ¢ A [convâ†‘] B
          â†’ Î” âŠ¢ U.wk Ï A [convâ†‘] U.wk Ï B
  wkConvâ†‘ {Ï} [Ï] âŠ¢Î” ([â†‘] Aâ€² Bâ€² D Dâ€² Aâ€²<>Bâ€²) =
    [â†‘] (U.wk Ï Aâ€²) (U.wk Ï Bâ€²) (wkRedâ†˜ [Ï] âŠ¢Î” D) (wkRedâ†˜ [Ï] âŠ¢Î” Dâ€²)
      (wkConvâ†“ [Ï] âŠ¢Î” Aâ€²<>Bâ€²)

  -- Weakening of algorithmic equality of types in WHNF.
  wkConvâ†“ : âˆ€ {A B Î“ Î”} ([Ï] : Ï âˆ· Î” âŠ‡ Î“) â†’ âŠ¢ Î”
         â†’ Î“ âŠ¢ A [convâ†“] B
         â†’ Î” âŠ¢ U.wk Ï A [convâ†“] U.wk Ï B
  wkConvâ†“ Ï âŠ¢Î” (U-refl x) = U-refl âŠ¢Î”
  wkConvâ†“ Ï âŠ¢Î” (â„•-refl x) = â„•-refl âŠ¢Î”
  wkConvâ†“ Ï âŠ¢Î” (Empty-refl x) = Empty-refl âŠ¢Î”
  wkConvâ†“ Ï âŠ¢Î” (Unit-refl x ok) = Unit-refl âŠ¢Î” ok
  wkConvâ†“ Ï âŠ¢Î” (ne x) = ne (wk~â†“ Ï âŠ¢Î” x)
  wkConvâ†“ Ï âŠ¢Î” (Î Î£-cong A<>B A<>Bâ‚ ok) =
    let âŠ¢ÏF = wk Ï âŠ¢Î” (syntacticEq (soundnessConvâ†‘ A<>B) .projâ‚)
    in  Î Î£-cong (wkConvâ†‘ Ï âŠ¢Î” A<>B)
          (wkConvâ†‘ (lift Ï) (âŠ¢Î” âˆ™ âŠ¢ÏF) A<>Bâ‚) ok
  wkConvâ†“ Ï âŠ¢Î” (Id-cong Aâ‚â‰¡Aâ‚‚ tâ‚â‰¡tâ‚‚ uâ‚â‰¡uâ‚‚) =
    Id-cong (wkConvâ†‘ Ï âŠ¢Î” Aâ‚â‰¡Aâ‚‚) (wkConvâ†‘Term Ï âŠ¢Î” tâ‚â‰¡tâ‚‚)
      (wkConvâ†‘Term Ï âŠ¢Î” uâ‚â‰¡uâ‚‚)

  -- Weakening of algorithmic equality of terms.
  wkConvâ†‘Term : âˆ€ {t u A Î“ Î”} ([Ï] : Ï âˆ· Î” âŠ‡ Î“) â†’ âŠ¢ Î”
             â†’ Î“ âŠ¢ t [convâ†‘] u âˆ· A
             â†’ Î” âŠ¢ U.wk Ï t [convâ†‘] U.wk Ï u âˆ· U.wk Ï A
  wkConvâ†‘Term {Ï} [Ï] âŠ¢Î” ([â†‘]â‚œ B tâ€² uâ€² D d dâ€² t<>u) =
    [â†‘]â‚œ (U.wk Ï B) (U.wk Ï tâ€²) (U.wk Ï uâ€²)
         (wkRedâ†˜ [Ï] âŠ¢Î” D) (wkRedâ†˜Term [Ï] âŠ¢Î” d) (wkRedâ†˜Term [Ï] âŠ¢Î” dâ€²)
         (wkConvâ†“Term [Ï] âŠ¢Î” t<>u)

  -- Weakening of algorithmic equality of terms in WHNF.
  wkConvâ†“Term : âˆ€ {t u A Î“ Î”} ([Ï] : Ï âˆ· Î” âŠ‡ Î“) â†’ âŠ¢ Î”
             â†’ Î“ âŠ¢ t [convâ†“] u âˆ· A
             â†’ Î” âŠ¢ U.wk Ï t [convâ†“] U.wk Ï u âˆ· U.wk Ï A
  wkConvâ†“Term Ï âŠ¢Î” (â„•-ins x) =
    â„•-ins (wk~â†“ Ï âŠ¢Î” x)
  wkConvâ†“Term Ï âŠ¢Î” (Empty-ins x) =
    Empty-ins (wk~â†“ Ï âŠ¢Î” x)
  wkConvâ†“Term Ï âŠ¢Î” (UnitÊ·-ins ok t~u) =
    UnitÊ·-ins ok (wk~â†“ Ï âŠ¢Î” t~u)
  wkConvâ†“Term Ï âŠ¢Î” (Î£Ê·-ins t u x) =
    Î£Ê·-ins (wkTerm Ï âŠ¢Î” t) (wkTerm Ï âŠ¢Î” u) (wk~â†“ Ï âŠ¢Î” x)
  wkConvâ†“Term {Ï = Ï} [Ï] âŠ¢Î” (ne-ins t u x xâ‚) =
    ne-ins (wkTerm [Ï] âŠ¢Î” t) (wkTerm [Ï] âŠ¢Î” u) (wkNeutral Ï x) (wk~â†“ [Ï] âŠ¢Î” xâ‚)
  wkConvâ†“Term Ï âŠ¢Î” (univ x xâ‚ xâ‚‚) =
    univ (wkTerm Ï âŠ¢Î” x) (wkTerm Ï âŠ¢Î” xâ‚) (wkConvâ†“ Ï âŠ¢Î” xâ‚‚)
  wkConvâ†“Term Ï âŠ¢Î” (zero-refl x) = zero-refl âŠ¢Î”
  wkConvâ†“Term Ï âŠ¢Î” (starÊ·-refl _ ok no-Î·) = starÊ·-refl âŠ¢Î” ok no-Î·
  wkConvâ†“Term Ï âŠ¢Î” (suc-cong t<>u) = suc-cong (wkConvâ†‘Term Ï âŠ¢Î” t<>u)
  wkConvâ†“Term Ï âŠ¢Î” (prod-cong {G = G} xâ‚ xâ‚‚ xâ‚ƒ ok) =
    let âŠ¢ÏF = wk Ï âŠ¢Î” (âŠ¢âˆ™â†’âŠ¢ (wf xâ‚))
        âŠ¢ÏG = wk (lift Ï) (âŠ¢Î” âˆ™ âŠ¢ÏF) xâ‚
    in  prod-cong âŠ¢ÏG (wkConvâ†‘Term Ï âŠ¢Î” xâ‚‚)
          (PE.subst (Î» x â†’ _ âŠ¢ _ [convâ†‘] _ âˆ· x) (wk-Î² G)
             (wkConvâ†‘Term Ï âŠ¢Î” xâ‚ƒ))
          ok
  wkConvâ†“Term {Ï = Ï} {Î” = Î”} [Ï] âŠ¢Î” (Î·-eq {F = F} {G = G} xâ‚ xâ‚‚ y yâ‚ t<>u) =
    let âŠ¢F , _ = syntacticÎ  (syntacticTerm xâ‚)
        âŠ¢ÏF = wk [Ï] âŠ¢Î” âŠ¢F
    in
    Î·-eq (wkTerm [Ï] âŠ¢Î” xâ‚) (wkTerm [Ï] âŠ¢Î” xâ‚‚)
      (wkFunction Ï y) (wkFunction Ï yâ‚) $
    PE.substâ‚ƒ (Î» x y z â†’ Î” âˆ™ U.wk Ï F âŠ¢ x [convâ†‘] y âˆ· z)
      (PE.congâ‚ƒ _âˆ˜âŸ¨_âŸ©_ (PE.sym (wk1-wkâ‰¡lift-wk1 _ _)) PE.refl PE.refl)
      (PE.congâ‚ƒ _âˆ˜âŸ¨_âŸ©_ (PE.sym (wk1-wkâ‰¡lift-wk1 _ _)) PE.refl PE.refl)
      PE.refl $
    wkConvâ†‘Term (lift [Ï]) (âŠ¢Î” âˆ™ âŠ¢ÏF) t<>u
  wkConvâ†“Term {Ï} [Ï] âŠ¢Î” (Î£-Î· {B} âŠ¢p âŠ¢r pProd rProd fstConv sndConv) =
    Î£-Î· (wkTerm [Ï] âŠ¢Î” âŠ¢p)
        (wkTerm [Ï] âŠ¢Î” âŠ¢r)
        (wkProduct Ï pProd)
        (wkProduct Ï rProd)
        (wkConvâ†‘Term [Ï] âŠ¢Î” fstConv)
        (PE.subst (Î» x â†’ _ âŠ¢ _ [convâ†‘] _ âˆ· x)
                  (wk-Î² B)
                  (wkConvâ†‘Term [Ï] âŠ¢Î” sndConv))
  wkConvâ†“Term {Ï = Ï} [Ï] âŠ¢Î” (Î·-unit [t] [u] tWhnf uWhnf ok) =
    Î·-unit (wkTerm [Ï] âŠ¢Î” [t]) (wkTerm [Ï] âŠ¢Î” [u])
           (wkWhnf Ï tWhnf) (wkWhnf Ï uWhnf) ok
  wkConvâ†“Term Ï âŠ¢Î” (Id-ins âŠ¢vâ‚ vâ‚~vâ‚‚) =
    Id-ins (wkTerm Ï âŠ¢Î” âŠ¢vâ‚) (wk~â†“ Ï âŠ¢Î” vâ‚~vâ‚‚)
  wkConvâ†“Term Ï âŠ¢Î” (rfl-refl tâ‰¡u) =
    rfl-refl (wkEqTerm Ï âŠ¢Î” tâ‰¡u)
