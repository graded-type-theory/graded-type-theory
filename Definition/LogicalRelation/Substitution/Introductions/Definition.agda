------------------------------------------------------------------------
-- Validity of definitions.
------------------------------------------------------------------------

open import Definition.Typed.EqualityRelation
open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.LogicalRelation.Substitution.Introductions.Definition
  {a} {M : Set a}
  {ğ•„ : Modality M}
  (R : Type-restrictions ğ•„)
  {{eqrel : EqRelSet R}}
  where

open EqRelSet eqrel
open Type-restrictions R

open import Definition.Untyped M
open import Definition.Untyped.Neutral M type-variant
open import Definition.Untyped.Properties M
open import Definition.Untyped.Whnf M type-variant

open import Definition.Typed R
open import Definition.Typed.Weakening R hiding (wk)
open import Definition.Typed.Weakening.Definition R

open import Definition.LogicalRelation.Hidden.Restricted R
open import Definition.LogicalRelation.Substitution R

open import Tools.Function
open import Tools.Level hiding (_âŠ”_)
open import Tools.Nat
open import Tools.Product
import Tools.PropositionalEquality as PE
open import Tools.Sum
open import Tools.Unit

private
  variable
    n Î± : Nat
    âˆ‡ âˆ‡â€² : DCon (Term 0) _
    Ï† : Unfolding _
    Î“ : Con Term _
    t tâ€² u A B : Term _
    l lâ€² : Universe-level
    p : M

------------------------------------------------------------------------
-- Validity of definition contexts

opaque

  -- Valid definition contexts.

  Â»áµ›_ : DCon (Term 0) n â†’ Set a
  Â»áµ› Îµ = Lift _ âŠ¤
  Â»áµ› (âˆ‡ âˆ™âŸ¨ opa Ï† âŸ©[ t âˆ· A ]) =
    Â»áµ› âˆ‡ Ã—
    Opacity-allowed Ã—
    (âˆƒ Î» l â†’ âˆ‡ Â» Îµ âŠ©áµ›âŸ¨ l âŸ© A Ã—
    (âˆƒ Î» âˆ‡â€² â†’ Ï† Â» âˆ‡â€² â†œ âˆ‡ Ã— âˆ‡â€² Â» Îµ âŠ©áµ›âŸ¨ l âŸ© t âˆ· A))
  Â»áµ› (âˆ‡ âˆ™âŸ¨ tra âŸ©[ t âˆ· A ]) = Â»áµ› âˆ‡ Ã— âˆƒ Î» l â†’ âˆ‡ Â» Îµ âŠ©áµ›âŸ¨ l âŸ© t âˆ· A

opaque
  unfolding Â»áµ›_

  -- A characterisation lemma for Â»áµ›_.

  Â»áµ›Îµâ‡” : Â»áµ› Îµ â‡” âŠ¤
  Â»áµ›Îµâ‡” = _

opaque
  unfolding Â»áµ›_

  -- A characterisation lemma for Â»áµ›_.

  Â»áµ›âˆ™áµ’â‡” :
    Â»áµ› âˆ‡ âˆ™âŸ¨ opa Ï† âŸ©[ t âˆ· A ] â‡”
    (Â»áµ› âˆ‡ Ã—
     Opacity-allowed Ã—
     (âˆƒ Î» l â†’ âˆ‡ Â» Îµ âŠ©áµ›âŸ¨ l âŸ© A Ã—
      (âˆƒ Î» âˆ‡â€² â†’ Ï† Â» âˆ‡â€² â†œ âˆ‡ Ã— âˆ‡â€² Â» Îµ âŠ©áµ›âŸ¨ l âŸ© t âˆ· A)))
  Â»áµ›âˆ™áµ’â‡” = idâ‡”

opaque
  unfolding Â»áµ›_

  -- A characterisation lemma for Â»áµ›_.

  Â»áµ›âˆ™áµ—â‡” : Â»áµ› âˆ‡ âˆ™âŸ¨ tra âŸ©[ t âˆ· A ] â‡” (Â»áµ› âˆ‡ Ã— âˆƒ Î» l â†’ âˆ‡ Â» Îµ âŠ©áµ›âŸ¨ l âŸ© t âˆ· A)
  Â»áµ›âˆ™áµ—â‡” = idâ‡”

opaque

  -- An introduction lemma for Â»áµ›_.

  Â»áµ›-âˆ™áµ’-intro :
    Â»áµ› âˆ‡ â†’
    Opacity-allowed â†’
    âˆ‡ Â» Îµ âŠ©áµ›âŸ¨ l âŸ© A â†’
    Ï† Â» âˆ‡â€² â†œ âˆ‡ â†’
    âˆ‡â€² Â» Îµ âŠ©áµ›âŸ¨ lâ€² âŸ© t âˆ· A â†’
    Â»áµ› âˆ‡ âˆ™âŸ¨ opa Ï† âŸ©[ t âˆ· A ]
  Â»áµ›-âˆ™áµ’-intro {l} {âˆ‡â€²} {lâ€²} Â»âˆ‡ ok âŠ©A Ï†â†œ âŠ©t = Â»áµ›âˆ™áµ’â‡” .projâ‚‚
    (Â»âˆ‡ , ok , l âŠ” lâ€² , emb-âŠ©áµ› â‰¤áµ˜âŠ”áµ˜Ë¡ âŠ©A , âˆ‡â€² , Ï†â†œ , emb-âŠ©áµ›âˆ· â‰¤áµ˜âŠ”áµ˜Ê³ âŠ©t)

opaque

  -- An introduction lemma for Â»áµ›_.

  Â»áµ›-âˆ™áµ—-intro : Â»áµ› âˆ‡ â†’ âˆ‡ Â» Îµ âŠ©áµ›âŸ¨ l âŸ© t âˆ· A â†’ Â»áµ› âˆ‡ âˆ™âŸ¨ tra âŸ©[ t âˆ· A ]
  Â»áµ›-âˆ™áµ—-intro {l} Â»âˆ‡ âŠ©t = Â»áµ›âˆ™áµ—â‡” .projâ‚‚ (Â»âˆ‡ , l , âŠ©t)

private instance

  Îµ-inc : Var-included or-empty (Îµ {A = Term})
  Îµ-inc = Îµ

opaque

  -- A well-formedness lemma for _â†¦âˆ·_âˆˆ_.

  wf-â†¦âˆˆáµ› : Î± â†¦âˆ· A âˆˆ âˆ‡ â†’ Â»áµ› âˆ‡ â†’ âˆƒ Î» l â†’ âˆ‡ Â» Îµ âŠ©áµ›âŸ¨ l âŸ© A
  wf-â†¦âˆˆáµ› {âˆ‡ = Îµ} ()
  wf-â†¦âˆˆáµ› {âˆ‡ = âˆ‡ âˆ™âŸ¨ opa Ï† âŸ©[ t âˆ· A ]} here Â»âˆ‡ =
    let _ , ok , l , âŠ©A , _ , Ï†â†œ , âŠ©t = Â»áµ›âˆ™áµ’â‡” .projâ‚ Â»âˆ‡
    in  l , defn-wk-âŠ©áµ› (stepáµ’â‚ ok (escape-âŠ©áµ› âŠ©A) Ï†â†œ (escape-âŠ©áµ›âˆ· âŠ©t)) âŠ©A
  wf-â†¦âˆˆáµ› {âˆ‡ = âˆ‡ âˆ™âŸ¨ tra âŸ©[ t âˆ· A ]} here Â»âˆ‡ =
    let _ , l , âŠ©t = Â»áµ›âˆ™áµ—â‡” .projâ‚ Â»âˆ‡
    in  l , defn-wk-âŠ©áµ› (stepáµ—â‚ (escape-âŠ©áµ›âˆ· âŠ©t)) (wf-âŠ©áµ›âˆ· âŠ©t)
  wf-â†¦âˆˆáµ› {âˆ‡ = âˆ‡ âˆ™âŸ¨ opa Ï† âŸ©[ t âˆ· A ]} (there Î±â†¦t) Â»âˆ‡ =
    let Â»âˆ‡â€² , ok , _ , âŠ©B , _ , Ï†â†œ , âŠ©u = Â»áµ›âˆ™áµ’â‡” .projâ‚ Â»âˆ‡
        l , âŠ©A = wf-â†¦âˆˆáµ› Î±â†¦t Â»âˆ‡â€²
    in  l , defn-wk-âŠ©áµ› (stepáµ’â‚ ok (escape-âŠ©áµ› âŠ©B) Ï†â†œ (escape-âŠ©áµ›âˆ· âŠ©u)) âŠ©A
  wf-â†¦âˆˆáµ› {âˆ‡ = âˆ‡ âˆ™âŸ¨ tra âŸ©[ t âˆ· A ]} (there Î±â†¦t) Â»âˆ‡ =
    let Â»âˆ‡â€² , _ , âŠ©u = Â»áµ›âˆ™áµ—â‡” .projâ‚ Â»âˆ‡
        l , âŠ©A = wf-â†¦âˆˆáµ› Î±â†¦t Â»âˆ‡â€²
    in  l , defn-wk-âŠ©áµ› (stepáµ—â‚ (escape-âŠ©áµ›âˆ· âŠ©u)) âŠ©A

opaque

  -- A well-formedness lemma for _â†¦_âˆ·_âˆˆ_.

  wf-â†¦âˆ·âˆˆáµ› : Î± â†¦ t âˆ· A âˆˆ âˆ‡ â†’ Â»áµ› âˆ‡ â†’ âˆƒ Î» l â†’ âˆ‡ Â» Îµ âŠ©áµ›âŸ¨ l âŸ© t âˆ· A
  wf-â†¦âˆ·âˆˆáµ› {âˆ‡ = Îµ} ()
  wf-â†¦âˆ·âˆˆáµ› {âˆ‡ = âˆ‡ âˆ™âŸ¨ tra âŸ©[ t âˆ· A ]} here Â»âˆ‡ =
    let _ , l , âŠ©t = Â»áµ›âˆ™áµ—â‡” .projâ‚ Â»âˆ‡
    in  l , defn-wk-âŠ©áµ›âˆ· (stepáµ—â‚ (escape-âŠ©áµ›âˆ· âŠ©t)) âŠ©t
  wf-â†¦âˆ·âˆˆáµ› {âˆ‡ = âˆ‡ âˆ™âŸ¨ opa Ï† âŸ©[ t âˆ· A ]} (there Î±â†¦t) Â»âˆ‡ =
    let Â»âˆ‡â€² , ok , _ , âŠ©B , _ , Ï†â†œ , âŠ©u = Â»áµ›âˆ™áµ’â‡” .projâ‚ Â»âˆ‡
        l , âŠ©t = wf-â†¦âˆ·âˆˆáµ› Î±â†¦t Â»âˆ‡â€²
    in  l , defn-wk-âŠ©áµ›âˆ· (stepáµ’â‚ ok (escape-âŠ©áµ› âŠ©B) Ï†â†œ (escape-âŠ©áµ›âˆ· âŠ©u)) âŠ©t
  wf-â†¦âˆ·âˆˆáµ› {âˆ‡ = âˆ‡ âˆ™âŸ¨ tra âŸ©[ t âˆ· A ]} (there Î±â†¦t) Â»âˆ‡ =
    let Â»âˆ‡â€² , _ , âŠ©u = Â»áµ›âˆ™áµ—â‡” .projâ‚ Â»âˆ‡
        l , âŠ©t = wf-â†¦âˆ·âˆˆáµ› Î±â†¦t Â»âˆ‡â€²
    in  l , defn-wk-âŠ©áµ›âˆ· (stepáµ—â‚ (escape-âŠ©áµ›âˆ· âŠ©u)) âŠ©t

opaque

  -- An escape lemma for Â»áµ›_.

  escape-Â»áµ› : Â»áµ› âˆ‡ â†’ Â» âˆ‡
  escape-Â»áµ› {âˆ‡ = Îµ} Â»âˆ‡ = Îµ
  escape-Â»áµ› {âˆ‡ = âˆ‡ âˆ™âŸ¨ opa Ï† âŸ©[ t âˆ· A ]} Â»âˆ‡ =
    let _ , ok , _ , âŠ©A , _ , Ï†â†œ , âŠ©t = Â»áµ›âˆ™áµ’â‡” .projâ‚ Â»âˆ‡
    in  âˆ™áµ’âŸ¨ ok , Ï†â†œ âŸ©[ escape-âŠ©áµ›âˆ· âŠ©t âˆ· escape-âŠ©áµ› âŠ©A ]
  escape-Â»áµ› {âˆ‡ = âˆ‡ âˆ™âŸ¨ tra âŸ©[ t âˆ· A ]} Â»âˆ‡ =
    âˆ™áµ—[ escape-âŠ©áµ›âˆ· (Â»áµ›âˆ™áµ—â‡” .projâ‚ Â»âˆ‡ .projâ‚‚ .projâ‚‚) ]

------------------------------------------------------------------------
-- Validity theorems for definitions

opaque

  -- Validity of Î´-reduction.

  Î´-redáµ› :
    Î± â†¦ t âˆ· A âˆˆ âˆ‡ â†’
    Â»áµ› âˆ‡ â†’
    âˆ‡ Â»âŠ©áµ› Î“ â†’
    âˆƒ Î» l â†’ âˆ‡ Â» Î“ âŠ©áµ›âŸ¨ l âŸ© defn Î± â‰¡ wk wkâ‚€ t âˆ· wk wkâ‚€ A
  Î´-redáµ› {Î±} {t} {A} {âˆ‡} {Î“} Î±â†¦t Â»âˆ‡ âŠ©Î“ =
    let l , âŠ©t = wf-â†¦âˆ·âˆˆáµ› Î±â†¦t Â»âˆ‡

        Î±â‡’t : âˆ€ {Îºâ€² âˆ‡â€²} {Î¾ : DExt _ Îºâ€² _} (Î¾âŠ‡ : Î¾ Â» âˆ‡â€² âŠ‡ âˆ‡)
                {m Î”} {Ïƒ : Subst m _} â¦ƒ inc : Var-included or-empty Î” â¦„
              â†’ âˆ‡â€² Â» Î” âŠ©Ë¢ Ïƒ âˆ· Î“
              â†’ âˆ‡â€² Â» Î” âŠ¢ defn Î± [ Ïƒ ] â‡’ wk wkâ‚€ t [ Ïƒ ] âˆ· wk wkâ‚€ A [ Ïƒ ]
        Î±â‡’t Î¾âŠ‡ âŠ©Ïƒ = PE.substâ‚‚ (_ Â» _ âŠ¢ defn Î± â‡’_âˆ·_)
                              (PE.sym (wkâ‚€-subst-invariant t))
                              (PE.sym (wkâ‚€-subst-invariant A))
                              (Î´-red (escape-âŠ©Ë¢âˆ· âŠ©Ïƒ .projâ‚)
                                     (there*-â†¦âˆ·âˆˆ Î¾âŠ‡ Î±â†¦t)
                                     PE.refl PE.refl)

    in  l , âŠ©áµ›âˆ·-â‡ Î±â‡’t (wk-âŠ©áµ›âˆ· wkâ‚€âˆ·âŠ‡ âŠ©Î“ âŠ©t)

opaque

  -- Validity of definitions.

  defnáµ› :
    Î± â†¦âˆ· A âˆˆ âˆ‡ â†’
    Â»áµ› âˆ‡ â†’
    âˆ‡ Â»âŠ©áµ› Î“ â†’
    âˆƒ Î» l â†’ âˆ‡ Â» Î“ âŠ©áµ›âŸ¨ l âŸ© defn Î± âˆ· wk wkâ‚€ A
  defnáµ› {Î±} {A} {âˆ‡} {Î“} Î±â†¦ Â»âˆ‡ âŠ©Î“ = case dichotomy-â†¦âˆˆ Î±â†¦ of Î» where
    (injâ‚ (t , Î±â†¦t)) â†’ let l , Î±â‰¡t = Î´-redáµ› Î±â†¦t Â»âˆ‡ âŠ©Î“
                       in  l , wf-âŠ©áµ›â‰¡âˆ· Î±â‰¡t .projâ‚
    (injâ‚‚ Î±â†¦âŠ˜)       â†’
      let Î±â†¦âˆ· = â†¦âŠ˜âˆˆâ‡’â†¦âˆˆ Î±â†¦âŠ˜
          l , âŠ©A = wf-â†¦âˆˆáµ› Î±â†¦âˆ· Â»âˆ‡

          Î±[]â‰¡Î±[] : âˆ€ {Îºâ€² âˆ‡â€²} {Î¾ : DExt _ Îºâ€² _} (Î¾âŠ‡ : Î¾ Â» âˆ‡â€² âŠ‡ âˆ‡)
                      {m Î”} {Ïƒâ‚ Ïƒâ‚‚ : Subst m _} (Ïƒâ‚â‰¡Ïƒâ‚‚ : âˆ‡â€² Â» Î” âŠ©Ë¢ Ïƒâ‚ â‰¡ Ïƒâ‚‚ âˆ· Î“)
                    â†’ âˆ‡â€² Â» Î” âŠ©âŸ¨ l âŸ© defn Î± [ Ïƒâ‚ ] â‰¡ defn Î± [ Ïƒâ‚‚ ] âˆ· wk wkâ‚€ A [ Ïƒâ‚ ]
          Î±[]â‰¡Î±[] Î¾âŠ‡ Ïƒâ‚â‰¡Ïƒâ‚‚ = with-inc-âŠ©â‰¡âˆ· $
            let âŠ¢Î” = escape-âŠ©Ë¢â‰¡âˆ· Ïƒâ‚â‰¡Ïƒâ‚‚ .projâ‚
                âŠ©á´¿A = wk-âŠ© (wkâ‚€âˆ·Ê·âŠ‡ âŠ¢Î”) (defn-wk-âŠ© Î¾âŠ‡ (âŠ©áµ›â†’âŠ© âŠ©A))
                Î±-ne = defn (there*-â†¦âŠ˜âˆˆ Î¾âŠ‡ Î±â†¦âŠ˜)
                A~A = ~-defn (defn âŠ¢Î” (there*-â†¦âˆˆ Î¾âŠ‡ Î±â†¦âˆ·) PE.refl)
                             (there*-â†¦âŠ˜âˆˆ Î¾âŠ‡ Î±â†¦âŠ˜)
            in  refl-âŠ©â‰¡âˆ· (PE.subst (_ Â» _ âŠ©âŸ¨ l âŸ© defn Î± âˆ·_)
                                   (PE.sym (wkâ‚€-subst-invariant A))
                                   (neutral-âŠ©âˆ· âŠ©á´¿A Î±-ne A~A))

      in  l , âŠ©áµ›âˆ·â‡” .projâ‚‚ (wk-âŠ©áµ› wkâ‚€âˆ·âŠ‡ âŠ©Î“ âŠ©A , Î±[]â‰¡Î±[])
