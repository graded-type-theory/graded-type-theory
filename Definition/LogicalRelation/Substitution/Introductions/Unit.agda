------------------------------------------------------------------------
-- Validity of the unit type.
------------------------------------------------------------------------

open import Definition.Typed.EqualityRelation
open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.LogicalRelation.Substitution.Introductions.Unit
  {a} {M : Set a}
  {ğ•„ : Modality M}
  (R : Type-restrictions ğ•„)
  {{eqrel : EqRelSet R}}
  where

open EqRelSet {{...}}
open Type-restrictions R

open import Definition.Untyped M hiding (_âˆ·_)
open import Definition.Untyped.Properties M
open import Definition.Typed R
open import Definition.Typed.Properties R
open import Definition.Typed.RedSteps R
open import Definition.LogicalRelation R
open import Definition.LogicalRelation.Properties R
open import Definition.LogicalRelation.Substitution R
open import Definition.LogicalRelation.Substitution.Conversion R
open import Definition.LogicalRelation.Substitution.Escape R
open import Definition.LogicalRelation.Substitution.Properties R
open import Definition.LogicalRelation.Substitution.Reflexivity R
open import Definition.LogicalRelation.Substitution.Introductions.SingleSubst R
open import Definition.LogicalRelation.Substitution.Introductions.Universe R
open import Definition.LogicalRelation.Irrelevance R

open import Tools.Function
open import Tools.Nat
open import Tools.Product
import Tools.PropositionalEquality as PE

private
  variable
    n : Nat
    Î“ Î” : Con Term n
    Ïƒ Ïƒâ€² : Subst _ _
    s : SigmaMode
    l : TypeLevel
    A Aâ€² t tâ€² u uâ€² : Term n
    p q : M

-- Validity of the Unit type.
Unitáµ› :
  âˆ€ {l} ([Î“] : âŠ©áµ› Î“) â†’ Unit-allowed s â†’ Î“ âŠ©áµ›âŸ¨ l âŸ© Unit s / [Î“]
Unitáµ› _ ok =
  wrap Î» âŠ¢Î” _ â†’
    Unitáµ£ (Unitâ‚œ (idRed:*: (Unitâ±¼ âŠ¢Î” ok)) ok)
  , Î» _ _ â†’ id (Unitâ±¼ âŠ¢Î” ok)

-- Validity of the Unit type as a term.
Unitáµ—áµ› :
  ([Î“] : âŠ©áµ› Î“) â†’
  Unit-allowed s â†’
  Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© Unit s âˆ· U / [Î“] / Uáµ› [Î“]
Unitáµ—áµ› _ ok âŠ¢Î” _ =
    Uâ‚œ Unit! (idRedTerm:*: âŠ¢Unit) Unitâ‚™ Unitâ‰…Unit [Unit]
  , (Î» _ _ â†’
       Uâ‚œâ‚Œ Unit! Unit! (idRedTerm:*: âŠ¢Unit) (idRedTerm:*: âŠ¢Unit)
         Unitâ‚™ Unitâ‚™ Unitâ‰…Unit [Unit] [Unit] (id âŠ¢Unitâ€²))
  where
  âŠ¢Unit     = Unitâ±¼ âŠ¢Î” ok
  âŠ¢Unitâ€²    = univ âŠ¢Unit
  Unitâ‰…Unit = â‰…â‚œ-Unitrefl âŠ¢Î” ok
  [Unit]    = Unitáµ£ (Unitâ‚œ (idRed:*: âŠ¢Unitâ€²) ok)

-- Validity of star.
staráµ› :
  âˆ€ {l} ([Î“] : âŠ©áµ› Î“) (ok : Unit-allowed s) â†’
  Î“ âŠ©áµ›âŸ¨ l âŸ© star s âˆ· Unit s / [Î“] / Unitáµ› [Î“] ok
staráµ› {l = l} [Î“] ok âŠ¢Î” [Ïƒ] =
  [star] , Î» _ _ â†’ reflEqTerm [Unit] [star]
  where
  âŠ¢star = starâ±¼ âŠ¢Î” ok
  [Unit] = projâ‚ (unwrap {l = l} (Unitáµ› [Î“] ok) âŠ¢Î” [Ïƒ])
  [star] = Unitâ‚œ star! (idRedTerm:*: âŠ¢star)
                 (â‰…â‚œ-starrefl âŠ¢Î” ok) staráµ£

-- Validity of Î·-unit.
Î·-unitáµ› : âˆ€ {l e e'} ([Î“] : âŠ©áµ› Î“)
  ([Unit] : Î“ âŠ©áµ›âŸ¨ l âŸ© UnitË¢ / [Î“])
  ([e] : Î“ âŠ©áµ›âŸ¨ l âŸ© e âˆ· UnitË¢ / [Î“] / [Unit])
  ([e'] : Î“ âŠ©áµ›âŸ¨ l âŸ© e' âˆ· UnitË¢ / [Î“] / [Unit])
  â†’ Î“ âŠ©áµ›âŸ¨ l âŸ© e â‰¡ e' âˆ· UnitË¢ / [Î“] / [Unit]
Î·-unitáµ› {Î“ = Î“} {l} {e} {e'} [Î“] [Unit] [e] [e'] {Î” = Î”} {Ïƒ} âŠ¢Î” [Ïƒ] =
  let J = projâ‚ (unwrap [Unit] âŠ¢Î” [Ïƒ])
      [Ïƒe] = projâ‚ ([e] âŠ¢Î” [Ïƒ])
      [Ïƒe'] = projâ‚ ([e'] âŠ¢Î” [Ïƒ])
      ok = âŠ©áµ›Unitâ†’Unit-allowed [Unit]
      UnitJ : Î” âŠ©âŸ¨ l âŸ© UnitË¢
      UnitJ = Unitáµ£ (Unitâ‚œ (idRed:*: (Unitâ±¼ âŠ¢Î” ok)) ok)
      [Ïƒe] = irrelevanceTerm J UnitJ [Ïƒe]
      [Ïƒe'] = irrelevanceTerm J UnitJ [Ïƒe']
      âŠ¢Ïƒe = escapeTerm UnitJ [Ïƒe]
      âŠ¢Ïƒe' = escapeTerm UnitJ [Ïƒe']
  in  irrelevanceEqTerm UnitJ J (Unitâ‚œâ‚Œ âŠ¢Ïƒe âŠ¢Ïƒe')

private

  unitrec-subst* : ([Î“] : âŠ©áµ› Î“)
                   (ok : UnitÊ·-allowed)
                   (âŠ¢Î” : âŠ¢ Î”)
                   ([Ïƒ] : Î” âŠ©Ë¢ Ïƒ âˆ· Î“ / [Î“] / âŠ¢Î”)
                   ([A] : Î“ âˆ™ UnitÊ· âŠ©áµ›âŸ¨ l âŸ© A / [Î“] âˆ™ Unitáµ› {l = l} [Î“] ok)
                   ([tâ€²] : Î” âŠ©âŸ¨ l âŸ© tâ€² âˆ· UnitÊ· / projâ‚ (unwrap (Unitáµ› [Î“] ok) âŠ¢Î” [Ïƒ]))
                 â†’ Î” âŠ¢ u [ Ïƒ ] âˆ· A [ liftSubst Ïƒ ] [ starÊ· ]â‚€
                 â†’ Î” âŠ¢ t â‡’* tâ€² âˆ· UnitÊ·
                 â†’ Î” âŠ¢ unitrec p q (A [ liftSubst Ïƒ ]) t (u [ Ïƒ ]) â‡’*
                       unitrec p q (A [ liftSubst Ïƒ ]) tâ€² (u [ Ïƒ ]) âˆ· A [ liftSubst Ïƒ ] [ t ]â‚€
  unitrec-subst* {Ïƒ = Ïƒ} {l = l} [Î“] ok âŠ¢Î” [Ïƒ] [A] [tâ€²] âŠ¢Ïƒu (id x) =
    let [Unit] = Unitáµ› {l = l} [Î“] ok
        [â‡‘Ïƒ] = liftSubstS [Î“] âŠ¢Î” [Unit] [Ïƒ]
        [ÏƒA] = projâ‚ (unwrap [A] {Ïƒ = liftSubst Ïƒ} (âŠ¢Î” âˆ™ Unitâ±¼ âŠ¢Î” ok) [â‡‘Ïƒ])
        âŠ¢ÏƒA = escape [ÏƒA]
    in  id (unitrecâ±¼ âŠ¢ÏƒA x âŠ¢Ïƒu ok)
  unitrec-subst* {Ïƒ = Ïƒ} {l = l} {A = A} {tâ€² = tâ€²} {u = u} {t = t}
                 [Î“] ok âŠ¢Î” [Ïƒ] [A] [tâ€²] âŠ¢Ïƒu (x â‡¨ d) =
    let [Unit] = Unitáµ› {l = l} [Î“] ok
        [ÏƒUnit] = projâ‚ (unwrap [Unit] âŠ¢Î” [Ïƒ])
        [â‡‘Ïƒ] = liftSubstS [Î“] âŠ¢Î” [Unit] [Ïƒ]
        [ÏƒA] = projâ‚ (unwrap [A] {Ïƒ = liftSubst Ïƒ} (âŠ¢Î” âˆ™ Unitâ±¼ âŠ¢Î” ok) [â‡‘Ïƒ])
        âŠ¢ÏƒA = escape [ÏƒA]
        [tâ‚] , [tâ‚â‰¡tâ€²] = redSubst*Term d [ÏƒUnit] [tâ€²]
        [t] , [tâ‰¡tâ‚] = redSubstTerm x [ÏƒUnit] [tâ‚]
        [Atâ‚] = projâ‚ (unwrap [A] âŠ¢Î” ([Ïƒ] , [tâ‚]))
        Atâ‚â‰¡At = projâ‚‚ (unwrap [A] {Ïƒ = consSubst Ïƒ _} âŠ¢Î” ([Ïƒ] , [tâ‚]))
                       {Ïƒâ€² = consSubst Ïƒ _} ([Ïƒ] , [t])
                       (reflSubst [Î“] âŠ¢Î” [Ïƒ] , symEqTerm [ÏƒUnit] [tâ‰¡tâ‚])
        âŠ¢Atâ‚â‰¡At = PE.substâ‚‚ (_ âŠ¢_â‰¡_) (PE.sym (singleSubstComp _ Ïƒ A))
                            (PE.sym (singleSubstComp t Ïƒ A))
                            (â‰…-eq (escapeEq [Atâ‚] Atâ‚â‰¡At))
        dâ€² = unitrec-subst* {u = u} [Î“] ok âŠ¢Î” [Ïƒ] [A] [tâ€²] âŠ¢Ïƒu d
    in  unitrec-subst âŠ¢ÏƒA âŠ¢Ïƒu x ok â‡¨ conv* dâ€² âŠ¢Atâ‚â‰¡At

  [UnitÊ·]-propâ†’Unit-prop : [UnitÊ·]-prop Î“ t u â†’ Î“ âŠ¢ t âˆ· UnitÊ· â†’ Î“ âŠ¢ u âˆ· UnitÊ·
                         â†’ Unit-prop Î“ Î£áµ£ t Ã— Unit-prop Î“ Î£áµ£ u
  [UnitÊ·]-propâ†’Unit-prop staráµ£ _ _ = staráµ£ , staráµ£
  [UnitÊ·]-propâ†’Unit-prop (ne (neNfâ‚œâ‚Œ neK neM kâ‰¡m)) âŠ¢t âŠ¢u =
      ne (neNfâ‚œ neK âŠ¢t (~-trans kâ‰¡m (~-sym kâ‰¡m)))
    , ne (neNfâ‚œ neM âŠ¢u (~-trans (~-sym kâ‰¡m) kâ‰¡m))



unitrecáµ›â€² : {Î“ : Con Term n}
            ([Î“] : âŠ©áµ› Î“)
            (ok : UnitÊ·-allowed)
          â†’ let [Unit] = Unitáµ› [Î“] ok in
            ([A] : Î“ âˆ™ UnitÊ· âŠ©áµ›âŸ¨ l âŸ© A / [Î“] âˆ™ [Unit])
            ([Aâ€²] : Î“ âˆ™ UnitÊ· âŠ©áµ›âŸ¨ l âŸ© Aâ€² / [Î“] âˆ™ [Unit])
            ([Aâ‰¡Aâ€²] : Î“ âˆ™ UnitÊ· âŠ©áµ›âŸ¨ l âŸ© A â‰¡ Aâ€² / [Î“] âˆ™ [Unit] / [A])
            ([t] : Î“ âŠ©áµ›âŸ¨ l âŸ© t âˆ· UnitÊ· / [Î“] / [Unit])
            ([tâ€²] : Î“ âŠ©áµ›âŸ¨ l âŸ© tâ€² âˆ· UnitÊ· / [Î“] / [Unit])
            ([tâ‰¡tâ€²] : Î“ âŠ©áµ›âŸ¨ l âŸ© t â‰¡ tâ€² âˆ· UnitÊ· / [Î“] / [Unit])
          â†’ let [Aâ‚Š] = substS [Î“] [Unit] [A] (staráµ› {l = l} [Î“] ok)
                [Aâ€²â‚Š] = substS [Î“] [Unit] [Aâ€²] (staráµ› {l = l} [Î“] ok) in
            ([u] : Î“ âŠ©áµ›âŸ¨ l âŸ© u âˆ· A [ starÊ· ]â‚€ / [Î“] / [Aâ‚Š])
            ([uâ€²] : Î“ âŠ©áµ›âŸ¨ l âŸ© uâ€² âˆ· Aâ€² [ starÊ· ]â‚€ / [Î“] / [Aâ€²â‚Š])
            ([uâ‰¡uâ€²] : Î“ âŠ©áµ›âŸ¨ l âŸ© u â‰¡ uâ€² âˆ· A [ starÊ· ]â‚€ / [Î“] / [Aâ‚Š])
            (âŠ¢Î” : âŠ¢ Î”)
            ([Ïƒ] : Î” âŠ©Ë¢ Ïƒ âˆ· Î“ / [Î“] / âŠ¢Î”)
            ([Ïƒâ€²] : Î” âŠ©Ë¢ Ïƒâ€² âˆ· Î“ / [Î“] / âŠ¢Î”)
            ([Ïƒâ‰¡Ïƒâ€²] : Î” âŠ©Ë¢ Ïƒ â‰¡ Ïƒâ€² âˆ· Î“ / [Î“] / âŠ¢Î” / [Ïƒ])
          â†’ âˆƒ Î» [Aâ‚œ] â†’ Î” âŠ©âŸ¨ l âŸ© (unitrec p q A t u) [ Ïƒ ] âˆ· A [ t ]â‚€ [ Ïƒ ] / [Aâ‚œ]
                     Ã— Î” âŠ©âŸ¨ l âŸ© (unitrec p q A t u) [ Ïƒ ] â‰¡ (unitrec p q Aâ€² tâ€² uâ€²) [ Ïƒâ€² ]
                              âˆ· A [ t ]â‚€ [ Ïƒ ] / [Aâ‚œ]
unitrecáµ›â€² {n} {l} {A} {Aâ€²} {t} {tâ€²} {u} {uâ€²} {m} {Î”} {Ïƒ} {Ïƒâ€²} {Î“}
          [Î“] ok [A] [Aâ€²] [Aâ‰¡Aâ€²] [t] [tâ€²] [tâ‰¡tâ€²]
          [u] [uâ€²] [uâ‰¡uâ€²] âŠ¢Î” [Ïƒ] [Ïƒâ€²] [Ïƒâ‰¡Ïƒâ€²] =
  let [Unit] = Unitáµ› [Î“] ok
      [ÏƒUnit] = projâ‚ (unwrap [Unit] âŠ¢Î” [Ïƒ])
      [star] = staráµ› {l = l} [Î“] ok
      [Ïƒstar] = projâ‚ ([star] âŠ¢Î” [Ïƒ])

      [â‡‘Ïƒ] = liftSubstS [Î“] âŠ¢Î” [Unit] [Ïƒ]
      [ÏƒA] = projâ‚ (unwrap [A] {Ïƒ = liftSubst Ïƒ} (âŠ¢Î” âˆ™ Unitâ±¼ âŠ¢Î” ok) [â‡‘Ïƒ])
      âŠ¢ÏƒA = escape [ÏƒA]
      [Aâ‚Š] = substS [Î“] [Unit] [A] [star]
      [ÏƒAâ‚Š]â€² = projâ‚ (unwrap [Aâ‚Š] âŠ¢Î” [Ïƒ])
      [ÏƒAâ‚Š] = irrelevanceâ€² (singleSubstLift A starÊ·) [ÏƒAâ‚Š]â€²
      [Ïƒu]â€² = projâ‚ ([u] âŠ¢Î” [Ïƒ])
      [Ïƒu] = irrelevanceTermâ€² (singleSubstLift A starÊ·) [ÏƒAâ‚Š]â€² [ÏƒAâ‚Š] [Ïƒu]â€²
      âŠ¢Ïƒu = escapeTerm [ÏƒAâ‚Š] [Ïƒu]
      [Aâ‚œ] = projâ‚ (unwrap (substS {t = t} [Î“] [Unit] [A] [t]) âŠ¢Î” [Ïƒ])
      [Ïƒâ‰¡Ïƒ] = reflSubst [Î“] âŠ¢Î” [Ïƒ]

      [â‡‘Ïƒâ€²] = liftSubstS [Î“] âŠ¢Î” [Unit] [Ïƒâ€²]
      [Ïƒâ€²Aâ€²] = projâ‚ (unwrap [Aâ€²] {Ïƒ = liftSubst Ïƒâ€²} (âŠ¢Î” âˆ™ Unitâ±¼ âŠ¢Î” ok) [â‡‘Ïƒâ€²])
      âŠ¢Ïƒâ€²Aâ€² = escape [Ïƒâ€²Aâ€²]
      [Aâ€²â‚Š] = substS [Î“] [Unit] [Aâ€²] [star]
      [Ïƒâ€²Aâ€²â‚Š]â€² = projâ‚ (unwrap [Aâ€²â‚Š] âŠ¢Î” [Ïƒâ€²])
      [Ïƒâ€²Aâ€²â‚Š] = irrelevanceâ€² (singleSubstLift Aâ€² starÊ·) [Ïƒâ€²Aâ€²â‚Š]â€²
      [Ïƒâ€²uâ€²]â€² = projâ‚ ([uâ€²] âŠ¢Î” [Ïƒâ€²])
      [Ïƒâ€²uâ€²] = irrelevanceTermâ€² (singleSubstLift Aâ€² starÊ·) [Ïƒâ€²Aâ€²â‚Š]â€² [Ïƒâ€²Aâ€²â‚Š] [Ïƒâ€²uâ€²]â€²
      âŠ¢Ïƒâ€²uâ€² = escapeTerm [Ïƒâ€²Aâ€²â‚Š] [Ïƒâ€²uâ€²]
      [Ïƒâ€²â‰¡Ïƒâ€²] = reflSubst [Î“] âŠ¢Î” [Ïƒâ€²]

      [Ïƒtâ‰¡Ïƒâ€²t] = projâ‚‚ ([t] âŠ¢Î” [Ïƒ]) [Ïƒâ€²] [Ïƒâ‰¡Ïƒâ€²]
      [Ïƒâ€²tâ‰¡Ïƒâ€²tâ€²] = [tâ‰¡tâ€²] âŠ¢Î” [Ïƒâ€²]
      [Ïƒtâ‰¡Ïƒâ€²tâ€²] = transEqTerm [ÏƒUnit] [Ïƒtâ‰¡Ïƒâ€²t] [Ïƒâ€²tâ‰¡Ïƒâ€²tâ€²]
      [Ïƒuâ‰¡Ïƒâ€²u]â€² = projâ‚‚ ([u] âŠ¢Î” [Ïƒ]) [Ïƒâ€²] [Ïƒâ‰¡Ïƒâ€²]
      [Ïƒâ€²uâ‰¡Ïƒâ€²uâ€²]â€² = [uâ‰¡uâ€²] âŠ¢Î” [Ïƒâ€²]

  in  [Aâ‚œ] , (case [Ïƒtâ‰¡Ïƒâ€²tâ€²] of Î» where
    (Unitâ‚œâ‚Œ k kâ€² d dâ€² kâ‰¡kâ€² prop) â†’
      let âŠ¢k = âŠ¢u-redâ‚œ d
          âŠ¢kâ€² = âŠ¢u-redâ‚œ dâ€²
          k-prop , kâ€²-prop = [UnitÊ·]-propâ†’Unit-prop prop âŠ¢k âŠ¢kâ€²
          [k] = Unitâ‚œ k (idRedTerm:*: âŠ¢k) (â‰…â‚œ-trans kâ‰¡kâ€² (â‰…â‚œ-sym kâ‰¡kâ€²)) k-prop
          [kâ€²] = Unitâ‚œ kâ€² (idRedTerm:*: âŠ¢kâ€²) (â‰…â‚œ-trans (â‰…â‚œ-sym kâ‰¡kâ€²) kâ‰¡kâ€²) kâ€²-prop
          [kâ‰¡kâ€²] = Unitâ‚œâ‚Œ k kâ€² (idRedTerm:*: âŠ¢k)
                          (idRedTerm:*: âŠ¢kâ€²) kâ‰¡kâ€² prop
          redâ‚ = unitrec-subst* {u = u} [Î“] ok âŠ¢Î” [Ïƒ] [A] [k] âŠ¢Ïƒu (redâ‚œ d)
          redâ‚‚ = unitrec-subst* {u = uâ€²} [Î“] ok âŠ¢Î” [Ïƒâ€²] [Aâ€²] [kâ€²] âŠ¢Ïƒâ€²uâ€² (redâ‚œ dâ€²)

          [Ïƒt] , [Ïƒtâ‰¡k] = redSubst*Term (redâ‚œ d) [ÏƒUnit] [k]
          [Ïƒâ€²tâ€²] , [Ïƒâ€²tâ€²â‰¡kâ€²] = redSubst*Term (redâ‚œ dâ€²) [ÏƒUnit] [kâ€²]

          [ÏƒAt]â€² = projâ‚ (unwrap [A] {Ïƒ = consSubst Ïƒ _} âŠ¢Î” ([Ïƒ] , [Ïƒt]))
          [ÏƒAt] = irrelevanceâ€² (PE.sym (singleSubstComp _ Ïƒ A)) [ÏƒAt]â€²
          [ÏƒAtâ‰¡ÏƒAk]â€² = projâ‚‚ (unwrap [A] {Ïƒ = consSubst Ïƒ _} âŠ¢Î” ([Ïƒ] , [Ïƒt]))
                             {Ïƒâ€² = consSubst Ïƒ k} ([Ïƒ] , [k])
                             ([Ïƒâ‰¡Ïƒ] , [Ïƒtâ‰¡k])
          [ÏƒAtâ‰¡ÏƒAk] = irrelevanceEqâ€³ (PE.sym (singleSubstComp _ Ïƒ A))
                                     (PE.sym (singleSubstComp k Ïƒ A))
                                     [ÏƒAt]â€² [ÏƒAt] [ÏƒAtâ‰¡ÏƒAk]â€²
          âŠ¢ÏƒAtâ‰¡ÏƒAk = â‰…-eq (escapeEq [ÏƒAt] [ÏƒAtâ‰¡ÏƒAk])

          [Ïƒâ€²Aâ€²tâ€²]â€² = projâ‚ (unwrap [Aâ€²] {Ïƒ = consSubst Ïƒâ€² _} âŠ¢Î” ([Ïƒâ€²] , [Ïƒâ€²tâ€²]))
          [Ïƒâ€²Aâ€²tâ€²] = irrelevanceâ€² (PE.sym (singleSubstComp _ Ïƒâ€² Aâ€²)) [Ïƒâ€²Aâ€²tâ€²]â€²
          [Ïƒâ€²Aâ€²tâ€²â‰¡Ïƒâ€²Aâ€²kâ€²]â€² = projâ‚‚ (unwrap [Aâ€²] {Ïƒ = consSubst Ïƒâ€² _} âŠ¢Î” ([Ïƒâ€²] , [Ïƒâ€²tâ€²]))
                                   {Ïƒâ€² = consSubst Ïƒâ€² kâ€²} ([Ïƒâ€²] , [kâ€²])
                                   ([Ïƒâ€²â‰¡Ïƒâ€²] , [Ïƒâ€²tâ€²â‰¡kâ€²])
          [Ïƒâ€²Aâ€²tâ€²â‰¡Ïƒâ€²Aâ€²kâ€²] = irrelevanceEqâ€³ (PE.sym (singleSubstComp _ Ïƒâ€² Aâ€²))
                                           (PE.sym (singleSubstComp kâ€² Ïƒâ€² Aâ€²))
                                           [Ïƒâ€²Aâ€²tâ€²]â€² [Ïƒâ€²Aâ€²tâ€²] [Ïƒâ€²Aâ€²tâ€²â‰¡Ïƒâ€²Aâ€²kâ€²]â€²
          âŠ¢Ïƒâ€²Aâ€²tâ€²â‰¡Ïƒâ€²Aâ€²kâ€² = â‰…-eq (escapeEq [Ïƒâ€²Aâ€²tâ€²] [Ïƒâ€²Aâ€²tâ€²â‰¡Ïƒâ€²Aâ€²kâ€²])

          [ÏƒAk]â€² = projâ‚ (unwrap [A] {Ïƒ = consSubst Ïƒ k} âŠ¢Î” ([Ïƒ] , [k]))
          [ÏƒAkâ‰¡Ïƒâ€²Akâ€²]â€² = projâ‚‚ (unwrap [A] {Ïƒ = consSubst Ïƒ k} âŠ¢Î” ([Ïƒ] , [k]))
                               {Ïƒâ€² = consSubst Ïƒâ€² kâ€²} ([Ïƒâ€²] , [kâ€²])
                               ([Ïƒâ‰¡Ïƒâ€²] , [kâ‰¡kâ€²])
      in  case prop of Î» where
        staráµ£ â†’ -- kâ‰¡star, kâ€²â‰¡star
          let redâ‚ƒ = unitrec-Î² âŠ¢ÏƒA âŠ¢Ïƒu ok â‡¨ id âŠ¢Ïƒu
              red = redâ‚ â‡¨âˆ·* conv* redâ‚ƒ (sym âŠ¢ÏƒAtâ‰¡ÏƒAk)
              [Ïƒu]â€³ = convTermâ‚‚ [ÏƒAt] [ÏƒAâ‚Š] [ÏƒAtâ‰¡ÏƒAk] [Ïƒu]
              [urâ‚œ]â€² , [urâ‚œâ‰¡u]â€² = redSubst*Term red [ÏƒAt] [Ïƒu]â€³
              [urâ‚œ] = irrelevanceTermâ€² (PE.sym (singleSubstLift A t))
                                       [ÏƒAt] [Aâ‚œ] [urâ‚œ]â€²
              [urâ‚œâ‰¡u] = irrelevanceEqTermâ€² (PE.sym (singleSubstLift A t))
                                           [ÏƒAt] [Aâ‚œ] [urâ‚œâ‰¡u]â€²

              redâ‚„ = unitrec-Î² âŠ¢Ïƒâ€²Aâ€² âŠ¢Ïƒâ€²uâ€² ok â‡¨ id âŠ¢Ïƒâ€²uâ€²
              redâ€² = redâ‚‚ â‡¨âˆ·* conv* redâ‚„ (sym âŠ¢Ïƒâ€²Aâ€²tâ€²â‰¡Ïƒâ€²Aâ€²kâ€²)
              [Ïƒâ€²uâ€²]â€³ = convTermâ‚‚ [Ïƒâ€²Aâ€²tâ€²] [Ïƒâ€²Aâ€²â‚Š] [Ïƒâ€²Aâ€²tâ€²â‰¡Ïƒâ€²Aâ€²kâ€²] [Ïƒâ€²uâ€²]
              [urâ‚œâ€²]â€² , [urâ‚œâ€²â‰¡uâ€²]â€² = redSubst*Term redâ€² [Ïƒâ€²Aâ€²tâ€²] [Ïƒâ€²uâ€²]â€³

              [ÏƒAtâ‰¡ÏƒA*] = irrelevanceEqâ€³ (substConsId A) (substConsId A)
                                         [ÏƒAt]â€² [Aâ‚œ] [ÏƒAtâ‰¡ÏƒAk]â€²
              [Ïƒuâ‰¡Ïƒâ€²u] = convEqTermâ‚‚ [Aâ‚œ] [ÏƒAâ‚Š]â€² [ÏƒAtâ‰¡ÏƒA*] [Ïƒuâ‰¡Ïƒâ€²u]â€²
              [Ïƒâ€²Aâ‚Š] = projâ‚ (unwrap [Aâ‚Š] âŠ¢Î” [Ïƒâ€²])

              [ÏƒA*â‰¡Ïƒâ€²A*] = irrelevanceEqâ€³ (substConsId A) (substConsId A)
                                          [ÏƒAk]â€² [ÏƒAâ‚Š]â€² [ÏƒAkâ‰¡Ïƒâ€²Akâ€²]â€²
              [ÏƒAtâ‰¡Ïƒâ€²A*] = transEq [Aâ‚œ] [ÏƒAâ‚Š]â€² [Ïƒâ€²Aâ‚Š] [ÏƒAtâ‰¡ÏƒA*] [ÏƒA*â‰¡Ïƒâ€²A*]
              [Ïƒâ€²uâ‰¡Ïƒâ€²uâ€²] = convEqTermâ‚‚ [Aâ‚œ] [Ïƒâ€²Aâ‚Š] [ÏƒAtâ‰¡Ïƒâ€²A*] [Ïƒâ€²uâ‰¡Ïƒâ€²uâ€²]â€²

              [Ïƒâ€²A*]â€² = projâ‚ (unwrap [A] {Ïƒ = consSubst Ïƒâ€² starÊ·} âŠ¢Î” ([Ïƒâ€²] , [Ïƒstar]))
              [Ïƒâ€²A*â‰¡Ïƒâ€²Aâ€²*]â€² = [Aâ‰¡Aâ€²] {Ïƒ = consSubst Ïƒâ€² starÊ·} âŠ¢Î” ([Ïƒâ€²] , [Ïƒstar])
              [Ïƒâ€²A*â‰¡Ïƒâ€²Aâ€²*] = irrelevanceEqâ€³ (substConsId A) (substConsId Aâ€²)
                                            [Ïƒâ€²A*]â€² [Ïƒâ€²Aâ‚Š] [Ïƒâ€²A*â‰¡Ïƒâ€²Aâ€²*]â€²
              [Ïƒâ€²Aâ€²tâ€²â‰¡Ïƒâ€²Aâ€²*] = irrelevanceEqâ€³ (PE.sym (singleSubstComp _ Ïƒâ€² Aâ€²))
                                              (substConsId {t = starÊ·} Aâ€²)
                                              [Ïƒâ€²Aâ€²tâ€²]â€² [Ïƒâ€²Aâ€²tâ€²] [Ïƒâ€²Aâ€²tâ€²â‰¡Ïƒâ€²Aâ€²kâ€²]â€²
              [ÏƒAtâ‰¡Ïƒâ€²Aâ€²tâ€²] = transEq [Aâ‚œ] [Ïƒâ€²Aâ‚Š] [Ïƒâ€²Aâ€²tâ€²] [ÏƒAtâ‰¡Ïƒâ€²A*]
                              (transEq [Ïƒâ€²Aâ‚Š] [Ïƒâ€²Aâ€²â‚Š]â€² [Ïƒâ€²Aâ€²tâ€²] [Ïƒâ€²A*â‰¡Ïƒâ€²Aâ€²*]
                                (symEq [Ïƒâ€²Aâ€²tâ€²] [Ïƒâ€²Aâ€²â‚Š]â€² [Ïƒâ€²Aâ€²tâ€²â‰¡Ïƒâ€²Aâ€²*]))
              [urâ‚œâ€²â‰¡uâ€²] = convEqTermâ‚‚ [Aâ‚œ] [Ïƒâ€²Aâ€²tâ€²] [ÏƒAtâ‰¡Ïƒâ€²Aâ€²tâ€²] [urâ‚œâ€²â‰¡uâ€²]â€²

          in  [urâ‚œ] , transEqTerm [Aâ‚œ] [urâ‚œâ‰¡u]
                        (transEqTerm [Aâ‚œ] [Ïƒuâ‰¡Ïƒâ€²u]
                        (transEqTerm [Aâ‚œ] [Ïƒâ€²uâ‰¡Ïƒâ€²uâ€²]
                          (symEqTerm [Aâ‚œ] [urâ‚œâ€²â‰¡uâ€²])))
        (ne (neNfâ‚œâ‚Œ neK neKâ€² k~kâ€²)) â†’
          let âŠ¢urâ‚– = unitrecâ±¼ âŠ¢ÏƒA âŠ¢k âŠ¢Ïƒu ok
              âŠ¢ÏƒAâ‰…ÏƒA = escapeEq [ÏƒA] (reflEq [ÏƒA])
              âŠ¢Ïƒuâ‰…Ïƒu = escapeTermEq [ÏƒAâ‚Š] (reflEqTerm [ÏƒAâ‚Š] [Ïƒu])
              k~k = ~-trans k~kâ€² (~-sym k~kâ€²)
              [ÏƒAk] = irrelevanceâ€² (PE.sym (singleSubstComp k Ïƒ A)) [ÏƒAk]â€²
              [urâ‚–]â€² = neuTerm [ÏƒAk] (unitrecâ‚™ neK) âŠ¢urâ‚–
                               (~-unitrec âŠ¢ÏƒAâ‰…ÏƒA k~k âŠ¢Ïƒuâ‰…Ïƒu ok)
              [urâ‚–] = convTermâ‚‚ [ÏƒAt] [ÏƒAk] [ÏƒAtâ‰¡ÏƒAk] [urâ‚–]â€²
              [urâ‚œ]â€² , [urâ‚œâ‰¡urâ‚–]â€² = redSubst*Term redâ‚ [ÏƒAt] [urâ‚–]
              [urâ‚œ] = irrelevanceTermâ€² (PE.sym (singleSubstLift A t))
                                       [ÏƒAt] [Aâ‚œ] [urâ‚œ]â€²
              [urâ‚œâ‰¡urâ‚–] = irrelevanceEqTermâ€² (PE.sym (singleSubstLift A t))
                                             [ÏƒAt] [Aâ‚œ] [urâ‚œâ‰¡urâ‚–]â€²

              âŠ¢urâ‚–â€² = unitrecâ±¼ âŠ¢Ïƒâ€²Aâ€² âŠ¢kâ€² âŠ¢Ïƒâ€²uâ€² ok
              âŠ¢Ïƒâ€²Aâ€²â‰…Ïƒâ€²Aâ€² = escapeEq [Ïƒâ€²Aâ€²] (reflEq [Ïƒâ€²Aâ€²])
              âŠ¢Ïƒâ€²uâ€²â‰…Ïƒâ€²uâ€² = escapeTermEq [Ïƒâ€²Aâ€²â‚Š] (reflEqTerm [Ïƒâ€²Aâ€²â‚Š] [Ïƒâ€²uâ€²])
              kâ€²~kâ€² = ~-trans (~-sym k~kâ€²) k~kâ€²
              [Ïƒâ€²Aâ€²kâ€²]â€² = projâ‚ (unwrap [Aâ€²] {Ïƒ = consSubst Ïƒâ€² kâ€²} âŠ¢Î” ([Ïƒâ€²] , [kâ€²]))
              [Ïƒâ€²Aâ€²kâ€²] = irrelevanceâ€² (PE.sym (singleSubstComp kâ€² Ïƒâ€² Aâ€²)) [Ïƒâ€²Aâ€²kâ€²]â€²
              [urâ‚–â€²]â€² = neuTerm [Ïƒâ€²Aâ€²kâ€²] (unitrecâ‚™ neKâ€²) âŠ¢urâ‚–â€²
                                (~-unitrec âŠ¢Ïƒâ€²Aâ€²â‰…Ïƒâ€²Aâ€² kâ€²~kâ€² âŠ¢Ïƒâ€²uâ€²â‰…Ïƒâ€²uâ€² ok)
              [urâ‚–â€²] = convTermâ‚‚ [Ïƒâ€²Aâ€²tâ€²] [Ïƒâ€²Aâ€²kâ€²] [Ïƒâ€²Aâ€²tâ€²â‰¡Ïƒâ€²Aâ€²kâ€²] [urâ‚–â€²]â€²
              [urâ‚œâ€²] , [urâ‚œâ€²â‰¡urâ‚–â€²]â€² = redSubst*Term redâ‚‚ [Ïƒâ€²Aâ€²tâ€²] [urâ‚–â€²]
              [Ïƒâ€²Akâ€²â‰¡Ïƒâ€²Aâ€²kâ€²]â€² = [Aâ‰¡Aâ€²] {Ïƒ = consSubst Ïƒâ€² kâ€²} âŠ¢Î” ([Ïƒâ€²] , [kâ€²])
              [Ïƒâ€²Akâ€²] = projâ‚ (unwrap [A] {Ïƒ = consSubst Ïƒâ€² kâ€²} âŠ¢Î” ([Ïƒâ€²] , [kâ€²]))
              [ÏƒAkâ‰¡Ïƒâ€²Aâ€²kâ€²]â€² = transEq [ÏƒAk]â€² [Ïƒâ€²Akâ€²] [Ïƒâ€²Aâ€²kâ€²]â€² [ÏƒAkâ‰¡Ïƒâ€²Akâ€²]â€² [Ïƒâ€²Akâ€²â‰¡Ïƒâ€²Aâ€²kâ€²]â€²
              [ÏƒAkâ‰¡Ïƒâ€²Aâ€²kâ€²] = irrelevanceEqâ€³ (PE.sym (singleSubstComp k Ïƒ A))
                                            (PE.sym (singleSubstComp kâ€² Ïƒâ€² Aâ€²))
                                            [ÏƒAk]â€² [ÏƒAk] [ÏƒAkâ‰¡Ïƒâ€²Aâ€²kâ€²]â€²
              [ÏƒAtâ‰¡Ïƒâ€²Aâ€²tâ€²]â€² = transEq [ÏƒAt] [ÏƒAk] [Ïƒâ€²Aâ€²tâ€²] [ÏƒAtâ‰¡ÏƒAk]
                               (transEq [ÏƒAk] [Ïƒâ€²Aâ€²kâ€²] [Ïƒâ€²Aâ€²tâ€²] [ÏƒAkâ‰¡Ïƒâ€²Aâ€²kâ€²]
                                 (symEq [Ïƒâ€²Aâ€²tâ€²] [Ïƒâ€²Aâ€²kâ€²] [Ïƒâ€²Aâ€²tâ€²â‰¡Ïƒâ€²Aâ€²kâ€²]))
              [ÏƒAtâ‰¡Ïƒâ€²Aâ€²tâ€²] = irrelevanceEqâ€² (PE.sym (singleSubstLift A _))
                                            [ÏƒAt] [Aâ‚œ] [ÏƒAtâ‰¡Ïƒâ€²Aâ€²tâ€²]â€²
              [urâ‚œâ€²â‰¡urâ‚–â€²] = convEqTermâ‚‚ [Aâ‚œ] [Ïƒâ€²Aâ€²tâ€²] [ÏƒAtâ‰¡Ïƒâ€²Aâ€²tâ€²] [urâ‚œâ€²â‰¡urâ‚–â€²]â€²

              [â‡‘Ïƒâ‰¡â‡‘Ïƒâ€²] = liftSubstSEq [Î“] âŠ¢Î” [Unit] [Ïƒ] [Ïƒâ‰¡Ïƒâ€²]
              [Ïƒâ€²A] = projâ‚ (unwrap [A] {Ïƒ = liftSubst Ïƒâ€²} (âŠ¢Î” âˆ™ Unitâ±¼ âŠ¢Î” ok) [â‡‘Ïƒâ€²])
              [ÏƒAâ‰¡Ïƒâ€²A] = projâ‚‚ (unwrap [A] {Ïƒ = liftSubst Ïƒ} (âŠ¢Î” âˆ™ Unitâ±¼ âŠ¢Î” ok) [â‡‘Ïƒ])
                               {Ïƒâ€² = liftSubst Ïƒâ€²} [â‡‘Ïƒâ€²] [â‡‘Ïƒâ‰¡â‡‘Ïƒâ€²]
              [Ïƒâ€²Aâ‰¡Ïƒâ€²Aâ€²] = [Aâ‰¡Aâ€²] {Ïƒ = liftSubst Ïƒâ€²} (âŠ¢Î” âˆ™ Unitâ±¼ âŠ¢Î” ok) [â‡‘Ïƒâ€²]
              [ÏƒAâ‰¡Ïƒâ€²Aâ€²] = transEq [ÏƒA] [Ïƒâ€²A] [Ïƒâ€²Aâ€²] [ÏƒAâ‰¡Ïƒâ€²A] [Ïƒâ€²Aâ‰¡Ïƒâ€²Aâ€²]
              âŠ¢ÏƒAâ‰…Ïƒâ€²Aâ€² = escapeEq [ÏƒA] [ÏƒAâ‰¡Ïƒâ€²Aâ€²]
              [Ïƒâ€²Aâ‚Š]â€² = projâ‚ (unwrap [Aâ‚Š] âŠ¢Î” [Ïƒâ€²])
              [ÏƒAâ‚Šâ‰¡Ïƒâ€²Aâ‚Š] = projâ‚‚ (unwrap [Aâ‚Š] âŠ¢Î” [Ïƒ]) [Ïƒâ€²] [Ïƒâ‰¡Ïƒâ€²]
              [Ïƒâ€²uâ‰¡Ïƒâ€²uâ€²]â€³ = convEqTermâ‚‚ [ÏƒAâ‚Š]â€² [Ïƒâ€²Aâ‚Š]â€² [ÏƒAâ‚Šâ‰¡Ïƒâ€²Aâ‚Š] [Ïƒâ€²uâ‰¡Ïƒâ€²uâ€²]â€²
              [Ïƒuâ‰¡Ïƒâ€²uâ€²]â€² = transEqTerm [ÏƒAâ‚Š]â€² [Ïƒuâ‰¡Ïƒâ€²u]â€² [Ïƒâ€²uâ‰¡Ïƒâ€²uâ€²]â€³
              [Ïƒuâ‰¡Ïƒâ€²uâ€²] = irrelevanceEqTermâ€² (singleSubstLift A _)
                                             [ÏƒAâ‚Š]â€² [ÏƒAâ‚Š] [Ïƒuâ‰¡Ïƒâ€²uâ€²]â€²
              âŠ¢Ïƒuâ‰…Ïƒâ€²uâ€² = escapeTermEq [ÏƒAâ‚Š] [Ïƒuâ‰¡Ïƒâ€²uâ€²]

              urâ‚–~urâ‚–â€² = ~-unitrec âŠ¢ÏƒAâ‰…Ïƒâ€²Aâ€² k~kâ€² âŠ¢Ïƒuâ‰…Ïƒâ€²uâ€² ok
              âŠ¢ÏƒAkâ‰…Ïƒâ€²Aâ€²kâ€² = â‰…-eq (escapeEq [ÏƒAk] [ÏƒAkâ‰¡Ïƒâ€²Aâ€²kâ€²])
              [urâ‚–â‰¡urâ‚–â€²]â€² = neuEqTerm [ÏƒAk] (unitrecâ‚™ neK) (unitrecâ‚™ neKâ€²)
                                      âŠ¢urâ‚– (conv âŠ¢urâ‚–â€² (sym âŠ¢ÏƒAkâ‰…Ïƒâ€²Aâ€²kâ€²)) urâ‚–~urâ‚–â€²
              [ÏƒAtâ‰¡ÏƒAk]â€³ = irrelevanceEqâ€² (PE.sym (singleSubstLift A _))
                                          [ÏƒAt] [Aâ‚œ] [ÏƒAtâ‰¡ÏƒAk]
              [urâ‚–â‰¡urâ‚–â€²] = convEqTermâ‚‚ [Aâ‚œ] [ÏƒAk] [ÏƒAtâ‰¡ÏƒAk]â€³ [urâ‚–â‰¡urâ‚–â€²]â€²

          in  [urâ‚œ] , transEqTerm [Aâ‚œ] [urâ‚œâ‰¡urâ‚–]
                       (transEqTerm [Aâ‚œ] [urâ‚–â‰¡urâ‚–â€²] (symEqTerm [Aâ‚œ] [urâ‚œâ€²â‰¡urâ‚–â€²])))

unitrecáµ› : {Î“ : Con Term n}
         â†’ ([Î“] : âŠ©áµ› Î“)
           (ok : UnitÊ·-allowed)
         â†’ let [Unit] = Unitáµ› [Î“] ok in
           ([A] : Î“ âˆ™ UnitÊ· âŠ©áµ›âŸ¨ l âŸ© A / [Î“] âˆ™ [Unit])
           ([t] : Î“ âŠ©áµ›âŸ¨ l âŸ© t âˆ· UnitÊ· / [Î“] / [Unit])
           ([u] : Î“ âŠ©áµ›âŸ¨ l âŸ© u âˆ· A [ starÊ· ]â‚€ / [Î“]
                / substS [Î“] [Unit] [A] (staráµ› {l = l} [Î“] ok))
         â†’ Î“ âŠ©áµ›âŸ¨ l âŸ© unitrec p q A t u âˆ· A [ t ]â‚€ / [Î“] / substS [Î“] [Unit] [A] [t]
unitrecáµ› {n} {l} {A} {t} {u} {Î“} [Î“] ok [A] [t] [u] {k} {Î”} {Ïƒ} âŠ¢Î” [Ïƒ] =
  let [Unit] = Unitáµ› [Î“] ok
      [Aâ‰¡A] = refláµ› ([Î“] âˆ™ [Unit]) [A]
      [tâ‰¡t] = refláµ—áµ› {t = t} [Î“] [Unit] [t]
      [uâ‰¡u] = refláµ—áµ› {t = u} [Î“] (substS [Î“] [Unit] [A] (staráµ› {l = l} [Î“] ok)) [u]
      [Ïƒâ‰¡Ïƒ] = reflSubst [Î“] âŠ¢Î” [Ïƒ]
      _ , [ur] , _ = unitrecáµ›â€² {t = t} {t} {u} {u} [Î“] ok [A] [A] [Aâ‰¡A]
                                  [t] [t] [tâ‰¡t] [u] [u] [uâ‰¡u] âŠ¢Î” [Ïƒ] [Ïƒ] [Ïƒâ‰¡Ïƒ]
  in  [ur] , Î» [Ïƒâ€²] [Ïƒâ‰¡Ïƒâ€²] â†’
    projâ‚‚ (projâ‚‚ (unitrecáµ›â€² {t = t} {t} {u} {u} [Î“] ok [A] [A] [Aâ‰¡A]
                            [t] [t] [tâ‰¡t] [u] [u] [uâ‰¡u] âŠ¢Î” [Ïƒ] [Ïƒâ€²] [Ïƒâ‰¡Ïƒâ€²]))

unitrec-congáµ› : {Î“ : Con Term n}
                ([Î“] : âŠ©áµ› Î“)
                (ok : UnitÊ·-allowed)
              â†’ let [Unit] = Unitáµ› [Î“] ok in
                ([A] : Î“ âˆ™ UnitÊ· âŠ©áµ›âŸ¨ l âŸ© A / [Î“] âˆ™ [Unit])
                ([Aâ€²] : Î“ âˆ™ UnitÊ· âŠ©áµ›âŸ¨ l âŸ© Aâ€² / [Î“] âˆ™ [Unit])
                ([Aâ‰¡Aâ€²] : Î“ âˆ™ UnitÊ· âŠ©áµ›âŸ¨ l âŸ© A â‰¡ Aâ€² / [Î“] âˆ™ [Unit] / [A])
                ([t] : Î“ âŠ©áµ›âŸ¨ l âŸ© t âˆ· UnitÊ· / [Î“] / [Unit])
                ([tâ€²] : Î“ âŠ©áµ›âŸ¨ l âŸ© tâ€² âˆ· UnitÊ· / [Î“] / [Unit])
                ([tâ‰¡tâ€²] : Î“ âŠ©áµ›âŸ¨ l âŸ© t â‰¡ tâ€² âˆ· UnitÊ· / [Î“] / [Unit])
              â†’ let [Aâ‚Š] = substS [Î“] [Unit] [A] (staráµ› {l = l} [Î“] ok) in
                ([u] : Î“ âŠ©áµ›âŸ¨ l âŸ© u âˆ· A [ starÊ· ]â‚€ / [Î“] / [Aâ‚Š])
                ([uâ€²] : Î“ âŠ©áµ›âŸ¨ l âŸ© uâ€² âˆ· A [ starÊ· ]â‚€ / [Î“] / [Aâ‚Š])
                ([uâ‰¡uâ€²] : Î“ âŠ©áµ›âŸ¨ l âŸ© u â‰¡ uâ€² âˆ· A [ starÊ· ]â‚€ / [Î“] / [Aâ‚Š])
              â†’ Î“ âŠ©áµ›âŸ¨ l âŸ© unitrec p q A t u â‰¡ unitrec p q Aâ€² tâ€² uâ€² âˆ· A [ t ]â‚€ / [Î“] / substS [Î“] [Unit] [A] [t]
unitrec-congáµ› {n} {l} {A} {Aâ€²} {t} {tâ€²} {u} {uâ€²} {Î“}
              [Î“] ok [A] [Aâ€²] [Aâ‰¡Aâ€²] [t] [tâ€²] [tâ‰¡tâ€²]
              [u] [uâ€²] [uâ‰¡uâ€²] {k} {Î”} {Ïƒ} âŠ¢Î” [Ïƒ] =
  let [Unit] = Unitáµ› [Î“] ok
      [star] = staráµ› {l = l} [Î“] ok
      [Unitâ‰¡Unit] = refláµ› [Î“] [Unit]
      [starâ‰¡star] = refláµ—áµ› {t = starÊ·} [Î“] [Unit] [star]
      [Aâ‚Š] = substS [Î“] [Unit] [A] [star]
      [Aâ€²â‚Š] = substS [Î“] [Unit] [Aâ€²] [star]
      [Aâ‚Šâ‰¡Aâ€²â‚Š] = substSEq {t = starÊ·} {starÊ·} [Î“] [Unit] [Unit] [Unitâ‰¡Unit]
                          [A] [Aâ€²] [Aâ‰¡Aâ€²] [star] [star] [starâ‰¡star]
      [uâ€²]â€² = conváµ› {t = uâ€²} [Î“] [Aâ‚Š] [Aâ€²â‚Š] [Aâ‚Šâ‰¡Aâ€²â‚Š] [uâ€²]
  in  projâ‚‚ (projâ‚‚ (unitrecáµ›â€² {t = t} {tâ€²} {u} {uâ€²} [Î“] ok [A] [Aâ€²] [Aâ‰¡Aâ€²]
                              [t] [tâ€²] [tâ‰¡tâ€²] [u] [uâ€²]â€² [uâ‰¡uâ€²] âŠ¢Î” [Ïƒ] [Ïƒ]
                              (reflSubst [Î“] âŠ¢Î” [Ïƒ])))
