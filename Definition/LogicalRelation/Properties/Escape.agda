------------------------------------------------------------------------
-- Escaping the logical relation
------------------------------------------------------------------------

open import Definition.Typed.EqualityRelation
open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.LogicalRelation.Properties.Escape
  {a} {M : Set a}
  {ùïÑ : Modality M}
  (R : Type-restrictions ùïÑ)
  {{eqrel : EqRelSet R}}
  where

open EqRelSet {{...}}
open Type-restrictions R

open import Definition.Untyped M hiding (K)
open import Definition.Untyped.Neutral M type-variant
open import Definition.Typed R
open import Definition.Typed.Inversion R
open import Definition.Typed.Properties R
open import Definition.Typed.Well-formed R
open import Definition.LogicalRelation R ‚¶É eqrel ‚¶Ñ
open import Definition.LogicalRelation.Properties.Reflexivity R
open import Definition.LogicalRelation.Properties.Whnf R

open import Tools.Function
open import Tools.Nat
open import Tools.Product

private
  variable
    n : Nat
    Œì : Con Term n
    A B t u : Term n
    l l‚Ä≤ : Universe-level
    b : BinderMode
    s : Strength
    p q : M

-- Reducible level equalities are well-formed.
mutual
  escapeLevelEq
    : Œì ‚ä©Level t ‚â° u ‚à∑Level
    ‚Üí Œì ‚ä¢ t ‚âÖ u ‚à∑ Level
  escapeLevelEq (Level‚Çú‚Çå k k‚Ä≤ D D‚Ä≤ prop) =
    let lk , lk‚Ä≤ = lsplit prop in
    ‚âÖ‚Çú-red (id (Level‚±º (wfTerm (redFirst*Term D))) , Level‚Çô) (D , lk) (D‚Ä≤ , lk‚Ä≤)
      (escapeLevel-prop (wfTerm (redFirst*Term D)) prop)

  escapeLevel-prop
    : ‚ä¢ Œì
    ‚Üí [Level]-prop Œì t u
    ‚Üí Œì ‚ä¢ t ‚âÖ u ‚à∑ Level
  escapeLevel-prop ‚ä¢Œì zero·µò·µ£ = ‚âÖ‚Çú-zero·µòrefl ‚ä¢Œì
  escapeLevel-prop ‚ä¢Œì (suc·µò·µ£ x) = ‚âÖ‚Çú-suc·µò-cong (escapeLevelEq x)
  escapeLevel-prop ‚ä¢Œì (ne n) = escapeSneEq n

  -- Reducible semi-neutral equalities are well-formed.
  escapeSneEq
    : Œì ‚ä©sne t ‚â° u
    ‚Üí Œì ‚ä¢ t ‚âÖ u ‚à∑ Level
  escapeSneEq (sne‚Çú‚Çå n‚ÇÅ n‚ÇÇ (max·µò·µ£ x y)) = ‚âÖ‚Çú-max·µò-cong (escapeLevelEq x) (escapeLevelEq y)
  escapeSneEq (sne‚Çú‚Çå n‚ÇÅ n‚ÇÇ (ne (neNf‚Çú‚Çå _ _ _ t~u))) = ~-to-‚âÖ‚Çú t~u

-- Reducible levels are well-formed.
escapeLevel
  : Œì ‚ä©Level t ‚à∑Level
  ‚Üí Œì ‚ä¢ t ‚à∑ Level
escapeLevel (Level‚Çú‚Çå k k‚Ä≤ D D‚Ä≤ prop) = redFirst*Term D

-- Reducible types are well-formed.
escape : ‚àÄ {l A} ‚Üí Œì ‚ä©‚ü® l ‚ü© A ‚Üí Œì ‚ä¢ A
escape (Level·µ£ D) = redFirst* D
escape (U·µ£‚Ä≤ _ _ _ D) = redFirst* D
escape (‚Ñï·µ£ D) = redFirst* D
escape (Empty·µ£ D) = redFirst* D
escape (Unit·µ£‚Ä≤ _ _ _ D _) = redFirst* D
escape (ne‚Ä≤ _ _ D _ _) = redFirst* D
escape (B·µ£‚Ä≤ _ _ _ D _ _ _ _ _) = redFirst* D
escape (Id·µ£ ‚ä©A) = redFirst* (_‚ä©‚ÇóId_.‚áí*Id ‚ä©A)

-- Reducible type equality is contained in the equality relation.
escapeEq :
  (‚ä©A : Œì ‚ä©‚ü® l ‚ü© A) ‚Üí
  Œì ‚ä©‚ü® l ‚ü© A ‚â° B / ‚ä©A ‚Üí
  Œì ‚ä¢ A ‚âÖ B

-- Reducible term equality is contained in the equality relation.
escapeTermEq :
  (‚ä©A : Œì ‚ä©‚ü® l ‚ü© A) ‚Üí
  Œì ‚ä©‚ü® l ‚ü© t ‚â° u ‚à∑ A / ‚ä©A ‚Üí
  Œì ‚ä¢ t ‚âÖ u ‚à∑ A

-- Reducible terms are well-formed.
escapeTerm : ‚àÄ {l A t} ‚Üí ([A] : Œì ‚ä©‚ü® l ‚ü© A)
              ‚Üí Œì ‚ä©‚ü® l ‚ü© t ‚à∑ A / [A]
              ‚Üí Œì ‚ä¢ t ‚à∑ A
escapeTerm ‚ä©A ‚ä©t = wf-‚ä¢‚â°‚à∑ (‚âÖ‚Çú-eq (escapeTermEq ‚ä©A ‚ä©t)) .proj‚ÇÇ .proj‚ÇÅ

-- If there is a well-formed equality between two identity types,
-- then the corresponding reduced identity types are equal.
Id‚âÖId :
  {‚ä©A : Œì ‚ä©‚Ä≤‚ü® l ‚ü©Id A}
  (A‚â°B : Œì ‚ä©‚ü® l ‚ü© A ‚â° B / Id·µ£ ‚ä©A) ‚Üí
  let open _‚ä©‚ÇóId_ ‚ä©A
      open _‚ä©‚ÇóId_‚â°_/_ A‚â°B
  in
  Œì ‚ä¢ Id Ty lhs rhs ‚âÖ Id Ty‚Ä≤ lhs‚Ä≤ rhs‚Ä≤
Id‚âÖId {‚ä©A = ‚ä©A} A‚â°B =
  ‚âÖ-Id-cong
    (escapeEq ‚ä©Ty Ty‚â°Ty‚Ä≤)
    (escapeTermEq ‚ä©Ty lhs‚â°lhs‚Ä≤)
    (escapeTermEq ‚ä©Ty rhs‚â°rhs‚Ä≤)
  where
  open _‚ä©‚ÇóId_ ‚ä©A
  open _‚ä©‚ÇóId_‚â°_/_ A‚â°B

escapeEq (Level·µ£ D) D‚Ä≤ =
  ‚âÖ-red (D , Level‚Çô) (D‚Ä≤ , Level‚Çô) (‚âÖ-Levelrefl (wf (redFirst* D)))
escapeEq (U·µ£‚Ä≤ _ _ _ D) (U‚Çå k‚Ä≤ D‚ÇÅ k‚â°k‚Ä≤) =
  ‚âÖ-red (D , U‚Çô) (D‚ÇÅ , U‚Çô) (‚âÖ-univ (‚âÖ‚Çú-U-cong (escapeLevelEq k‚â°k‚Ä≤)))
escapeEq (‚Ñï·µ£ D) D‚Ä≤ =
  ‚âÖ-red (D , ‚Ñï‚Çô) (D‚Ä≤ , ‚Ñï‚Çô) (‚âÖ-‚Ñïrefl (wf (redFirst* D)))
escapeEq (Empty·µ£ D) D‚Ä≤ =
  ‚âÖ-red (D , Empty‚Çô) (D‚Ä≤ , Empty‚Çô) (‚âÖ-Emptyrefl (wfEq (subset* D)))
escapeEq (Unit·µ£‚Ä≤ _ _ _ D ok) (Unit‚Çå _ D‚Ä≤ k‚â°k‚Ä≤) =
  ‚âÖ-red (D , Unit‚Çô) (D‚Ä≤ , Unit‚Çô) (‚âÖ-Unit-cong (escapeLevelEq k‚â°k‚Ä≤) ok)
escapeEq (ne‚Ä≤ _ _ D neK _) (ne‚Çå _ _ D‚Ä≤ neM K‚â°M) =
  ‚âÖ-red (D , ne! neK) (D‚Ä≤ , ne! neM) K‚â°M
escapeEq (B·µ£‚Ä≤ W _ _ D _ _ _ _ _) (B‚Çå _ _ D‚Ä≤ A‚â°B _ _) =
  ‚âÖ-red (D , ‚ü¶ W ‚üß‚Çô) (D‚Ä≤ , ‚ü¶ W ‚üß‚Çô) A‚â°B
escapeEq (Id·µ£ ‚ä©A) A‚â°B =
  ‚âÖ-red (_‚ä©‚ÇóId_.‚áí*Id ‚ä©A , Id‚Çô) (_‚ä©‚ÇóId_‚â°_/_.‚áí*Id‚Ä≤ A‚â°B , Id‚Çô) (Id‚âÖId A‚â°B)

escapeTermEq (Level·µ£ D) (Level‚Çú‚Çå k k‚Ä≤ d d‚Ä≤ prop) =
  let lk , lk‚Ä≤ = lsplit prop
  in ‚âÖ‚Çú-red (D , Level‚Çô) (d , lk) (d‚Ä≤ , lk‚Ä≤)
      (escapeLevel-prop (wf (redFirst* D)) prop)
escapeTermEq (U·µ£‚Ä≤ _ _ _ D) (U‚Çú‚Çå A B d d‚Ä≤ typeA typeB A‚â°B [A] [B] [A‚â°B]) =
  ‚âÖ‚Çú-red (D , U‚Çô) (d , typeWhnf typeA) (d‚Ä≤ , typeWhnf typeB)  A‚â°B
escapeTermEq (‚Ñï·µ£ D) (‚Ñï‚Çú‚Çå _ _ d d‚Ä≤ k‚â°k‚Ä≤ prop) =
  let natK , natK‚Ä≤ = split prop
  in  ‚âÖ‚Çú-red (D , ‚Ñï‚Çô) (d , naturalWhnf natK)
        (d‚Ä≤ , naturalWhnf natK‚Ä≤) k‚â°k‚Ä≤
escapeTermEq (Empty·µ£ D) (Empty‚Çú‚Çå k k‚Ä≤ d d‚Ä≤ k‚â°k‚Ä≤ prop) =
  let natK , natK‚Ä≤ = esplit prop
  in  ‚âÖ‚Çú-red (D , Empty‚Çô) (d , ne! natK) (d‚Ä≤ , ne! natK‚Ä≤) k‚â°k‚Ä≤
escapeTermEq (Unit·µ£‚Ä≤ k [k] k< D ok) (Unit‚Çú‚Çå _ _ d d‚Ä≤ prop) =
  let _ , _ , ‚ä¢t‚Ä≤ = wf-‚ä¢‚â°‚à∑ (subset*Term (d .proj‚ÇÅ))
      _ , _ , ‚ä¢u‚Ä≤ = wf-‚ä¢‚â°‚à∑ (subset*Term (d‚Ä≤ .proj‚ÇÅ))
  in
  ‚âÖ‚Çú-red (D , Unit‚Çô) d d‚Ä≤
    (case prop of Œª where
       (Unit‚Çú‚ÇåÀ¢ Œ∑) ‚Üí ‚âÖ‚Çú-Œ∑-unit (escapeLevel [k]) ‚ä¢t‚Ä≤ ‚ä¢u‚Ä≤ ok Œ∑
       (Unit‚Çú‚Çå ∑ (star·µ£ k‚â°k‚Ä≤ k‚Ä≤‚â°k‚Ä≥) _) ‚Üí
         ‚âÖ-conv
           (‚âÖ‚Çú-star-cong (escapeLevelEq k‚Ä≤‚â°k‚Ä≥) ok)
           (Unit-cong (‚âÖ‚Çú-eq (‚âÖ‚Çú-sym (escapeLevelEq k‚â°k‚Ä≤))) ok)
       (Unit‚Çú‚Çå ∑ (ne (neNf‚Çú‚Çå _ _ _ t‚Ä≤~u‚Ä≤)) _) ‚Üí ~-to-‚âÖ‚Çú t‚Ä≤~u‚Ä≤)
escapeTermEq (ne‚Ä≤ _ _ D neK _) (ne‚Çú‚Çå _ _ d d‚Ä≤ (neNf‚Çú‚Çå _ neT neU t‚â°u)) =
  ‚âÖ‚Çú-red (D , ne! neK) (d , ne! neT) (d‚Ä≤ , ne! neU) (~-to-‚âÖ‚Çú t‚â°u)
escapeTermEq
  (B·µ£‚Ä≤ BŒ†! _ _ D _ _ _ _ _) (Œ†‚Çú‚Çå _ _ d d‚Ä≤ funcF funcG f‚â°g _) =
  ‚âÖ‚Çú-red (D , Œ†Œ£‚Çô) (d , functionWhnf funcF) (d‚Ä≤ , functionWhnf funcG)
    f‚â°g
escapeTermEq
  (B·µ£‚Ä≤ BŒ£! _ _ D _ _ _ _ _) (Œ£‚Çú‚Çå _ _ d d‚Ä≤ pProd rProd p‚âÖr _) =
  ‚âÖ‚Çú-red (D , Œ†Œ£‚Çô) (d , productWhnf pProd) (d‚Ä≤ , productWhnf rProd) p‚âÖr
escapeTermEq {Œì = Œì} (Id·µ£ ‚ä©A) t‚â°u@(_ , _ , t‚áí*t‚Ä≤ , u‚áí*u‚Ä≤ , _) =
  case ‚ä©Id‚â°‚à∑-view-inhabited ‚ä©A t‚â°u of Œª where
    (ne _ t‚Ä≤-n u‚Ä≤-n t‚Ä≤~u‚Ä≤) ‚Üí
      lemma (ne! t‚Ä≤-n) (ne! u‚Ä≤-n) (~-to-‚âÖ‚Çú t‚Ä≤~u‚Ä≤)
    (rfl‚Çå lhs‚â°rhs) ‚Üí
      lemma rfl‚Çô rfl‚Çô
        (                                   $‚ü® ‚âÖ-Id-cong
                                                 (escapeEq ‚ä©Ty (reflEq ‚ä©Ty))
                                                 (escapeTermEq ‚ä©Ty (reflEqTerm ‚ä©Ty ‚ä©lhs))
                                                 (escapeTermEq ‚ä©Ty lhs‚â°rhs) ‚ü©
         Œì ‚ä¢ Id Ty lhs lhs ‚âÖ Id Ty lhs rhs  ‚Üí‚ü® ‚âÖ-eq ‚ü©
         Œì ‚ä¢ Id Ty lhs lhs ‚â° Id Ty lhs rhs  ‚Üí‚ü® ‚âÖ-conv (‚âÖ‚Çú-rflrefl (escapeTerm ‚ä©Ty ‚ä©lhs)) ‚ü©
         (Œì ‚ä¢‚âÖ rfl ‚à∑ Id Ty lhs rhs)         ‚ñ°)
  where
  open _‚ä©‚ÇóId_ ‚ä©A
  lemma = Œª t‚Ä≤-whnf u‚Ä≤-whnf ‚Üí
            ‚âÖ‚Çú-red (‚áí*Id , Id‚Çô) (t‚áí*t‚Ä≤ , t‚Ä≤-whnf) (u‚áí*u‚Ä≤ , u‚Ä≤-whnf)

opaque

  -- If a unit type is reducible, then that unit type is allowed.

  ‚ä©Unit‚ÜíUnit-allowed :
    Œì ‚ä©‚ü® l‚Ä≤ ‚ü© Unit s t ‚Üí Unit-allowed s
  ‚ä©Unit‚ÜíUnit-allowed = inversion-Unit-allowed ‚àò‚Üí escape

opaque

  -- If the type Œ†Œ£‚ü®¬†b¬†‚ü©¬†p¬†,¬†q¬†‚ñ∑¬†A¬†‚ñπ¬†B is in the logical relation, then
  -- it is allowed.

  ‚ä©Œ†Œ£‚ÜíŒ†Œ£-allowed :
    Œì ‚ä©‚ü® l ‚ü© Œ†Œ£‚ü® b ‚ü© p , q ‚ñ∑ A ‚ñπ B ‚Üí
    Œ†Œ£-allowed b p q
  ‚ä©Œ†Œ£‚ÜíŒ†Œ£-allowed = proj‚ÇÇ ‚àò‚Üí proj‚ÇÇ ‚àò‚Üí inversion-Œ†Œ£ ‚àò‚Üí escape
