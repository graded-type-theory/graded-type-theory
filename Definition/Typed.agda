------------------------------------------------------------------------
-- Typing and reduction relations
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Modality

module Definition.Typed
  {РёЊ} {M : Set РёЊ}
  {­ЮЋё : Modality M}
  (R : Type-restrictions ­ЮЋё)
  where

open Type-restrictions R

open import Definition.Untyped M
import Definition.Untyped.Erased ­ЮЋё as Erased
open import Definition.Untyped.Neutral M type-variant

open import Tools.Fin
open import Tools.Nat
open import Tools.Product
import Tools.PropositionalEquality as PE
open import Tools.Relation


infix 24 РѕЎ_

private
  variable
    n : Nat
    ╬Њ : Con Term _
    A AРѓЂ AРѓѓ AРђ▓ B BРѓЂ BРѓѓ C E F FРђ▓ G H : Term _
    a f g l lРѓЂ lРѓѓ lРѓЃ lРђ▓ nРђ▓ s sРђ▓ t tРѓЂ tРѓѓ tРђ▓ u uРѓЂ uРѓѓ uРђ▓ v vРѓЂ vРѓѓ vРђ▓ w wРѓЂ wРѓѓ wРђ▓ z zРђ▓ :
      Term _
    ¤Ѓ ¤ЃРђ▓ : Subst _ _
    x : Fin _
    p pРђ▓ q qРђ▓ r : M
    b : BinderMode
    k : Strength

-- Well-typed variables
data _Рѕи_Рѕѕ_ : (x : Fin n) (A : Term n) (╬Њ : Con Term n) Рєњ Set РёЊ where
  here  :                 x0 Рѕи wk1 A Рѕѕ ╬Њ РѕЎ A
  there : x Рѕи A Рѕѕ ╬Њ Рєњ (x +1) Рѕи wk1 A Рѕѕ ╬Њ РѕЎ B

mutual
  -- Well-formed context
  data Ріб_ : Con Term n Рєњ Set РёЊ where
    ╬х  : Ріб ╬х
    РѕЎ_ : ╬Њ Ріб A Рєњ Ріб ╬Њ РѕЎ A

  -- Well-formed type
  data _Ріб_ (╬Њ : Con Term n) : Term n Рєњ Set РёЊ where
    LevelР▒╝ : Ріб ╬Њ Рєњ ╬Њ Ріб Level
    UР▒╝     : ╬Њ Ріб l Рѕи Level
           Рєњ ╬Њ Ріб U l
    univ   : ╬Њ Ріб A Рѕи U l
           Рєњ ╬Њ Ріб A
    EmptyР▒╝ : Ріб ╬Њ Рєњ ╬Њ Ріб Empty
    UnitР▒╝  : ╬Њ Ріб l Рѕи Level Рєњ Unit-allowed k Рєњ ╬Њ Ріб Unit k l
    ╬а╬БР▒╝    : ╬Њ РѕЎ F Ріб G
           Рєњ ╬а╬Б-allowed b p q
           Рєњ ╬Њ     Ріб ╬а╬БРЪе b РЪЕ p , q Рќи F Рќ╣ G
    РёЋР▒╝     : Ріб ╬Њ Рєњ ╬Њ Ріб РёЋ
    IdР▒╝    : ╬Њ Ріб A
           Рєњ ╬Њ Ріб t Рѕи A
           Рєњ ╬Њ Ріб u Рѕи A
           Рєњ ╬Њ Ріб Id A t u

  -- Well-formed term of a type
  data _Ріб_Рѕи_ (╬Њ : Con Term n) : Term n Рєњ Term n Рєњ Set РёЊ where
    conv      : ╬Њ Ріб t Рѕи A
              Рєњ ╬Њ Ріб A РЅА B
              Рєњ ╬Њ Ріб t Рѕи B

    var       : Ріб ╬Њ
              Рєњ x Рѕи A Рѕѕ ╬Њ
              Рєњ ╬Њ Ріб var x Рѕи A

    LevelР▒╝    : Ріб ╬Њ Рєњ ╬Њ Ріб Level Рѕи U zeroрхў
    zeroрхўР▒╝    : Ріб ╬Њ
              Рєњ ╬Њ Ріб zeroрхў Рѕи Level
    sucрхўР▒╝     : ╬Њ Ріб l Рѕи Level
              Рєњ ╬Њ Ріб sucрхў l Рѕи Level
    maxрхўР▒╝     : ╬Њ Ріб lРѓЂ Рѕи Level
              Рєњ ╬Њ Ріб lРѓѓ Рѕи Level
              Рєњ ╬Њ Ріб lРѓЂ maxрхў lРѓѓ Рѕи Level

    UР▒╝        : ╬Њ Ріб l Рѕи Level
              Рєњ ╬Њ Ріб U l Рѕи U (sucрхў l)

    EmptyР▒╝    : Ріб ╬Њ Рєњ ╬Њ Ріб Empty Рѕи U zeroрхў
    emptyrecР▒╝ : ╬Њ Ріб A Рєњ ╬Њ Ріб t Рѕи Empty Рєњ ╬Њ Ріб emptyrec p A t Рѕи A

    UnitР▒╝     : ╬Њ Ріб l Рѕи Level Рєњ Unit-allowed k Рєњ ╬Њ Ріб Unit k l Рѕи U l
    starР▒╝     : ╬Њ Ріб l Рѕи Level
              Рєњ Unit-allowed k
              Рєњ ╬Њ Ріб star k l Рѕи Unit k l
    unitrecР▒╝  : ╬Њ Ріб l Рѕи Level
              Рєњ ╬Њ РѕЎ Unit╩и l Ріб A
              Рєњ ╬Њ Ріб t Рѕи Unit╩и l
              Рєњ ╬Њ Ріб u Рѕи A [ star╩и l ]Рѓђ
              Рєњ Unit╩и-allowed
              Рєњ ╬Њ Ріб unitrec p q l A t u Рѕи A [ t ]Рѓђ

    ╬а╬БР▒╝       : ╬Њ     Ріб lРѓЂ Рѕи Level
              Рєњ ╬Њ     Ріб lРѓѓ Рѕи Level
              Рєњ ╬Њ     Ріб F Рѕи U lРѓЂ
              Рєњ ╬Њ РѕЎ F Ріб G Рѕи U (wk1 lРѓѓ)
              Рєњ ╬а╬Б-allowed b p q
              Рєњ ╬Њ     Ріб ╬а╬БРЪе b РЪЕ p , q Рќи F Рќ╣ G Рѕи U (lРѓЂ maxрхў lРѓѓ)

    lamР▒╝      : ╬Њ РѕЎ F Ріб G
              Рєњ ╬Њ РѕЎ F Ріб t Рѕи G
              Рєњ ╬а-allowed p q
              Рєњ ╬Њ     Ріб lam p t Рѕи ╬а p , q Рќи F Рќ╣ G
    _РѕўР▒╝_      : ╬Њ Ріб t Рѕи ╬а p , q Рќи F Рќ╣ G
              Рєњ ╬Њ Ріб u Рѕи F
              Рєњ ╬Њ Ріб t РѕўРЪе p РЪЕ u Рѕи G [ u ]Рѓђ

    prodР▒╝     : ╬Њ РѕЎ F Ріб G
              Рєњ ╬Њ Ріб t Рѕи F
              Рєњ ╬Њ Ріб u Рѕи G [ t ]Рѓђ
              Рєњ ╬Б-allowed k p q
              Рєњ ╬Њ Ріб prod k p t u Рѕи ╬БРЪе k РЪЕ p , q Рќи F Рќ╣ G
    fstР▒╝      : ╬Њ РѕЎ F Ріб G
              Рєњ ╬Њ Ріб t Рѕи ╬Б╦б p , q Рќи F Рќ╣ G
              Рєњ ╬Њ Ріб fst p t Рѕи F
    sndР▒╝      : ╬Њ РѕЎ F Ріб G
              Рєњ ╬Њ Ріб t Рѕи ╬Б╦б p , q Рќи F Рќ╣ G
              Рєњ ╬Њ Ріб snd p t Рѕи G [ fst p t ]Рѓђ
    prodrecР▒╝  : ╬Њ РѕЎ (╬Б╩и p , qРђ▓ Рќи F Рќ╣ G) Ріб A
              Рєњ ╬Њ Ріб t Рѕи ╬Б╩и p , qРђ▓ Рќи F Рќ╣ G
              Рєњ ╬Њ РѕЎ F РѕЎ G Ріб u Рѕи A [ prod╩и p (var x1) (var x0) ]РєЉ┬▓
              Рєњ ╬Б╩и-allowed p qРђ▓
              Рєњ ╬Њ Ріб prodrec r p q A t u Рѕи A [ t ]Рѓђ

    РёЋР▒╝        : Ріб ╬Њ Рєњ ╬Њ Ріб РёЋ Рѕи U zeroрхў
    zeroР▒╝     : Ріб ╬Њ
              Рєњ ╬Њ Ріб zero Рѕи РёЋ
    sucР▒╝      : Рѕђ {n}
              Рєњ ╬Њ Ріб     n Рѕи РёЋ
              Рєњ ╬Њ Ріб suc n Рѕи РёЋ
    natrecР▒╝   : Рѕђ {n}
              Рєњ ╬Њ         Ріб z Рѕи A [ zero ]Рѓђ
              Рєњ ╬Њ РѕЎ РёЋ РѕЎ A Ріб s Рѕи A [ suc (var x1) ]РєЉ┬▓
              Рєњ ╬Њ         Ріб n Рѕи РёЋ
              Рєњ ╬Њ         Ріб natrec p q r A z s n Рѕи A [ n ]Рѓђ

    IdР▒╝       : ╬Њ Ріб A Рѕи U l
              Рєњ ╬Њ Ріб t Рѕи A
              Рєњ ╬Њ Ріб u Рѕи A
              Рєњ ╬Њ Ріб Id A t u Рѕи U l
    rflР▒╝      : ╬Њ Ріб t Рѕи A
              Рєњ ╬Њ Ріб rfl Рѕи Id A t t
    JР▒╝        : ╬Њ Ріб t Рѕи A
              Рєњ ╬Њ РѕЎ A РѕЎ Id (wk1 A) (wk1 t) (var x0) Ріб B
              Рєњ ╬Њ Ріб u Рѕи B [ t , rfl ]РѓЂРѓђ
              Рєњ ╬Њ Ріб v Рѕи A
              Рєњ ╬Њ Ріб w Рѕи Id A t v
              Рєњ ╬Њ Ріб J p q A t B u v w Рѕи B [ v , w ]РѓЂРѓђ
    KР▒╝        : ╬Њ РѕЎ Id A t t Ріб B
              Рєњ ╬Њ Ріб u Рѕи B [ rfl ]Рѓђ
              Рєњ ╬Њ Ріб v Рѕи Id A t t
              Рєњ K-allowed
              Рєњ ╬Њ Ріб K p A t B u v Рѕи B [ v ]Рѓђ
    []-congР▒╝  : ╬Њ Ріб A
              Рєњ ╬Њ Ріб t Рѕи A
              Рєњ ╬Њ Ріб u Рѕи A
              Рєњ ╬Њ Ріб v Рѕи Id A t u
              Рєњ []-cong-allowed k
              Рєњ let open Erased k in
                ╬Њ Ріб []-cong k A t u v Рѕи
                  Id (Erased A) ([ t ]) ([ u ])

  -- Type equality
  data _Ріб_РЅА_ (╬Њ : Con Term n) : Term n Рєњ Term n Рєњ Set РёЊ where
    refl   : ╬Њ Ріб A
           Рєњ ╬Њ Ріб A РЅА A
    sym    : ╬Њ Ріб A РЅА B
           Рєњ ╬Њ Ріб B РЅА A
    trans  : ╬Њ Ріб A РЅА B
           Рєњ ╬Њ Ріб B РЅА C
           Рєњ ╬Њ Ріб A РЅА C
    univ   : ╬Њ Ріб A РЅА B Рѕи U l
           Рєњ ╬Њ Ріб A РЅА B
    U-cong : ╬Њ Ріб lРѓЂ РЅА lРѓѓ Рѕи Level
           Рєњ ╬Њ Ріб U lРѓЂ РЅА U lРѓѓ
    ╬а╬Б-cong
           : ╬Њ     Ріб F РЅА H
           Рєњ ╬Њ РѕЎ F Ріб G РЅА E
           Рєњ ╬а╬Б-allowed b p q
           Рєњ ╬Њ     Ріб ╬а╬БРЪе b РЪЕ p , q Рќи F Рќ╣ G РЅА ╬а╬БРЪе b РЪЕ p , q Рќи H Рќ╣ E
    Unit-cong
           : ╬Њ Ріб lРѓЂ РЅА lРѓѓ Рѕи Level
           Рєњ Unit-allowed k
           Рєњ ╬Њ Ріб Unit k lРѓЂ РЅА Unit k lРѓѓ
    Id-cong
           : ╬Њ Ріб AРѓЂ РЅА AРѓѓ
           Рєњ ╬Њ Ріб tРѓЂ РЅА tРѓѓ Рѕи AРѓЂ
           Рєњ ╬Њ Ріб uРѓЂ РЅА uРѓѓ Рѕи AРѓЂ
           Рєњ ╬Њ Ріб Id AРѓЂ tРѓЂ uРѓЂ РЅА Id AРѓѓ tРѓѓ uРѓѓ

  -- Term equality
  data _Ріб_РЅА_Рѕи_ (╬Њ : Con Term n) : Term n Рєњ Term n Рєњ Term n Рєњ Set РёЊ where
    conv          : ╬Њ Ріб t РЅА u Рѕи A
                  Рєњ ╬Њ Ріб A РЅА B
                  Рєњ ╬Њ Ріб t РЅА u Рѕи B

    refl          : ╬Њ Ріб t Рѕи A
                  Рєњ ╬Њ Ріб t РЅА t Рѕи A
    sym           : ╬Њ Ріб A
                  Рєњ ╬Њ Ріб t РЅА u Рѕи A
                  Рєњ ╬Њ Ріб u РЅА t Рѕи A
    trans         : ╬Њ Ріб t РЅА u Рѕи A
                  Рєњ ╬Њ Ріб u РЅА v Рѕи A
                  Рєњ ╬Њ Ріб t РЅА v Рѕи A

    sucрхў-cong     : Рѕђ {t t'}
                  Рєњ ╬Њ Ріб t РЅА t' Рѕи Level
                  Рєњ ╬Њ Ріб sucрхў t РЅА sucрхў t' Рѕи Level
    maxрхў-cong     : Рѕђ {t t' u u'}
                  Рєњ ╬Њ Ріб t РЅА t' Рѕи Level
                  Рєњ ╬Њ Ріб u РЅА u' Рѕи Level
                  Рєњ ╬Њ Ріб t maxрхў u РЅА t' maxрхў u' Рѕи Level
    maxрхў-zero╦А    : ╬Њ Ріб l Рѕи Level
                  Рєњ ╬Њ Ріб zeroрхў maxрхў l РЅА l Рѕи Level
    maxрхў-zero╩│    : ╬Њ Ріб l Рѕи Level
                  Рєњ ╬Њ Ріб l maxрхў zeroрхў РЅА l Рѕи Level
    maxрхў-sucрхў     : ╬Њ Ріб lРѓЂ Рѕи Level
                  Рєњ ╬Њ Ріб lРѓѓ Рѕи Level
                  Рєњ ╬Њ Ріб sucрхў lРѓЂ maxрхў sucрхў lРѓѓ РЅА sucрхў (lРѓЂ maxрхў lРѓѓ) Рѕи Level
    maxрхў-assoc    : ╬Њ Ріб lРѓЂ Рѕи Level
                  Рєњ ╬Њ Ріб lРѓѓ Рѕи Level
                  Рєњ ╬Њ Ріб lРѓЃ Рѕи Level
                  Рєњ ╬Њ Ріб (lРѓЂ maxрхў lРѓѓ) maxрхў lРѓЃ РЅА lРѓЂ maxрхў (lРѓѓ maxрхў lРѓЃ) Рѕи Level
    maxрхў-comm     : ╬Њ Ріб lРѓЂ Рѕи Level
                  Рєњ ╬Њ Ріб lРѓѓ Рѕи Level
                  Рєњ ╬Њ Ріб lРѓЂ maxрхў lРѓѓ РЅА lРѓѓ maxрхў lРѓЂ Рѕи Level
    maxрхў-idem     : ╬Њ Ріб l Рѕи Level
                  Рєњ ╬Њ Ріб l maxрхў l РЅА l Рѕи Level
    maxрхў-sub      : ╬Њ Ріб l Рѕи Level
                  Рєњ ╬Њ Ріб l maxрхў sucрхў l РЅА sucрхў l Рѕи Level

    U-cong        : ╬Њ Ріб lРѓЂ РЅА lРѓѓ Рѕи Level
                  Рєњ ╬Њ Ріб U lРѓЂ РЅА U lРѓѓ Рѕи U (sucрхў lРѓЂ)

    emptyrec-cong : ╬Њ Ріб A РЅА B
                  Рєњ ╬Њ Ріб t РЅА u Рѕи Empty
                  Рєњ ╬Њ Ріб emptyrec p A t РЅА emptyrec p B u Рѕи A

    Unit-cong     : ╬Њ Ріб lРѓЂ РЅА lРѓѓ Рѕи Level
                  Рєњ Unit-allowed k
                  Рєњ ╬Њ Ріб Unit k lРѓЂ РЅА Unit k lРѓѓ Рѕи U lРѓЂ

    ╬и-unit        : ╬Њ Ріб l Рѕи Level
                  Рєњ ╬Њ Ріб t Рѕи Unit k l
                  Рєњ ╬Њ Ріб tРђ▓ Рѕи Unit k l
                  Рєњ Unit-allowed k
                  Рєњ Unit-with-╬и k
                  Рєњ ╬Њ Ріб t РЅА tРђ▓ Рѕи Unit k l

    star-cong     : ╬Њ Ріб l РЅА lРђ▓ Рѕи Level
                  Рєњ Unit-allowed k
                  Рєњ ╬Њ Ріб star k l РЅА star k lРђ▓ Рѕи Unit k l

    unitrec-cong  : ╬Њ Ріб l Рѕи Level
                  Рєњ ╬Њ Ріб lРђ▓ Рѕи Level
                  Рєњ ╬Њ Ріб l РЅА lРђ▓ Рѕи Level
                  Рєњ ╬Њ РѕЎ Unit╩и l Ріб A РЅА AРђ▓
                  Рєњ ╬Њ Ріб t РЅА tРђ▓ Рѕи Unit╩и l
                  Рєњ ╬Њ Ріб u РЅА uРђ▓ Рѕи A [ star╩и l ]Рѓђ
                  Рєњ Unit╩и-allowed
                  Рєњ ┬г Unit╩и-╬и
                  Рєњ ╬Њ Ріб unitrec p q l A t u РЅА unitrec p q lРђ▓ AРђ▓ tРђ▓ uРђ▓ Рѕи
                      A [ t ]Рѓђ
    unitrec-╬▓     : ╬Њ Ріб l Рѕи Level
                  Рєњ ╬Њ РѕЎ Unit╩и l Ріб A
                  Рєњ ╬Њ Ріб u Рѕи A [ star╩и l ]Рѓђ
                  Рєњ Unit╩и-allowed
                  Рєњ ┬г Unit╩и-╬и
                  Рєњ ╬Њ Ріб unitrec p q l A (star╩и l) u РЅА u Рѕи A [ star╩и l ]Рѓђ
    unitrec-╬▓-╬и   : ╬Њ Ріб l Рѕи Level
                  Рєњ ╬Њ РѕЎ Unit╩и l Ріб A
                  Рєњ ╬Њ Ріб t Рѕи Unit╩и l
                  Рєњ ╬Њ Ріб u Рѕи A [ star╩и l ]Рѓђ
                  Рєњ Unit╩и-allowed
                  Рєњ Unit╩и-╬и
                  Рєњ ╬Њ Ріб unitrec p q l A t u РЅА u Рѕи A [ t ]Рѓђ

    ╬а╬Б-cong       : ╬Њ     Ріб lРѓЂ Рѕи Level
                  Рєњ ╬Њ     Ріб lРѓѓ Рѕи Level
                  Рєњ ╬Њ     Ріб F РЅА H Рѕи U lРѓЂ
                  Рєњ ╬Њ РѕЎ F Ріб G РЅА E Рѕи U (wk1 lРѓѓ)
                  Рєњ ╬а╬Б-allowed b p q
                  Рєњ ╬Њ     Ріб ╬а╬БРЪе b РЪЕ p , q Рќи F Рќ╣ G РЅА
                            ╬а╬БРЪе b РЪЕ p , q Рќи H Рќ╣ E Рѕи U (lРѓЂ maxрхў lРѓѓ)

    app-cong      : Рѕђ {b}
                  Рєњ ╬Њ Ріб f РЅА g Рѕи ╬а p , q Рќи F Рќ╣ G
                  Рєњ ╬Њ Ріб a РЅА b Рѕи F
                  Рєњ ╬Њ Ріб f РѕўРЪе p РЪЕ a РЅА g РѕўРЪе p РЪЕ b Рѕи G [ a ]Рѓђ
    ╬▓-red         : ╬Њ РѕЎ F Ріб G
                  Рєњ ╬Њ РѕЎ F Ріб t Рѕи G
                  Рєњ ╬Њ     Ріб a Рѕи F
                  Рєњ p PE.РЅА pРђ▓
                  Рєњ -- Note that q can be chosen arbitrarily.
                    ╬а-allowed p q
                  Рєњ ╬Њ     Ріб lam p t РѕўРЪе pРђ▓ РЪЕ a РЅА t [ a ]Рѓђ Рѕи G [ a ]Рѓђ
    ╬и-eq          : ╬Њ РѕЎ F Ріб G
                  Рєњ ╬Њ     Ріб f Рѕи ╬а p , q Рќи F Рќ╣ G
                  Рєњ ╬Њ     Ріб g Рѕи ╬а p , q Рќи F Рќ╣ G
                  Рєњ ╬Њ РѕЎ F Ріб wk1 f РѕўРЪе p РЪЕ var x0 РЅА wk1 g РѕўРЪе p РЪЕ var x0 Рѕи G
                  Рєњ ╬а-allowed p q
                  Рєњ ╬Њ     Ріб f РЅА g Рѕи ╬а p , q Рќи F Рќ╣ G

    prod-cong     : ╬Њ РѕЎ F Ріб G
                  Рєњ ╬Њ Ріб t РЅА tРђ▓ Рѕи F
                  Рєњ ╬Њ Ріб u РЅА uРђ▓ Рѕи G [ t ]Рѓђ
                  Рєњ ╬Б-allowed k p q
                  Рєњ ╬Њ Ріб prod k p t u РЅА prod k p tРђ▓ uРђ▓ Рѕи ╬БРЪе k РЪЕ p , q Рќи F Рќ╣ G

    fst-cong      : ╬Њ РѕЎ F Ріб G
                  Рєњ ╬Њ Ріб t РЅА tРђ▓ Рѕи ╬Б╦б p , q Рќи F Рќ╣ G
                  Рєњ ╬Њ Ріб fst p t РЅА fst p tРђ▓ Рѕи F
    ╬Б-╬▓РѓЂ          : ╬Њ РѕЎ F Ріб G
                  Рєњ ╬Њ Ріб t Рѕи F
                  Рєњ ╬Њ Ріб u Рѕи G [ t ]Рѓђ
                  Рєњ p PE.РЅА pРђ▓
                  Рєњ -- Note that q can be chosen arbitrarily.
                    ╬Б╦б-allowed p q
                  Рєњ ╬Њ Ріб fst p (prod╦б pРђ▓ t u) РЅА t Рѕи F
    snd-cong      : ╬Њ РѕЎ F Ріб G
                  Рєњ ╬Њ Ріб t РЅА u Рѕи ╬Б╦б p , q Рќи F Рќ╣ G
                  Рєњ ╬Њ Ріб snd p t РЅА snd p u Рѕи G [ fst p t ]Рѓђ
    ╬Б-╬▓Рѓѓ          : ╬Њ РѕЎ F Ріб G
                  Рєњ ╬Њ Ріб t Рѕи F
                  Рєњ ╬Њ Ріб u Рѕи G [ t ]Рѓђ
                  Рєњ p PE.РЅА pРђ▓
                  Рєњ -- Note that q can be chosen arbitrarily.
                    ╬Б╦б-allowed p q
                  Рєњ ╬Њ Ріб snd p (prod╦б pРђ▓ t u) РЅА u Рѕи G [ fst p (prod╦б pРђ▓ t u) ]Рѓђ
    ╬Б-╬и           : ╬Њ РѕЎ F Ріб G
                  Рєњ ╬Њ Ріб t Рѕи ╬Б╦б p , q Рќи F Рќ╣ G
                  Рєњ ╬Њ Ріб u Рѕи ╬Б╦б p , q Рќи F Рќ╣ G
                  Рєњ ╬Њ Ріб fst p t РЅА fst p u Рѕи F
                  Рєњ ╬Њ Ріб snd p t РЅА snd p u Рѕи G [ fst p t ]Рѓђ
                  Рєњ ╬Б╦б-allowed p q
                  Рєњ ╬Њ Ріб t РЅА u Рѕи ╬Б╦б p , q Рќи F Рќ╣ G

    prodrec-cong  : ╬Њ РѕЎ ╬Б╩и p , qРђ▓ Рќи F Рќ╣ G Ріб A РЅА AРђ▓
                  Рєњ ╬Њ Ріб t РЅА tРђ▓ Рѕи ╬Б╩и p , qРђ▓ Рќи F Рќ╣ G
                  Рєњ ╬Њ РѕЎ F РѕЎ G Ріб u РЅА uРђ▓ Рѕи A [ prod╩и p (var x1) (var x0) ]РєЉ┬▓
                  Рєњ ╬Б╩и-allowed p qРђ▓
                  Рєњ ╬Њ Ріб prodrec r p q A t u РЅА prodrec r p q AРђ▓ tРђ▓ uРђ▓ Рѕи A [ t ]Рѓђ
    prodrec-╬▓     : ╬Њ РѕЎ ╬Б╩и p , qРђ▓ Рќи F Рќ╣ G Ріб A
                  Рєњ ╬Њ Ріб t Рѕи F
                  Рєњ ╬Њ Ріб tРђ▓ Рѕи G [ t ]Рѓђ
                  Рєњ ╬Њ РѕЎ F РѕЎ G Ріб u Рѕи A [ prod╩и p (var x1) (var x0) ]РєЉ┬▓
                  Рєњ p PE.РЅА pРђ▓
                  Рєњ ╬Б╩и-allowed p qРђ▓
                  Рєњ ╬Њ Ріб prodrec r p q A (prod╩и pРђ▓ t tРђ▓) u РЅА
                        u [ t , tРђ▓ ]РѓЂРѓђ Рѕи A [ prod╩и pРђ▓ t tРђ▓ ]Рѓђ

    suc-cong      : Рѕђ {n}
                  Рєњ ╬Њ Ріб t РЅА n Рѕи РёЋ
                  Рєњ ╬Њ Ріб suc t РЅА suc n Рѕи РёЋ
    natrec-cong   : Рѕђ {n}
                  Рєњ ╬Њ РѕЎ РёЋ     Ріб A РЅА AРђ▓
                  Рєњ ╬Њ         Ріб z РЅА zРђ▓ Рѕи A [ zero ]Рѓђ
                  Рєњ ╬Њ РѕЎ РёЋ РѕЎ A Ріб s РЅА sРђ▓ Рѕи A [ suc (var x1) ]РєЉ┬▓
                  Рєњ ╬Њ         Ріб n РЅА nРђ▓ Рѕи РёЋ
                  Рєњ ╬Њ         Ріб natrec p q r A z s n РЅА
                                natrec p q r AРђ▓ zРђ▓ sРђ▓ nРђ▓ Рѕи
                                A [ n ]Рѓђ
    natrec-zero   : ╬Њ         Ріб z Рѕи A [ zero ]Рѓђ
                  Рєњ ╬Њ РѕЎ РёЋ РѕЎ A Ріб s Рѕи A [ suc (var x1) ]РєЉ┬▓
                  Рєњ ╬Њ         Ріб natrec p q r A z s zero РЅА z Рѕи A [ zero ]Рѓђ
    natrec-suc    : Рѕђ {n}
                  Рєњ ╬Њ         Ріб z Рѕи A [ zero ]Рѓђ
                  Рєњ ╬Њ РѕЎ РёЋ РѕЎ A Ріб s Рѕи A [ suc (var x1) ]РєЉ┬▓
                  Рєњ ╬Њ         Ріб n Рѕи РёЋ
                  Рєњ ╬Њ         Ріб natrec p q r A z s (suc n) РЅА
                                s [ n , natrec p q r A z s n ]РѓЂРѓђ Рѕи
                                A [ suc n ]Рѓђ

    Id-cong       : ╬Њ Ріб AРѓЂ РЅА AРѓѓ Рѕи U l
                  Рєњ ╬Њ Ріб tРѓЂ РЅА tРѓѓ Рѕи AРѓЂ
                  Рєњ ╬Њ Ріб uРѓЂ РЅА uРѓѓ Рѕи AРѓЂ
                  Рєњ ╬Њ Ріб Id AРѓЂ tРѓЂ uРѓЂ РЅА Id AРѓѓ tРѓѓ uРѓѓ Рѕи U l
    J-cong        : ╬Њ Ріб AРѓЂ РЅА AРѓѓ
                  Рєњ ╬Њ Ріб tРѓЂ Рѕи AРѓЂ
                  Рєњ ╬Њ Ріб tРѓЂ РЅА tРѓѓ Рѕи AРѓЂ
                  Рєњ ╬Њ РѕЎ AРѓЂ РѕЎ Id (wk1 AРѓЂ) (wk1 tРѓЂ) (var x0) Ріб BРѓЂ РЅА BРѓѓ
                  Рєњ ╬Њ Ріб uРѓЂ РЅА uРѓѓ Рѕи BРѓЂ [ tРѓЂ , rfl ]РѓЂРѓђ
                  Рєњ ╬Њ Ріб vРѓЂ РЅА vРѓѓ Рѕи AРѓЂ
                  Рєњ ╬Њ Ріб wРѓЂ РЅА wРѓѓ Рѕи Id AРѓЂ tРѓЂ vРѓЂ
                  Рєњ ╬Њ Ріб J p q AРѓЂ tРѓЂ BРѓЂ uРѓЂ vРѓЂ wРѓЂ РЅА
                        J p q AРѓѓ tРѓѓ BРѓѓ uРѓѓ vРѓѓ wРѓѓ Рѕи BРѓЂ [ vРѓЂ , wРѓЂ ]РѓЂРѓђ
    J-╬▓           : ╬Њ Ріб t Рѕи A
                  Рєњ ╬Њ РѕЎ A РѕЎ Id (wk1 A) (wk1 t) (var x0) Ріб B
                  Рєњ ╬Њ Ріб u Рѕи B [ t , rfl ]РѓЂРѓђ
                  Рєњ t PE.РЅА tРђ▓
                  Рєњ ╬Њ Ріб J p q A t B u tРђ▓ rfl РЅА u Рѕи B [ t , rfl ]РѓЂРѓђ
    K-cong        : ╬Њ Ріб AРѓЂ РЅА AРѓѓ
                  Рєњ ╬Њ Ріб tРѓЂ РЅА tРѓѓ Рѕи AРѓЂ
                  Рєњ ╬Њ РѕЎ Id AРѓЂ tРѓЂ tРѓЂ Ріб BРѓЂ РЅА BРѓѓ
                  Рєњ ╬Њ Ріб uРѓЂ РЅА uРѓѓ Рѕи BРѓЂ [ rfl ]Рѓђ
                  Рєњ ╬Њ Ріб vРѓЂ РЅА vРѓѓ Рѕи Id AРѓЂ tРѓЂ tРѓЂ
                  Рєњ K-allowed
                  Рєњ ╬Њ Ріб K p AРѓЂ tРѓЂ BРѓЂ uРѓЂ vРѓЂ РЅА K p AРѓѓ tРѓѓ BРѓѓ uРѓѓ vРѓѓ Рѕи
                      BРѓЂ [ vРѓЂ ]Рѓђ
    K-╬▓           : ╬Њ РѕЎ Id A t t Ріб B
                  Рєњ ╬Њ Ріб u Рѕи B [ rfl ]Рѓђ
                  Рєњ K-allowed
                  Рєњ ╬Њ Ріб K p A t B u rfl РЅА u Рѕи B [ rfl ]Рѓђ
    []-cong-cong  : ╬Њ Ріб AРѓЂ РЅА AРѓѓ
                  Рєњ ╬Њ Ріб tРѓЂ РЅА tРѓѓ Рѕи AРѓЂ
                  Рєњ ╬Њ Ріб uРѓЂ РЅА uРѓѓ Рѕи AРѓЂ
                  Рєњ ╬Њ Ріб vРѓЂ РЅА vРѓѓ Рѕи Id AРѓЂ tРѓЂ uРѓЂ
                  Рєњ []-cong-allowed k
                  Рєњ let open Erased k in
                    ╬Њ Ріб []-cong k AРѓЂ tРѓЂ uРѓЂ vРѓЂ РЅА []-cong k AРѓѓ tРѓѓ uРѓѓ vРѓѓ Рѕи
                      Id (Erased AРѓЂ) ([ tРѓЂ ]) ([ uРѓЂ ])
    []-cong-╬▓     : ╬Њ Ріб t Рѕи A
                  Рєњ t PE.РЅА tРђ▓
                  Рєњ []-cong-allowed k
                  Рєњ let open Erased k in
                    ╬Њ Ріб []-cong k A t tРђ▓ rfl РЅА rfl Рѕи
                      Id (Erased A) ([ t ]) ([ tРђ▓ ])
    equality-reflection
                  : Equality-reflection
                  Рєњ ╬Њ Ріб Id A t u
                  Рєњ ╬Њ Ріб v Рѕи Id A t u
                  Рєњ ╬Њ Ріб t РЅА u Рѕи A

-- Term reduction
data _Ріб_РЄњ_Рѕи_ (╬Њ : Con Term n) : Term n Рєњ Term n Рєњ Term n Рєњ Set РёЊ where
  conv           : ╬Њ Ріб t РЄњ u Рѕи A
                 Рєњ ╬Њ Ріб A РЅА B
                 Рєњ ╬Њ Ріб t РЄњ u Рѕи B

  maxрхў-subst╦А    : ╬Њ Ріб t РЄњ tРђ▓ Рѕи Level
                 Рєњ ╬Њ Ріб u Рѕи Level
                 Рєњ ╬Њ Ріб t maxрхў u РЄњ tРђ▓ maxрхў u Рѕи Level
  maxрхў-subst╩│    : ╬Њ Ріб t Рѕи Level
                 Рєњ ╬Њ Ріб u РЄњ uРђ▓ Рѕи Level
                 Рєњ ╬Њ Ріб sucрхў t maxрхў u РЄњ sucрхў t maxрхў uРђ▓ Рѕи Level
  maxрхў-zero╦А     : ╬Њ Ріб l Рѕи Level
                 Рєњ ╬Њ Ріб zeroрхў maxрхў l РЄњ l Рѕи Level
  maxрхў-zero╩│     : ╬Њ Ріб l Рѕи Level
                 Рєњ ╬Њ Ріб sucрхў l maxрхў zeroрхў РЄњ sucрхў l Рѕи Level
  maxрхў-sucрхў      : ╬Њ Ріб lРѓЂ Рѕи Level
                 Рєњ ╬Њ Ріб lРѓѓ Рѕи Level
                 Рєњ ╬Њ Ріб sucрхў lРѓЂ maxрхў sucрхў lРѓѓ РЄњ sucрхў (lРѓЂ maxрхў lРѓѓ) Рѕи Level

  emptyrec-subst : Рѕђ {n}
                 Рєњ ╬Њ Ріб A
                 Рєњ ╬Њ     Ріб n РЄњ nРђ▓ Рѕи Empty
                 Рєњ ╬Њ     Ріб emptyrec p A n РЄњ emptyrec p A nРђ▓ Рѕи A

  unitrec-subst : ╬Њ Ріб l Рѕи Level
                Рєњ ╬Њ РѕЎ Unit╩и l Ріб A
                Рєњ ╬Њ Ріб u Рѕи A [ star╩и l ]Рѓђ
                Рєњ ╬Њ Ріб t РЄњ tРђ▓ Рѕи Unit╩и l
                Рєњ Unit╩и-allowed
                Рєњ ┬г Unit╩и-╬и
                Рєњ ╬Њ Ріб unitrec p q l A t u РЄњ unitrec p q l A tРђ▓ u Рѕи
                    A [ t ]Рѓђ
  unitrec-╬▓     : ╬Њ Ріб lРѓЂ Рѕи Level
                Рєњ ╬Њ Ріб lРѓЂ РЅА lРѓѓ Рѕи Level
                Рєњ ╬Њ РѕЎ Unit╩и lРѓЂ Ріб A
                Рєњ ╬Њ Ріб u Рѕи A [ star╩и lРѓЂ ]Рѓђ
                Рєњ Unit╩и-allowed
                Рєњ ┬г Unit╩и-╬и
                Рєњ ╬Њ Ріб unitrec p q lРѓЂ A (star╩и lРѓѓ) u РЄњ u Рѕи A [ star╩и lРѓЂ ]Рѓђ
  unitrec-╬▓-╬и   : ╬Њ Ріб l Рѕи Level
                Рєњ ╬Њ РѕЎ Unit╩и l Ріб A
                Рєњ ╬Њ Ріб t Рѕи Unit╩и l
                Рєњ ╬Њ Ріб u Рѕи A [ star╩и l ]Рѓђ
                Рєњ Unit╩и-allowed
                Рєњ Unit╩и-╬и
                Рєњ ╬Њ Ріб unitrec p q l A t u РЄњ u Рѕи A [ t ]Рѓђ

  app-subst      : ╬Њ Ріб t РЄњ u Рѕи ╬а p , q Рќи F Рќ╣ G
                 Рєњ ╬Њ Ріб a Рѕи F
                 Рєњ ╬Њ Ріб t РѕўРЪе p РЪЕ a РЄњ u РѕўРЪе p РЪЕ a Рѕи G [ a ]Рѓђ
  ╬▓-red          : ╬Њ РѕЎ F Ріб G
                 Рєњ ╬Њ РѕЎ F Ріб t Рѕи G
                 Рєњ ╬Њ     Ріб a Рѕи F
                 Рєњ p PE.РЅА pРђ▓
                 Рєњ -- Note that q can be chosen arbitrarily.
                   ╬а-allowed p q
                 Рєњ ╬Њ     Ріб lam p t РѕўРЪе pРђ▓ РЪЕ a РЄњ t [ a ]Рѓђ Рѕи G [ a ]Рѓђ

  fst-subst      : ╬Њ РѕЎ F Ріб G
                 Рєњ ╬Њ Ріб t РЄњ u Рѕи ╬Б╦б p , q Рќи F Рќ╣ G
                 Рєњ ╬Њ Ріб fst p t РЄњ fst p u Рѕи F
  ╬Б-╬▓РѓЂ           : ╬Њ РѕЎ F Ріб G
                 Рєњ ╬Њ Ріб t Рѕи F
                 Рєњ ╬Њ Ріб u Рѕи G [ t ]Рѓђ
                 Рєњ p PE.РЅА pРђ▓
                 Рєњ -- Note that q can be chosen arbitrarily.
                   ╬Б╦б-allowed p q
                 Рєњ ╬Њ Ріб fst p (prod╦б pРђ▓ t u) РЄњ t Рѕи F
  snd-subst      : ╬Њ РѕЎ F Ріб G
                 Рєњ ╬Њ Ріб t РЄњ u Рѕи ╬Б╦б p , q Рќи F Рќ╣ G
                 Рєњ ╬Њ Ріб snd p t РЄњ snd p u Рѕи G [ fst p t ]Рѓђ
  ╬Б-╬▓Рѓѓ           : ╬Њ РѕЎ F Ріб G
                 Рєњ ╬Њ Ріб t Рѕи F
                 Рєњ ╬Њ Ріб u Рѕи G [ t ]Рѓђ
                 Рєњ p PE.РЅА pРђ▓
                 Рєњ -- Note that q can be chosen arbitrarily.
                   ╬Б╦б-allowed p q
                 Рєњ ╬Њ Ріб snd p (prod╦б pРђ▓ t u) РЄњ u Рѕи G [ fst p (prod╦б pРђ▓ t u) ]Рѓђ

  prodrec-subst  : ╬Њ РѕЎ ╬Б╩и p , qРђ▓ Рќи F Рќ╣ G Ріб A
                 Рєњ ╬Њ РѕЎ F РѕЎ G Ріб u Рѕи A [ prod╩и p (var x1) (var x0) ]РєЉ┬▓
                 Рєњ ╬Њ Ріб t РЄњ tРђ▓ Рѕи ╬Б╩и p , qРђ▓ Рќи F Рќ╣ G
                 Рєњ ╬Б╩и-allowed p qРђ▓
                 Рєњ ╬Њ Ріб prodrec r p q A t u РЄњ prodrec r p q A tРђ▓ u Рѕи A [ t ]Рѓђ
  prodrec-╬▓      : ╬Њ РѕЎ ╬Б╩и p , qРђ▓ Рќи F Рќ╣ G Ріб A
                 Рєњ ╬Њ Ріб t Рѕи F
                 Рєњ ╬Њ Ріб tРђ▓ Рѕи G [ t ]Рѓђ
                 Рєњ ╬Њ РѕЎ F РѕЎ G Ріб u Рѕи A [ prod╩и p (var x1) (var x0) ]РєЉ┬▓
                 Рєњ p PE.РЅА pРђ▓
                 Рєњ ╬Б╩и-allowed p qРђ▓
                 Рєњ ╬Њ Ріб prodrec r p q A (prod╩и pРђ▓ t tРђ▓) u РЄњ
                       u [ t , tРђ▓ ]РѓЂРѓђ Рѕи A [ prod╩и pРђ▓ t tРђ▓ ]Рѓђ

  natrec-subst   : Рѕђ {n}
                 Рєњ ╬Њ         Ріб z Рѕи A [ zero ]Рѓђ
                 Рєњ ╬Њ РѕЎ РёЋ РѕЎ A Ріб s Рѕи A [ suc (var x1) ]РєЉ┬▓
                 Рєњ ╬Њ         Ріб n РЄњ nРђ▓ Рѕи РёЋ
                 Рєњ ╬Њ         Ріб natrec p q r A z s n РЄњ
                               natrec p q r A z s nРђ▓ Рѕи
                               A [ n ]Рѓђ
  natrec-zero    : ╬Њ         Ріб z Рѕи A [ zero ]Рѓђ
                 Рєњ ╬Њ РѕЎ РёЋ РѕЎ A Ріб s Рѕи A [ suc (var x1) ]РєЉ┬▓
                 Рєњ ╬Њ         Ріб natrec p q r A z s zero РЄњ z Рѕи A [ zero ]Рѓђ
  natrec-suc     : Рѕђ {n}
                 Рєњ ╬Њ         Ріб z Рѕи A [ zero ]Рѓђ
                 Рєњ ╬Њ РѕЎ РёЋ РѕЎ A Ріб s Рѕи A [ suc (var x1) ]РєЉ┬▓
                 Рєњ ╬Њ         Ріб n Рѕи РёЋ
                 Рєњ ╬Њ         Ріб natrec p q r A z s (suc n) РЄњ
                               s [ n , natrec p q r A z s n ]РѓЂРѓђ Рѕи
                               A [ suc n ]Рѓђ

  J-subst        : ╬Њ Ріб t Рѕи A
                 Рєњ ╬Њ РѕЎ A РѕЎ Id (wk1 A) (wk1 t) (var x0) Ріб B
                 Рєњ ╬Њ Ріб u Рѕи B [ t , rfl ]РѓЂРѓђ
                 Рєњ ╬Њ Ріб v Рѕи A
                 Рєњ ╬Њ Ріб wРѓЂ РЄњ wРѓѓ Рѕи Id A t v
                 Рєњ ╬Њ Ріб J p q A t B u v wРѓЂ РЄњ J p q A t B u v wРѓѓ Рѕи
                     B [ v , wРѓЂ ]РѓЂРѓђ
  J-╬▓            : ╬Њ Ріб t Рѕи A
                 Рєњ ╬Њ Ріб tРђ▓ Рѕи A
                 Рєњ ╬Њ Ріб t РЅА tРђ▓ Рѕи A
                 Рєњ ╬Њ РѕЎ A РѕЎ Id (wk1 A) (wk1 t) (var x0) Ріб B
                 Рєњ ╬Њ Ріб B [ t , rfl ]РѓЂРѓђ РЅА B [ tРђ▓ , rfl ]РѓЂРѓђ
                 Рєњ ╬Њ Ріб u Рѕи B [ t , rfl ]РѓЂРѓђ
                 Рєњ ╬Њ Ріб J p q A t B u tРђ▓ rfl РЄњ u Рѕи B [ t , rfl ]РѓЂРѓђ
  K-subst        : ╬Њ РѕЎ Id A t t Ріб B
                 Рєњ ╬Њ Ріб u Рѕи B [ rfl ]Рѓђ
                 Рєњ ╬Њ Ріб vРѓЂ РЄњ vРѓѓ Рѕи Id A t t
                 Рєњ K-allowed
                 Рєњ ╬Њ Ріб K p A t B u vРѓЂ РЄњ K p A t B u vРѓѓ Рѕи B [ vРѓЂ ]Рѓђ
  K-╬▓            : ╬Њ РѕЎ Id A t t Ріб B
                 Рєњ ╬Њ Ріб u Рѕи B [ rfl ]Рѓђ
                 Рєњ K-allowed
                 Рєњ ╬Њ Ріб K p A t B u rfl РЄњ u Рѕи B [ rfl ]Рѓђ
  []-cong-subst  : ╬Њ Ріб A
                 Рєњ ╬Њ Ріб t Рѕи A
                 Рєњ ╬Њ Ріб u Рѕи A
                 Рєњ ╬Њ Ріб vРѓЂ РЄњ vРѓѓ Рѕи Id A t u
                 Рєњ []-cong-allowed k
                 Рєњ let open Erased k in
                   ╬Њ Ріб []-cong k A t u vРѓЂ РЄњ []-cong k A t u vРѓѓ Рѕи
                     Id (Erased A) ([ t ]) ([ u ])
  []-cong-╬▓      : ╬Њ Ріб A
                 Рєњ ╬Њ Ріб t Рѕи A
                 Рєњ ╬Њ Ріб tРђ▓ Рѕи A
                 Рєњ ╬Њ Ріб t РЅА tРђ▓ Рѕи A
                 Рєњ []-cong-allowed k
                 Рєњ let open Erased k in
                   ╬Њ Ріб []-cong k A t tРђ▓ rfl РЄњ rfl Рѕи
                     Id (Erased A) ([ t ]) ([ tРђ▓ ])

-- Type reduction
data _Ріб_РЄњ_ (╬Њ : Con Term n) : Term n Рєњ Term n Рєњ Set РёЊ where
  univ : ╬Њ Ріб A РЄњ B Рѕи U l
       Рєњ ╬Њ Ріб A РЄњ B

-- Term reduction closure
data _Ріб_РЄњ*_Рѕи_ (╬Њ : Con Term n) : Term n Рєњ Term n Рєњ Term n Рєњ Set РёЊ where
  id  : ╬Њ Ріб t Рѕи A
      Рєњ ╬Њ Ріб t РЄњ* t Рѕи A
  _РЄе_ : ╬Њ Ріб t  РЄњ  tРђ▓ Рѕи A
      Рєњ ╬Њ Ріб tРђ▓ РЄњ* u  Рѕи A
      Рєњ ╬Њ Ріб t  РЄњ* u  Рѕи A

-- Type reduction closure
data _Ріб_РЄњ*_ (╬Њ : Con Term n) : Term n Рєњ Term n Рєњ Set РёЊ where
  id  : ╬Њ Ріб A
      Рєњ ╬Њ Ріб A РЄњ* A
  _РЄе_ : ╬Њ Ріб A  РЄњ  AРђ▓
      Рєњ ╬Њ Ріб AРђ▓ РЄњ* B
      Рєњ ╬Њ Ріб A  РЄњ* B

-- Type reduction to whnf
_Ріб_Рєў_ : (╬Њ : Con Term n) Рєњ Term n Рєњ Term n Рєњ Set РёЊ
╬Њ Ріб A Рєў B = ╬Њ Ріб A РЄњ* B ├Ќ Whnf B

-- Term reduction to whnf
_Ріб_Рєў_Рѕи_ : (╬Њ : Con Term n) Рєњ Term n Рєњ Term n Рєњ Term n Рєњ Set РёЊ
╬Њ Ріб t Рєў u Рѕи A = ╬Њ Ріб t РЄњ* u Рѕи A ├Ќ Whnf u

-- A context ╬Њ is consistent if the empty type is not inhabited in┬а╬Њ.

Consistent : Con Term n Рєњ Set РёЊ
Consistent ╬Њ = Рѕђ t Рєњ ┬г ╬Њ Ріб t Рѕи Empty
