------------------------------------------------------------------------
-- Definitions related to type and usage restrictions
------------------------------------------------------------------------

import Graded.Modality

module Graded.Restrictions
  {a} {M : Set a}
  (open Graded.Modality M)
  (ğ•„ : Modality)
  where

open Modality ğ•„

open import Tools.Bool
open import Tools.Empty
open import Tools.Function
open import Tools.Level
open import Tools.Product
open import Tools.PropositionalEquality
open import Tools.Relation
open import Tools.Unit

open import Graded.Modality.Properties ğ•„
open import Graded.Usage.Restrictions M

open import Definition.Typed.Restrictions ğ•„

private variable
  TR : Type-restrictions
  UR : Usage-restrictions

-- No type restrictions except that if the modality is trivial, then
-- []-cong is not allowed.

no-type-restrictions : Type-restrictions
no-type-restrictions = Î» where
    .Unit-allowed     â†’ Lift _ âŠ¤
    .Î Î£-allowed       â†’ Î» _ _ _ â†’ Lift _ âŠ¤
    .K-allowed        â†’ Lift _ âŠ¤
    .[]-cong-allowed  â†’ Â¬ Trivial
    .[]-congâ†’Erased   â†’ _
    .[]-congâ†’Â¬Trivial â†’ idá¶ 
  where
  open Type-restrictions

-- No usage restrictions, and Id-erased, Erased-matches-for-J and
-- Erased-matches-for-K are all inhabited.

no-usage-restrictions : Usage-restrictions
no-usage-restrictions = Î» where
  .Usage-restrictions.Prodrec-allowed       â†’ Î» _ _ _ â†’ Lift _ âŠ¤
  .Usage-restrictions.Id-erased             â†’ Lift _ âŠ¤
  .Usage-restrictions.Id-erased?            â†’ yes _
  .Usage-restrictions.Erased-matches-for-J  â†’ Lift _ âŠ¤
  .Usage-restrictions.Erased-matches-for-J? â†’ yes _
  .Usage-restrictions.Erased-matches-for-K  â†’ Lift _ âŠ¤
  .Usage-restrictions.Erased-matches-for-K? â†’ yes _

-- The function adds the restriction that the two quantities on a Î -
-- or Î£-type have to be equal.

equal-binder-quantities : Type-restrictions â†’ Type-restrictions
equal-binder-quantities R = record R
  { Î Î£-allowed     = Î» b p q â†’ Î Î£-allowed b p q Ã— p â‰¡ q
  ; []-congâ†’Erased = Î» ok â†’
      []-congâ†’Erased ok .projâ‚ , []-congâ†’Erased ok .projâ‚‚ , refl
  }
  where
  open Type-restrictions R

-- The function adds the restriction that the second quantities
-- associated with Î - and Î£-types are equal to ğŸ˜.

second-Î Î£-quantities-ğŸ˜ :
  Type-restrictions â†’ Type-restrictions
second-Î Î£-quantities-ğŸ˜ R = record R
  { Î Î£-allowed     = Î» b p q â†’ Î Î£-allowed b p q Ã— q â‰¡ ğŸ˜
  ; []-congâ†’Erased = Î» ok â†’
      []-congâ†’Erased ok .projâ‚ , []-congâ†’Erased ok .projâ‚‚ , refl
  }
  where
  open Type-restrictions R

-- The function second-Î Î£-quantities-ğŸ˜-or-Ï‰Â ğ•„ adds the restriction
-- that if the first quantity associated with a Î - or Î£-type is the Ï‰
-- grade ofÂ ğ•„, then the second quantity is also Ï‰, and if the first
-- quantity is not Ï‰, then the second quantity is the ğŸ˜ ofÂ ğ•„.

second-Î Î£-quantities-ğŸ˜-or-Ï‰ :
  Type-restrictions â†’ Type-restrictions
second-Î Î£-quantities-ğŸ˜-or-Ï‰ R = record R
  { Î Î£-allowed = Î» b p q â†’
      Î Î£-allowed b p q Ã—
      (p â‰¡ Ï‰ â†’ q â‰¡ Ï‰) Ã—
      (p â‰¢ Ï‰ â†’ q â‰¡ ğŸ˜)
  ; []-congâ†’Erased = Î» ok â†’
        []-congâ†’Erased ok .projâ‚
      , []-congâ†’Erased ok .projâ‚‚
      , idá¶ 
      , Î» _ â†’ refl
  }
  where
  open Type-restrictions R

-- The property of not allowing erased matches.
--
-- "Erased" matches are allowed for trivial modalities.

No-erased-matches : Type-restrictions â†’ Usage-restrictions â†’ Set a
No-erased-matches TR UR =
  Â¬ Trivial â†’
  (âˆ€ {r p q} â†’ Prodrec-allowed r p q â†’ r â‰¢ ğŸ˜) Ã—
  Â¬ []-cong-allowed Ã—
  Â¬ Erased-matches-for-J Ã—
  Â¬ Erased-matches-for-K
  where
  open Type-restrictions TR
  open Usage-restrictions UR

-- The function adds the restriction that erased matches are not
-- allowed.

no-erased-matches-TR : Type-restrictions â†’ Type-restrictions
no-erased-matches-TR TR = record TR
  { []-cong-allowed  = Lift _ âŠ¥
  ; []-congâ†’Erased   = Î» ()
  ; []-congâ†’Â¬Trivial = Î» ()
  }
  where
  open Type-restrictions TR

-- The function adds the restriction that erased matches are not
-- allowed (for prodrec the restriction only applies to non-trivial
-- modalities).

no-erased-matches-UR : Usage-restrictions â†’ Usage-restrictions
no-erased-matches-UR UR = record UR
  { Prodrec-allowed       = Î» r p q â†’
                              Prodrec-allowed r p q Ã—
                              (Â¬ Trivial â†’ r â‰¢ ğŸ˜)
  ; Erased-matches-for-J  = Lift _ âŠ¥
  ; Erased-matches-for-J? = no (Î» ())
  ; Erased-matches-for-K  = Lift _ âŠ¥
  ; Erased-matches-for-K? = no (Î» ())
  }
  where
  open Usage-restrictions UR

-- The restrictions obtained from no-erased-matches-TR and
-- no-erased-matches-UR satisfy No-erased-matches.

No-erased-matches-no-erased-matches :
  âˆ€ TR UR â†’
  No-erased-matches (no-erased-matches-TR TR) (no-erased-matches-UR UR)
No-erased-matches-no-erased-matches _ _ ğŸ™â‰¢ğŸ˜ =
  (_$ ğŸ™â‰¢ğŸ˜) âˆ˜â†’ projâ‚‚ , (Î» ()) , (Î» ()) , (Î» ())
