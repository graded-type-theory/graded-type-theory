import Tools.Algebra as A
open import Tools.Bool hiding (_âˆ§_)
open import Tools.Nat hiding (_+_)
open import Tools.Product
open import Tools.PropositionalEquality
open import Graded.Modality
open import Graded.Mode.Restrictions

-- A "semiring with meet" with the following recursively defined
-- operator nr can be turned into a modality:
--
-- nr 0 p q r = ğŸ˜
-- nr (1+ n) p q r = p âˆ§ (q + r nr n p q r)
-- âˆƒ n â†’ âˆ€ p q r â†’ nr (1+ n) p q r â‰¡ nr n p q r

module Graded.Modality.Instances.Recursive
  {a} {M : Set a} (ğ•„ : Semiring-with-meet M)
  (open Semiring-with-meet ğ•„)
  (nr : Nat â†’ A.Opâ‚ƒ M M)
  (nr-rec : (n : Nat) (p q r : M)
          â†’ nr (1+ n) p q r â‰¡ p âˆ§ (q + r Â· (nr n p q r)))
  (nr-0 : (p q r : M) â†’ nr 0 p q r â‰¡ ğŸ˜)
  (nr-fix : âˆƒ Î» n â†’ (p q r : M) â†’ nr (1+ n) p q r â‰¡ nr n p q r)
  (rs : Mode-restrictions)
  (open Mode-restrictions rs)
  (ğŸ˜-well-behaved : T ğŸ˜áµ-allowed â†’ Has-well-behaved-zero M ğ•„) where

open import Graded.Modality.Properties.Addition ğ•„
open import Graded.Modality.Properties.Meet ğ•„
open import Graded.Modality.Properties.Multiplication ğ•„
open import Graded.Modality.Properties.PartialOrder ğ•„
open import Tools.Algebra M
import Tools.Reasoning.PartialOrder
import Tools.Reasoning.PropositionalEquality

private variable
  p q r : M

_âŠ›_â–·_ : Opâ‚ƒ M
_âŠ›_â–·_ = nr (projâ‚ nr-fix)

solvesIneqs : ((p q r : M) â†’ (p âŠ› q â–· r) â‰¤ q + r Â· (p âŠ› q â–· r)) Ã—
              ((p q r : M) â†’ (p âŠ› q â–· r) â‰¤ p)
solvesIneqs =
  let n , fix = nr-fix
  in  (Î» p q r â†’ â‰¤-trans (â‰¤-reflexive (trans (sym (fix p q r)) (nr-rec n p q r))) (âˆ§-decreasingÊ³ p _))
    , (Î» p q r â†’ â‰¤-trans (â‰¤-reflexive (trans (sym (fix p q r)) (nr-rec n p q r))) (âˆ§-decreasingË¡ p _))

+-sub-interchangeable-nr : (n : Nat) (r : M) â†’ _+_ SubInterchangeable (Î» p q â†’ nr n p q r) by _â‰¤_
+-sub-interchangeable-nr 0 r p q pâ€² qâ€² = begin
  nr 0 p q r + nr 0 pâ€² qâ€² r â‰ˆâŸ¨ +-cong (nr-0 p q r) (nr-0 pâ€² qâ€² r) âŸ©
  ğŸ˜ + ğŸ˜                     â‰ˆâŸ¨ +-identityË¡ ğŸ˜ âŸ©
  ğŸ˜                         â‰ˆË˜âŸ¨ nr-0 (p + pâ€²) (q + qâ€²) r âŸ©
  nr 0 (p + pâ€²) (q + qâ€²) r âˆ
  where
  open Tools.Reasoning.PartialOrder â‰¤-poset
+-sub-interchangeable-nr (1+ n) r p q pâ€² qâ€² = begin
  nr (1+ n) p q r + nr (1+ n) pâ€² qâ€² r
    â‰ˆâŸ¨ +-cong (nr-rec n p q r) (nr-rec n pâ€² qâ€² r) âŸ©
  (p âˆ§ (q + r Â· nr n p q r)) + (pâ€² âˆ§ (qâ€² + r Â· nr n pâ€² qâ€² r))
    â‰ˆâŸ¨ +-distribÊ³-âˆ§ _ _ _ âŸ©
  (p + (pâ€² âˆ§ (qâ€² + r Â· nr n pâ€² qâ€² r))) âˆ§ ((q + r Â· nr n p q r) + (pâ€² âˆ§ (qâ€² + r Â· nr n pâ€² qâ€² r)))
    â‰ˆâŸ¨ âˆ§-cong (+-distribË¡-âˆ§ _ _ _) (+-distribË¡-âˆ§ _ _ _) âŸ©
  ((p + pâ€²) âˆ§ (p + (qâ€² + r Â· nr n pâ€² qâ€² r))) âˆ§ (((q + r Â· nr n p q r) + pâ€²)
    âˆ§ ((q + r Â· nr n p q r) + (qâ€² + r Â· nr n pâ€² qâ€² r)))
    â‰¤âŸ¨ âˆ§-monotone (âˆ§-decreasingË¡ _ _) (âˆ§-decreasingÊ³ _ _) âŸ©
  (p + pâ€²) âˆ§ (q + r Â· nr n p q r) + qâ€² + r Â· nr n pâ€² qâ€² r
    â‰ˆâŸ¨ âˆ§-congË¡ (+-assoc _ _ _) âŸ©
  (p + pâ€²) âˆ§ (q + r Â· nr n p q r + qâ€² + r Â· nr n pâ€² qâ€² r)
    â‰ˆË˜âŸ¨ âˆ§-congË¡ (+-congË¡ (+-assoc _ _ _)) âŸ©
  (p + pâ€²) âˆ§ (q + (r Â· nr n p q r + qâ€²) + r Â· nr n pâ€² qâ€² r)
    â‰ˆâŸ¨ âˆ§-congË¡  (+-congË¡ (+-congÊ³ (+-comm _ _))) âŸ©
  (p + pâ€²) âˆ§ (q + (qâ€² + r Â· nr n p q r) + r Â· nr n pâ€² qâ€² r)
    â‰ˆâŸ¨ âˆ§-congË¡ (+-congË¡ (+-assoc _ _ _)) âŸ©
  (p + pâ€²) âˆ§ (q + qâ€² + r Â· nr n p q r + r Â· nr n pâ€² qâ€² r)
    â‰ˆË˜âŸ¨ âˆ§-congË¡ (+-assoc _ _ _) âŸ©
  (p + pâ€²) âˆ§ ((q + qâ€²) + (r Â· nr n p q r + r Â· nr n pâ€² qâ€² r))
    â‰ˆË˜âŸ¨ âˆ§-congË¡ (+-congË¡ (Â·-distribË¡-+ _ _ _)) âŸ©
  (p + pâ€²) âˆ§ ((q + qâ€²) + (r Â· (nr n p q r + nr n pâ€² qâ€² r)))
    â‰¤âŸ¨ âˆ§-monotoneÊ³ (+-monotoneÊ³ (Â·-monotoneÊ³ (+-sub-interchangeable-nr n r p q pâ€² qâ€²))) âŸ©
  (p + pâ€²) âˆ§ ((q + qâ€²) + (r Â· nr n (p + pâ€²) (q + qâ€²) r))
    â‰ˆË˜âŸ¨ nr-rec n (p + pâ€²) (q + qâ€²) r âŸ©
  nr (1+ n) (p + pâ€²) (q + qâ€²) r âˆ
  where
  open Tools.Reasoning.PartialOrder â‰¤-poset

Â·-sub-distribÊ³-nr : (n : Nat) (r : M) â†’ _Â·_ SubDistributesOverÊ³ (Î» p q â†’ nr n p q r) by _â‰¤_
Â·-sub-distribÊ³-nr 0 r q p pâ€² = begin
  nr 0 p pâ€² r Â· q â‰ˆâŸ¨ Â·-congÊ³ (nr-0 p pâ€² r) âŸ©
  ğŸ˜ Â· q           â‰ˆâŸ¨ Â·-zeroË¡ q âŸ©
  ğŸ˜               â‰ˆË˜âŸ¨ nr-0 (p Â· q) (pâ€² Â· q) r âŸ©
  nr 0 (p Â· q) (pâ€² Â· q) r âˆ
  where
  open Tools.Reasoning.PartialOrder â‰¤-poset
Â·-sub-distribÊ³-nr (1+ n) r q p pâ€² = begin
  nr (1+ n) p pâ€² r Â· q
    â‰ˆâŸ¨ Â·-congÊ³ (nr-rec n p pâ€² r) âŸ©
  (p âˆ§ pâ€² + r Â· nr n p pâ€² r) Â· q
    â‰ˆâŸ¨ Â·-distribÊ³-âˆ§ q p _ âŸ©
  (p Â· q) âˆ§ (pâ€² + r Â· nr n p pâ€² r) Â· q
    â‰ˆâŸ¨ âˆ§-congË¡ (Â·-distribÊ³-+ q pâ€² _) âŸ©
  (p Â· q) âˆ§ (pâ€² Â· q) + (r Â· nr n p pâ€² r) Â· q
    â‰ˆâŸ¨ âˆ§-congË¡ (+-congË¡ (Â·-assoc r _ q)) âŸ©
  (p Â· q) âˆ§ (pâ€² Â· q) + r Â· (nr n p pâ€² r Â· q)
    â‰¤âŸ¨ âˆ§-monotoneÊ³ (+-monotoneÊ³ (Â·-monotoneÊ³ (Â·-sub-distribÊ³-nr n r q p pâ€²))) âŸ©
  (p Â· q) âˆ§ (pâ€² Â· q) + r Â· nr n (p Â· q) (pâ€² Â· q) r
    â‰ˆË˜âŸ¨ nr-rec n (p Â· q) (pâ€² Â· q) r âŸ©
  nr (1+ n) (p Â· q) (pâ€² Â· q) r âˆ
  where
  open Tools.Reasoning.PartialOrder â‰¤-poset

nr-sub-distribË¡-âˆ§ : (n : Nat) (r : M) â†’ (Î» p q  â†’ nr n p q r) SubDistributesOverË¡ _âˆ§_ by _â‰¤_
nr-sub-distribË¡-âˆ§ 0 r p q qâ€² = begin
  nr 0 p (q âˆ§ qâ€²) r â‰ˆâŸ¨ nr-0 p (q âˆ§ qâ€²) r âŸ©
  ğŸ˜                 â‰ˆË˜âŸ¨ âˆ§-idem ğŸ˜ âŸ©
  ğŸ˜ âˆ§ ğŸ˜             â‰ˆË˜âŸ¨ âˆ§-cong (nr-0 p q r) (nr-0 p qâ€² r) âŸ©
  nr 0 p q r âˆ§ nr 0 p qâ€² r âˆ
  where
  open Tools.Reasoning.PartialOrder â‰¤-poset
nr-sub-distribË¡-âˆ§ (1+ n) r p q qâ€² = begin
  nr (1+ n) p (q âˆ§ qâ€²) r
    â‰ˆâŸ¨ nr-rec n p (q âˆ§ qâ€²) r âŸ©
  p âˆ§ ((q âˆ§ qâ€²) + r Â· nr n p (q âˆ§ qâ€²) r)
    â‰ˆâŸ¨ âˆ§-cong (sym (âˆ§-idem p)) (+-distribÊ³-âˆ§ _ q qâ€²) âŸ©
  (p âˆ§ p) âˆ§ ((q + r Â· nr n p (q âˆ§ qâ€²) r) âˆ§ (qâ€² + r Â· nr n p (q âˆ§ qâ€²) r))
    â‰¤âŸ¨ âˆ§-monotoneÊ³ (âˆ§-monotone (+-monotoneÊ³ (Â·-monotoneÊ³ (nr-sub-distribË¡-âˆ§ n r p q qâ€²)))
                              (+-monotoneÊ³ (Â·-monotoneÊ³ (nr-sub-distribË¡-âˆ§ n r p q qâ€²)))) âŸ©
  (p âˆ§ p) âˆ§ ((q + r Â· (nr n p q r âˆ§ nr n p qâ€² r)) âˆ§ (qâ€² + r Â· (nr n p q r âˆ§ nr n p qâ€² r)))
    â‰¤âŸ¨ âˆ§-monotoneÊ³ (âˆ§-monotone (+-monotoneÊ³ (Â·-monotoneÊ³ (âˆ§-decreasingË¡ _ _)))
                              (+-monotoneÊ³ (Â·-monotoneÊ³ (âˆ§-decreasingÊ³ _ _)))) âŸ©
  (p âˆ§ p) âˆ§ ((q + r Â· nr n p q r) âˆ§ (qâ€² + r Â· nr n p qâ€² r))
    â‰ˆË˜âŸ¨ âˆ§-assoc (p âˆ§ p) _ _ âŸ©
  ((p âˆ§ p) âˆ§ (q + r Â· nr n p q r)) âˆ§ (qâ€² + r Â· nr n p qâ€² r)
    â‰ˆâŸ¨ âˆ§-congÊ³ (âˆ§-assoc p p _) âŸ©
  (p âˆ§ p âˆ§ (q + r Â· nr n p q r)) âˆ§ (qâ€² + r Â· nr n p qâ€² r)
    â‰ˆâŸ¨ âˆ§-congÊ³ (âˆ§-congË¡ (âˆ§-comm p _)) âŸ©
  (p âˆ§ (q + r Â· nr n p q r) âˆ§ p) âˆ§ (qâ€² + r Â· nr n p qâ€² r)
    â‰ˆË˜âŸ¨ âˆ§-congÊ³ (âˆ§-assoc p _ p) âŸ©
  ((p âˆ§ (q + r Â· nr n p q r)) âˆ§ p) âˆ§ (qâ€² + r Â· nr n p qâ€² r)
    â‰ˆâŸ¨ âˆ§-assoc _ _ _ âŸ©
  (p âˆ§ q + r Â· nr n p q r) âˆ§ (p âˆ§ qâ€² + r Â· nr n p qâ€² r)
    â‰ˆË˜âŸ¨ âˆ§-cong (nr-rec n p q r) (nr-rec n p qâ€² r) âŸ©
  nr (1+ n) p q r âˆ§ nr (1+ n) p qâ€² r âˆ
  where
  open Tools.Reasoning.PartialOrder â‰¤-poset

nr-sub-distribÊ³-âˆ§ : (n : Nat) (r : M) â†’ (Î» p q  â†’ nr n p q r) SubDistributesOverÊ³ _âˆ§_ by _â‰¤_
nr-sub-distribÊ³-âˆ§ 0 r q p pâ€² = begin
  nr 0 (p âˆ§ pâ€²) q r â‰ˆâŸ¨ nr-0 (p âˆ§ pâ€²) q r âŸ©
  ğŸ˜                 â‰ˆË˜âŸ¨ âˆ§-idem ğŸ˜ âŸ©
  ğŸ˜ âˆ§ ğŸ˜             â‰ˆË˜âŸ¨ âˆ§-cong (nr-0 p q r) (nr-0 pâ€² q r) âŸ©
  nr 0 p q r âˆ§ nr 0 pâ€² q r âˆ
  where
  open Tools.Reasoning.PartialOrder â‰¤-poset
nr-sub-distribÊ³-âˆ§ (1+ n) r q p pâ€² = begin
  nr (1+ n) (p âˆ§ pâ€²) q r â‰ˆâŸ¨ nr-rec n (p âˆ§ pâ€²) q r âŸ©
  (p âˆ§ pâ€²) âˆ§ (q + r Â· nr n (p âˆ§ pâ€²) q r) â‰¤âŸ¨ âˆ§-monotoneÊ³ (+-monotoneÊ³ (Â·-monotoneÊ³ (nr-sub-distribÊ³-âˆ§ n r q p pâ€²))) âŸ©
  (p âˆ§ pâ€²) âˆ§ (q + r Â· (nr n p q r âˆ§ nr n pâ€² q r)) â‰ˆâŸ¨ âˆ§-congË¡ (+-congË¡ (Â·-distribË¡-âˆ§ r _ _)) âŸ©
  (p âˆ§ pâ€²) âˆ§ (q + (r Â· nr n p q r âˆ§ r Â· nr n pâ€² q r)) â‰ˆâŸ¨ âˆ§-congË¡ (+-distribË¡-âˆ§ q _ _) âŸ©
  (p âˆ§ pâ€²) âˆ§ (q + r Â· nr n p q r) âˆ§ (q + r Â· nr n pâ€² q r) â‰ˆË˜âŸ¨ âˆ§-assoc _ _ _ âŸ©
  ((p âˆ§ pâ€²) âˆ§ (q + r Â· nr n p q r)) âˆ§ (q + r Â· nr n pâ€² q r) â‰ˆâŸ¨ âˆ§-congÊ³ (âˆ§-assoc p pâ€² _) âŸ©
  (p âˆ§ pâ€² âˆ§ (q + r Â· nr n p q r)) âˆ§ (q + r Â· nr n pâ€² q r) â‰ˆâŸ¨ âˆ§-congÊ³ (âˆ§-congË¡ (âˆ§-comm pâ€² _)) âŸ©
  (p âˆ§ (q + r Â· nr n p q r) âˆ§ pâ€²) âˆ§ (q + r Â· nr n pâ€² q r) â‰ˆË˜âŸ¨ âˆ§-congÊ³ (âˆ§-assoc p _ pâ€²) âŸ©
  ((p âˆ§ (q + r Â· nr n p q r)) âˆ§ pâ€²) âˆ§ (q + r Â· nr n pâ€² q r) â‰ˆâŸ¨ âˆ§-assoc _ _ _ âŸ©
  (p âˆ§ q + r Â· nr n p q r) âˆ§ (pâ€² âˆ§ q + r Â· nr n pâ€² q r) â‰ˆË˜âŸ¨ âˆ§-cong (nr-rec n p q r) (nr-rec n pâ€² q r) âŸ©
  nr (1+ n) p q r âˆ§ nr (1+ n) pâ€² q r âˆ
  where
  open Tools.Reasoning.PartialOrder â‰¤-poset

is-semiring-with-meet-and-star : Semiring-with-meet-and-star M
is-semiring-with-meet-and-star = record
  { semiring-with-meet = ğ•„
  ; _âŠ›_â–·_ = _âŠ›_â–·_
  ; âŠ›-ineq = solvesIneqs
  ; +-sub-interchangeable-âŠ› = +-sub-interchangeable-nr (projâ‚ nr-fix)
  ; Â·-sub-distribÊ³-âŠ› = Â·-sub-distribÊ³-nr (projâ‚ nr-fix)
  ; âŠ›-sub-distrib-âˆ§ = Î» r â†’ nr-sub-distribË¡-âˆ§ (projâ‚ nr-fix) r , nr-sub-distribÊ³-âˆ§ (projâ‚ nr-fix) r
  }

isModality : Modality M
isModality = record
  { semiring-with-meet-and-star = is-semiring-with-meet-and-star
  ; mode-restrictions = rs
  ; ğŸ˜-well-behaved = ğŸ˜-well-behaved
  }

module ğŸ˜-bound (ğŸ˜-max : (p : M) â†’ p â‰¤ ğŸ˜) where

  greatestSolnr : âˆ€ {x} (n : Nat) â†’ x â‰¤ q + r Â· x â†’ x â‰¤ p â†’ x â‰¤ nr n p q r
  greatestSolnr 0 xâ‰¤q+rx xâ‰¤p = â‰¤-trans (ğŸ˜-max _) (â‰¤-reflexive (sym (nr-0 _ _ _)))
  greatestSolnr {q} {r} {p} {x} (1+ n) xâ‰¤q+rx xâ‰¤p = begin
    x â‰ˆË˜âŸ¨ âˆ§-idem x âŸ©
    x âˆ§ x â‰¤âŸ¨ âˆ§-monotone xâ‰¤p xâ‰¤q+rx âŸ©
    p âˆ§ (q + r Â· x) â‰¤âŸ¨ âˆ§-monotoneÊ³ (+-monotoneÊ³ (Â·-monotoneÊ³ (greatestSolnr n xâ‰¤q+rx xâ‰¤p))) âŸ©
    p âˆ§ (q + r Â· nr n p q r) â‰ˆË˜âŸ¨ nr-rec n p q r âŸ©
    nr (1+ n) p q r âˆ
    where
    open Tools.Reasoning.PartialOrder â‰¤-poset

  greatestSol : âˆ€ {x} â†’ x â‰¤ q + r Â· x â†’ x â‰¤ p â†’ x â‰¤ p âŠ› q â–· r
  greatestSol {q} {r} {p} {x} xâ‰¤q+rx xâ‰¤p = greatestSolnr (projâ‚ nr-fix) xâ‰¤q+rx xâ‰¤p
