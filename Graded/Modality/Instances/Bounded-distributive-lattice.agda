------------------------------------------------------------------------
-- Bounded, distributive lattices can be turned into modalities (if
-- equality with âŠ¤ is decidable)
------------------------------------------------------------------------

module Graded.Modality.Instances.Bounded-distributive-lattice
  {a} (M : Set a)
  where

open import Graded.Modality M
import Graded.Modality.Instances.LowerBounded as L
open import Graded.Modality.Variant a
open import Graded.Modality.Properties.Subtraction

open import Tools.Algebra M
open import Tools.Bool using (T; false)
open import Tools.Product
open import Tools.PropositionalEquality
import Tools.Reasoning.PropositionalEquality
open import Tools.Relation

private variable
  p q : M

-- Bounded, distributive lattices over M.

record Bounded-distributive-lattice : Set a where
  no-eta-equality
  pattern
  infixr 40 _âˆ§_ _âˆ¨_
  field
    -- Meet.
    _âˆ§_ : M â†’ M â†’ M

    -- Join.
    _âˆ¨_ : M â†’ M â†’ M

    -- The least element.
    âŠ¥ : M

    -- The greatest element.
    âŠ¤ : M

    -- Join and meet form a distributive lattice.
    is-distributive-lattice : IsDistributiveLattice _âˆ¨_ _âˆ§_

  open IsDistributiveLattice is-distributive-lattice public
  open DistributiveLattice is-distributive-lattice public

  -- An induced ordering relation.

  _â‰¤_ : M â†’ M â†’ Set a
  p â‰¤ q = p â‰¡ p âˆ§ q

  field
    -- âŠ¥ is the least element.
    âŠ¥â‰¤ : âˆ€ p â†’ âŠ¥ â‰¤ p

    -- âŠ¤ is the greatest element.
    â‰¤âŠ¤ : âˆ€ p â†’ p â‰¤ âŠ¤

-- Bounded, distributive lattices can be turned into "semirings with
-- meet" (if equality with âŠ¤ is decidable).

semiring-with-meet :
  (bl : Bounded-distributive-lattice) â†’
  let open Bounded-distributive-lattice bl in
  ((p : M) â†’ Dec (p â‰¡ âŠ¤)) â†’
  Semiring-with-meet
semiring-with-meet bl@record{} is-âŠ¤? = record
  { _+_           = _âˆ§_
  ; _Â·_           = _âˆ¨_
  ; _âˆ§_           = _âˆ§_
  ; ğŸ˜             = âŠ¤
  ; ğŸ™             = âŠ¥
  ; Ï‰             = âŠ¥
  ; Ï‰â‰¤ğŸ™           = âŠ¥â‰¤ _
  ; Ï‰Â·+â‰¤Ï‰Â·Ê³       = âŠ¥âˆ¨âˆ§â‰¤âŠ¥âˆ¨Ê³
  ; is-ğŸ˜?         = is-âŠ¤?
  ; +-Â·-Semiring  = record
    { isSemiringWithoutAnnihilatingZero = record
      { +-isCommutativeMonoid = record
        { isMonoid = record
          { isSemigroup = âˆ§-isSemigroup
          ; identity    = âˆ§-identityË¡ , commâˆ§idË¡â‡’idÊ³ âˆ§-comm âˆ§-identityË¡
          }
        ; comm = âˆ§-comm
        }
      ; *-cong = congâ‚‚ _âˆ¨_
      ; *-assoc = âˆ¨-assoc
      ; *-identity = âˆ¨-identityË¡ , commâˆ§idË¡â‡’idÊ³ âˆ¨-comm âˆ¨-identityË¡
      ; distrib = âˆ¨-distrib-âˆ§
      }
    ; zero = âˆ¨-zeroË¡ , commâˆ§zeË¡â‡’zeÊ³ âˆ¨-comm âˆ¨-zeroË¡
    }
  ; âˆ§-Semilattice = âˆ§-isSemilattice
  ; Â·-distrib-âˆ§   = âˆ¨-distrib-âˆ§
  ; +-distrib-âˆ§   =
      âˆ§-distribË¡-âˆ§ , commâˆ§distrË¡â‡’distrÊ³ âˆ§-comm âˆ§-distribË¡-âˆ§
  }
  where
  open Bounded-distributive-lattice bl
  open Tools.Reasoning.PropositionalEquality

  âˆ§-distribË¡-âˆ§ : _âˆ§_ DistributesOverË¡ _âˆ§_
  âˆ§-distribË¡-âˆ§ p q r =
    p âˆ§ (q âˆ§ r)        â‰¡Ë˜âŸ¨ cong (_âˆ§ _) (âˆ§-idem _) âŸ©
    (p âˆ§ p) âˆ§ (q âˆ§ r)  â‰¡âŸ¨ âˆ§-assoc _ _ _ âŸ©
    p âˆ§ (p âˆ§ (q âˆ§ r))  â‰¡Ë˜âŸ¨ cong (_ âˆ§_) (âˆ§-assoc _ _ _) âŸ©
    p âˆ§ ((p âˆ§ q) âˆ§ r)  â‰¡Ë˜âŸ¨ âˆ§-assoc _ _ _ âŸ©
    (p âˆ§ (p âˆ§ q)) âˆ§ r  â‰¡âŸ¨ cong (_âˆ§ _) (âˆ§-comm _ _) âŸ©
    ((p âˆ§ q) âˆ§ p) âˆ§ r  â‰¡âŸ¨ âˆ§-assoc _ _ _ âŸ©
    (p âˆ§ q) âˆ§ (p âˆ§ r)  âˆ

  âˆ§-identityË¡ : LeftIdentity âŠ¤ _âˆ§_
  âˆ§-identityË¡ p =
    âŠ¤ âˆ§ p  â‰¡âŸ¨ âˆ§-comm _ _ âŸ©
    p âˆ§ âŠ¤  â‰¡Ë˜âŸ¨ â‰¤âŠ¤ _ âŸ©
    p      âˆ

  âˆ¨-identityË¡ : LeftIdentity âŠ¥ _âˆ¨_
  âˆ¨-identityË¡ p =
    âŠ¥ âˆ¨ p        â‰¡âŸ¨ cong (_âˆ¨ _) (âŠ¥â‰¤ _) âŸ©
    (âŠ¥ âˆ§ p) âˆ¨ p  â‰¡âŸ¨ cong (_âˆ¨ _) (âˆ§-comm _ _) âŸ©
    (p âˆ§ âŠ¥) âˆ¨ p  â‰¡âŸ¨ âˆ¨-comm _ _ âŸ©
    p âˆ¨ (p âˆ§ âŠ¥)  â‰¡âŸ¨ âˆ¨-absorbs-âˆ§ _ _ âŸ©
    p            âˆ

  âˆ¨-zeroË¡ : LeftZero âŠ¤ _âˆ¨_
  âˆ¨-zeroË¡ p =
    âŠ¤ âˆ¨ p        â‰¡âŸ¨ cong (_ âˆ¨_) (â‰¤âŠ¤ _) âŸ©
    âŠ¤ âˆ¨ (p âˆ§ âŠ¤)  â‰¡âŸ¨ cong (_ âˆ¨_) (âˆ§-comm _ _) âŸ©
    âŠ¤ âˆ¨ (âŠ¤ âˆ§ p)  â‰¡âŸ¨ âˆ¨-absorbs-âˆ§ _ _ âŸ©
    âŠ¤            âˆ

  opaque

    âŠ¥âˆ¨âˆ§â‰¤âŠ¥âˆ¨Ê³ : âŠ¥ âˆ¨ (p âˆ§ q) â‰¤ âŠ¥ âˆ¨ q
    âŠ¥âˆ¨âˆ§â‰¤âŠ¥âˆ¨Ê³ {p} {q} =
      âŠ¥ âˆ¨ (p âˆ§ q)              â‰¡âŸ¨ âˆ¨-identityË¡ _ âŸ©
      p âˆ§ q                    â‰¡Ë˜âŸ¨ cong (_ âˆ§_) (âˆ§-idem _) âŸ©
      p âˆ§ (q âˆ§ q)              â‰¡Ë˜âŸ¨ âˆ§-assoc _ _ _ âŸ©
      (p âˆ§ q) âˆ§ q              â‰¡Ë˜âŸ¨ congâ‚‚ _âˆ§_ (âˆ¨-identityË¡ _) (âˆ¨-identityË¡ _) âŸ©
      (âŠ¥ âˆ¨ (p âˆ§ q)) âˆ§ (âŠ¥ âˆ¨ q)  âˆ

-- One can define natrec-star operators for bounded, distributive
-- lattices (if equality with âŠ¤ is decidable).

has-star :
  (bl : Bounded-distributive-lattice) â†’
  let open Bounded-distributive-lattice bl in
  {is-âŠ¤? : (p : M) â†’ Dec (p â‰¡ âŠ¤)} â†’
  Has-star (semiring-with-meet bl is-âŠ¤?)
has-star bl@record{} = L.has-star _ âŠ¥ âŠ¥â‰¤
  where
  open Bounded-distributive-lattice bl

-- Bounded, distributive lattices for which equality with âŠ¤ is
-- decidable can be turned into modalities (without ğŸ˜áµ).

modality :
  (variant : Modality-variant)
  (ğ•ƒ : Bounded-distributive-lattice) â†’
  let open Modality-variant variant
      open Bounded-distributive-lattice ğ•ƒ
  in
  {is-âŠ¤? : (p : M) â†’ Dec (p â‰¡ âŠ¤)} â†’
  (T ğŸ˜áµ-allowed â†’ Has-well-behaved-zero (semiring-with-meet ğ•ƒ is-âŠ¤?)) â†’
  Modality
modality variant ğ•ƒ@record{} = L.isModality
  (semiring-with-meet ğ•ƒ _)
  âŠ¥
  âŠ¥â‰¤
  variant
  where
  open Bounded-distributive-lattice ğ•ƒ

opaque

  -- Bounded, distributive lattices support Subtraction

  supports-subtraction :
    (bl : Bounded-distributive-lattice) â†’
    let open Bounded-distributive-lattice bl in
    (_â‰ŸâŠ¤ : (p : M) â†’ Dec (p â‰¡ âŠ¤)) â†’
    Supports-subtraction (semiring-with-meet bl _â‰ŸâŠ¤)
  supports-subtraction bl@record{} _â‰ŸâŠ¤ =
    Additionâ‰¡Meet.supports-subtraction (semiring-with-meet bl _â‰ŸâŠ¤)
      Î» _ _ â†’ refl
