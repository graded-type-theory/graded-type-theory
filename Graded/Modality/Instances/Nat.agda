------------------------------------------------------------------------
-- A modality for the natural numbers.
--
-- Unlike â„•âŠâˆ, it is not possible to define an nr function for this
-- modality. The usage rule for natrec using greatest lower bounds
-- is still supported.
------------------------------------------------------------------------

open import Tools.Bool hiding (_âˆ§_; âˆ§-decreasingË¡; âˆ§-decreasingÊ³)

module Graded.Modality.Instances.Nat where

open import Tools.Empty
open import Tools.Function
open import Tools.Level using (lzero)
import Tools.Nat as N
open import Tools.Nat using (Nat; 1+; 2+; _âŠ”_; _âˆ¸_; Sequence)
open import Tools.Product
open import Tools.PropositionalEquality
open import Tools.Relation
open import Tools.Sum
import Tools.Reasoning.PartialOrder as RPo

open import Graded.Modality Nat
open import Graded.Modality.Variant lzero

import Graded.Modality.Properties.Addition
import Graded.Modality.Properties.Greatest-lower-bound
import Graded.Modality.Properties.Has-well-behaved-zero
import Graded.Modality.Properties.Natrec
import Graded.Modality.Properties.PartialOrder
import Graded.Modality.Properties.Subtraction

open import Definition.Untyped Nat
open import Definition.Typed.Restrictions
open import Graded.FullReduction.Assumptions
open import Graded.Usage.Restrictions

private variable
  p q r z zâ‚ zâ‚‚ s sâ‚ sâ‚‚ : Nat
  páµ¢ : Sequence Nat
  variant : Modality-variant
  TR : Type-restrictions _
  UR : Usage-restrictions _

------------------------------------------------------------------------
-- The modality

opaque

  -- A "semiring with meet" for Nat.

  Nat-semiring-with-meet : Semiring-with-meet
  Nat-semiring-with-meet = record
    { _+_ = _+_
    ; _Â·_ = _*_
    ; _âˆ§_ = _âŠ”_
    ; ğŸ˜ = 0
    ; ğŸ™ = 1
    ; Ï‰ = 1
    ; +-Â·-Semiring = +-*-isSemiring
    ; âˆ§-Semilattice = âŠ”-isSemilattice
    ; Â·-distrib-âˆ§ = *-distribË¡-âŠ” , *-distribÊ³-âŠ”
    ; +-distrib-âˆ§ = +-distribË¡-âŠ” , +-distribÊ³-âŠ”
    ; Ï‰â‰¤ğŸ™ = refl
    ; Ï‰Â·+â‰¤Ï‰Â·Ê³ = Î» {p} {q} â†’
      sym (mâ‰¥nâ‡’mâŠ”nâ‰¡m (â‰¤-trans (mâ‰¤n+m (q + 0) p)
        (â‰¤-reflexive (sym (+-assoc p q 0)))))
    ; is-ğŸ˜? = _â‰Ÿ 0
    }
    where
    open N

open Semiring-with-meet Nat-semiring-with-meet

opaque
  unfolding Nat-semiring-with-meet

  instance
    -- The semiring has a well-behaved zero.

    Nat-has-well-behaved-zero :
      Has-well-behaved-zero Nat-semiring-with-meet
    Nat-has-well-behaved-zero = record
      { non-trivial = Î» ()
      ; zero-product = m*nâ‰¡0â‡’mâ‰¡0âˆ¨nâ‰¡0 _
      ; +-positiveË¡ = Î» { {(0)} _ â†’ refl ; {1+ p} () }
      ; âˆ§-positiveË¡ = Î» { {(0)} x â†’ refl
                       ; {1+ p} {(0)} ()
                       ; {1+ p} {1+ q} () }
      }
      where
      open N

opaque

  -- A modality (of any kind) for Nat defined using the nr function

  Nat-modality : Modality-variant â†’ Modality
  Nat-modality variant = record
    { variant = variant
    ; semiring-with-meet = Nat-semiring-with-meet
    ; ğŸ˜-well-behaved = Î» _ â†’ Nat-has-well-behaved-zero
    }

opaque
  unfolding Nat-semiring-with-meet

  -- The order for the modality is the reverse order for Nat

  â‰¤â‡”â‰¥ : p â‰¤ q â‡” q N.â‰¤ p
  â‰¤â‡”â‰¥ = N.mâŠ”nâ‰¡mâ‡’nâ‰¤m âˆ˜â†’ sym , sym âˆ˜â†’ N.mâ‰¥nâ‡’mâŠ”nâ‰¡m

opaque

  -- Zero is the greatest element

  â‰¤0 : âˆ€ p â†’ p â‰¤ 0
  â‰¤0 p = â‰¤â‡”â‰¥ .projâ‚‚ N.zâ‰¤n

opaque

  -- 1+ p is less than p

  1+-decreasing : âˆ€ p â†’ 1+ p â‰¤ p
  1+-decreasing p = â‰¤â‡”â‰¥ .projâ‚‚ (N.mâ‰¤n+m p 1)

------------------------------------------------------------------------
-- Nr functions for Nat

opaque
  unfolding Nat-semiring-with-meet

  -- The modality does not have an nr function

  Â¬Nat-Has-nr : Â¬ Has-nr Nat-semiring-with-meet
  Â¬Nat-Has-nr has-nr = lemma _ (nr-suc {0} {1} {0} {1} {0})
    where
    open Has-nr has-nr
    lemma : âˆ€ n â†’ n â‰¢ n âŠ” 1+ (n + 0)
    lemma Nat.zero ()
    lemma (1+ n) eq = lemma n (N.1+-injective eq)

------------------------------------------------------------------------
-- Instances of Full-reduction-assumptions

-- Instances of Type-restrictionsÂ (Nat-modalityÂ variant) and are
-- suitable for the full reduction theorem if
-- * whenever Î£Ë¢-allowedÂ mÂ n holds, then m is âŒÂ 1Â âŒŸ, or
--   m is âŒÂ 0Â âŒŸ, and ğŸ˜áµ is allowed.

Suitable-for-full-reduction :
  âˆ€ variant â†’ Type-restrictions (Nat-modality variant) â†’ Set
Suitable-for-full-reduction variant TRs =
  âˆ€ p q â†’ Î£Ë¢-allowed p q â†’ p â‰¡ 1 âŠ p â‰¡ 0 Ã— T ğŸ˜áµ-allowed
  where
  open Modality-variant variant
  open Type-restrictions TRs

opaque
  unfolding Nat-modality Nat-semiring-with-meet

  -- Given an instance of Type-restrictionsÂ (â„•âŠâˆ-modalityÂ variant) one
  -- can create a "suitable" instance.

  suitable-for-full-reduction :
    Type-restrictions (Nat-modality variant) â†’
    âˆƒ (Suitable-for-full-reduction variant)
  suitable-for-full-reduction {variant} TR =
    record TR
      { Î Î£-allowed = Î» b p q â†’
          Î Î£-allowed b p q Ã— (b â‰¡ BMÎ£ ğ•¤ â†’ p â‰¡ 1 âŠ p â‰¡ 0 Ã— T ğŸ˜áµ-allowed)
      ; []-cong-allowed = Î» s â†’
          []-cong-allowed s Ã— T ğŸ˜áµ-allowed
      ; []-congâ†’Erased = Î» (hypâ‚ , hypâ‚‚) â†’
          let okâ‚ , okâ‚‚ = []-congâ†’Erased hypâ‚
          in  okâ‚ , okâ‚‚ , Î» { refl â†’ injâ‚‚ (refl , hypâ‚‚) }
      ; []-congâ†’Â¬Trivial = Î» _ ()
      }
    , Î» _ _ (_ , hyp) â†’ hyp refl
    where
    open Modality-variant variant
    open Type-restrictions TR

opaque
  unfolding Nat-modality Nat-semiring-with-meet

  -- The full reduction assumptions hold for Nat-modalityÂ variant and
  -- any "suitable" instance of Type-restrictions.

  full-reduction-assumptions :
    Suitable-for-full-reduction variant TR â†’
    Full-reduction-assumptions TR UR
  full-reduction-assumptions hyp = record
    { sinkâŠğŸ™â‰¤ğŸ˜ = Î» _ _ â†’ injâ‚‚ refl
    ; â‰¡ğŸ™âŠğŸ™â‰¤ğŸ˜ = Î» {p} {q} ok â†’
        case hyp p q ok of Î» where
          (injâ‚ pâ‰¡1) â†’ injâ‚ pâ‰¡1
          (injâ‚‚ (pâ‰¡0 , ok)) â†’ injâ‚‚ (pâ‰¡0 , ok , refl)
    }

opaque
  unfolding Nat-modality Nat-semiring-with-meet

  -- Type and usage restrictions that satisfy the full reduction
  -- assumptions are "suitable".

  full-reduction-assumptions-suitable :
    Full-reduction-assumptions TR UR â†’
    Suitable-for-full-reduction variant TR
  full-reduction-assumptions-suitable as p q ok =
    case â‰¡ğŸ™âŠğŸ™â‰¤ğŸ˜ ok of Î» where
      (injâ‚ pâ‰¡1) â†’ injâ‚ pâ‰¡1
      (injâ‚‚ (pâ‰¡0 , ok , _)) â†’ injâ‚‚ (pâ‰¡0 , ok)
    where
    open Full-reduction-assumptions as

------------------------------------------------------------------------
-- Subtraction

open Graded.Modality.Properties.Addition Nat-semiring-with-meet
open Graded.Modality.Properties.Greatest-lower-bound Nat-semiring-with-meet
open Graded.Modality.Properties.Has-well-behaved-zero Nat-semiring-with-meet
open Graded.Modality.Properties.Natrec Nat-semiring-with-meet
open Graded.Modality.Properties.PartialOrder Nat-semiring-with-meet
open Graded.Modality.Properties.Subtraction Nat-semiring-with-meet

opaque

  unfolding Nat-semiring-with-meet

  -- An inversion lemma for Subtraction

  p-qâ‰¤r-inv : p - q â‰¤ r â†’ p â‰¤ q Ã— (p âˆ¸ q) â‰¤ r
  p-qâ‰¤r-inv {p} {(0)} {r} p-qâ‰¤r =
    sym (N.âŠ”-identityÊ³ p) , trans p-qâ‰¤r (âˆ§-congË¡ (N.+-identityÊ³ r))
  p-qâ‰¤r-inv {(0)} {1+ q} {r} p-qâ‰¤r =
    case trans p-qâ‰¤r (N.+-suc r q) of Î» ()
  p-qâ‰¤r-inv {1+ p} {1+ q} {r} p-qâ‰¤r =
    let pâ‰¤q , pâˆ¸qâ‰¤r = p-qâ‰¤r-inv {p} {q} (N.1+-injective
                        (trans p-qâ‰¤r (âˆ§-congË¡ (N.+-suc r q))))
    in  (+-monotoneÊ³ pâ‰¤q) , pâˆ¸qâ‰¤r

opaque
  unfolding Nat-semiring-with-meet

  -- If p â‰¤ q then p - q â‰¡ p âˆ¸ q

  p-qâ‰¡pâˆ¸q : p â‰¤ q â†’ p - q â‰¡ (p âˆ¸ q)
  p-qâ‰¡pâˆ¸q {q} pâ‰¤q =
    let pâˆ¸q+qâ‰¡p = N.mâˆ¸n+nâ‰¡m (â‰¤â‡”â‰¥ .projâ‚ pâ‰¤q)
    in  â‰¤â‡”â‰¥ .projâ‚‚ (N.â‰¤-reflexive pâˆ¸q+qâ‰¡p)
         , Î» râ€² p-qâ‰¤râ€² â†’ p-qâ‰¤r-inv {q = q} p-qâ‰¤râ€² .projâ‚‚

opaque

  -- The semiring supports subtraction with
  --   p - q â‰¡ p âˆ¸ q whenever p â‰¤ q
  -- and not defined otherwise

  supports-subtraction : Supports-subtraction
  supports-subtraction {p} {q} {r} p-qâ‰¤r =
    let pâ‰¤q , _ = p-qâ‰¤r-inv {q = q} p-qâ‰¤r
        pâˆ¸q+qâ‰¡p = N.mâˆ¸n+nâ‰¡m (â‰¤â‡”â‰¥ .projâ‚ pâ‰¤q)
    in  p âˆ¸ q , p-qâ‰¡pâˆ¸q pâ‰¤q


-- An alternative definition of the subtraction relation with
--   p - q â‰¡ p âˆ¸ q whenever p â‰¤ q
-- and not defined otherwise

data _-_â‰¡â€²_ : (p q r : Nat) â†’ Set where
  m-nâ‰¡â€²mâˆ¸n : p â‰¤ q â†’ p - q â‰¡â€² (p âˆ¸ q)

opaque

  -- The two subtraction relations are equivalent.

  -â‰¡â†”-â‰¡â€² : (p - q â‰¡ r) â‡” (p - q â‰¡â€² r)
  -â‰¡â†”-â‰¡â€² = left , right
    where
    left : p - q â‰¡ r â†’ p - q â‰¡â€² r
    left p-qâ‰¡r@(p-qâ‰¤r , _) =
      let pâ‰¤q , _ = p-qâ‰¤r-inv p-qâ‰¤r
      in  case -â‰¡-functional p-qâ‰¡r (p-qâ‰¡pâˆ¸q pâ‰¤q) of Î» {
            refl â†’
          m-nâ‰¡â€²mâˆ¸n pâ‰¤q }
    right : p - q â‰¡â€² r â†’ p - q â‰¡ r
    right (m-nâ‰¡â€²mâˆ¸n pâ‰¤q) = p-qâ‰¡pâˆ¸q pâ‰¤q

------------------------------------------------------------------------
-- Greatest-lower-bounds

opaque
  unfolding Nat-semiring-with-meet

  -- An inversion lemma for greatest lower bounds of nráµ¢ sequences

  nráµ¢-GLB-inv :
    âˆ€ r z s â†’ Greatest-lower-bound p (nráµ¢ r z s) â†’
    r â‰¡ 0 âŠ (r â‰¡ 1 Ã— s â‰¡ 0) âŠ (z â‰¡ 0 Ã— s â‰¡ 0)
  nráµ¢-GLB-inv 0 _ s p-glb = injâ‚ refl
  nráµ¢-GLB-inv (1+ 0) _ 0 p-glb = injâ‚‚ (injâ‚ (refl , refl))
  nráµ¢-GLB-inv (2+ r) 0 0 p-glb = injâ‚‚ (injâ‚‚ (refl , refl))
  nráµ¢-GLB-inv {p} (2+ r) (1+ z) 0 p-glb =
    case â‰¤-antisym
           (â‰¤-trans (p-glb .projâ‚ (1+ p)) (â‰¤â‡”â‰¥ .projâ‚‚ (lemmaâ‚ƒ (1+ p))))
           (1+-decreasing p) of Î» ()
    where
    lemmaâ‚ : âˆ€ p q â†’ p â‰¢ 0 â†’ 1+ p N.â‰¤ (2+ q) Â· p
    lemmaâ‚ 0 q pâ‰¢0 = âŠ¥-elim (pâ‰¢0 refl)
    lemmaâ‚ (1+ p) q pâ‰¢0 = let open N.â‰¤-Reasoning in begin
      2+ p                       â‰¤âŸ¨ N.sâ‰¤s (N.sâ‰¤s (N.mâ‰¤m+n p (p + q Â· 1+ p))) âŸ©
      1+ (1+ p + (p + q Â· 1+ p)) â‰¡Ë˜âŸ¨ cong 1+ (N.+-suc p (p + q Â· 1+ p)) âŸ©
      1+ (p + 1+ (p + q Â· 1+ p)) â‰¡âŸ¨âŸ©
      2+ q Â· 1+ p âˆ
    lemmaâ‚‚ : âˆ€ i â†’ nráµ¢ (2+ r) (1+ z) 0 i â‰¢ 0
    lemmaâ‚‚ 0 ()
    lemmaâ‚‚ (1+ i) nráµ¢â‰¡0 =
      case N.m*nâ‰¡0â‡’mâ‰¡0âˆ¨nâ‰¡0 (2+ r) {nráµ¢ (2+ r) (1+ z) 0 i} nráµ¢â‰¡0 of Î» where
        (injâ‚ ())
        (injâ‚‚ x) â†’ lemmaâ‚‚ i x
    lemmaâ‚ƒ : âˆ€ i â†’ i N.â‰¤ nráµ¢ (2+ r) (1+ z) 0 i
    lemmaâ‚ƒ 0 = N.zâ‰¤n
    lemmaâ‚ƒ (1+ i) = let open N.â‰¤-Reasoning in begin
      1+ i                           â‰¤âŸ¨ N.sâ‰¤s (lemmaâ‚ƒ i) âŸ©
      1+ (nráµ¢ (2+ r) (1+ z) 0 i)     â‰¤âŸ¨ lemmaâ‚ _ r (lemmaâ‚‚ i) âŸ©
      (2+ r) Â· nráµ¢ (2+ r) (1+ z) 0 i â‰¡âŸ¨âŸ©
      nráµ¢ (2+ r) (1+ z) 0 (1+ i)     âˆ
  nráµ¢-GLB-inv {p} (1+ r) z (1+ s) p-glb =
    case â‰¤-antisym
           (â‰¤-trans (p-glb .projâ‚ (1+ p)) (â‰¤â‡”â‰¥ .projâ‚‚ (lemma (1+ p))))
           (1+-decreasing p) of Î» ()
    where
    lemma : âˆ€ i â†’ i N.â‰¤ nráµ¢ (1+ r) z (1+ s) i
    lemma 0 = N.zâ‰¤n
    lemma (1+ i) = let open N.â‰¤-Reasoning in begin
      1+ i                                    â‰¤âŸ¨ N.sâ‰¤s (lemma i) âŸ©
      1+ (nráµ¢ (1+ r) z (1+ s) i)              â‰¤âŸ¨ N.mâ‰¤n+m _ _ âŸ©
      s + 1+ (nráµ¢ (1+ r) z (1+ s) i)          â‰¡âŸ¨ N.+-suc _ _ âŸ©
      (1+ s) + nráµ¢ (1+ r) z (1+ s) i          â‰¡Ë˜âŸ¨ +-congË¡ {x = 1+ s} (Â·-identityË¡ _) âŸ©
      (1+ s) + 1 Â· (nráµ¢ (1+ r) z (1+ s) i)    â‰¤âŸ¨ N.+-mono-â‰¤ {x = 1+ s} N.â‰¤-refl
                                                 (N.*-mono-â‰¤ (N.sâ‰¤s (N.zâ‰¤n {n = r})) N.â‰¤-refl) âŸ©
      (1+ s) + (1+ r) Â· nráµ¢ (1+ r) z (1+ s) i â‰¡âŸ¨âŸ©
      nráµ¢ (1+ r) z (1+ s) (1+ i)              âˆ

opaque
  unfolding Nat-semiring-with-meet

  -- The existence of a greatest lower bound to the sequence nráµ¢Â rÂ zÂ s
  -- is decidable.

  nráµ¢-GLB-dec : âˆ€ r z s â†’ Dec (âˆƒ Î» p â†’ Greatest-lower-bound p (nráµ¢ r z s))
  nráµ¢-GLB-dec 0 z s = yes (_ , nráµ¢-ğŸ˜-GLB)
  nráµ¢-GLB-dec r 0 0 = yes (_ , GLB-nráµ¢-ğŸ˜)
  nráµ¢-GLB-dec (1+ 0) (1+ z) 0 = yes (_ , nráµ¢-const-GLB)
  nráµ¢-GLB-dec (2+ r) (1+ z) 0 =
    no (Î» (_ , p-glb) â†’
      case nráµ¢-GLB-inv _ _ _ p-glb of Î» where
        (injâ‚ ())
        (injâ‚‚ (injâ‚ ()))
        (injâ‚‚ (injâ‚‚ ())))
  nráµ¢-GLB-dec (1+ r) z (1+ s) =
    no (Î» (_ , p-glb) â†’
      case nráµ¢-GLB-inv _ _ _ p-glb of Î» where
        (injâ‚ ())
        (injâ‚‚ (injâ‚ ()))
        (injâ‚‚ (injâ‚‚ ())))

opaque
  unfolding Nat-semiring-with-meet

  -- The modality supports the usage rule for natrec using
  -- greatest lower bounds.

  Nat-supports-glb-for-natrec :
    Supports-GLB-for-natrec Nat-semiring-with-meet
  Nat-supports-glb-for-natrec = record
    { +-GLBË¡ = +-GLBË¡
    ; Â·-GLBË¡ = Î» {_} {_} {q} â†’ Â·-GLBË¡ {q = q}
    ; Â·-GLBÊ³ = Â·-GLBÊ³
    ; +nráµ¢-GLB = +nráµ¢-GLB --+nráµ¢-GLB
    }
    where
    +-GLBË¡ :
      Greatest-lower-bound p páµ¢ â†’
      Greatest-lower-bound (q + p) (Î» i â†’ q + páµ¢ i)
    +-GLBË¡ {p} {páµ¢} {q} (pâ‰¤páµ¢ , p-glb) =
      let páµ¢â‰¤p , p-lub = N.+-LUB {k = q} páµ¢ (â‰¤â‡”â‰¥ .projâ‚ âˆ˜â†’ pâ‰¤páµ¢)
                           Î» r páµ¢â‰¤r â†’ â‰¤â‡”â‰¥ .projâ‚ (p-glb r (â‰¤â‡”â‰¥ .projâ‚‚ âˆ˜â†’ páµ¢â‰¤r))
      in  â‰¤â‡”â‰¥ .projâ‚‚ âˆ˜â†’ páµ¢â‰¤p , Î» r râ‰¤páµ¢ â†’ â‰¤â‡”â‰¥ .projâ‚‚ (p-lub r (â‰¤â‡”â‰¥ .projâ‚ âˆ˜â†’ râ‰¤páµ¢))
    Â·-GLBË¡ :
      Greatest-lower-bound p páµ¢ â†’
      Greatest-lower-bound (q Â· p) (Î» i â†’ q Â· páµ¢ i)
    Â·-GLBË¡ {p} {páµ¢} {q} (pâ‰¤páµ¢ , p-glb) =
      let páµ¢â‰¤p , p-lub = N.*-LUB {k = q} páµ¢ (â‰¤â‡”â‰¥ .projâ‚ âˆ˜â†’ pâ‰¤páµ¢)
                           Î» r páµ¢â‰¤r â†’ â‰¤â‡”â‰¥ .projâ‚ (p-glb r (â‰¤â‡”â‰¥ .projâ‚‚ âˆ˜â†’ páµ¢â‰¤r))
      in  â‰¤â‡”â‰¥ .projâ‚‚ âˆ˜â†’ páµ¢â‰¤p , Î» r râ‰¤páµ¢ â†’ â‰¤â‡”â‰¥ .projâ‚‚ (p-lub r (â‰¤â‡”â‰¥ .projâ‚ âˆ˜â†’ râ‰¤páµ¢))
    Â·-GLBÊ³ :
      Greatest-lower-bound p páµ¢ â†’
      Greatest-lower-bound (p Â· q) (Î» i â†’ páµ¢ i Â· q)
    Â·-GLBÊ³ {p} {páµ¢} {q} p-glb =
      GLB-cong (N.*-comm q p) (Î» i â†’ N.*-comm q (páµ¢ i))
        (Â·-GLBË¡ {q = q} p-glb)
    open RPo â‰¤-poset
    +-nráµ¢-GLBâ€² :
      Greatest-lower-bound p (nráµ¢ 0 zâ‚ sâ‚) â†’
      Greatest-lower-bound q (nráµ¢ 0 zâ‚‚ sâ‚‚) â†’
      âˆƒ Î» r â†’ Greatest-lower-bound r (nráµ¢ 0 (zâ‚ + zâ‚‚) (sâ‚ + sâ‚‚)) Ã— p + q â‰¤ r
    +-nráµ¢-GLBâ€² {zâ‚} {sâ‚} {zâ‚‚} {sâ‚‚} p-GLB q-GLB =
      case GLB-unique p-GLB nráµ¢-ğŸ˜-GLB of Î» {
        refl â†’
      case GLB-unique q-GLB nráµ¢-ğŸ˜-GLB of Î» {
        refl â†’
      _ , nráµ¢-ğŸ˜-GLB , +-sub-interchangeable-âˆ§ zâ‚ sâ‚ zâ‚‚ sâ‚‚ }}
    +nráµ¢-GLB :
      Greatest-lower-bound p (nráµ¢ r zâ‚ sâ‚) â†’
      Greatest-lower-bound q (nráµ¢ r zâ‚‚ sâ‚‚) â†’
      âˆƒ Î» râ€² â†’ Greatest-lower-bound râ€² (nráµ¢ r (zâ‚ + zâ‚‚) (sâ‚ + sâ‚‚)) Ã— p + q â‰¤ râ€²
    +nráµ¢-GLB {p} {r} {zâ‚} {sâ‚} {q} {zâ‚‚} {sâ‚‚}
             p-GLB@(pâ‰¤ , p-glb) q-GLB@(qâ‰¤ , q-glb) =
      case nráµ¢-GLB-inv r zâ‚ sâ‚ p-GLB of Î» where
        (injâ‚ refl) â†’ +-nráµ¢-GLBâ€² p-GLB q-GLB
        (injâ‚‚ (injâ‚ (refl , refl))) â†’
          case nráµ¢-GLB-inv r zâ‚‚ sâ‚‚ q-GLB of Î» where
            (injâ‚ ())
            (injâ‚‚ (injâ‚ (refl , refl))) â†’
              _ , nráµ¢-const-GLB , +-monotone (pâ‰¤ 0) (qâ‰¤ 0)
            (injâ‚‚ (injâ‚‚ (refl , refl))) â†’
              _ , nráµ¢-const-GLB , +-monotone (pâ‰¤ 0) (qâ‰¤ 0)
        (injâ‚‚ (injâ‚‚ (refl , refl))) â†’
          case nráµ¢-GLB-inv r zâ‚‚ sâ‚‚ q-GLB of Î» where
            (injâ‚ refl) â†’ +-nráµ¢-GLBâ€² p-GLB q-GLB
            (injâ‚‚ (injâ‚ (refl , refl))) â†’
              _ , nráµ¢-const-GLB , +-monotone (pâ‰¤ 0) (qâ‰¤ 0)
            (injâ‚‚ (injâ‚‚ (refl , refl))) â†’
              _ , GLB-const nráµ¢-ğŸ˜
                , +-monotone (pâ‰¤ 0) (qâ‰¤ 0)
