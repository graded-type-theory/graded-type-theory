------------------------------------------------------------------------
-- A bounded, distributive lattice for which division is not total for
-- the associated "semiring with meet"
------------------------------------------------------------------------

module
  Graded.Modality.Instances.Bounded-distributive-lattice.No-division
  where

open import Graded.Modality
import Graded.Modality.Properties.Division
open import Graded.Modality.Instances.Bounded-distributive-lattice
  as BDL using (Bounded-distributive-lattice)

open import Tools.Empty renaming (âŠ¥ to âŠ¥â€²)
open import Tools.Function
open import Tools.Nat as N using (Nat; 1+; _âŠ”_; _âŠ“_)
open import Tools.Product
open import Tools.PropositionalEquality as PE
import Tools.Reasoning.PropositionalEquality
open import Tools.Relation
open import Tools.Sum using (_âŠ_; injâ‚; injâ‚‚)

private variable
  m n : Nat

-- The elements of âŠ¤âŠâ„•âŠâ„• are of the formÂ âŠ¥, leftÂ n, or rightÂ n.

data âŠ¤âŠâ„•âŠâ„• : Set where
  âŠ¥          : âŠ¤âŠâ„•âŠâ„•
  left right : Nat â†’ âŠ¤âŠâ„•âŠâ„•

open import Tools.Algebra âŠ¤âŠâ„•âŠâ„•

-- One can turn âŠ¤âŠâ„•âŠâ„• into a bounded distributive lattice with the
-- following ordering:
--
-- * âŠ¥Â â‰¤Â p.
--
-- * leftÂ mÂ â‰¤Â leftÂ n iff mÂ â‰¥Â n.
--
-- * rightÂ mÂ â‰¤Â rightÂ n iff mÂ â‰¥Â n.
--
-- * leftÂ nÂ â‰¤Â rightÂ n.

bounded-distributive-lattice : Bounded-distributive-lattice âŠ¤âŠâ„•âŠâ„•
bounded-distributive-lattice = record
  { _âˆ§_                     = _âˆ§_
  ; _âˆ¨_                     = _âˆ¨_
  ; âŠ¥                       = âŠ¥
  ; âŠ¤                       = right 0
  ; is-distributive-lattice = record
    { isLattice = record
      { isEquivalence = PE.isEquivalence
      ; âˆ¨-comm        = âˆ¨-comm
      ; âˆ¨-assoc = Î» where
          âŠ¥         _         _         â†’ refl
          (left  _) âŠ¥         _         â†’ refl
          (left  _) (left  _) âŠ¥         â†’ refl
          (left  _) (left  _) (left  _) â†’ cong left  (N.âŠ“-assoc _ _ _)
          (left  _) (left  _) (right _) â†’ cong right (N.âŠ“-assoc _ _ _)
          (left  _) (right _) âŠ¥         â†’ refl
          (left  _) (right _) (left  _) â†’ cong right (N.âŠ“-assoc _ _ _)
          (left  _) (right _) (right _) â†’ cong right (N.âŠ“-assoc _ _ _)
          (right _) âŠ¥         _         â†’ refl
          (right _) (left  _) âŠ¥         â†’ refl
          (right _) (left  _) (left  _) â†’ cong right (N.âŠ“-assoc _ _ _)
          (right _) (left  _) (right _) â†’ cong right (N.âŠ“-assoc _ _ _)
          (right _) (right _) âŠ¥         â†’ refl
          (right _) (right _) (left  _) â†’ cong right (N.âŠ“-assoc _ _ _)
          (right _) (right _) (right _) â†’ cong right (N.âŠ“-assoc _ _ _)
      ; âˆ¨-cong = PE.congâ‚‚ _âˆ¨_
      ; âˆ§-comm = âˆ§-comm
      ; âˆ§-assoc = Î» where
          âŠ¥         _         _         â†’ refl
          (left  _) âŠ¥         _         â†’ refl
          (left  _) (left  _) âŠ¥         â†’ refl
          (left  m) (left  _) (left  _) â†’ cong left  (N.âŠ”-assoc m _ _)
          (left  m) (left  _) (right _) â†’ cong left  (N.âŠ”-assoc m _ _)
          (left  _) (right _) âŠ¥         â†’ refl
          (left  m) (right _) (left  _) â†’ cong left  (N.âŠ”-assoc m _ _)
          (left  m) (right _) (right _) â†’ cong left  (N.âŠ”-assoc m _ _)
          (right _) âŠ¥         _         â†’ refl
          (right _) (left  _) âŠ¥         â†’ refl
          (right m) (left  _) (left  _) â†’ cong left  (N.âŠ”-assoc m _ _)
          (right m) (left  _) (right _) â†’ cong left  (N.âŠ”-assoc m _ _)
          (right _) (right _) âŠ¥         â†’ refl
          (right m) (right _) (left  _) â†’ cong left  (N.âŠ”-assoc m _ _)
          (right m) (right _) (right _) â†’ cong right (N.âŠ”-assoc m _ _)
      ; âˆ§-cong     = PE.congâ‚‚ _âˆ§_
      ; absorptive =
            (Î» where
               (left  _) (left  _) â†’ cong left  (N.âŠ“-absorbs-âŠ” _ _)
               (left  _) (right _) â†’ cong left  (N.âŠ“-absorbs-âŠ” _ _)
               (left  _) âŠ¥         â†’ refl
               (right _) (left  _) â†’ cong right (N.âŠ“-absorbs-âŠ” _ _)
               (right _) (right _) â†’ cong right (N.âŠ“-absorbs-âŠ” _ _)
               (right _) âŠ¥         â†’ refl
               âŠ¥         _         â†’ refl)
          , (Î» where
               (left  _) (left  _) â†’ cong left  (N.âŠ”-absorbs-âŠ“ _ _)
               (left  _) (right _) â†’ cong left  (N.âŠ”-absorbs-âŠ“ _ _)
               (left  _) âŠ¥         â†’ cong left  (N.âŠ”-idem _)
               (right _) (left  _) â†’ cong right (N.âŠ”-absorbs-âŠ“ _ _)
               (right _) (right _) â†’ cong right (N.âŠ”-absorbs-âŠ“ _ _)
               (right _) âŠ¥         â†’ cong right (N.âŠ”-idem _)
               âŠ¥         _         â†’ refl)
      }
    ; âˆ¨-distrib-âˆ§ =
          commâˆ§distrÊ³â‡’distrË¡ âˆ¨-comm âˆ¨-distribÊ³-âˆ§
        , âˆ¨-distribÊ³-âˆ§
    ; âˆ§-distrib-âˆ¨ =
          commâˆ§distrÊ³â‡’distrË¡ âˆ§-comm âˆ§-distribÊ³-âˆ¨
        , âˆ§-distribÊ³-âˆ¨

    }
  ; âŠ¥â‰¤ = Î» _ â†’ refl
  ; â‰¤âŠ¤ = Î» where
      (left  _) â†’ cong left  (sym (N.âŠ”-identityÊ³ _))
      (right _) â†’ cong right (sym (N.âŠ”-identityÊ³ _))
      âŠ¥         â†’ refl
  }
  where
  open Tools.Reasoning.PropositionalEquality

  infix 40 _âˆ§_ _âˆ¨_

  _âˆ§_ : âŠ¤âŠâ„•âŠâ„• â†’ âŠ¤âŠâ„•âŠâ„• â†’ âŠ¤âŠâ„•âŠâ„•
  âŠ¥       âˆ§ _       = âŠ¥
  _       âˆ§ âŠ¥       = âŠ¥
  left  m âˆ§ left  n = left  (m âŠ” n)
  right m âˆ§ right n = right (m âŠ” n)
  left  m âˆ§ right n = left  (m âŠ” n)
  right m âˆ§ left  n = left  (m âŠ” n)

  _âˆ¨_ : âŠ¤âŠâ„•âŠâ„• â†’ âŠ¤âŠâ„•âŠâ„• â†’ âŠ¤âŠâ„•âŠâ„•
  âŠ¥       âˆ¨ q       = q
  p       âˆ¨ âŠ¥       = p
  left  m âˆ¨ left  n = left  (m âŠ“ n)
  right m âˆ¨ right n = right (m âŠ“ n)
  left  m âˆ¨ right n = right (m âŠ“ n)
  right m âˆ¨ left  n = right (m âŠ“ n)

  âˆ§-comm : Commutative _âˆ§_
  âˆ§-comm = Î» where
    âŠ¥         âŠ¥         â†’ refl
    âŠ¥         (left  _) â†’ refl
    âŠ¥         (right _) â†’ refl
    (left  _) âŠ¥         â†’ refl
    (left  m) (left  _) â†’ cong left  (N.âŠ”-comm m _)
    (left  m) (right _) â†’ cong left  (N.âŠ”-comm m _)
    (right _) âŠ¥         â†’ refl
    (right m) (left  _) â†’ cong left  (N.âŠ”-comm m _)
    (right m) (right _) â†’ cong right (N.âŠ”-comm m _)

  âˆ¨-comm : Commutative _âˆ¨_
  âˆ¨-comm = Î» where
    âŠ¥         âŠ¥         â†’ refl
    âŠ¥         (left  _) â†’ refl
    âŠ¥         (right _) â†’ refl
    (left  _) âŠ¥         â†’ refl
    (left  _) (left  _) â†’ cong left  (N.âŠ“-comm _ _)
    (left  _) (right _) â†’ cong right (N.âŠ“-comm _ _)
    (right _) âŠ¥         â†’ refl
    (right _) (left  _) â†’ cong right (N.âŠ“-comm _ _)
    (right _) (right _) â†’ cong right (N.âŠ“-comm _ _)

  lemmaâ‚ : m â‰¡ m âŠ” (n âŠ“ m)
  lemmaâ‚ {m = m} {n = n} =
    m            â‰¡Ë˜âŸ¨ N.âŠ”-absorbs-âŠ“ m n âŸ©
    m âŠ” (m âŠ“ n)  â‰¡âŸ¨ cong (m âŠ”_) (N.âŠ“-comm _ _) âŸ©
    m âŠ” (n âŠ“ m)  âˆ

  lemmaâ‚‚ : m â‰¡ (n âŠ“ m) âŠ” m
  lemmaâ‚‚ {m = m} {n = n} =
    m            â‰¡âŸ¨ lemmaâ‚ âŸ©
    m âŠ” (n âŠ“ m)  â‰¡âŸ¨ N.âŠ”-comm m _ âŸ©
    (n âŠ“ m) âŠ” m  âˆ

  âˆ¨-distribÊ³-âˆ§ : _âˆ¨_ DistributesOverÊ³ _âˆ§_
  âˆ¨-distribÊ³-âˆ§ = Î» where
    (left  _) (left  n) (left  _) â†’ cong left  (N.âŠ“-distribÊ³-âŠ” _ n _)
    (right _) (left  n) (left  _) â†’ cong right (N.âŠ“-distribÊ³-âŠ” _ n _)
    âŠ¥         (left  _) (left  _) â†’ refl
    (left  _) (left  n) (right _) â†’ cong left  (N.âŠ“-distribÊ³-âŠ” _ n _)
    (right _) (left  n) (right _) â†’ cong right (N.âŠ“-distribÊ³-âŠ” _ n _)
    âŠ¥         (left  _) (right _) â†’ refl
    (left  _) (left  _) âŠ¥         â†’ cong left  lemmaâ‚‚
    (right _) (left  _) âŠ¥         â†’ cong right lemmaâ‚‚
    âŠ¥         (left  _) âŠ¥         â†’ refl
    (left  _) (right n) (left  _) â†’ cong left  (N.âŠ“-distribÊ³-âŠ” _ n _)
    (right _) (right n) (left  _) â†’ cong right (N.âŠ“-distribÊ³-âŠ” _ n _)
    âŠ¥         (right _) (left  _) â†’ refl
    (left  _) (right n) (right _) â†’ cong right (N.âŠ“-distribÊ³-âŠ” _ n _)
    (right _) (right n) (right _) â†’ cong right (N.âŠ“-distribÊ³-âŠ” _ n _)
    âŠ¥         (right _) (right _) â†’ refl
    (left  _) (right _) âŠ¥         â†’ cong left  lemmaâ‚‚
    (right _) (right _) âŠ¥         â†’ cong right lemmaâ‚‚
    âŠ¥         (right _) âŠ¥         â†’ refl
    (left  _) âŠ¥         (left  _) â†’ cong left  lemmaâ‚
    (right _) âŠ¥         (left  _) â†’ cong right lemmaâ‚
    âŠ¥         âŠ¥         (left  _) â†’ refl
    (left  _) âŠ¥         (right _) â†’ cong left  lemmaâ‚
    (right _) âŠ¥         (right _) â†’ cong right lemmaâ‚
    âŠ¥         âŠ¥         (right _) â†’ refl
    (left  _) âŠ¥         âŠ¥         â†’ cong left  (sym (N.âŠ”-idem _))
    (right _) âŠ¥         âŠ¥         â†’ cong right (sym (N.âŠ”-idem _))
    âŠ¥         âŠ¥         âŠ¥         â†’ refl

  âˆ§-distribÊ³-âˆ¨ : _âˆ§_ DistributesOverÊ³ _âˆ¨_
  âˆ§-distribÊ³-âˆ¨ = Î» where
    (left  _) (left  n) (left  _) â†’ cong left (N.âŠ”-distribÊ³-âŠ“ _ n _)
    (right _) (left  n) (left  _) â†’ cong left (N.âŠ”-distribÊ³-âŠ“ _ n _)
    âŠ¥         (left  _) (left  _) â†’ refl
    (left  _) (left  n) (right _) â†’ cong left (N.âŠ”-distribÊ³-âŠ“ _ n _)
    (right _) (left  n) (right _) â†’ cong right (N.âŠ”-distribÊ³-âŠ“ _ n _)
    âŠ¥         (left  _) (right _) â†’ refl
    (left  _) (left  _) âŠ¥         â†’ refl
    (right _) (left  _) âŠ¥         â†’ refl
    âŠ¥         (left  _) âŠ¥         â†’ refl
    (left  _) (right n) (left  _) â†’ cong left (N.âŠ”-distribÊ³-âŠ“ _ n _)
    (right _) (right n) (left  _) â†’ cong right (N.âŠ”-distribÊ³-âŠ“ _ n _)
    âŠ¥         (right _) (left  _) â†’ refl
    (left  _) (right n) (right _) â†’ cong left (N.âŠ”-distribÊ³-âŠ“ _ n _)
    (right _) (right n) (right _) â†’ cong right (N.âŠ”-distribÊ³-âŠ“ _ n _)
    âŠ¥         (right _) (right _) â†’ refl
    (left  _) (right _) âŠ¥         â†’ refl
    (right _) (right _) âŠ¥         â†’ refl
    âŠ¥         (right _) âŠ¥         â†’ refl
    (left  _) âŠ¥         (left  _) â†’ refl
    (right _) âŠ¥         (left  _) â†’ refl
    âŠ¥         âŠ¥         (left  _) â†’ refl
    (left  _) âŠ¥         (right _) â†’ refl
    (right _) âŠ¥         (right _) â†’ refl
    âŠ¥         âŠ¥         (right _) â†’ refl
    (left  _) âŠ¥         âŠ¥         â†’ refl
    (right _) âŠ¥         âŠ¥         â†’ refl
    âŠ¥         âŠ¥         âŠ¥         â†’ refl



-- The "semiring with meet" associated to
-- bounded-distributive-lattice.

semiring-with-meet : Semiring-with-meet âŠ¤âŠâ„•âŠâ„•
semiring-with-meet =
  BDL.semiring-with-meet _ bounded-distributive-lattice

-- The zero-product property fails for this "semiring with meet".

Â¬-zero-product :
  let open Semiring-with-meet semiring-with-meet in
  Â¬ (âˆ€ {p q} â†’ p Â· q â‰¡ ğŸ˜ â†’ p â‰¡ ğŸ˜ âŠ q â‰¡ ğŸ˜)
Â¬-zero-product =
  ({p q : âŠ¤âŠâ„•âŠâ„•} â†’ p Â· q â‰¡ right 0 â†’ p â‰¡ right 0 âŠ q â‰¡ right 0)        â†’âŸ¨ (Î» hyp â†’ hyp) âŸ©
  (right 1 Â· left 0 â‰¡ right 0 â†’ right 1 â‰¡ right 0 âŠ left 0 â‰¡ right 0)  â†’âŸ¨ _$ refl âŸ©
  right 1 â‰¡ right 0 âŠ left 0 â‰¡ right 0                                 â†’âŸ¨ (Î» { (injâ‚ ()); (injâ‚‚ ()) }) âŸ©
  âŠ¥â€²                                                                   â–¡
  where
  open Semiring-with-meet semiring-with-meet

-- This "semiring with meet" does not have a well-behaved zero.

Â¬-Has-well-behaved-zero :
  Â¬ Has-well-behaved-zero âŠ¤âŠâ„•âŠâ„• semiring-with-meet
Â¬-Has-well-behaved-zero =
  Has-well-behaved-zero âŠ¤âŠâ„•âŠâ„• semiring-with-meet                 â†’âŸ¨ Has-well-behaved-zero.zero-product âŸ©
  ({p q : âŠ¤âŠâ„•âŠâ„•} â†’ p Â· q â‰¡ right 0 â†’ p â‰¡ right 0 âŠ q â‰¡ right 0)  â†’âŸ¨ Â¬-zero-product âŸ©
  âŠ¥â€²                                                             â–¡
  where
  open Semiring-with-meet semiring-with-meet

open Graded.Modality.Properties.Division semiring-with-meet

-- Division by leftÂ n is not supported for semiring-with-meet.

Â¬-Supports-division-by-left : Â¬ Supports-division-by (left n)
Â¬-Supports-division-by-left {n = nâ‚€} =
  Supports-division-by (left nâ‚€)    â†’âŸ¨ Supports-division-byâ‡” {q = left _} .projâ‚ âŸ©
  (âˆ€ p â†’ âˆƒ Î» r â†’ p / left nâ‚€ â‰¡ r)   â†’âŸ¨ _$ _ âŸ©
  (âˆƒ Î» r â†’ right nâ‚€ / left nâ‚€ â‰¡ r)  â†’âŸ¨ rnâ‚€/lnâ‚€â‰¢ _ âˆ˜â†’ projâ‚‚ âŸ©
  âŠ¥â€²                                â–¡
  where
  open Semiring-with-meet semiring-with-meet

  right-injective : right m â‰¡ right n â†’ m â‰¡ n
  right-injective refl = refl

  rnâ‚€/lnâ‚€â‰¢ : âˆ€ p â†’ Â¬ right nâ‚€ / left nâ‚€ â‰¡ p
  rnâ‚€/lnâ‚€â‰¢ âŠ¥ =
    right nâ‚€ / left nâ‚€ â‰¡ âŠ¥  â†’âŸ¨ projâ‚ âŸ©
    right nâ‚€ / left nâ‚€ â‰¤ âŠ¥  â†’âŸ¨ (Î» ()) âŸ©
    âŠ¥â€²                    â–¡
  rnâ‚€/lnâ‚€â‰¢ (left n) =
    (right nâ‚€ / left nâ‚€ â‰¡ left n)  â†’âŸ¨ projâ‚ âŸ©
    (right nâ‚€ / left nâ‚€ â‰¤ left n)  â†’âŸ¨ idá¶  âŸ©
    right nâ‚€ â‰¤ left (nâ‚€ âŠ“ n)       â†’âŸ¨ (Î» ()) âŸ©
    âŠ¥â€²                             â–¡
  rnâ‚€/lnâ‚€â‰¢ (right n) =
    (right nâ‚€ / left nâ‚€ â‰¡ right n)  â†’âŸ¨ (Î» hyp â†’ hyp .projâ‚‚ _ (cong right (sym (N.âŠ”-absorbs-âŠ“ _ _)))) âŸ©
    right n â‰¤ right (1+ n)          â†’âŸ¨ right-injective âŸ©
    n â‰¡ n âŠ” 1+ n                    â†’âŸ¨ trans (sym (N.mâ‰¥nâ‡’mâŠ”nâ‰¡m (N.nâ‰¤1+n _))) âˆ˜â†’ trans (N.âŠ”-comm (1+ n) n) âˆ˜â†’ sym âŸ©
    1+ n â‰¡ n                        â†’âŸ¨ (Î» ()) âŸ©
    âŠ¥â€²                              â–¡

-- Division is not supported for semiring-with-meet.

Â¬-Supports-division : Â¬ Supports-division
Â¬-Supports-division =
  Supports-division              â†’âŸ¨ _$ left _ âŸ©
  Supports-division-by (left 0)  â†’âŸ¨ Â¬-Supports-division-by-left âŸ©
  âŠ¥â€²                             â–¡

-- Division is not total for semiring-with-meet.

Â¬-division-total : Â¬ ((p q : âŠ¤âŠâ„•âŠâ„•) â†’ âˆƒ (p / q â‰¡_))
Â¬-division-total =
  ((p q : âŠ¤âŠâ„•âŠâ„•) â†’ âˆƒ (p / q â‰¡_))  â†’âŸ¨ (Î» hyp p â†’ Supports-division-byâ‡” {q = p} .projâ‚‚ (flip hyp p)) âŸ©
  Supports-division               â†’âŸ¨ Â¬-Supports-division âŸ©
  âŠ¥â€²                              â–¡
