------------------------------------------------------------------------
-- The types Dedicated-nr and No-dedicated-nr
------------------------------------------------------------------------

import Graded.Modality

module Graded.Modality.Dedicated-nr
  {a} {M : Set a}
  (open Graded.Modality M)
  (ùïÑ : Modality)
  where

open Modality ùïÑ

open import Tools.Empty
open import Tools.Function
open import Tools.Nullary
open import Tools.PropositionalEquality

private variable
  A : Set _

------------------------------------------------------------------------
-- Dedicated-nr

-- A wrapper type, intended to be used in the types of instance
-- arguments.

record Dedicated-nr : Set a where
  constructor dedicated-nr
  field
    nr : Nr-available

-- This wrapper type is propositional.

Dedicated-nr-propositional : (p q : Dedicated-nr) ‚Üí p ‚â° q
Dedicated-nr-propositional (dedicated-nr n‚ÇÅ) (dedicated-nr n‚ÇÇ) =
  cong dedicated-nr (Nr-available-propositional n‚ÇÅ n‚ÇÇ)

------------------------------------------------------------------------
-- No-dedicated-nr

-- A wrapper type, intended to be used in the types of instance
-- arguments.

record No-dedicated-nr : Set a where
  constructor no-dedicated-nr
  field
    no-nr : ¬¨ Nr-available

------------------------------------------------------------------------
-- Some lemmas related to both Dedicated-nr and No-dedicated-nr

-- One cannot both have and not have a dedicated nr function.

not-nr-and-no-nr :
  ‚¶É nr : Dedicated-nr ‚¶Ñ ‚¶É no-nr : No-dedicated-nr ‚¶Ñ ‚Üí ‚ä•
not-nr-and-no-nr
  ‚¶É nr = dedicated-nr n ‚¶Ñ ‚¶É no-nr = no-dedicated-nr nn ‚¶Ñ =
  nn n

-- The property of either having or not having a dedicated nr
-- function.

data Dedicated-nr? : Set a where
  does-have-nr     : ‚¶É has-nr : Dedicated-nr ‚¶Ñ ‚Üí Dedicated-nr?
  does-not-have-nr : ‚¶É no-nr : No-dedicated-nr ‚¶Ñ ‚Üí Dedicated-nr?

-- One either has or does not have a dedicated nr function.

dedicated-nr? : Dedicated-nr?
dedicated-nr? = case Nr-available-decided of Œª where
  (yes has-nr) ‚Üí does-have-nr ‚¶É has-nr = dedicated-nr has-nr ‚¶Ñ
  (no no-nr)   ‚Üí
    does-not-have-nr ‚¶É no-nr = no-dedicated-nr no-nr ‚¶Ñ
