------------------------------------------------------------------------
-- Restrictions on usage derivations
------------------------------------------------------------------------

import Graded.Modality

module Graded.Usage.Restrictions
  {a} {M : Set a}
  (open Graded.Modality M)
  (ğ•„ : Modality)
  where

open import Graded.Mode ğ•„
open import Graded.Usage.Erased-matches
open import Graded.Usage.Restrictions.Natrec ğ•„
open import Definition.Untyped.NotParametrised

open import Tools.Bool
open import Tools.Function
open import Tools.Level
open import Tools.PropositionalEquality
open import Tools.Relation
open import Tools.Sum
open import Tools.Empty
open import Tools.Unit

open Modality ğ•„

private variable
  p q r : M
  m mâ€²  : Mode
  s : Strength
  â¦ƒ ok â¦„ : T _

-- Restrictions on/choices for usage derivations.

record Usage-restrictions : Set (lsuc a) where
  no-eta-equality
  field
    -- Which usage rule for natrec should be used.
    natrec-mode : Natrec-mode

    -- The prodrec constructor's quantities have to satisfy this
    -- predicate (when the mode is ğŸ™áµ).
    Prodrec-allowed-ğŸ™áµ : (r p q : M) â†’ Set a

    -- The unitrec constructor's quantities have to satisfy this
    -- predicate (when the mode is ğŸ™áµ).
    Unitrec-allowed-ğŸ™áµ : (p q : M) â†’ Set a

    -- The emptyrec constructor's quantity has to satisfy this
    -- predicate (when the mode is ğŸ™áµ).
    Emptyrec-allowed-ğŸ™áµ : M â†’ Set a

    -- Should []-cong be allowed (when the mode is ğŸ™áµ)?
    []-cong-allowed-mode-ğŸ™áµ : Strength â†’ Set a

    -- Should strong unit types act as "sinks"?
    starË¢-sink : Bool

    -- Are most things erased in the usage rule for Id?
    Id-erased : Set a

    -- Id-erased is decided.
    Id-erased? : Dec Id-erased

    -- What kinds of erased matches are allowed for the J rule (for
    -- the current mode)?
    erased-matches-for-J : Mode â†’ Erased-matches

    -- The usage rules for J are at least as permissive for ğŸ˜áµ[Â okÂ ]
    -- as forÂ ğŸ™áµ. (See Graded.Usage.Properties.Jâ‚˜-generalised and
    -- Graded.Usage.Properties.Jâ‚€â‚˜â‚-generalised.)
    erased-matches-for-J-â‰¤áµ‰áµ :
      erased-matches-for-J ğŸ™áµ â‰¤áµ‰áµ erased-matches-for-J ğŸ˜áµ[ ok ]

    -- What kinds of erased matches are allowed for the K rule (for
    -- the current mode)?
    erased-matches-for-K : Mode â†’ Erased-matches

    -- The usage rules for K are at least as permissive for ğŸ˜áµ[Â okÂ ]
    -- as forÂ ğŸ™áµ. (See Graded.Usage.Properties.Kâ‚˜-generalised and
    -- Graded.Usage.Properties.Kâ‚€â‚˜â‚-generalised.)
    erased-matches-for-K-â‰¤áµ‰áµ :
      erased-matches-for-K ğŸ™áµ â‰¤áµ‰áµ erased-matches-for-K ğŸ˜áµ[ ok ]

  -- Three mutually exclusive types which correspond to each of the
  -- three possibilities for natrec-mode

  Nr-available : Set a
  Nr-available = Natrec-mode-has-nr natrec-mode

  Nr-not-available : Set a
  Nr-not-available = Natrec-mode-no-nr natrec-mode

  Nr-not-available-GLB : Set a
  Nr-not-available-GLB = Natrec-mode-no-nr-glb natrec-mode

  -- Do strong unit types act as "sinks"?

  StarË¢-sink : Set
  StarË¢-sink = T starË¢-sink

  -- Strong unit types are not allowed to both act and not act as
  -- sinks.

  not-sink-and-no-sink : StarË¢-sink â†’ Â¬ StarË¢-sink â†’ âŠ¥
  not-sink-and-no-sink sink Â¬sink with starË¢-sink
  â€¦ | false = sink
  â€¦ | true = Â¬sink _

  -- Strong unit types either act or do not act as sinks.

  sink-or-no-sink : StarË¢-sink âŠ Â¬ StarË¢-sink
  sink-or-no-sink with starË¢-sink
  â€¦ | false = injâ‚‚ idá¶ 
  â€¦ | true = injâ‚ _

  private opaque

    -- A lemma used below.

    Â·áµ-lemma :
      (f : Mode â†’ Erased-matches) â†’
      (âˆ€ â¦ƒ ok â¦„ â†’ f ğŸ™áµ â‰¤áµ‰áµ f ğŸ˜áµ[ ok ]) â†’
      f m â‰¤áµ‰áµ f (mâ€² Â·áµ m)
    Â·áµ-lemma          {mâ€² = ğŸ™áµ} _ _   = â‰¤áµ‰áµ-reflexive
    Â·áµ-lemma {m = ğŸ™áµ} {mâ€² = ğŸ˜áµ} _ hyp = hyp
    Â·áµ-lemma {m = ğŸ˜áµ} {mâ€² = ğŸ˜áµ} f _   =
      subst (_â‰¤áµ‰áµ_ _) (cong f ğŸ˜áµ-cong) â‰¤áµ‰áµ-reflexive

  opaque

    -- The usage rules for J are at least as permissive for mâ€²Â Â·áµÂ m as
    -- forÂ m. (See Graded.Usage.Properties.Jâ‚˜-generalised and
    -- Graded.Usage.Properties.Jâ‚€â‚˜â‚-generalised.)

    erased-matches-for-J-â‰¤áµ‰áµÂ·áµ :
      erased-matches-for-J m â‰¤áµ‰áµ erased-matches-for-J (mâ€² Â·áµ m)
    erased-matches-for-J-â‰¤áµ‰áµÂ·áµ =
      Â·áµ-lemma erased-matches-for-J erased-matches-for-J-â‰¤áµ‰áµ

  opaque

    -- The usage rules for K are at least as permissive for mâ€²Â Â·áµÂ m as
    -- forÂ m. (See Graded.Usage.Properties.Kâ‚˜-generalised and
    -- Graded.Usage.Properties.Kâ‚€â‚˜â‚-generalised.)

    erased-matches-for-K-â‰¤áµ‰áµÂ·áµ :
      erased-matches-for-K m â‰¤áµ‰áµ erased-matches-for-K (mâ€² Â·áµ m)
    erased-matches-for-K-â‰¤áµ‰áµÂ·áµ =
      Â·áµ-lemma erased-matches-for-K erased-matches-for-K-â‰¤áµ‰áµ

  ----------------------------------------------------------------------
  -- Variants of Prodrec-allowed-ğŸ™áµ, Unitrec-allowed-ğŸ™áµ,
  -- Emptyrec-allowed-ğŸ™áµ and []-cong-allowed-mode-ğŸ™áµ

  -- The prodrec constructor's quantities have to satisfy this
  -- predicate (for the current mode).

  Prodrec-allowed : Mode â†’ (r p q : M) â†’ Set a
  Prodrec-allowed ğŸ™áµ = Prodrec-allowed-ğŸ™áµ
  Prodrec-allowed ğŸ˜áµ = Î» _ _ _ â†’ Lift _ âŠ¤

  -- The unitrec constructor's quantities have to satisfy this
  -- predicate (for the current mode).

  Unitrec-allowed : Mode â†’ (p q : M) â†’ Set a
  Unitrec-allowed ğŸ™áµ = Unitrec-allowed-ğŸ™áµ
  Unitrec-allowed ğŸ˜áµ = Î» _ _ â†’ Lift _ âŠ¤

  -- The emptyrec constructor's quantity has to satisfy this
  -- predicate (for the current mode).

  Emptyrec-allowed : Mode â†’ M â†’ Set a
  Emptyrec-allowed ğŸ™áµ = Emptyrec-allowed-ğŸ™áµ
  Emptyrec-allowed ğŸ˜áµ = Î» _ â†’ Lift _ âŠ¤

  -- Should []-cong be allowed for the current mode?

  []-cong-allowed-mode : Strength â†’ Mode â†’ Set a
  []-cong-allowed-mode s ğŸ™áµ = []-cong-allowed-mode-ğŸ™áµ s
  []-cong-allowed-mode _ ğŸ˜áµ = Lift _ âŠ¤
