------------------------------------------------------------------------
-- The extraction function.
------------------------------------------------------------------------

open import Graded.Modality

module Graded.Erasure.Extraction
  {a} {M : Set a}
  (ğ•„ : Modality M)
  where

open Modality ğ•„

open import Tools.Bool
open import Tools.Function
open import Tools.Nat
open import Tools.Relation

open import Definition.Untyped M as U
open import Graded.Erasure.Target as T
open import Graded.Erasure.Target.Non-terminating

private
  variable
    m n : Nat
    Î“ : Con U.Term n
    A t tâ€² u : U.Term n
    v vâ€² w : T.Term n
    p : M

-- Extraction for prodrec when the match is not erased.

erase-prodrecÏ‰ :
  Strictness â†’ M â†’ T.Term n â†’ T.Term (2+ n) â†’ T.Term n
erase-prodrecÏ‰ s p t u = case is-ğŸ˜? p of Î» where
    (yes pâ‰¡ğŸ˜) â†’ T.lam (u T.[ T.liftSubst (T.sgSubst (loop s)) ])
                  T.âˆ˜âŸ¨ s âŸ© t
    (no pâ‰¢ğŸ˜) â†’ T.prodrec t u

mutual

  -- The extraction function.
  --
  -- Function and constructor applications are made strict if the
  -- first argument is "strict".
  --
  -- A non-terminating term, loopÂ s, is used instead of â†¯ in some
  -- places. The idea is that it should be safe to replace this term
  -- with, say, a term that throws an exception.

  erase : Strictness â†’ U.Term n â†’ T.Term n
  erase = eraseâ€² false

  -- A variant of the extraction function.
  --
  -- If the boolean is true, then erasable arguments are removed
  -- entirely.

  eraseâ€² : Bool â†’ Strictness â†’ U.Term n â†’ T.Term n
  eraseâ€² remove s = eraseâ€³
    where
    eraseâ€³ : U.Term n â†’ T.Term n
    eraseâ€³ (var x)                 = T.var x
    eraseâ€³ U                       = â†¯
    eraseâ€³ (Î Î£âŸ¨ _ âŸ© _ , _ â–· _ â–¹ _) = â†¯
    eraseâ€³ (U.lam p t)             = case remove of Î» where
      false â†’ T.lam (eraseâ€³ t)
      true  â†’ case is-ğŸ˜? p of Î» where
        (no _)  â†’ T.lam (eraseâ€³ t)
        (yes _) â†’ eraseâ€³ t T.[ â†¯ ]â‚€
    eraseâ€³ (t U.âˆ˜âŸ¨ p âŸ© u) = case is-ğŸ˜? p of Î» where
      (no _)  â†’ eraseâ€³ t T.âˆ˜âŸ¨ s âŸ© eraseâ€³ u
      (yes _) â†’ case remove of Î» where
        false â†’ eraseâ€³ t T.âˆ˜âŸ¨ s âŸ© â†¯
        true  â†’ eraseâ€³ t
    eraseâ€³ (U.prod _ p t u) = case is-ğŸ˜? p of Î» where
      (no _)  â†’ prodâŸ¨ s âŸ© (eraseâ€³ t) (eraseâ€³ u)
      (yes _) â†’ eraseâ€³ u
    eraseâ€³ (U.fst p t) = case is-ğŸ˜? p of Î» where
      (no _)  â†’ T.fst (eraseâ€³ t)
      (yes _) â†’ loop s
    eraseâ€³ (U.snd p t) = case is-ğŸ˜? p of Î» where
      (no _)  â†’ T.snd (eraseâ€³ t)
      (yes _) â†’ eraseâ€³ t
    eraseâ€³ (U.prodrec r p _ _ t u) = case is-ğŸ˜? r of Î» where
      (no _)  â†’ erase-prodrecÏ‰ s p (eraseâ€³ t) (eraseâ€³ u)
      (yes _) â†’ eraseâ€³ u T.[ loop s , loop s ]
    eraseâ€³ â„•                        = â†¯
    eraseâ€³ U.zero                   = T.zero
    eraseâ€³ (U.suc t)                = sucâŸ¨ s âŸ© (eraseâ€³ t)
    eraseâ€³ (U.natrec p q r A t u v) =
      T.natrec (eraseâ€³ t) (eraseâ€³ u) (eraseâ€³ v)
    eraseâ€³ Unit!                 = â†¯
    eraseâ€³ U.star!               = T.star
    eraseâ€³ (U.unitrec p q A t u) = case is-ğŸ˜? p of Î» where
      (no _)  â†’ T.unitrec (eraseâ€³ t) (eraseâ€³ u)
      (yes _) â†’ eraseâ€³ u
    eraseâ€³ Empty               = â†¯
    eraseâ€³ (emptyrec p A t)    = loop s
    eraseâ€³ (Id _ _ _)          = â†¯
    eraseâ€³ U.rfl               = â†¯
    eraseâ€³ (J _ _ _ _ _ u _ _) = eraseâ€³ u
    eraseâ€³ (K _ _ _ _ u _)     = eraseâ€³ u
    eraseâ€³ ([]-cong _ _ _ _ _) = â†¯

mutual

  -- Extraction of substitutions.

  eraseSubst : Strictness â†’ U.Subst m n â†’ T.Subst m n
  eraseSubst = eraseSubstâ€² false

  -- A variant of eraseSubst.

  eraseSubstâ€² : Bool â†’ Strictness â†’ U.Subst m n â†’ T.Subst m n
  eraseSubstâ€² b s Ïƒ x = eraseâ€² b s (Ïƒ x)
