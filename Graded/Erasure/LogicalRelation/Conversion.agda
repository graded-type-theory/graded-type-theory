------------------------------------------------------------------------
-- Type conversion lemmata for the logical relation
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Erasure.LogicalRelation.Assumptions
open import Graded.Modality

module Graded.Erasure.LogicalRelation.Conversion
  {a} {M : Set a}
  {ğ•„ : Modality M}
  {R : Type-restrictions ğ•„}
  (as : Assumptions R)
  where

open Assumptions as
open Modality ğ•„
open Type-restrictions R

open import Graded.Erasure.LogicalRelation as
import Graded.Erasure.Target as T

open import Definition.LogicalRelation R
open import Definition.LogicalRelation.Hidden R
open import Definition.LogicalRelation.Fundamental.Reducibility R
open import Definition.LogicalRelation.ShapeView R
open import Definition.LogicalRelation.Properties.Conversion R
open import Definition.LogicalRelation.Properties.Escape R
open import Definition.Untyped M
open import Definition.Untyped.Neutral M type-variant
open import Definition.Untyped.Properties M

open import Definition.Typed R
open import Definition.Typed.Consequences.Injectivity R
open import Definition.Typed.Properties R
open import Definition.Typed.Reasoning.Type R
open import Definition.Typed.Substitution R
open import Definition.Typed.Weakening R hiding (wk)

open import Tools.Function
open import Tools.Nat
open import Tools.Product
import Tools.PropositionalEquality as PE
open import Tools.Relation
open import Tools.Unit

private
  variable
    n : Nat
    Î“ : Con Term n
    A B t : Term n
    v : T.Term n
    p : M

-- Conversion of logical relation for erasure using ShapeView
-- If t Â® v âˆ· A and Î” âŠ© A â‰¡ B then t Â® v âˆ· B

convTermÊ³â€² : âˆ€ {l lâ€²}
           â†’ ([A] : Î” âŠ©âŸ¨ l âŸ© A)
             ([B] : Î” âŠ©âŸ¨ lâ€² âŸ© B)
           â†’ Î” âŠ¢ A â‰¡ B
           â†’ ShapeView Î” l lâ€² A B [A] [B]
           â†’ t Â®âŸ¨ l âŸ© v âˆ· A / [A]
           â†’ t Â®âŸ¨ lâ€² âŸ© v âˆ· B / [B]
convTermÊ³â€² _ _ Aâ‰¡B (Uáµ¥ UA UB) tÂ®v = tÂ®v
convTermÊ³â€² _ _ Aâ‰¡B (â„•áµ¥ â„•A â„•B) tÂ®v = tÂ®v
convTermÊ³â€²
  {A} {B} {l} {lâ€²}
  _ _ Aâ‰¡B (Unitáµ¥ {s} (Unitâ‚œ Aâ‡’*Unit _) (Unitâ‚œ Bâ‡’*Unit _)) tÂ®v =
  case Unit-injectivity
         (Unit s l  â‰¡Ë˜âŸ¨ subset* Aâ‡’*Unit âŸ©âŠ¢
          A         â‰¡âŸ¨ Aâ‰¡B âŸ©âŠ¢
          B         â‰¡âŸ¨ subset* Bâ‡’*Unit âŸ©âŠ¢âˆ
          Unit s lâ€² âˆ) of Î» {
    (_ , PE.refl) â†’
  tÂ®v }
convTermÊ³â€²
  [A] [B] Aâ‰¡B
  (Báµ¥ (BÎ  p q) (Báµ£ F G Aâ‡’Î  Aâ‰¡A [F] [G] G-ext _)
     (Báµ£ Fâ‚ Gâ‚ Bâ‡’Î â‚ Aâ‰¡Aâ‚ [F]â‚ [G]â‚ G-extâ‚ _))
  tÂ®v
     with is-ğŸ˜? p
... | yes PE.refl = tÂ®v .projâ‚ , Î» [a]â€² â†’
  let Î â‰¡Î â‚ = reductionâ€² (Aâ‡’Î  , Î Î£â‚™) (Bâ‡’Î â‚ , Î Î£â‚™) Aâ‰¡B
      Fâ‰¡Fâ‚ , Gâ‰¡Gâ‚ , _ , _ = injectivity Î â‰¡Î â‚
      [Fâ‚â‰¡F] = âŠ©â‰¡â†’âŠ©â‰¡/ ([F]â‚ _) $
               PE.substâ‚‚ (_âŠ©âŸ¨_âŸ©_â‰¡_ _ _) (PE.sym $ wk-id _)
                 (PE.sym $ wk-id _) $
               reducible-âŠ©â‰¡ (sym Fâ‰¡Fâ‚) .projâ‚‚
      [a] = convTermâ‚ ([F]â‚ (idÊ· âŠ¢Î”)) ([F] (idÊ· âŠ¢Î”)) [Fâ‚â‰¡F] [a]â€²
      Gâ‰¡Gâ‚â€² = wkEq (liftÊ· id (escape ([F] (idÊ· âŠ¢Î”)))) Gâ‰¡Gâ‚
      G[a]â‰¡Gâ‚[a] = substTypeEq Gâ‰¡Gâ‚â€²
                     (refl (escapeTerm ([F] (idÊ· âŠ¢Î”)) [a]))
      [Gaâ‰¡Gâ‚a] = âŠ©â‰¡â†’âŠ©â‰¡/ ([G] _ _) (reducible-âŠ©â‰¡ G[a]â‰¡Gâ‚[a] .projâ‚‚)
      tÂ®vâ€² = tÂ®v .projâ‚‚ [a]
      SV = goodCases ([G] (idÊ· âŠ¢Î”) [a]) ([G]â‚ (idÊ· âŠ¢Î”) [a]â€²) [Gaâ‰¡Gâ‚a]
  in  convTermÊ³â€² ([G] (idÊ· âŠ¢Î”) [a]) ([G]â‚ (idÊ· âŠ¢Î”) [a]â€²) G[a]â‰¡Gâ‚[a] SV tÂ®vâ€²
... | no pâ‰¢ğŸ˜ = tÂ®v .projâ‚ , Î» [a]â€² aÂ®wâ€² â†’
  let Î â‰¡Î â‚ = reductionâ€² (Aâ‡’Î  , Î Î£â‚™) (Bâ‡’Î â‚ , Î Î£â‚™) Aâ‰¡B
      Fâ‰¡Fâ‚ , Gâ‰¡Gâ‚ , _ , _ = injectivity Î â‰¡Î â‚
      [Fâ‚â‰¡F] = âŠ©â‰¡â†’âŠ©â‰¡/ ([F]â‚ _) $
               PE.substâ‚‚ (_âŠ©âŸ¨_âŸ©_â‰¡_ _ _) (PE.sym $ wk-id _)
                 (PE.sym $ wk-id _) $
               reducible-âŠ©â‰¡ (sym Fâ‰¡Fâ‚) .projâ‚‚
      [a] = convTermâ‚ ([F]â‚ (idÊ· âŠ¢Î”)) ([F] (idÊ· âŠ¢Î”)) [Fâ‚â‰¡F] [a]â€²
      Gâ‰¡Gâ‚â€² = wkEq (liftÊ· id (escape ([F] (idÊ· âŠ¢Î”)))) Gâ‰¡Gâ‚
      G[a]â‰¡Gâ‚[a] = substTypeEq Gâ‰¡Gâ‚â€²
                     (refl (escapeTerm ([F] (idÊ· âŠ¢Î”)) [a]))
      [Gaâ‰¡Gâ‚a] = âŠ©â‰¡â†’âŠ©â‰¡/ ([G] _ _) (reducible-âŠ©â‰¡ G[a]â‰¡Gâ‚[a] .projâ‚‚)
      SV = goodCases ([F]â‚ (idÊ· âŠ¢Î”)) ([F] (idÊ· âŠ¢Î”)) [Fâ‚â‰¡F]
      Fâ‚â‰¡F = PE.substâ‚‚ (Î” âŠ¢_â‰¡_) (PE.sym (wk-id Fâ‚)) (PE.sym (wk-id F)) (sym Fâ‰¡Fâ‚)
      aÂ®w = convTermÊ³â€² ([F]â‚ (idÊ· âŠ¢Î”)) ([F] (idÊ· âŠ¢Î”)) Fâ‚â‰¡F SV aÂ®wâ€²
      tÂ®vâ€² = tÂ®v .projâ‚‚ [a] aÂ®w
      SVâ€² = goodCases ([G] (idÊ· âŠ¢Î”) [a]) ([G]â‚ (idÊ· âŠ¢Î”) [a]â€²) [Gaâ‰¡Gâ‚a]
  in  convTermÊ³â€² ([G] (idÊ· âŠ¢Î”) [a]) ([G]â‚ (idÊ· âŠ¢Î”) [a]â€²) G[a]â‰¡Gâ‚[a] SVâ€² tÂ®vâ€²
convTermÊ³â€² {v = v}
  [A] [B] Aâ‰¡B
  (Báµ¥ (BÎ£ _ p _) (Báµ£ F G Aâ‡’Î£ Aâ‰¡A [F] [G] G-ext _)
     (Báµ£ Fâ‚ Gâ‚ Bâ‡’Î£â‚ Aâ‰¡Aâ‚ [F]â‚ [G]â‚ G-extâ‚ _))
  (tâ‚ , tâ‚‚ , tâ‡’tâ€² , [tâ‚] , vâ‚‚ , tâ‚‚Â®vâ‚‚ , extra) =
  let Î£â‰¡Î£â‚ = reductionâ€² (Aâ‡’Î£ , Î Î£â‚™) (Bâ‡’Î£â‚ , Î Î£â‚™) Aâ‰¡B
      Fâ‰¡Fâ‚ , Gâ‰¡Gâ‚ , _ = Î£-injectivity Î£â‰¡Î£â‚
      [F]â€² = [F] (idÊ· âŠ¢Î”)
      [F]â‚â€² = [F]â‚ (idÊ· âŠ¢Î”)
      [Fâ‰¡Fâ‚] = âŠ©â‰¡â†’âŠ©â‰¡/ [F]â€² $
               PE.substâ‚‚ (_âŠ©âŸ¨_âŸ©_â‰¡_ _ _) (PE.sym $ wk-id _)
                 (PE.sym $ wk-id _) $
               reducible-âŠ©â‰¡ Fâ‰¡Fâ‚ .projâ‚‚
      Fâ‰¡Fâ‚â€² = PE.substâ‚‚ (Î” âŠ¢_â‰¡_) (PE.sym (wk-id F)) (PE.sym (wk-id Fâ‚)) Fâ‰¡Fâ‚
      [tâ‚]â€² = convTermâ‚ [F]â€² [F]â‚â€² [Fâ‰¡Fâ‚] [tâ‚]
      Gâ‰¡Gâ‚â€² = wkEq (liftÊ· id (escape [F]â€²)) Gâ‰¡Gâ‚
      G[tâ‚]â‰¡Gâ‚[tâ‚] = substTypeEq Gâ‰¡Gâ‚â€² (refl (escapeTerm [F]â€² [tâ‚]))
      [Gtâ‚] = [G] (idÊ· âŠ¢Î”) [tâ‚]
      [Gtâ‚]â‚ = [G]â‚ (idÊ· âŠ¢Î”) [tâ‚]â€²
      [Gtâ‚â‰¡Gâ‚tâ‚] = âŠ©â‰¡â†’âŠ©â‰¡/ [Gtâ‚] (reducible-âŠ©â‰¡ G[tâ‚]â‰¡Gâ‚[tâ‚] .projâ‚‚)
      tâ‡’tâ€³ = conv* tâ‡’tâ€² Î£â‰¡Î£â‚
      SVâ‚‚ = goodCases [Gtâ‚] [Gtâ‚]â‚ [Gtâ‚â‰¡Gâ‚tâ‚]
      tâ‚‚Â®vâ‚‚â€² = convTermÊ³â€² [Gtâ‚] [Gtâ‚]â‚ G[tâ‚]â‰¡Gâ‚[tâ‚] SVâ‚‚ tâ‚‚Â®vâ‚‚
      SVâ‚ = goodCases [F]â€² [F]â‚â€² [Fâ‰¡Fâ‚]
      extraâ€² =
        Î£-Â®-elim (Î» _ â†’ Î£-Â® _ _ [F]â‚â€² tâ‚ v vâ‚‚ p) extra
                 Î£-Â®-intro-ğŸ˜
                 Î» vâ‚ vâ‡’p tâ‚Â®vâ‚ â†’
                   let tâ‚Â®vâ‚â€² = convTermÊ³â€² [F]â€² [F]â‚â€² Fâ‰¡Fâ‚â€² SVâ‚ tâ‚Â®vâ‚
                   in  Î£-Â®-intro-Ï‰ vâ‚ vâ‡’p tâ‚Â®vâ‚â€²
  in  tâ‚ , tâ‚‚ , tâ‡’tâ€³ , [tâ‚]â€² , vâ‚‚ , tâ‚‚Â®vâ‚‚â€² , extraâ€²
convTermÊ³â€² {A} {B} _ _ Aâ‰¡B (Idáµ¥ âŠ©A âŠ©B) (rfláµ£ tâ‡’*rfl â‡’*â†¯) =
  rfláµ£
    (conv* tâ‡’*rfl
       (Id (_âŠ©â‚—Id_.Ty âŠ©A) (_âŠ©â‚—Id_.lhs âŠ©A) (_âŠ©â‚—Id_.rhs âŠ©A)  â‰¡Ë˜âŸ¨ subset* (_âŠ©â‚—Id_.â‡’*Id âŠ©A) âŸ©âŠ¢
        A                                                  â‰¡âŸ¨ Aâ‰¡B âŸ©âŠ¢
        B                                                  â‰¡âŸ¨ subset* (_âŠ©â‚—Id_.â‡’*Id âŠ©B) âŸ©âŠ¢âˆ
        Id (_âŠ©â‚—Id_.Ty âŠ©B) (_âŠ©â‚—Id_.lhs âŠ©B) (_âŠ©â‚—Id_.rhs âŠ©B)  âˆ))
    â‡’*â†¯
convTermÊ³â€² _ _ Aâ‰¡B (embáµ¥â‚ â‰¤áµ˜-refl Aâ‰¡Bâ€²) =
  convTermÊ³â€² _ _ Aâ‰¡B Aâ‰¡Bâ€²
convTermÊ³â€² _ _ Aâ‰¡B (embáµ¥â‚ (â‰¤áµ˜-step p) Aâ‰¡Bâ€²) =
  convTermÊ³â€² _ _ Aâ‰¡B (embáµ¥â‚ p Aâ‰¡Bâ€²)
convTermÊ³â€² _ _ Aâ‰¡B (embáµ¥â‚‚ â‰¤áµ˜-refl Aâ‰¡Bâ€²) =
  convTermÊ³â€² _ _ Aâ‰¡B Aâ‰¡Bâ€²
convTermÊ³â€² _ _ Aâ‰¡B (embáµ¥â‚‚ (â‰¤áµ˜-step p) Aâ‰¡Bâ€²) =
  convTermÊ³â€² _ _ Aâ‰¡B (embáµ¥â‚‚ p Aâ‰¡Bâ€²)
-- Impossible cases
convTermÊ³â€² _ _ _ (Emptyáµ¥ _ _) ()
convTermÊ³â€² _ _ _ (ne record{} _) ()

-- Conversion of logical relation for erasure
-- If t Â® v âˆ· A and Î” âŠ¢ A â‰¡ B then t Â® v âˆ· B

convTermÊ³ : âˆ€ {l lâ€² A B t v}
          â†’ ([A] : Î” âŠ©âŸ¨ l âŸ© A)
            ([B] : Î” âŠ©âŸ¨ lâ€² âŸ© B)
          â†’ Î” âŠ¢ A â‰¡ B
          â†’ t Â®âŸ¨ l âŸ© v âˆ· A / [A]
          â†’ t Â®âŸ¨ lâ€² âŸ© v âˆ· B / [B]
convTermÊ³ [A] [B] Aâ‰¡B tÂ®v =
  let [Aâ‰¡B] = âŠ©â‰¡â†’âŠ©â‰¡/ [A] (reducible-âŠ©â‰¡ Aâ‰¡B .projâ‚‚)
  in convTermÊ³â€² [A] [B] Aâ‰¡B (goodCases [A] [B] [Aâ‰¡B]) tÂ®v
