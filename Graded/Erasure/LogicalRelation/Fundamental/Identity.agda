------------------------------------------------------------------------
-- Validity for the identity type
------------------------------------------------------------------------

{-# OPTIONS --hidden-argument-puns #-}

import Graded.Modality
open import Definition.Typed.EqualityRelation
import Definition.Typed
open import Definition.Typed.Restrictions
import Definition.Untyped hiding (_âˆ·_)

module Graded.Erasure.LogicalRelation.Fundamental.Identity
  {a} {M : Set a}
  (open Graded.Modality M)
  (open Definition.Untyped M renaming (_[_,_] to _[_,_]â‚â‚€))
  {ğ•„ : Modality}
  (open Modality ğ•„)
  (R : Type-restrictions ğ•„)
  (open Definition.Typed R)
  â¦ƒ ğŸ˜-well-behaved : Has-well-behaved-zero semiring-with-meet â¦„
  {k} {Î” : Con Term k} (âŠ¢Î” : âŠ¢ Î”)
  â¦ƒ eqrel : EqRelSet R â¦„
  where

open Has-well-behaved-zero ğŸ˜-well-behaved
open Type-restrictions R

open import Definition.Typed.Consequences.Canonicity R
open import Definition.Typed.Consequences.DerivedRules R
open import Definition.Typed.Consequences.Inversion R
import Definition.Typed.Consequences.RedSteps R as RS
open import Definition.Typed.Consequences.Substitution R
open import Definition.Typed.Consequences.Syntactic R
open import Definition.Typed.Properties R
open import Definition.Typed.Reasoning.Reduction R
open import Definition.Typed.Reasoning.Type R

open import Definition.LogicalRelation R
open import Definition.LogicalRelation.Fundamental R
open import Definition.LogicalRelation.Substitution R
import Definition.LogicalRelation.Substitution.Introductions.Erased R
  as IE
open import
  Definition.LogicalRelation.Substitution.Introductions.Identity R
open import
  Definition.LogicalRelation.Substitution.Introductions.Universe R
open import Definition.LogicalRelation.Substitution.Properties R

open import Definition.Untyped.Properties M

open import Graded.Context ğ•„
open import Graded.Context.Properties.Has-well-behaved-zero ğ•„
open import Graded.Derived.Erased.Untyped ğ•„ as Erased using (Erased)
open import Graded.Erasure.LogicalRelation R is-ğŸ˜? âŠ¢Î”
open import Graded.Erasure.LogicalRelation.Conversion R is-ğŸ˜? âŠ¢Î”
open import Graded.Erasure.LogicalRelation.Reduction R is-ğŸ˜? âŠ¢Î”
open import Graded.Erasure.LogicalRelation.Subsumption R is-ğŸ˜? âŠ¢Î”
import Graded.Erasure.Target as T
open import Graded.Mode ğ•„

open import Tools.Fin
open import Tools.Function
open import Tools.Product
open import Tools.PropositionalEquality as PE using (_â‰¡_)
open import Tools.Reasoning.PropositionalEquality
open import Tools.Relation
open import Tools.Sum using (_âŠ_; injâ‚; injâ‚‚)

private variable
  Î“           : Con Term _
  Î³ Î´         : Conâ‚˜ _
  A B t u v w : Term _
  m           : Mode
  âŠ©Î“          : âŠ©áµ› _
  p q         : M

opaque

  -- Validity of Id.

  IdÊ³ :
    Î“ âŠ¢ A âˆ· U â†’
    Î“ âŠ¢ t âˆ· A â†’
    Î“ âŠ¢ u âˆ· A â†’
    âˆƒ Î» (âŠ©Î“ : âŠ©áµ› Î“) â†’
    âˆƒ Î» (âŠ©U : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© U / âŠ©Î“) â†’
    Î³ â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© Id A t u âˆ·[ m ] U / âŠ©Î“ / âŠ©U
  IdÊ³ âŠ¢A âŠ¢t âŠ¢u =
      valid (wfTerm âŠ¢A)
    , Uáµ› _
    , Î» âŠ©Ïƒ _ â†’
        Uáµ£ (substitutionTerm (Idâ±¼ âŠ¢A âŠ¢t âŠ¢u) (wellformedSubst _ _ âŠ©Ïƒ) âŠ¢Î”)
          â—€ _

opaque
  unfolding Idáµ› âŠ©Id

  -- Validity of rfl.

  rflÊ³ :
    Î“ âŠ¢ t âˆ· A â†’
    âˆƒ Î» (âŠ©Î“ : âŠ©áµ› Î“) â†’
    âˆƒ Î» (âŠ©Id : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© Id A t t / âŠ©Î“) â†’
    Î³ â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© rfl âˆ·[ m ] Id A t t / âŠ©Î“ / âŠ©Id
  rflÊ³ âŠ¢t =
    case fundamentalTerm âŠ¢t of Î» {
      (âŠ©Î“ , âŠ©A , âŠ©t) â†’
      âŠ©Î“
    , Idáµ› âŠ©A âŠ©t âŠ©t
    , Î» âŠ©Ïƒ _ â†’
        rfláµ£
          (rfl  âˆâŸ¨ rflâ±¼ (substitutionTerm âŠ¢t (wellformedSubst _ _ âŠ©Ïƒ) âŠ¢Î”) âŸ©â‡’)
          T.refl
          â—€ _ }

private opaque

  -- Any context of a size that is equal to 0 is equal to (via a
  -- transport) the contextÂ Îµ.

  â‰¡0â†’â‰¡Îµ :
    (kâ‰¡0 : k â‰¡ 0) (Î” : Con Term k) â†’
    Î” â‰¡ PE.subst (Con Term) (PE.sym kâ‰¡0) Îµ
  â‰¡0â†’â‰¡Îµ PE.refl Îµ = PE.refl

opaque
  unfolding Idáµ› âŠ©Id

  -- Validity of []-cong for empty "target contexts".

  []-congÊ³ :
    k â‰¡ 0 â†’
    Î“ âŠ¢ t âˆ· A â†’
    Î“ âŠ¢ u âˆ· A â†’
    Î“ âŠ¢ v âˆ· Id A t u â†’
    []-cong-allowed â†’
    âˆƒ Î» (âŠ©Î“ : âŠ©áµ› Î“) â†’
    âˆƒ Î» (âŠ©Id : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© Id (Erased A) Erased.[ t ] Erased.[ u ] / âŠ©Î“) â†’
    Î³ â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© []-cong A t u v âˆ·[ m ]
      Id (Erased A) Erased.[ t ] Erased.[ u ] / âŠ©Î“ / âŠ©Id
  []-congÊ³ {t} {A} {u} {v} PE.refl âŠ¢t âŠ¢u âŠ¢v ok =
    case â‰¡0â†’â‰¡Îµ PE.refl Î” of Î» {
      PE.refl â†’
    case fundamentalTerm âŠ¢t of Î» {
      (âŠ©Î“ , âŠ©A , âŠ©t) â†’
      âŠ©Î“
    , Idáµ› (Erasedáµ› âŠ©A) ([]áµ› t âŠ©t) ([]áµ› u (fundamentalTermâ€² âŠ©A âŠ¢u))
    , Î» {Ïƒ = Ïƒ} âŠ©Ïƒ _ â†’
        case substitutionTerm âŠ¢v (wellformedSubst _ _ âŠ©Ïƒ) âŠ¢Î” of Î» {
          âŠ¢v[Ïƒ] â†’
        case ÎµâŠ¢âˆ·Idâ†’ÎµâŠ¢â‰¡âˆ· âŠ¢v[Ïƒ] of Î» {
          t[Ïƒ]â‰¡u[Ïƒ] â†’
        rfláµ£
          (([]-cong A t u v) [ Ïƒ ]    â‡’*âŸ¨ []-cong-subst* (ÎµâŠ¢â‡’*rflâˆ·Id âŠ¢v[Ïƒ]) ok âŸ©
           ([]-cong A t u rfl) [ Ïƒ ]  â‡’âŸ¨ []-cong-Î²-â‡’ t[Ïƒ]â‰¡u[Ïƒ] ok âŸ©âˆ
           rfl                        âˆ)
          T.refl
          â—€ _ }}}}
    where
    open IE ([]-congâ†’Erased ok)

opaque
  unfolding Idáµ› âŠ©Id

  -- Validity ofÂ K.

  KÊ³ :
    Î“ âŠ¢ t âˆ· A â†’
    Î“ âˆ™ Id A t t âŠ¢ B â†’
    Î“ âŠ¢ u âˆ· B [ rfl ]â‚€ â†’
    Î“ âŠ¢ v âˆ· Id A t t â†’
    K-allowed â†’
    (âŠ©B[rfl] : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© B [ rfl ]â‚€ / âŠ©Î“)
    (âŠ©B[v] : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© B [ v ]â‚€ / âŠ©Î“) â†’
    Î³ â‰¤á¶œ Î´ â†’
    Î´ â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© u âˆ·[ m ] B [ rfl ]â‚€ / âŠ©Î“ / âŠ©B[rfl] â†’
    k â‰¡ 0 âŠ
    (âˆƒâ‚ƒ Î» Î· âŠ©A (âŠ©t : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© t âˆ· A / âŠ©Î“ / âŠ©A) â†’
     Î³ â‰¤á¶œ Î· Ã— Î· â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© v âˆ·[ m ] Id A t t / âŠ©Î“ / Idáµ› âŠ©A âŠ©t âŠ©t) â†’
    Î³ â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© K p A t B u v âˆ·[ m ] B [ v ]â‚€ / âŠ©Î“ / âŠ©B[v]
  KÊ³
    {t} {A} {B} {u} {v} {m} {p}
    âŠ¢t âŠ¢B âŠ¢u âŠ¢v ok âŠ©B[rfl] âŠ©B[v] Î³â‰¤Î´ âŠ©Ê³u kâ‰¡0âŠâŠ©Ê³v {Ïƒ} âŠ©Ïƒ ÏƒÂ®Ïƒâ€²
    with is-ğŸ˜? âŒœ m âŒ
  â€¦ | yes _ = _
  â€¦ | no _  =
    let âŠ©B[v][Ïƒ]   , _ = âŠ©B[v]   .unwrap _ âŠ©Ïƒ
        âŠ©B[rfl][Ïƒ] , _ = âŠ©B[rfl] .unwrap _ âŠ©Ïƒ
    in
    case wellformedSubst _ _ âŠ©Ïƒ of Î» {
      âŠ¢Ë¢Ïƒ â†’
    case (case kâ‰¡0âŠâŠ©Ê³v of Î» where
            (injâ‚ PE.refl) â†’
              case â‰¡0â†’â‰¡Îµ PE.refl Î” of Î» {
                PE.refl â†’
              ÎµâŠ¢â‡’*rflâˆ·Id (substitutionTerm âŠ¢v âŠ¢Ë¢Ïƒ âŠ¢Î”) }
            (injâ‚‚ (_ , _ , _ , Î³â‰¤Î· , âŠ©Ê³v)) â†’
              case âŠ©Ê³v âŠ©Ïƒ
                     (subsumptionSubst {l = Â¹} ÏƒÂ®Ïƒâ€² Î» _ â†’
                      â‰¤á¶œâ†’âŸ¨âŸ©â‰¡ğŸ˜â†’âŸ¨âŸ©â‰¡ğŸ˜ Î³â‰¤Î·) of Î» {
                (rfláµ£ v[Ïƒ]â‡’*rfl _) â†’ v[Ïƒ]â‡’*rfl }) of Î» {
      v[Ïƒ]â‡’*rfl â†’
    case substitution âŠ¢B
           (liftSubstâ€² (wfTerm âŠ¢t) âŠ¢Î” (Idâ±¼ âŠ¢t âŠ¢t) âŠ¢Ë¢Ïƒ)
           (âŠ¢Î” âˆ™ substitution (Idâ±¼ âŠ¢t âŠ¢t) âŠ¢Ë¢Ïƒ âŠ¢Î”) of Î» {
      âŠ¢B[â‡‘Ïƒ] â†’
    case
      B [ liftSubst Ïƒ ] [ rfl ]â‚€      â‰¡âŸ¨ substTypeEq (refl âŠ¢B[â‡‘Ïƒ]) (sym (subset*Term v[Ïƒ]â‡’*rfl)) âŸ©âŠ¢âˆâ‰¡
      B [ liftSubst Ïƒ ] [ v [ Ïƒ ] ]â‚€  â‰¡Ë˜âŸ¨ singleSubstLift B _ âŸ©
      B [ v ]â‚€ [ Ïƒ ]                  âˆ
      of Î» {
      B[â‡‘Ïƒ][rfl]â‰¡B[v][Ïƒ] â†’
    case
      B [ rfl ]â‚€ [ Ïƒ ]            â‰¡âŸ¨ singleSubstLift B _ âŸ©âŠ¢â‰¡
      B [ liftSubst Ïƒ ] [ rfl ]â‚€  â‰¡âŸ¨ B[â‡‘Ïƒ][rfl]â‰¡B[v][Ïƒ] âŸ©âŠ¢âˆ
      B [ v ]â‚€ [ Ïƒ ]              âˆ
      of Î» {
      B[rfl][Ïƒ]â‰¡B[v][Ïƒ] â†’
    case PE.subst (_âŠ¢_âˆ·_ _ _) (singleSubstLift B _) $
         substitutionTerm âŠ¢u âŠ¢Ë¢Ïƒ âŠ¢Î” of Î» {
      âŠ¢u[Ïƒ] â†’
    redSubstTerm* âŠ©B[v][Ïƒ]
      (convTermÊ³ âŠ©B[rfl][Ïƒ] âŠ©B[v][Ïƒ] B[rfl][Ïƒ]â‰¡B[v][Ïƒ] $
       âŠ©Ê³u âŠ©Ïƒ (subsumptionSubst {l = Â¹} ÏƒÂ®Ïƒâ€² Î» _ â†’ â‰¤á¶œâ†’âŸ¨âŸ©â‰¡ğŸ˜â†’âŸ¨âŸ©â‰¡ğŸ˜ Î³â‰¤Î´))
      ((K p A t B u v) [ Ïƒ ]    â‡’*âŸ¨ PE.subst (_âŠ¢_â‡’*_âˆ·_ _ _ _) (PE.sym $ singleSubstLift B _) $
                                    RS.K-subst* âŠ¢B[â‡‘Ïƒ] âŠ¢u[Ïƒ] v[Ïƒ]â‡’*rfl ok âŸ©
       (K p A t B u rfl) [ Ïƒ ]  â‡’âŸ¨ conv (K-Î²-â‡’ âŠ¢B[â‡‘Ïƒ] âŠ¢u[Ïƒ] ok) B[â‡‘Ïƒ][rfl]â‰¡B[v][Ïƒ] âŸ©âˆ
       u [ Ïƒ ]                  âˆ)
      T.refl }}}}}}

opaque
  unfolding Idáµ› âŠ©Id

  -- Validity ofÂ J.

  JÊ³ :
    Î“ âŠ¢ t âˆ· A â†’
    Î“ âˆ™ A âˆ™ Id (wk1 A) (wk1 t) (var x0) âŠ¢ B â†’
    Î“ âŠ¢ u âˆ· B [ t , rfl ]â‚â‚€ â†’
    Î“ âŠ¢ v âˆ· A â†’
    Î“ âŠ¢ w âˆ· Id A t v â†’
    (âŠ©B[t,rfl] : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© B [ t , rfl ]â‚â‚€ / âŠ©Î“)
    (âŠ©B[v,w] : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© B [ v , w ]â‚â‚€ / âŠ©Î“) â†’
    Î³ â‰¤á¶œ Î´ â†’
    Î´ â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© u âˆ·[ m ] B [ t , rfl ]â‚â‚€ / âŠ©Î“ / âŠ©B[t,rfl] â†’
    k â‰¡ 0 âŠ
    (âˆƒâ‚‚ Î» Î· âŠ©A â†’
     âˆƒ Î» (âŠ©t : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© t âˆ· A / âŠ©Î“ / âŠ©A) â†’
     âˆƒ Î» (âŠ©v : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© v âˆ· A / âŠ©Î“ / âŠ©A) â†’
     Î³ â‰¤á¶œ Î· Ã— Î· â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© w âˆ·[ m ] Id A t v / âŠ©Î“ / Idáµ› âŠ©A âŠ©t âŠ©v) â†’
    Î³ â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© J p q A t B u v w âˆ·[ m ] B [ v , w ]â‚â‚€ / âŠ©Î“ / âŠ©B[v,w]
  JÊ³
    {Î“} {t} {A} {B} {u} {v} {w} {Î³} {m} {p} {q}
    âŠ¢t âŠ¢B âŠ¢u âŠ¢v âŠ¢w âŠ©B[t,rfl] âŠ©B[v,w] Î³â‰¤Î´ âŠ©Ê³u kâ‰¡0âŠâŠ©Ê³w {Ïƒ} âŠ©Ïƒ ÏƒÂ®Ïƒâ€²
    with is-ğŸ˜? âŒœ m âŒ
  â€¦ | yes _ = _
  â€¦ | no _  =
    let âŠ©B[v,w][Ïƒ]   , _ = âŠ©B[v,w]   .unwrap _ âŠ©Ïƒ
        âŠ©B[t,rfl][Ïƒ] , _ = âŠ©B[t,rfl] .unwrap _ âŠ©Ïƒ
    in
    case wellformedSubst _ _ âŠ©Ïƒ of Î» {
      âŠ¢Ë¢Ïƒ â†’
    case (case kâ‰¡0âŠâŠ©Ê³w of Î» where
            (injâ‚ PE.refl) â†’
              case â‰¡0â†’â‰¡Îµ PE.refl Î” of Î» {
                PE.refl â†’
              ÎµâŠ¢â‡’*rflâˆ·Id (substitutionTerm âŠ¢w âŠ¢Ë¢Ïƒ âŠ¢Î”) }
            (injâ‚‚ (_ , _ , _ , _ , Î³â‰¤Î· , âŠ©Ê³w)) â†’
              case âŠ©Ê³w âŠ©Ïƒ
                     (subsumptionSubst {l = Â¹} ÏƒÂ®Ïƒâ€² Î» _ â†’
                      â‰¤á¶œâ†’âŸ¨âŸ©â‰¡ğŸ˜â†’âŸ¨âŸ©â‰¡ğŸ˜ Î³â‰¤Î·) of Î» {
                (rfláµ£ w[Ïƒ]â‡’*rfl _) â†’ w[Ïƒ]â‡’*rfl }) of Î» {
      w[Ïƒ]â‡’*rfl â†’
    case inversion-rfl-Id
           (syntacticEqTerm (subset*Term w[Ïƒ]â‡’*rfl)
              .projâ‚‚ .projâ‚‚) of Î» {
      t[Ïƒ]â‰¡v[Ïƒ] â†’
    case syntacticEqTerm t[Ïƒ]â‰¡v[Ïƒ] of Î» {
      (âŠ¢A[Ïƒ] , âŠ¢t[Ïƒ] , _) â†’
    case J-motive-context âŠ¢t of Î» {
      (âŠ¢Î“âˆ™A âˆ™ âŠ¢Id) â†’
    case substitution âŠ¢B
           (PE.subst
              (Î» u â†’
                 Î” âˆ™ A [ Ïƒ ] âˆ™ u âŠ¢Ë¢ liftSubst (liftSubst Ïƒ) âˆ·
                 Î“ âˆ™ A âˆ™ Id (wk1 A) (wk1 t) (var x0))
              (Id-wk1-wk1-0[â‡‘]â‰¡ A t) $
            liftSubstâ€² âŠ¢Î“âˆ™A (âŠ¢Î” âˆ™ âŠ¢A[Ïƒ]) âŠ¢Id $
            liftSubstâ€² (wfTerm âŠ¢t) âŠ¢Î” (syntacticTerm âŠ¢t) âŠ¢Ë¢Ïƒ)
           (J-motive-context âŠ¢t[Ïƒ]) of Î» {
      âŠ¢B[â‡‘â‡‘Ïƒ] â†’
    case
      B [ liftSubstn Ïƒ 2 ] [ t [ Ïƒ ] , rfl ]â‚â‚€      â‰¡âŸ¨ J-result-cong (refl âŠ¢B[â‡‘â‡‘Ïƒ]) t[Ïƒ]â‰¡v[Ïƒ] $
                                                       conv (sym (subset*Term w[Ïƒ]â‡’*rfl))
                                                         (Id-cong (refl âŠ¢A[Ïƒ]) (refl âŠ¢t[Ïƒ]) (sym t[Ïƒ]â‰¡v[Ïƒ])) âŸ©âŠ¢âˆâ‰¡
      B [ liftSubstn Ïƒ 2 ] [ v [ Ïƒ ] , w [ Ïƒ ] ]â‚â‚€  â‰¡Ë˜âŸ¨ [,]-[]-commute B âŸ©
      B [ v , w ]â‚â‚€ [ Ïƒ ]                           âˆ
      of Î» {
      B[â‡‘Ïƒ][t[Ïƒ],rfl]â‰¡B[v,w][Ïƒ] â†’
    case
      B [ t , rfl ]â‚â‚€ [ Ïƒ ]                     â‰¡âŸ¨ [,]-[]-commute B âŸ©âŠ¢â‰¡
      B [ liftSubstn Ïƒ 2 ] [ t [ Ïƒ ] , rfl ]â‚â‚€  â‰¡âŸ¨ B[â‡‘Ïƒ][t[Ïƒ],rfl]â‰¡B[v,w][Ïƒ] âŸ©âŠ¢âˆ
      B [ v , w ]â‚â‚€ [ Ïƒ ]                       âˆ
      of Î» {
      B[t,rfl][Ïƒ]â‰¡B[v,w][Ïƒ] â†’
    case PE.subst (_âŠ¢_âˆ·_ _ _) ([,]-[]-commute B) $
         substitutionTerm âŠ¢u âŠ¢Ë¢Ïƒ âŠ¢Î” of Î» {
      âŠ¢u[Ïƒ] â†’
    redSubstTerm* âŠ©B[v,w][Ïƒ]
      (convTermÊ³ âŠ©B[t,rfl][Ïƒ] âŠ©B[v,w][Ïƒ] B[t,rfl][Ïƒ]â‰¡B[v,w][Ïƒ] $
       âŠ©Ê³u âŠ©Ïƒ (subsumptionSubst {l = Â¹} ÏƒÂ®Ïƒâ€² Î» _ â†’ â‰¤á¶œâ†’âŸ¨âŸ©â‰¡ğŸ˜â†’âŸ¨âŸ©â‰¡ğŸ˜ Î³â‰¤Î´))
      ((J p q A t B u v w) [ Ïƒ ]    â‡’*âŸ¨ PE.subst (_âŠ¢_â‡’*_âˆ·_ _ _ _) (PE.sym $ [,]-[]-commute B) $
                                        RS.J-subst* âŠ¢B[â‡‘â‡‘Ïƒ] âŠ¢u[Ïƒ] w[Ïƒ]â‡’*rfl âŸ©
       (J p q A t B u v rfl) [ Ïƒ ]  â‡’âŸ¨ conv (J-Î²-â‡’ t[Ïƒ]â‰¡v[Ïƒ] âŠ¢B[â‡‘â‡‘Ïƒ] âŠ¢u[Ïƒ])
                                         B[â‡‘Ïƒ][t[Ïƒ],rfl]â‰¡B[v,w][Ïƒ] âŸ©âˆ
       u [ Ïƒ ]                      âˆ)
      T.refl }}}}}}}}}
