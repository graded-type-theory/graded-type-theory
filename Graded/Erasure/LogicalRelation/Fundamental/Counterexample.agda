------------------------------------------------------------------------
-- The fundamental lemma does not hold in general without the
-- assumption that erased matches are disallowed or the context is
-- empty
------------------------------------------------------------------------

{-# OPTIONS --hidden-argument-puns #-}

open import Graded.Modality
open import Graded.Usage.Restrictions
open import Definition.Typed.EqualityRelation
open import Definition.Typed.Restrictions

module Graded.Erasure.LogicalRelation.Fundamental.Counterexample
  {a} {M : Set a}
  {ğ•„ : Modality M}
  (open Modality ğ•„)
  (TR : Type-restrictions ğ•„)
  (UR : Usage-restrictions ğ•„)
  â¦ƒ ğŸ˜-well-behaved : Has-well-behaved-zero M semiring-with-meet â¦„
  {{eqrel : EqRelSet TR}}
  where

open Type-restrictions TR
open Usage-restrictions UR

open import Graded.Context ğ•„
import Graded.Derived.Erased.Untyped ğ•„ as Erased
open import Graded.Modality.Properties ğ•„
open import Graded.Usage ğ•„ UR
open import Graded.Usage.Erased-matches
open import Graded.Mode ğ•„

open import Definition.Untyped M hiding (_âˆ·_)
open import Definition.Typed TR
open import Definition.Typed.Consequences.Consistency TR
open import Definition.Typed.Consequences.DerivedRules TR
open import Definition.Typed.Consequences.Substitution TR
open import Definition.Typed.Properties TR
open import Definition.LogicalRelation TR
open import Definition.LogicalRelation.Substitution TR

open import Graded.Erasure.Consequences.Soundness TR UR
open import Graded.Erasure.Extraction ğ•„ is-ğŸ˜?
open import Graded.Erasure.LogicalRelation.Assumptions TR
import Graded.Erasure.LogicalRelation
import Graded.Erasure.LogicalRelation.Hidden
open import Graded.Erasure.Target using (Strictness)

open import Tools.Bool using (T)
open import Tools.Empty
open import Tools.Fin
open import Tools.Function
open import Tools.Nat
open import Tools.Product
open import Tools.PropositionalEquality using (_â‰¡_; _â‰¢_)
open import Tools.Relation
open import Tools.Sum

private variable
  k   : Nat
  p q : M
  s   : Strength
  sem : Some-erased-matches
  str : Strictness

-- The module LR exports some module instantiations.

private module LR {Î” : Con Term k} (âŠ¢Î” : âŠ¢ Î”) (str : Strictness) where

  private

    as : Assumptions
    as = record { âŠ¢Î” = âŠ¢Î”; str = str }

  open Graded.Erasure.LogicalRelation is-ğŸ˜? as public
  open Graded.Erasure.LogicalRelation.Hidden is-ğŸ˜? as public

-- If Prodrec-allowedÂ ğŸ™áµÂ ğŸ˜Â pÂ ğŸ˜ holds for some p (which means that
-- certain kinds of erased matches are allowed), and if additionally
-- Î£Ê·-allowedÂ pÂ ğŸ˜ holds, then one can prove a negation of a variant of
-- the fundamental lemma without the assumption "erased matches are
-- not allowed or the context is empty" (for any strictness).

negation-of-fundamental-lemma-with-erased-matchesâ‚ :
  Prodrec-allowed ğŸ™áµ ğŸ˜ p ğŸ˜ â†’
  Î£Ê·-allowed p ğŸ˜ â†’
  Â¬ (âˆ€ {k} {Î” : Con Term k} (âŠ¢Î” : âŠ¢ Î”) â†’
     let open LR âŠ¢Î” str in
     Consistent Î” â†’
     âˆ€ {n} {Î“ : Con Term n} {t A : Term n} {Î³ : Conâ‚˜ n} {m} â†’
     Î“ âŠ¢ t âˆ· A â†’ Î³ â–¸[ m ] t â†’
     âˆƒâ‚‚ Î» ([Î“] : âŠ©áµ› Î“) ([A] : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© A / [Î“]) â†’
       Î³ â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© t âˆ·[ m ] A / [Î“] / [A])
negation-of-fundamental-lemma-with-erased-matchesâ‚
  {p} {str} P-ok Î£Ê·-ok hyp =
  case soundness-â„•-only-source-counterexampleâ‚ P-ok Î£Ê·-ok of Î»
    (consistent , âŠ¢t , â–¸t , _) â†’
  Â¬tÂ®t $ hidden-Â®-intro-fundamental non-trivial $
  hyp âŠ¢Î” consistent âŠ¢t â–¸t
  where
  Î” : Con Term 1
  Î” = Îµ âˆ™ (Î£Ê· p , ğŸ˜ â–· â„• â–¹ â„•)

  t : Term 1
  t = prodrec ğŸ˜ p ğŸ˜ â„• (var x0) zero

  A : Term 1
  A = â„•

  âŠ¢Î” : âŠ¢ Î”
  âŠ¢Î” = Îµ âˆ™ Î Î£â±¼ (â„•â±¼ Îµ) (â„•â±¼ (Îµ âˆ™ â„•â±¼ Îµ)) Î£Ê·-ok

  open LR âŠ¢Î” str

  Â¬tÂ®t : Â¬ t Â®âŸ¨ Â¹ âŸ© erase str t âˆ· A
  Â¬tÂ®t tÂ®t = case Â®-â„• tÂ®t of Î» where
    (zeroáµ£ tâ‡’* _) â†’
      case whnfRed*Term tâ‡’* (ne (prodrecâ‚™ (var _))) of Î» ()
    (sucáµ£ tâ‡’* _ _ _) â†’
      case whnfRed*Term tâ‡’* (ne (prodrecâ‚™ (var _))) of Î» ()

opaque

  -- If []-cong-allowed holds, then one can prove a negation of a
  -- variant of the statement of the fundamental lemma without the
  -- assumption "erased matches are not allowed or the context is
  -- empty" (for any strictness).

  negation-of-fundamental-lemma-with-erased-matchesâ‚‚ :
    []-cong-allowed s â†’
    Â¬ (âˆ€ {k} {Î” : Con Term k} (âŠ¢Î” : âŠ¢ Î”) â†’
       let open LR âŠ¢Î” str in
       Consistent Î” â†’
       âˆ€ {n} {Î“ : Con Term n} {t A : Term n} {Î³ : Conâ‚˜ n} {m} â†’
       Î“ âŠ¢ t âˆ· A â†’ Î³ â–¸[ m ] t â†’
       âˆƒâ‚‚ Î» ([Î“] : âŠ©áµ› Î“) ([A] : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© A / [Î“]) â†’
         Î³ â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© t âˆ·[ m ] A / [Î“] / [A])
  negation-of-fundamental-lemma-with-erased-matchesâ‚‚ {s} {str} ok hyp =
    Â¬tÂ®t $ hidden-Â®-intro-fundamental non-trivial $
    hyp âŠ¢Î” consistent âŠ¢t â–¸t
    where
    open Erased s
    Î” : Con Term 1
    Î” = Îµ âˆ™ Id â„• zero zero

    t : Term 1
    t = []-cong s â„• zero zero (var x0)

    A : Term 1
    A = Id (Erased â„•) ([ zero ]) ([ zero ])

    âŠ¢Î” : âŠ¢ Î”
    âŠ¢Î” = Îµ âˆ™ Idâ±¼ (zeroâ±¼ Îµ) (zeroâ±¼ Îµ)

    consistent : Consistent Î”
    consistent = inhabited-consistent (singleSubst (rflâ±¼ (zeroâ±¼ Îµ)))

    âŠ¢t : Î” âŠ¢ t âˆ· A
    âŠ¢t = []-congâ±¼â€² ok (var âŠ¢Î” here)

    â–¸t : ğŸ˜á¶œ â–¸[ ğŸ™áµ ] t
    â–¸t = []-congâ‚˜ â„•â‚˜ zeroâ‚˜ zeroâ‚˜ var

    open LR âŠ¢Î” str

    Â¬tÂ®t : Â¬ t Â®âŸ¨ Â¹ âŸ© erase str t âˆ· A
    Â¬tÂ®t tÂ®t =
      case Â®-Id tÂ®t of Î» {
        (rfláµ£ tâ‡’* _) â†’
      case whnfRed*Term tâ‡’* (ne ([]-congâ‚™ (var _))) of Î» () }

opaque

  -- If erased-matches-for-JÂ ğŸ™áµ is equal to not-noneÂ sem, then one can
  -- prove a negation of a variant of the statement of the fundamental
  -- lemma without the assumption "erased matches are not allowed or
  -- the context is empty" (for any strictness).

  negation-of-fundamental-lemma-with-erased-matchesâ‚ƒ :
    erased-matches-for-J ğŸ™áµ â‰¡ not-none sem â†’
    Â¬ (âˆ€ {k} {Î” : Con Term k} (âŠ¢Î” : âŠ¢ Î”) â†’
       let open LR âŠ¢Î” str in
       Consistent Î” â†’
       âˆ€ {n} {Î“ : Con Term n} {t A : Term n} {Î³ : Conâ‚˜ n} {m} â†’
       Î“ âŠ¢ t âˆ· A â†’ Î³ â–¸[ m ] t â†’
       âˆƒâ‚‚ Î» ([Î“] : âŠ©áµ› Î“) ([A] : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© A / [Î“]) â†’
         Î³ â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© t âˆ·[ m ] A / [Î“] / [A])
  negation-of-fundamental-lemma-with-erased-matchesâ‚ƒ
    {str} â‰¡not-none hyp =
    case soundness-â„•-only-source-counterexampleâ‚ƒ â‰¡not-none of Î»
      (consistent , âŠ¢t , â–¸t , _) â†’
    Â¬tÂ®t $ hidden-Â®-intro-fundamental non-trivial $
    hyp âŠ¢Î” consistent âŠ¢t â–¸t
    where
    Î” : Con Term 1
    Î” = Îµ âˆ™ Id â„• zero zero

    t : Term 1
    t = J ğŸ˜ ğŸ˜ â„• zero â„• zero zero (var {n = 1} x0)

    A : Term 1
    A = â„•

    âŠ¢Î” : âŠ¢ Î”
    âŠ¢Î” = Îµ âˆ™ Idâ±¼ (zeroâ±¼ Îµ) (zeroâ±¼ Îµ)

    open LR âŠ¢Î” str

    Â¬tÂ®t : Â¬ t Â®âŸ¨ Â¹ âŸ© erase str t âˆ· A
    Â¬tÂ®t tÂ®t = case Â®-â„• tÂ®t of Î» where
      (zeroáµ£ tâ‡’* _)    â†’ case whnfRed*Term tâ‡’* (ne (Jâ‚™ (var _))) of Î» ()
      (sucáµ£ tâ‡’* _ _ _) â†’ case whnfRed*Term tâ‡’* (ne (Jâ‚™ (var _))) of Î» ()

opaque

  -- If the K rule is allowed and erased-matches-for-KÂ ğŸ™áµ is equal to
  -- not-noneÂ sem, then one can prove a negation of a variant of the
  -- statement of the fundamental lemma without the assumption "erased
  -- matches are not allowed or the context is empty" (for any
  -- strictness).

  negation-of-fundamental-lemma-with-erased-matchesâ‚„ :
    K-allowed â†’
    erased-matches-for-K ğŸ™áµ â‰¡ not-none sem â†’
    Â¬ (âˆ€ {k} {Î” : Con Term k} (âŠ¢Î” : âŠ¢ Î”) â†’
       let open LR âŠ¢Î” str in
       Consistent Î” â†’
       âˆ€ {n} {Î“ : Con Term n} {t A : Term n} {Î³ : Conâ‚˜ n} {m} â†’
       Î“ âŠ¢ t âˆ· A â†’ Î³ â–¸[ m ] t â†’
       âˆƒâ‚‚ Î» ([Î“] : âŠ©áµ› Î“) ([A] : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© A / [Î“]) â†’
         Î³ â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© t âˆ·[ m ] A / [Î“] / [A])
  negation-of-fundamental-lemma-with-erased-matchesâ‚„
    {str} K-ok â‰¡not-none hyp =
    case soundness-â„•-only-source-counterexampleâ‚„ K-ok â‰¡not-none of Î»
      (consistent , âŠ¢t , â–¸t , _) â†’
    Â¬tÂ®t $ hidden-Â®-intro-fundamental non-trivial $
    hyp âŠ¢Î” consistent âŠ¢t â–¸t
    where
    Î” : Con Term 1
    Î” = Îµ âˆ™ Id â„• zero zero

    t : Term 1
    t = K ğŸ˜ â„• zero â„• zero (var {n = 1} x0)

    A : Term 1
    A = â„•

    âŠ¢Î” : âŠ¢ Î”
    âŠ¢Î” = Îµ âˆ™ Idâ±¼ (zeroâ±¼ Îµ) (zeroâ±¼ Îµ)

    open LR âŠ¢Î” str

    Â¬tÂ®t : Â¬ t Â®âŸ¨ Â¹ âŸ© erase str t âˆ· A
    Â¬tÂ®t tÂ®t = case Â®-â„• tÂ®t of Î» where
      (zeroáµ£ tâ‡’* _)    â†’ case whnfRed*Term tâ‡’* (ne (Kâ‚™ (var _))) of Î» ()
      (sucáµ£ tâ‡’* _ _ _) â†’ case whnfRed*Term tâ‡’* (ne (Kâ‚™ (var _))) of Î» ()

opaque

  -- If Unitrec-allowedÂ ğŸ™áµÂ ğŸ˜Â ğŸ˜ holds (which means that certain kinds
  -- of erased matches are allowed), and if additionally UnitÊ·-allowed
  -- holds, then one can prove a negation of a variant of the
  -- fundamental lemma without the assumption "erased matches are not
  -- allowed or the context is empty" (for any strictness).

  negation-of-fundamental-lemma-with-erased-matchesâ‚… :
    UnitÊ·-allowed â†’
    Unitrec-allowed ğŸ™áµ ğŸ˜ ğŸ˜ â†’
    Â¬ (âˆ€ {k} {Î” : Con Term k} (âŠ¢Î” : âŠ¢ Î”) â†’
       let open LR âŠ¢Î” str in
       Consistent Î” â†’
       âˆ€ {n} {Î“ : Con Term n} {t A : Term n} {Î³ : Conâ‚˜ n} {m} â†’
       Î“ âŠ¢ t âˆ· A â†’ Î³ â–¸[ m ] t â†’
       âˆƒâ‚‚ Î» ([Î“] : âŠ©áµ› Î“) ([A] : Î“ âŠ©áµ›âŸ¨ Â¹ âŸ© A / [Î“]) â†’
         Î³ â–¸ Î“ âŠ©Ê³âŸ¨ Â¹ âŸ© t âˆ·[ m ] A / [Î“] / [A])
  negation-of-fundamental-lemma-with-erased-matchesâ‚…
    {str} Unit-ok ok hyp =
    case soundness-â„•-only-source-counterexampleâ‚… ok Unit-ok of Î»
      (consistent , âŠ¢t , â–¸t , _) â†’
    Â¬tÂ®t $ hidden-Â®-intro-fundamental non-trivial $
    hyp âŠ¢Î” consistent âŠ¢t â–¸t
    where
    Î” : Con Term 1
    Î” = Îµ âˆ™ UnitÊ·

    t : Term 1
    t = unitrec ğŸ˜ ğŸ˜ â„• (var x0) zero

    A : Term 1
    A = â„•

    âŠ¢Î” : âŠ¢ Î”
    âŠ¢Î” = Îµ âˆ™ Unitâ±¼ Îµ Unit-ok

    open LR âŠ¢Î” str

    Â¬tÂ®t : Â¬ t Â®âŸ¨ Â¹ âŸ© erase str t âˆ· A
    Â¬tÂ®t tÂ®t = case Â®-â„• tÂ®t of Î» where
      (zeroáµ£ tâ‡’* _)    â†’ case whnfRed*Term tâ‡’* (ne (unitrecâ‚™ (var _))) of Î» ()
      (sucáµ£ tâ‡’* _ _ _) â†’ case whnfRed*Term tâ‡’* (ne (unitrecâ‚™ (var _))) of Î» ()
