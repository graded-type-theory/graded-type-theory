------------------------------------------------------------------------
-- Irrelevance lemmata for the logical relation
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Erasure.LogicalRelation.Assumptions
open import Graded.Modality

module Graded.Erasure.LogicalRelation.Irrelevance
  {a} {M : Set a}
  {ğ•„ : Modality M}
  {R : Type-restrictions ğ•„}
  (as : Assumptions R)
  where

open Assumptions as
open Modality ğ•„
open Type-restrictions R

open import Graded.Erasure.LogicalRelation as

open import Definition.LogicalRelation R
open import Definition.LogicalRelation.ShapeView R
open import Definition.LogicalRelation.Weakening.Restricted R
import Definition.LogicalRelation.Irrelevance R as I

open import Definition.Typed R
open import Definition.Typed.Consequences.Injectivity R
open import Definition.Typed.Properties R
open import Definition.Typed.Reasoning.Type R

open import Definition.Untyped M
open import Definition.Untyped.Neutral M type-variant
open import Definition.Untyped.Properties M

open import Tools.Function
open import Tools.Nat
open import Tools.Product
import Tools.PropositionalEquality as PE
open import Tools.Relation

private
  variable
    n : Nat
    Aâ€² : Term n

-- Irrelevance of logical relation for erasure using a ShapeView

irrelevanceTermSV : âˆ€ {l lâ€² t v A}
                  â†’ ([A] : Î” âŠ©âŸ¨ l âŸ© A)
                    ([A]â€² : Î” âŠ©âŸ¨ lâ€² âŸ© A)
                  â†’ t Â®âŸ¨ l âŸ© v âˆ· A / [A]
                  â†’ ShapeView Î” l lâ€² A A [A] [A]â€²
                  â†’ t Â®âŸ¨ lâ€² âŸ© v âˆ· A / [A]â€²
irrelevanceTermSV _ _ tÂ®v (Leveláµ¥ _ _) =
  tÂ®v
irrelevanceTermSV .(Uáµ£ UA) .(Uáµ£ UB) tÂ®v (Uáµ¥ UA UB) = tÂ®v
irrelevanceTermSV .(â„•áµ£ â„•A) .(â„•áµ£ â„•B) tÂ®v (â„•áµ¥ â„•A â„•B) = tÂ®v
irrelevanceTermSV
  _ _ tÂ®v (Liftáµ¥ (Liftáµ£ Aâ‡’*Liftâ‚ _ âŠ©Bâ‚) (Liftáµ£ Aâ‡’*Liftâ‚‚ _ âŠ©Bâ‚‚)) =
  case whrDet* (Aâ‡’*Liftâ‚ , Liftâ‚™) (Aâ‡’*Liftâ‚‚ , Liftâ‚™) of Î» {
    PE.refl â†’
  irrelevanceTermSV _ _ tÂ®v (goodCasesRefl âŠ©Bâ‚ âŠ©Bâ‚‚) }
irrelevanceTermSV
  {A} _ _ (staráµ£ tâ‡›â‹† uâ‰¡uâ€² vâ‡’*â‹†)
  (Unitáµ¥ {s} (Unitáµ£ u _ _ Aâ‡’*Unitâ‚ _) (Unitáµ£ v _ _ Aâ‡’*Unitâ‚‚ _)) =
  case Unit-injectivity
         (Unit s u  â‰¡Ë˜âŸ¨ subset* Aâ‡’*Unitâ‚ âŸ©âŠ¢
          A         â‰¡âŸ¨ subset* Aâ‡’*Unitâ‚‚ âŸ©âŠ¢âˆ
          Unit s v  âˆ) of Î» {
    (_ , uâ‰¡v) â†’
  staráµ£ tâ‡›â‹† (trans (symâ€² uâ‰¡v) uâ‰¡uâ€²) vâ‡’*â‹† }
irrelevanceTermSV
  [A] [A]â€² tÂ®v
  (Báµ¥ (BÎ  p q) (Báµ£ F G D Aâ‰¡A [F] [G] G-ext _)
     (Báµ£ Fâ‚ Gâ‚ Dâ‚ Aâ‰¡Aâ‚ [F]â‚ [G]â‚ G-extâ‚ _))
       with B-PE-injectivity BÎ ! BÎ ! (whrDet* (D , Î Î£â‚™) (Dâ‚ , Î Î£â‚™))
... | PE.refl , PE.refl , _
       with is-ğŸ˜? p
... | (yes pâ‰¡ğŸ˜) = tÂ®v .projâ‚ , Î» [a]â€² â†’
  let [a] = I.irrelevanceTerm ([F]â‚ (id âŠ¢Î”)) ([F] (id âŠ¢Î”)) [a]â€²
      tÂ®vâ€² = tÂ®v .projâ‚‚ [a]
      SVâ€² = goodCasesRefl ([G] (id âŠ¢Î”) [a]) ([G]â‚ (id âŠ¢Î”) [a]â€²)
  in  irrelevanceTermSV ([G] (id âŠ¢Î”) [a]) ([G]â‚ (id âŠ¢Î”) [a]â€²) tÂ®vâ€² SVâ€²
... | (no pâ‰¢ğŸ˜) = tÂ®v .projâ‚ , Î» [a]â€² aÂ®wâ€² â†’
  let [a] = I.irrelevanceTerm ([F]â‚ (id âŠ¢Î”)) ([F] (id âŠ¢Î”)) [a]â€²
      SV = goodCasesRefl ([F]â‚ (id âŠ¢Î”)) ([F] (id âŠ¢Î”))
      aÂ®w = irrelevanceTermSV ([F]â‚ (id âŠ¢Î”)) ([F] (id âŠ¢Î”)) aÂ®wâ€² SV
      tÂ®vâ€² = tÂ®v .projâ‚‚ [a] aÂ®w
      SVâ€² = goodCasesRefl ([G] (id âŠ¢Î”) [a]) ([G]â‚ (id âŠ¢Î”) [a]â€²)
  in  irrelevanceTermSV ([G] (id âŠ¢Î”) [a]) ([G]â‚ (id âŠ¢Î”) [a]â€²) tÂ®vâ€² SVâ€²
irrelevanceTermSV {v = v}
  [A] [A]â€² (tâ‚ , tâ‚‚ , tâ‡’tâ€² , [tâ‚] , vâ‚‚ , tâ‚‚Â®vâ‚‚ , extra)
  (Báµ¥ (BÎ£ _ p _) (Báµ£ F G D Aâ‰¡A [F] [G] G-ext _)
     (Báµ£ Fâ‚ Gâ‚ Dâ‚ Aâ‰¡Aâ‚ [F]â‚ [G]â‚ G-extâ‚ _))
  with B-PE-injectivity BÎ£! BÎ£! (whrDet* (D , Î Î£â‚™) (Dâ‚ , Î Î£â‚™))
... | PE.refl , PE.refl , _ =
  let [F]â€² = [F] (id âŠ¢Î”)
      [F]â‚â€² = [F]â‚ (id âŠ¢Î”)
      [tâ‚]â€² = I.irrelevanceTerm [F]â€² [F]â‚â€² [tâ‚]
      [Gtâ‚] = [G] (id âŠ¢Î”) [tâ‚]
      [Gtâ‚]â‚ = [G]â‚ (id âŠ¢Î”) [tâ‚]â€²
      tâ‚‚Â®vâ‚‚â€² = irrelevanceTermSV [Gtâ‚] [Gtâ‚]â‚ tâ‚‚Â®vâ‚‚
                 (goodCasesRefl [Gtâ‚] [Gtâ‚]â‚)
  in  tâ‚ , tâ‚‚ , tâ‡’tâ€² , [tâ‚]â€² , vâ‚‚ , tâ‚‚Â®vâ‚‚â€²
      , Î£-Â®-elim (Î» _ â†’ Î£-Â® _ _ [F]â‚â€² tâ‚ v vâ‚‚ p) extra
                 Î£-Â®-intro-ğŸ˜
                 Î» vâ‚ vâ‡’p tâ‚Â®vâ‚ pâ‰¢ğŸ˜ â†’
                   Î£-Â®-intro-Ï‰ vâ‚ vâ‡’p (irrelevanceTermSV [F]â€² [F]â‚â€² tâ‚Â®vâ‚
                               (goodCasesRefl [F]â€² [F]â‚â€²)) pâ‰¢ğŸ˜
irrelevanceTermSV _ _ tÂ®v (Idáµ¥ âŠ©A@record{} âŠ©B) =
  case whrDet* (_âŠ©â‚—Id_.â‡’*Id âŠ©A , Idâ‚™) (_âŠ©â‚—Id_.â‡’*Id âŠ©B , Idâ‚™) of Î» {
    PE.refl â†’
  tÂ®v }
-- Impossible cases
irrelevanceTermSV _ _ () (Emptyáµ¥ _ _)
irrelevanceTermSV _ _ () (ne record{} _)

-- Irrelevance of logical relation for erasure

irrelevanceTerm : âˆ€ {l lâ€² t v A}
                â†’ ([A] : Î” âŠ©âŸ¨ l âŸ© A)
                  ([A]â€² : Î” âŠ©âŸ¨ lâ€² âŸ© A)
                â†’ t Â®âŸ¨ l âŸ© v âˆ· A / [A]
                â†’ t Â®âŸ¨ lâ€² âŸ© v âˆ· A / [A]â€²
irrelevanceTerm [A] [A]â€² tÂ®v =
  irrelevanceTermSV [A] [A]â€² tÂ®v (goodCasesRefl [A] [A]â€²)

-- Irrelevance of logical relation for erasure with propositionally equal types

irrelevanceTermâ€² : âˆ€ {l lâ€² t v A}
                 â†’ A PE.â‰¡ Aâ€²
                 â†’ ([A] : Î” âŠ©âŸ¨ l âŸ© A)
                 â†’ ([A]â€² : Î” âŠ©âŸ¨ lâ€² âŸ© Aâ€²)
                 â†’ t Â®âŸ¨ l âŸ© v âˆ· A / [A]
                 â†’ t Â®âŸ¨ lâ€² âŸ© v âˆ· Aâ€² / [A]â€²
irrelevanceTermâ€² PE.refl [A] [A]â€² tÂ®v = irrelevanceTerm [A] [A]â€² tÂ®v
