------------------------------------------------------------------------
-- Irrelevance lemmata for the logical relation
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Erasure.LogicalRelation.Assumptions
open import Graded.Modality

module Graded.Erasure.LogicalRelation.Irrelevance
  {a} {M : Set a}
  {ğ•„ : Modality M}
  {R : Type-restrictions ğ•„}
  (as : Assumptions R)
  where

open Assumptions as
open Modality ğ•„
open Type-restrictions R

open import Graded.Erasure.LogicalRelation as

open import Definition.LogicalRelation.Simplified R
open import Definition.Typed.Consequences.Injectivity R
open import Definition.Typed.Properties R
open import Definition.Typed.Reasoning.Type R
open import Definition.Typed.Weakening.Definition R

open import Definition.Untyped M
open import Definition.Untyped.Properties M
open import Definition.Untyped.Whnf M type-variant

open import Tools.Function
open import Tools.Nat
open import Tools.Product
import Tools.PropositionalEquality as PE
open import Tools.Relation

private
  variable
    n : Nat
    Aâ€² : Term n

private opaque

  -- Irrelevance of logical relation for erasure using a ShapeView

  irrelevanceTermSV : âˆ€ {t v A}
                    â†’ {[A] : ts Â» Î” âŠ¨ A}
                      {[A]â€² : ts Â» Î” âŠ¨ A}
                    â†’ t Â® v âˆ· A / [A]
                    â†’ ShapeView (ts Â» Î”) A A [A] [A]â€²
                    â†’ t Â® v âˆ· A / [A]â€²
  irrelevanceTermSV tÂ®v (Uáµ¥ UA UAâ€²) = tÂ®v
  irrelevanceTermSV tÂ®v (â„•áµ¥ â„•A â„•Aâ€²) = tÂ®v
  irrelevanceTermSV tÂ®v (Emptyáµ¥ EmptyA EmptyAâ€²) = tÂ®v
  irrelevanceTermSV {A} tÂ®v
    (Unitáµ¥ {s} (Unitáµ£ l Aâ‡’*Unitâ‚) (Unitáµ£ lâ€² Aâ‡’*Unitâ‚‚)) =
    case Unit-injectivity
           (Unit s l  â‰¡Ë˜âŸ¨ subset* Aâ‡’*Unitâ‚ âŸ©âŠ¢
            A         â‰¡âŸ¨ subset* Aâ‡’*Unitâ‚‚ âŸ©âŠ¢âˆ
            Unit s lâ€² âˆ) of Î» {
      (_ , PE.refl) â†’
    tÂ®v }
  irrelevanceTermSV tÂ®v (ne neA neAâ€²) = tÂ®v
  irrelevanceTermSV
    tÂ®v
    (Báµ¥ BMÎ  p q (Báµ£ F G D [F] [G])
      (Báµ£ Fâ‚ Gâ‚ Dâ‚ [F]â‚ [G]â‚))
       with B-PE-injectivity BÎ ! BÎ ! (whrDet* (D , Î Î£â‚™) (Dâ‚ , Î Î£â‚™))
  ... | PE.refl , PE.refl , _
         with is-ğŸ˜? p
  ... | (yes pâ‰¡ğŸ˜) = tÂ®v .projâ‚ , Î» âŠ¢a â†’
    irrelevanceTermSV (tÂ®v .projâ‚‚ âŠ¢a)
      (goodCasesRefl ([G] âŠ¢a) ([G]â‚ âŠ¢a))
  ... | (no pâ‰¢ğŸ˜) = tÂ®v .projâ‚ , Î» âŠ¢a aÂ®wâ€² â†’
    let aÂ®w = irrelevanceTermSV aÂ®wâ€² (goodCasesRefl [F]â‚ [F])
    in  irrelevanceTermSV (tÂ®v .projâ‚‚ âŠ¢a aÂ®w)
          (goodCasesRefl ([G] âŠ¢a) ([G]â‚ âŠ¢a))
  irrelevanceTermSV {v = v}
    (tâ‚ , tâ‚‚ , tâ‡’tâ€² , âŠ¢tâ‚ , vâ‚‚ , tâ‚‚Â®vâ‚‚ , extra)
    (Báµ¥ (BMÎ£ _) p _ (Báµ£ F G D [F] [G])
       (Báµ£ Fâ‚ Gâ‚ Dâ‚ [F]â‚ [G]â‚))
    with B-PE-injectivity BÎ£! BÎ£! (whrDet* (D , Î Î£â‚™) (Dâ‚ , Î Î£â‚™))
  ... | PE.refl , PE.refl , _ =
    tâ‚ , tâ‚‚ , tâ‡’tâ€² , âŠ¢tâ‚ , vâ‚‚
       , irrelevanceTermSV tâ‚‚Â®vâ‚‚
           (goodCasesRefl ([G] âŠ¢tâ‚) ([G]â‚ âŠ¢tâ‚))
       , Î£-Â®-elim (Î» _ â†’ Î£-Â® _ [F]â‚ tâ‚ v vâ‚‚ p) extra Î£-Â®-intro-ğŸ˜
           Î» vâ‚ vâ‡’p tâ‚Â®vâ‚ pâ‰¢ğŸ˜ â†’
             Î£-Â®-intro-Ï‰ vâ‚ vâ‡’p
               (irrelevanceTermSV tâ‚Â®vâ‚
                 (goodCasesRefl [F] [F]â‚))
               pâ‰¢ğŸ˜
  irrelevanceTermSV tÂ®v (Idáµ¥ âŠ¨A@record{} âŠ¨B) =
    case whrDet* (_âŠ¨Id_.â‡’*Id âŠ¨A , Idâ‚™) (_âŠ¨Id_.â‡’*Id âŠ¨B , Idâ‚™) of Î» {
      PE.refl â†’
    tÂ®v }

opaque

  -- Irrelevance of logical relation for erasure

  irrelevanceTerm : âˆ€ {t v A}
                  â†’ ([A] : ts Â» Î” âŠ¨ A)
                    ([A]â€² : ts Â» Î” âŠ¨ A)
                  â†’ t Â® v âˆ· A / [A]
                  â†’ t Â® v âˆ· A / [A]â€²
  irrelevanceTerm [A] [A]â€² tÂ®v =
    irrelevanceTermSV tÂ®v (goodCasesRefl [A] [A]â€²)

opaque

  -- Irrelevance of logical relation for erasure with propositionally equal types

  irrelevanceTermâ€² : âˆ€ {t v A}
                   â†’ A PE.â‰¡ Aâ€²
                   â†’ ([A] : ts Â» Î” âŠ¨ A)
                   â†’ ([A]â€² : ts Â» Î” âŠ¨ Aâ€²)
                   â†’ t Â® v âˆ· A / [A]
                   â†’ t Â® v âˆ· Aâ€² / [A]â€²
  irrelevanceTermâ€² PE.refl [A] [A]â€² tÂ®v = irrelevanceTerm [A] [A]â€² tÂ®v
