------------------------------------------------------------------------
-- Some assumptions used to define the logical relation for erasure
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Modality

module Graded.Erasure.LogicalRelation.Assumptions
  {a} {M : Set a}
  {ğ•„ : Modality M}
  (R : Type-restrictions ğ•„)
  where

open Type-restrictions R

open import Definition.Typed R
open import Definition.Typed.EqualityRelation R
open import Definition.Typed.Consequences.Canonicity R
open import Definition.Typed.Inversion R
open import Definition.Typed.Properties R
open import Definition.Typed.Well-formed R
open import Definition.Untyped M
open import Definition.Untyped.Neutral M type-variant
open import Definition.Untyped.Properties M

open import Graded.Erasure.Target using (Strictness)

open import Tools.Fin
open import Tools.Function
open import Tools.Level using (lsuc)
open import Tools.Nat
open import Tools.Product
open import Tools.Sum

private variable
  n                             : Nat
  Î“                             : Con Term _
  A B C t tâ‚ tâ‚‚ u v vâ‚ vâ‚‚ wâ‚ wâ‚‚ : Term _
  p q qâ€² r                      : M

------------------------------------------------------------------------
-- Is-reduction-relation

-- An axiomatisation of "reduction" relations that can be used in the
-- logical relation. The context is fixed.

record Is-reduction-relation
         (Î“ : Con Term n) (_â‡›_âˆ·_ : Term n â†’ Term n â†’ Term n â†’ Set a) :
         Set (lsuc a) where
  field
    -- Conversion.

    conv-â‡› : t â‡› u âˆ· A â†’ Î“ âŠ¢ A â‰¡ B â†’ t â‡› u âˆ· B

    -- Reduction is contained in the relation.

    â‡’*â†’â‡› : Î“ âŠ¢ t â‡’* u âˆ· A â†’ t â‡› u âˆ· A

    -- The relation is contained in definitional equality.

    â‡›â†’âŠ¢â‰¡ : t â‡› u âˆ· A â†’ Î“ âŠ¢ t â‰¡ u âˆ· A

    -- Transitivity.

    trans-â‡› : t â‡› u âˆ· A â†’ u â‡› v âˆ· A â†’ t â‡› v âˆ· A

    -- If t "reduces" to both u andÂ v, and u is a WHNF, then v
    -- "reduces" toÂ u.

    whnf-â‡› : t â‡› u âˆ· A â†’ Whnf u â†’ t â‡› v âˆ· A â†’ v â‡› u âˆ· A

    -- Some congruence properties.

    lower-â‡› :
      tâ‚ â‡› tâ‚‚ âˆ· Lift u A â†’
      lower tâ‚ â‡› lower tâ‚‚ âˆ· A

    app-â‡› :
      tâ‚ â‡› tâ‚‚ âˆ· Î  p , q â–· A â–¹ B â†’
      Î“ âŠ¢ u âˆ· A â†’
      tâ‚ âˆ˜âŸ¨ p âŸ© u â‡› tâ‚‚ âˆ˜âŸ¨ p âŸ© u âˆ· B [ u ]â‚€

    fst-â‡› :
      tâ‚ â‡› tâ‚‚ âˆ· Î£Ë¢ p , q â–· A â–¹ B â†’
      fst p tâ‚ â‡› fst p tâ‚‚ âˆ· A

    snd-â‡› :
      tâ‚ â‡› tâ‚‚ âˆ· Î£Ë¢ p , q â–· A â–¹ B â†’
      snd p tâ‚ â‡› snd p tâ‚‚ âˆ· B [ fst p tâ‚ ]â‚€

    prodrec-â‡› :
      Î“ âˆ™ Î£Ê· p , qâ€² â–· A â–¹ B âŠ¢ C â†’
      tâ‚ â‡› tâ‚‚ âˆ· Î£Ê· p , qâ€² â–· A â–¹ B â†’
      Î“ âˆ™ A âˆ™ B âŠ¢ u âˆ· C [ prodÊ· p (var x1) (var x0) ]â†‘Â² â†’
      prodrec r p q C tâ‚ u â‡› prodrec r p q C tâ‚‚ u âˆ· C [ tâ‚ ]â‚€

    natrec-â‡› :
      Î“ âŠ¢ t âˆ· A [ zero ]â‚€ â†’
      Î“ âˆ™ â„• âˆ™ A âŠ¢ u âˆ· A [ suc (var x1) ]â†‘Â² â†’
      vâ‚ â‡› vâ‚‚ âˆ· â„• â†’
      natrec p q r A t u vâ‚ â‡› natrec p q r A t u vâ‚‚ âˆ· A [ vâ‚ ]â‚€

    J-â‡› :
      Î“ âˆ™ A âˆ™ Id (wk1 A) (wk1 t) (var x0) âŠ¢ B â†’
      Î“ âŠ¢ u âˆ· B [ t , rfl ]â‚â‚€ â†’
      wâ‚ â‡› wâ‚‚ âˆ· Id A t v â†’
      J p q A t B u v wâ‚ â‡› J p q A t B u v wâ‚‚ âˆ· B [ v , wâ‚ ]â‚â‚€

    K-â‡› :
      Î“ âˆ™ Id A t t âŠ¢ B â†’
      Î“ âŠ¢ u âˆ· B [ rfl ]â‚€ â†’
      vâ‚ â‡› vâ‚‚ âˆ· Id A t t â†’
      K-allowed â†’
      K p A t B u vâ‚ â‡› K p A t B u vâ‚‚ âˆ· B [ vâ‚ ]â‚€

  opaque

    -- If t reduces toÂ u, then t and u are well-typed.

    wf-â‡› : t â‡› u âˆ· A â†’ Î“ âŠ¢ t âˆ· A Ã— Î“ âŠ¢ u âˆ· A
    wf-â‡› = projâ‚‚ âˆ˜â†’ wf-âŠ¢â‰¡âˆ· âˆ˜â†’ â‡›â†’âŠ¢â‰¡

opaque instance

  -- Reduction is a "reduction" relation.

  â‡’*-is-reduction-relation : Is-reduction-relation Î“ (Î“ âŠ¢_â‡’*_âˆ·_)
  â‡’*-is-reduction-relation = record
    { conv-â‡›    = conv*
    ; â‡’*â†’â‡›      = idá¶ 
    ; â‡›â†’âŠ¢â‰¡      = subset*Term
    ; trans-â‡›   = _â‡¨âˆ·*_
    ; whnf-â‡›    = Î» tâ‡’*u u-whnf â†’ whrDetâ†˜Term (tâ‡’*u , u-whnf)
    ; lower-â‡›   = lower-subst*
    ; app-â‡›     = app-subst*
    ; fst-â‡›     = fst-subst*
    ; snd-â‡›     = snd-subst*
    ; prodrec-â‡› = prodrec-subst*
    ; natrec-â‡›  = natrec-subst*
    ; J-â‡›       = J-subst*
    ; K-â‡›       = K-subst*
    }

opaque instance

  -- Definitional equality is a "reduction" relation.

  â‰¡-is-reduction-relation : Is-reduction-relation Î“ (Î“ âŠ¢_â‰¡_âˆ·_)
  â‰¡-is-reduction-relation = record
    { conv-â‡›    = conv
    ; â‡’*â†’â‡›      = subset*Term
    ; â‡›â†’âŠ¢â‰¡      = idá¶ 
    ; trans-â‡›   = trans
    ; whnf-â‡›    = Î» tâ‰¡u _ tâ‰¡v â†’ trans (symâ€² tâ‰¡v) tâ‰¡u
    ; lower-â‡›   = lower-cong
    ; app-â‡›     = Î» tâ‚â‰¡tâ‚‚ âŠ¢u â†’ app-cong tâ‚â‰¡tâ‚‚ (refl âŠ¢u)
    ; fst-â‡›     = fst-congâ€²
    ; snd-â‡›     = snd-congâ€²
    ; prodrec-â‡› = Î» âŠ¢C tâ‚â‰¡tâ‚‚ âŠ¢u â†’
                    prodrec-congâ€² (refl âŠ¢C) tâ‚â‰¡tâ‚‚ (refl âŠ¢u)
    ; natrec-â‡›  = Î» âŠ¢t âŠ¢u vâ‚â‰¡vâ‚‚ â†’
                    natrec-cong (refl (âŠ¢âˆ™â†’âŠ¢ (wfTerm âŠ¢u))) (refl âŠ¢t)
                      (refl âŠ¢u) vâ‚â‰¡vâ‚‚
    ; J-â‡›       = Î» âŠ¢B âŠ¢u wâ‚â‰¡wâ‚‚ â†’
                    let âŠ¢A , âŠ¢t , âŠ¢v =
                          inversion-Id (wf-âŠ¢â‰¡âˆ· wâ‚â‰¡wâ‚‚ .projâ‚)
                    in
                    J-congâ€² (refl âŠ¢A) (refl âŠ¢t) (refl âŠ¢B) (refl âŠ¢u)
                      (refl âŠ¢v) wâ‚â‰¡wâ‚‚
    ; K-â‡›       = Î» âŠ¢B âŠ¢u vâ‚â‰¡vâ‚‚ ok â†’
                    let âŠ¢A , âŠ¢t , _ =
                          inversion-Id (wf-âŠ¢â‰¡âˆ· vâ‚â‰¡vâ‚‚ .projâ‚)
                    in
                    K-cong (refl âŠ¢A) (refl âŠ¢t) (refl âŠ¢B) (refl âŠ¢u) vâ‚â‰¡vâ‚‚
                      ok
    }

opaque

  -- Propositional equality is a "reduction" relation for the empty
  -- context, or if equality reflection is allowed.

  Id-is-reduction-relation :
    Empty-con Î“ âŠ Equality-reflection â†’
    Is-reduction-relation Î“ (Î» t u A â†’ âˆƒ Î» v â†’ Î“ âŠ¢ v âˆ· Id A t u)
  Id-is-reduction-relation {Î“} ok = record
    { conv-â‡›    = Î» (_ , âŠ¢v) Aâ‰¡B â†’
                    let _ , âŠ¢t , âŠ¢u = inversion-Id (wf-âŠ¢âˆ· âŠ¢v) in
                    _ , conv âŠ¢v (Id-cong Aâ‰¡B (refl âŠ¢t) (refl âŠ¢u))
    ; â‡’*â†’â‡›      = âŠ¢â‰¡â†’â‡› âˆ˜â†’ subset*Term
    ; â‡›â†’âŠ¢â‰¡      = â‡›â†’âŠ¢â‰¡
    ; trans-â‡›   = Î» (_ , âŠ¢vâ‚) (_ , âŠ¢vâ‚‚) â†’ _ , âŠ¢transitivity âŠ¢vâ‚ âŠ¢vâ‚‚
    ; whnf-â‡›    = Î» (_ , âŠ¢vâ‚) _ (_ , âŠ¢vâ‚‚) â†’
                    _ , âŠ¢transitivity (âŠ¢symmetry âŠ¢vâ‚‚) âŠ¢vâ‚
    ; lower-â‡›   = âŠ¢â‰¡â†’â‡› âˆ˜â†’ R.lower-â‡› âˆ˜â†’ â‡›â†’âŠ¢â‰¡
    ; app-â‡›     = Î» tâ‚â‡›tâ‚‚ âŠ¢u â†’ âŠ¢â‰¡â†’â‡› (R.app-â‡› (â‡›â†’âŠ¢â‰¡ tâ‚â‡›tâ‚‚) âŠ¢u)
    ; fst-â‡›     = âŠ¢â‰¡â†’â‡› âˆ˜â†’ R.fst-â‡› âˆ˜â†’ â‡›â†’âŠ¢â‰¡
    ; snd-â‡›     = âŠ¢â‰¡â†’â‡› âˆ˜â†’ R.snd-â‡› âˆ˜â†’ â‡›â†’âŠ¢â‰¡
    ; prodrec-â‡› = Î» âŠ¢C tâ‚â‡›tâ‚‚ âŠ¢u â†’ âŠ¢â‰¡â†’â‡› (R.prodrec-â‡› âŠ¢C (â‡›â†’âŠ¢â‰¡ tâ‚â‡›tâ‚‚) âŠ¢u)
    ; natrec-â‡›  = Î» âŠ¢A âŠ¢u vâ‚â‡›vâ‚‚ â†’ âŠ¢â‰¡â†’â‡› (R.natrec-â‡› âŠ¢A âŠ¢u (â‡›â†’âŠ¢â‰¡ vâ‚â‡›vâ‚‚))
    ; J-â‡›       = Î» âŠ¢B âŠ¢u wâ‚â‡›wâ‚‚ â†’ âŠ¢â‰¡â†’â‡› (R.J-â‡› âŠ¢B âŠ¢u (â‡›â†’âŠ¢â‰¡ wâ‚â‡›wâ‚‚))
    ; K-â‡›       = Î» âŠ¢B âŠ¢u vâ‚â‡›vâ‚‚ ok â†’ âŠ¢â‰¡â†’â‡› (R.K-â‡› âŠ¢B âŠ¢u (â‡›â†’âŠ¢â‰¡ vâ‚â‡›vâ‚‚) ok)
    }
    where
    module R = Is-reduction-relation (â‰¡-is-reduction-relation {Î“ = Î“})

    â‡›â†’âŠ¢â‰¡ : (âˆƒ Î» v â†’ Î“ âŠ¢ v âˆ· Id A t u) â†’ Î“ âŠ¢ t â‰¡ u âˆ· A
    â‡›â†’âŠ¢â‰¡ (_ , âŠ¢v) = case ok of Î» where
      (injâ‚ Îµ) â†’
        ÎµâŠ¢âˆ·Idâ†’ÎµâŠ¢â‰¡âˆ· âŠ¢v
      (injâ‚‚ ok) â†’
        equality-reflectionâ€² ok âŠ¢v

    âŠ¢â‰¡â†’â‡› : Î“ âŠ¢ t â‰¡ u âˆ· A â†’ âˆƒ Î» v â†’ Î“ âŠ¢ v âˆ· Id A t u
    âŠ¢â‰¡â†’â‡› tâ‰¡u = rfl , rflâ±¼â€² tâ‰¡u

------------------------------------------------------------------------
-- Assumptions

-- Assumptions used to define the logical relation for erasure.

record Assumptions : Set (lsuc a) where
  infix 4 _â‡›_âˆ·_

  field
    -- An "EqRelSet".
    â¦ƒ eqRelSet â¦„ : EqRelSet

  open EqRelSet eqRelSet public

  field
    -- The size of the context below.
    {k} : Nat

    -- A context.
    {Î”} : Con Term k

    -- The context is well-formed.
    âŠ¢Î” : âŠ¢ Î”

    -- Neutrals-included holds or Î” is empty.
    â¦ƒ inc â¦„ : Neutrals-included or-empty Î”

    -- Should applications be extracted to strict or non-strict
    -- applications?
    str : Strictness

    -- A "reduction" relation.
    _â‡›_âˆ·_ : Term k â†’ Term k â†’ Term k â†’ Set a

    -- The "reduction" relation satisfies certain properties.
    is-reduction-relation : Is-reduction-relation Î” _â‡›_âˆ·_

  open Is-reduction-relation is-reduction-relation public

  instance

    -- Equality reflection is not allowed or Î” is empty.

    no-equality-reflection-or-empty :
      No-equality-reflection or-empty Î”
    no-equality-reflection-or-empty =
      or-empty-map
        (No-equality-reflectionâ‡” .projâ‚‚ âˆ˜â†’
         flip Equality-reflection-allowedâ†’Â¬Neutrals-included)
        inc
