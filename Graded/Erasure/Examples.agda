------------------------------------------------------------------------
-- Some simple examples related to the erasure modality and extraction
------------------------------------------------------------------------

open import Graded.Modality.Instances.Erasure
open import Graded.Mode.Restrictions
open import Graded.Usage.Restrictions
open import Definition.Typed.Restrictions

module Graded.Erasure.Examples
  {p q}
  (MR : Mode-restrictions)
  (TR : Type-restrictions Erasure)
  (open Type-restrictions TR)
  (UR : Usage-restrictions Erasure)
  -- It is assumed that "Î Â ğŸ˜Â ,Â p" is allowed.
  (Î -ğŸ˜-ok : Î -allowed ğŸ˜ p)
  -- It is assumed that "Î Â Ï‰Â ,Â q" is allowed.
  (Î -Ï‰-ok : Î -allowed Ï‰ q)
  where

open import Definition.Typed TR as DT hiding (id)
open import Definition.Untyped Erasure hiding (id; _âˆ·_)

open import Graded.Modality.Instances.Erasure.Modality MR

open import Graded.Context ErasureModality
open import Graded.Erasure.Extraction
  ErasureModality
  (Has-well-behaved-zero.is-ğŸ˜? erasure-has-well-behaved-zero)
import Graded.Erasure.Target as T
import Graded.Erasure.Target.Properties as TP
open import Graded.Mode ErasureModality
open import Graded.Usage ErasureModality UR
open import Graded.Usage.Inversion ErasureModality UR

open import Tools.Fin
open import Tools.Function
open import Tools.Nullary
open import Tools.PropositionalEquality
open import Tools.Sum as âŠ using (_âŠ_; injâ‚; injâ‚‚)

private variable
  Î³ : Conâ‚˜ _

private

  -- Some lemmas used below.

  âŠ¢â„• : âŠ¢ Îµ âˆ™ â„•
  âŠ¢â„• = Îµ âˆ™ â„•â±¼ Îµ

  âŠ¢U : âŠ¢ Îµ âˆ™ U
  âŠ¢U = Îµ âˆ™ Uâ±¼ Îµ

  UâŠ¢0 : Îµ âˆ™ U âŠ¢ var x0
  UâŠ¢0 = univ (var âŠ¢U here)

  âŠ¢U0 : âŠ¢ Îµ âˆ™ U âˆ™ var x0
  âŠ¢U0 = âŠ¢U âˆ™ UâŠ¢0

  UâŠ¢id : Îµ âˆ™ U âŠ¢ lam Ï‰ (var x0) âˆ· Î  Ï‰ , q â–· var x0 â–¹ var x1
  UâŠ¢id = lamâ±¼ UâŠ¢0 (var âŠ¢U0 here) Î -Ï‰-ok

------------------------------------------------------------------------
-- A polymorphic identity function

-- A (closed) polymorphic identity function with an erased type
-- argument.

id : Term 0
id = lam ğŸ˜ (lam Ï‰ (var x0))

-- The polymorphic identity function is well-typed (in the empty
-- context).

âŠ¢id : Îµ âŠ¢ id âˆ· Î  ğŸ˜ , p â–· U â–¹ Î  Ï‰ , q â–· var x0 â–¹ var x1
âŠ¢id = lamâ±¼ (Uâ±¼ Îµ) UâŠ¢id Î -ğŸ˜-ok

-- The polymorphic identity function is well-resourced (with respect
-- to the empty usage context).

â–¸id : Îµ â–¸[ ğŸ™áµ ] id
â–¸id = lamâ‚˜ (lamâ‚˜ var)

-- The polymorphic identity function applied to two arguments.

id-â„•-zero : Term 0
id-â„•-zero = id âˆ˜âŸ¨ ğŸ˜ âŸ© â„• âˆ˜âŸ¨ Ï‰ âŸ© zero

-- The erasure of id-â„•-zero includes an erased part (T.â†¯).

erase-id-â„•-zero :
  erase id-â„•-zero â‰¡ T.lam (T.lam (T.var x0)) T.âˆ˜ T.â†¯ T.âˆ˜ T.zero
erase-id-â„•-zero = refl

-- The term id-â„•-zero is well-typed (in the empty context).

âŠ¢id-â„•-zero : Îµ âŠ¢ id-â„•-zero âˆ· â„•
âŠ¢id-â„•-zero = (âŠ¢id âˆ˜â±¼ â„•â±¼ Îµ) âˆ˜â±¼ zeroâ±¼ Îµ

-- The term id-â„•-zero is well-resourced (with respect to the empty
-- usage context).

â–¸id-â„•-zero : Îµ â–¸[ ğŸ™áµ ] id-â„•-zero
â–¸id-â„•-zero = (â–¸id âˆ˜â‚˜ â„•â‚˜) âˆ˜â‚˜ zeroâ‚˜

-- The term id-â„•-zero reduces to zero.

id-â„•-zeroâ‡’*zero : Îµ âŠ¢ id-â„•-zero â‡’* zero âˆ· â„•
id-â„•-zeroâ‡’*zero =
  app-subst
    (Î²-red (Uâ±¼ Îµ) (Î Î£â±¼ UâŠ¢0 (univ (var âŠ¢U0 (there here))) Î -Ï‰-ok)
       UâŠ¢id (â„•â±¼ Îµ) refl Î -ğŸ˜-ok)
    (zeroâ±¼ Îµ) â‡¨
  (Î²-red (â„•â±¼ Îµ) (â„•â±¼ âŠ¢â„•) (var âŠ¢â„• here) (zeroâ±¼ Îµ) refl Î -Ï‰-ok â‡¨
   DT.id (zeroâ±¼ Îµ))

-- The erasure of id-â„•-zero reduces to zero.

erase-id-â„•-zeroâ‡’*zero : erase id-â„•-zero T.â‡’* T.zero
erase-id-â„•-zeroâ‡’*zero =
  T.trans (T.app-subst T.Î²-red) (T.trans T.Î²-red T.refl)

------------------------------------------------------------------------
-- A function that uses an erased argument in a non-erased position

-- A (closed) identity function that takes an erased argument.

idâ‚€ : Term 0
idâ‚€ = lam ğŸ˜ (var x0)

-- The function idâ‚€ is well-typed (in the empty context).

âŠ¢idâ‚€ : Îµ âŠ¢ idâ‚€ âˆ· Î  ğŸ˜ , p â–· â„• â–¹ â„•
âŠ¢idâ‚€ = lamâ±¼ (â„•â±¼ Îµ) (var (Îµ âˆ™ â„•â±¼ Îµ) here) Î -ğŸ˜-ok

-- The function idâ‚€ is not well-resourced.

Â¬â–¸idâ‚€ : Â¬ Î³ â–¸[ ğŸ™áµ ] idâ‚€
Â¬â–¸idâ‚€ â–¸idâ‚€ =
  case inv-usage-lam â–¸idâ‚€ of Î» {
    (invUsageLam â–¸0 _) â†’
  case inv-usage-var â–¸0 of Î» {
    (_ âˆ™ ()) }}

-- The function idâ‚€ applied to an argument.

idâ‚€-zero : Term 0
idâ‚€-zero = idâ‚€ âˆ˜âŸ¨ ğŸ˜ âŸ© zero

-- The erasure of idâ‚€-zero includes an erased part (T.â†¯).

erase-idâ‚€-zero : erase idâ‚€-zero â‰¡ T.lam (T.var x0) T.âˆ˜ T.â†¯
erase-idâ‚€-zero = refl

-- The term idâ‚€-zero is well-typed (in the empty context).

âŠ¢idâ‚€-zero : Îµ âŠ¢ idâ‚€-zero âˆ· â„•
âŠ¢idâ‚€-zero = âŠ¢idâ‚€ âˆ˜â±¼ zeroâ±¼ Îµ

-- The term idâ‚€-zero is not well-resourced.

Â¬â–¸idâ‚€-zero : Â¬ Î³ â–¸[ ğŸ™áµ ] idâ‚€-zero
Â¬â–¸idâ‚€-zero â–¸idâ‚€-zero =
  case inv-usage-app â–¸idâ‚€-zero of Î» {
    (invUsageApp â–¸idâ‚€ _ _) â†’
  Â¬â–¸idâ‚€ â–¸idâ‚€ }

-- The term idâ‚€-zero reduces to zero.

idâ‚€-zeroâ‡’*zero : Îµ âŠ¢ idâ‚€-zero â‡’* zero âˆ· â„•
idâ‚€-zeroâ‡’*zero =
  Î²-red (â„•â±¼ Îµ) (â„•â±¼ âŠ¢â„•) (var âŠ¢â„• here) (zeroâ±¼ Îµ) refl Î -ğŸ˜-ok â‡¨
  DT.id (zeroâ±¼ Îµ)

-- The erasure of idâ‚€-zero reduces to T.â†¯.

erase-idâ‚€-zeroâ‡’*â†¯ : erase idâ‚€-zero T.â‡’* T.â†¯
erase-idâ‚€-zeroâ‡’*â†¯ = T.trans T.Î²-red T.refl

-- The erasure of idâ‚€-zero does not reduce to T.zero.

Â¬erase-idâ‚€-zeroâ‡’*zero : Â¬ erase idâ‚€-zero T.â‡’* T.zero
Â¬erase-idâ‚€-zeroâ‡’*zero =
  erase idâ‚€-zero T.â‡’* T.zero         â†’âŸ¨ TP.red*Det erase-idâ‚€-zeroâ‡’*â†¯ âŸ©
  T.â†¯ T.â‡’* T.zero âŠ T.zero T.â‡’* T.â†¯  â†’âŸ¨ âŠ.map TP.â†¯-noRed TP.zero-noRed âŸ©
  T.zero â‰¡ T.â†¯ âŠ T.â†¯ â‰¡ T.zero        â†’âŸ¨ (Î» { (injâ‚ ()); (injâ‚‚ ()) }) âŸ©
  âŠ¥                                  â–¡
