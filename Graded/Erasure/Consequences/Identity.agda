------------------------------------------------------------------------
-- Some consequences of the fundamental lemma that are related to
-- identity types
------------------------------------------------------------------------

{-# OPTIONS --hidden-argument-puns #-}

open import Definition.Typed.Restrictions
import Graded.Modality
open import Graded.Usage.Restrictions

module Graded.Erasure.Consequences.Identity
  {a} {M : Set a}
  (open Graded.Modality M)
  {ğ•„ : Modality}
  (TR : Type-restrictions ğ•„)
  (UR : Usage-restrictions ğ•„)
  where

open Modality ğ•„
open Type-restrictions TR

open import Definition.Typed TR
open import Definition.Typed.Consequences.DerivedRules TR
open import Definition.Typed.Consequences.Inversion TR
open import Definition.Typed.Consequences.Syntactic TR
open import Definition.Typed.EqRelInstance TR
open import Definition.Typed.Properties TR
open import Definition.Untyped M hiding (_âˆ·_)

open import Graded.Context ğ•„
open import Graded.Derived.Erased.Eta.Typed TR
import Graded.Derived.Erased.Untyped ğ•„ as Erased
open import Graded.Derived.Erased.Eta.Usage ğ•„ UR
import Graded.Erasure.LogicalRelation TR as L
open import Graded.Erasure.LogicalRelation.Fundamental TR UR
open import Graded.Erasure.LogicalRelation.Fundamental.Assumptions TR UR
import Graded.Erasure.LogicalRelation.Hidden TR as H
open import Graded.Modality.Properties ğ•„
open import Graded.Mode ğ•„
open import Graded.Usage ğ•„ UR
open import Graded.Usage.Properties ğ•„ UR

open import Tools.Bool using (T)
open import Tools.Function
open import Tools.Product
import Tools.PropositionalEquality as PE

private variable
  Î“           : Con Term _
  Î³â‚ Î³â‚‚ Î³â‚ƒ Î³â‚„ : Conâ‚˜ _
  A t u v     : Term _
  s           : Strength

opaque

  -- If the modality's zero is well-behaved, the type IdÂ AÂ tÂ u is
  -- inhabited in a context that satisfies Fundamental-assumptionsâ»,
  -- and the witness of inhabitance is a term that is well-resourced
  -- with respect to ğŸ˜á¶œ, then t is definitionally equal toÂ u.

  Idâ†’â‰¡ :
    â¦ƒ ğŸ˜-well-behaved : Has-well-behaved-zero semiring-with-meet â¦„ â†’
    Fundamental-assumptionsâ» Î“ â†’
    Î“ âŠ¢ v âˆ· Id A t u â†’
    ğŸ˜á¶œ â–¸[ ğŸ™áµ ] v â†’
    Î“ âŠ¢ t â‰¡ u âˆ· A
  Idâ†’â‰¡ ok âŠ¢v â–¸v =
    case Â®-Id $
         Fundamental.fundamentalErased-ğŸ™áµ
           (record
              { well-formed       = wfTerm âŠ¢v
              ; other-assumptions = ok
              })
           âŠ¢v â–¸v of Î» {
      (rfláµ£ vâ‡’rfl _) â†’
    inversion-rfl-Id
      (syntacticEqTerm (subset*Term vâ‡’rfl) .projâ‚‚ .projâ‚‚) }
    where
    open Fundamental-assumptionsâ» ok
    open H is-ğŸ˜? (wfTerm âŠ¢v)
    open L is-ğŸ˜? (wfTerm âŠ¢v)

opaque

  -- A variant of the previous lemma: If the modality's zero is
  -- well-behaved, []-cong is allowed, the type IdÂ AÂ tÂ u is inhabited
  -- in a context that satisfies Fundamental-assumptionsâ», and the
  -- witness of inhabitance as well as the terms A, t and u are
  -- well-resourced with respect to any context and the mode ğŸ˜áµ?, then
  -- t is definitionally equal toÂ u.
  --
  -- Note that if []-cong is allowed, then (at the time of writing)
  -- Fundamental-assumptionsâ» only holds for the empty context.

  Idâ†’â‰¡â€² :
    â¦ƒ ğŸ˜-well-behaved : Has-well-behaved-zero semiring-with-meet â¦„ â†’
    []-cong-allowed s â†’
    Fundamental-assumptionsâ» Î“ â†’
    Î³â‚ â–¸[ ğŸ˜áµ? ] A â†’
    Î³â‚‚ â–¸[ ğŸ˜áµ? ] t â†’
    Î³â‚ƒ â–¸[ ğŸ˜áµ? ] u â†’
    Î³â‚„ â–¸[ ğŸ˜áµ? ] v â†’
    Î“ âŠ¢ v âˆ· Id A t u â†’
    Î“ âŠ¢ t â‰¡ u âˆ· A
  Idâ†’â‰¡â€² {s} {Î“} {A} {t} {u} {v} []-cong-ok ok â–¸A â–¸t â–¸u â–¸v =
    Î“ âŠ¢ v âˆ· Id A t u                                       â†’âŸ¨ []-congâ±¼â€² []-cong-ok âŸ©
    Î“ âŠ¢ []-cong _ A t u v âˆ· Id (Erased A) ([ t ]) ([ u ])  â†’âŸ¨ flip (Idâ†’â‰¡ ok) ([]-congâ‚˜ â–¸A â–¸t â–¸u â–¸v) âŸ©
    Î“ âŠ¢ ([ t ]) â‰¡ ([ u ]) âˆ· Erased A                       â†’âŸ¨ projâ‚ âˆ˜â†’ projâ‚‚ âˆ˜â†’ prod-congâ»Â¹ âŸ©
    Î“ âŠ¢ t â‰¡ u âˆ· A                                          â–¡
    where
    open Erased s

opaque

  -- Another variant of Idâ†’â‰¡.
  --
  -- Note that if []-cong is allowed, then (at the time of writing)
  -- Fundamental-assumptionsâ» only holds for the empty context.

  Idâ†’â‰¡â€³ :
    â¦ƒ ğŸ˜-well-behaved : Has-well-behaved-zero semiring-with-meet â¦„
    â¦ƒ ok : T ğŸ˜áµ-allowed â¦„ â†’
    []-cong-allowed s â†’
    Fundamental-assumptionsâ» Î“ â†’
    Î³â‚ â–¸[ ğŸ˜áµ ] A â†’
    Î³â‚‚ â–¸[ ğŸ˜áµ ] t â†’
    Î³â‚ƒ â–¸[ ğŸ˜áµ ] u â†’
    Î³â‚„ â–¸[ ğŸ˜áµ ] v â†’
    Î“ âŠ¢ v âˆ· Erased.Erased ğ•¤ (Id A t u) â†’
    Î“ âŠ¢ t â‰¡ u âˆ· A
  Idâ†’â‰¡â€³ {Î“} {A} {t} {u} {v} []-cong-ok ok â–¸A â–¸t â–¸u â–¸v =
    Î“ âŠ¢ v âˆ· Erased (Id A t u)  â†’âŸ¨ erasedâ±¼ âŸ©
    Î“ âŠ¢ erased v âˆ· Id A t u    â†’âŸ¨ Idâ†’â‰¡â€² []-cong-ok ok
                                    (â–¸-cong (PE.sym ğŸ˜áµ?â‰¡ğŸ˜áµ) â–¸A) (â–¸-cong (PE.sym ğŸ˜áµ?â‰¡ğŸ˜áµ) â–¸t)
                                    (â–¸-cong (PE.sym ğŸ˜áµ?â‰¡ğŸ˜áµ) â–¸u) (â–¸-cong (PE.sym ğŸ˜áµ?â‰¡ğŸ˜áµ) $ â–¸erased â–¸v) âŸ©
    Î“ âŠ¢ t â‰¡ u âˆ· A              â–¡
    where
    open import Graded.Derived.Erased.Eta.Untyped ğ•„
