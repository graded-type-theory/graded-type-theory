------------------------------------------------------------------------
-- The Logical relation for erasure.
------------------------------------------------------------------------

{-# OPTIONS --hidden-argument-puns #-}

open import Definition.Typed.Restrictions
open import Graded.Erasure.LogicalRelation.Assumptions
open import Graded.Modality

module Graded.Erasure.LogicalRelation
  {a} {M : Set a}
  {ğ•„ : Modality M}
  {R : Type-restrictions ğ•„}
  (as : Assumptions R)
  where

open Assumptions as
open Modality ğ•„

open import Definition.Untyped M as U hiding (_âˆ˜_; K)

open import Definition.LogicalRelation R
open import Definition.LogicalRelation.Substitution R
open import Definition.Typed R
open import Graded.Context ğ•„
open import Graded.Mode ğ•„
open import Definition.Typed.Weakening R

open import Graded.Erasure.Target as T hiding (_â‡’*_)
open import Graded.Erasure.Extraction ğ•„

open import Tools.Empty
open import Tools.Function
open import Tools.Level
open import Tools.Nat
open import Tools.Product
open import Tools.PropositionalEquality as PE using (_â‰¡_)
open import Tools.Relation
open import Tools.Unit


private
  variable
    m n : Nat
    A B t tâ‚ tâ€² u : U.Term n
    v vâ‚‚ vâ€² w : T.Term n
    p : M
    l : TypeLevel
    s : Strength

------------------------------------------------------------------------
-- The logical relation

-- In the non-strict setting terms of type U are related to all target
-- terms, and in the strict setting they are related to those terms
-- that reduce toÂ â†¯. (All types are erased by the extraction
-- function.)

data _Â®_âˆ·U (t : U.Term k) (v : T.Term k) : Set a where
  Uáµ£ : (str â‰¡ strict â†’ v T.â‡’* â†¯) â†’ t Â® v âˆ·U

-- Terms of type â„• are related if both reduce to zero or if both
-- reduce to the successors of related terms (in the strict setting
-- the target term's reduct has to be a numeral).

data _Â®_âˆ·â„• (t : U.Term k) (v : T.Term k) : Set a where
  zeroáµ£ : Î” âŠ¢ t â‡’* U.zero âˆ· â„• â†’ v T.â‡’* T.zero â†’
          t Â® v âˆ·â„•
  sucáµ£  : Î” âŠ¢ t â‡’* U.suc tâ€² âˆ· â„• â†’ v T.â‡’* T.suc vâ€² â†’ NumeralâŸ¨ str âŸ© vâ€² â†’
          tâ€² Â® vâ€² âˆ·â„• â†’ t Â® v âˆ·â„•

-- Terms of type Empty are not related to anything.
-- (There are no terms of the Empty type in a consistent context).

data _Â®_âˆ·Empty (t : U.Term k) (v : T.Term k) : Set a where

-- Terms of type Unit are related if both reduce to star.

data _Â®_âˆ·UnitâŸ¨_âŸ© (t : U.Term k) (v : T.Term k) (s : Strength) : Set a where
  staráµ£ : Î” âŠ¢ t â‡’* U.star s âˆ· Unit s â†’ v T.â‡’* T.star â†’ t Â® v âˆ·UnitâŸ¨ s âŸ©

-- Equality proofs are related in the non-strict setting if the source
-- term reduces to rfl. In the strict setting the target term should
-- additionally reduce toÂ â†¯.

data _Â®_âˆ·IdâŸ¨_âŸ©âŸ¨_âŸ©âŸ¨_âŸ©
       (t : U.Term k) (v : T.Term k) (Ty lhs rhs : U.Term k) :
       Set a where
  rfláµ£ : Î” âŠ¢ t â‡’* U.rfl âˆ· Id Ty lhs rhs â†’ (str â‰¡ strict â†’ v T.â‡’* â†¯) â†’
         t Â® v âˆ·IdâŸ¨ Ty âŸ©âŸ¨ lhs âŸ©âŸ¨ rhs âŸ©

mutual

  -- Logical relation for erasure
  infix 19 _Â®âŸ¨_âŸ©_âˆ·_/_

  _Â®âŸ¨_âŸ©_âˆ·_/_ : (t : U.Term k) (l : TypeLevel) (v : T.Term k)
               (A : U.Term k) ([A] : Î” âŠ©âŸ¨ l âŸ© A) â†’ Set a
  t Â®âŸ¨ l âŸ© v âˆ· A / Uáµ£ x     = t Â® v âˆ·U
  t Â®âŸ¨ l âŸ© v âˆ· A / â„•áµ£ x     = t Â® v âˆ·â„•
  t Â®âŸ¨ l âŸ© v âˆ· A / Emptyáµ£ x = t Â® v âˆ·Empty
  t Â®âŸ¨ l âŸ© v âˆ· A / Unitáµ£ {s = s} x  = t Â® v âˆ·UnitâŸ¨ s âŸ©
  t Â®âŸ¨ l âŸ© v âˆ· A / neâ€² K D neK Kâ‰¡K = Lift a âŠ¥

  -- Î :
  t Â®âŸ¨ l âŸ© v âˆ· A / Báµ£â€² (BÎ  p q) F G D âŠ¢F âŠ¢G Aâ‰¡A [F] [G] G-ext _ =
    (str â‰¡ strict â†’ âˆƒ Î» vâ€² â†’ v T.â‡’* T.lam vâ€²) Ã—
    (âˆ€ {a} â†’ ([a] : Î” âŠ©âŸ¨ l âŸ© a âˆ· U.wk id F / [F] id âŠ¢Î”) â†’
     Î -Â® l F G t a v ([F] id âŠ¢Î”) ([G] id âŠ¢Î” [a]) p (is-ğŸ˜? p))

  -- Î£:
  -- t and v are related if:
  -- t reduces to a pair (tâ‚, tâ‚‚),
  -- tâ‚‚ is related to some vâ‚‚ and
  -- there is extra data depending on whether the first component
  -- is erased (see below).
  t Â®âŸ¨ l âŸ© v âˆ· A / Báµ£â€² (BÎ£ m p q) F G D âŠ¢F âŠ¢G Aâ‰¡A [F] [G] G-ext _ =
    âˆƒâ‚‚ Î» tâ‚ tâ‚‚ â†’
    Î” âŠ¢ t â‡’* U.prod m p tâ‚ tâ‚‚ âˆ· Î£âŸ¨ m âŸ© p , q â–· F â–¹ G Ã—
    Î£ (Î” âŠ©âŸ¨ l âŸ© tâ‚ âˆ· U.wk id F / [F] id âŠ¢Î”) Î» [tâ‚] â†’
    âˆƒ Î» vâ‚‚ â†’
    tâ‚‚ Â®âŸ¨ l âŸ© vâ‚‚ âˆ· U.wk (lift id) G U.[ tâ‚ ]â‚€ / [G] id âŠ¢Î” [tâ‚] Ã—
    Î£-Â® l F ([F] id âŠ¢Î”) tâ‚ v vâ‚‚ p

  -- Identity types.
  t Â®âŸ¨ _ âŸ© v âˆ· A / Idáµ£ âŠ©A = t Â® v âˆ·IdâŸ¨ Ty âŸ©âŸ¨ lhs âŸ©âŸ¨ rhs âŸ©
    where
    open _âŠ©â‚—Id_ âŠ©A

  -- Subsumption:
  t Â®âŸ¨ Â¹ âŸ© v âˆ· A / emb 0<1 [A] = t Â®âŸ¨ â° âŸ© v âˆ· A / [A]


  -- Extra data for Î -types, depending on whether the function argument
  -- is erased or not.

  Î -Â® : (l : TypeLevel) (F : U.Term k) (G : U.Term (1+ k))
        (t b : U.Term k) (v : T.Term k)
        ([F] : Î” âŠ©âŸ¨ l âŸ© U.wk id F)
        ([G] : Î” âŠ©âŸ¨ l âŸ© U.wk (lift id) G U.[ b ]â‚€)
        (p : M) (pâ‰ŸğŸ˜ : Dec (p PE.â‰¡ ğŸ˜)) â†’ Set a
  -- Erased Î :
  -- In the strict setting t is related to v if the applications tÂ âˆ˜Â a
  -- and vÂ âˆ˜Â â†¯ are related. In the non-strict setting tÂ âˆ˜Â a should be
  -- related toÂ v.
  Î -Â® l F G t a v [F] [Ga] p (yes pâ‰¡ğŸ˜) =
    (t U.âˆ˜âŸ¨ p âŸ© a) Â®âŸ¨ l âŸ© app-ğŸ˜ str v âˆ· U.wk (lift id) G U.[ a ]â‚€ / [Ga]
  -- Non-erased Î :
  -- Functions t and v are related if the applications
  -- tâˆ˜a and vâˆ˜w are related for all related a and w.
  Î -Â® l F G t a v [F] [Ga] p (no pâ‰¢ğŸ˜) =
    âˆ€ {w} â†’ a Â®âŸ¨ l âŸ© w âˆ· U.wk id F / [F]
          â†’ (t U.âˆ˜âŸ¨ p âŸ© a) Â®âŸ¨ l âŸ© v T.âˆ˜âŸ¨ str âŸ© w âˆ·
              U.wk (lift id) G U.[ a ]â‚€ / [Ga]

  -- Extra data for Î£-types, depending on whether the first component
  -- is erased or not.

  Î£-Â® :
    (l : TypeLevel) (F : U.Term k) â†’
    Î” âŠ©âŸ¨ l âŸ© U.wk id F â†’
    U.Term k â†’ T.Term k â†’ T.Term k â†’ (p : M) â†’ Set a
  Î£-Â® l F [F] tâ‚ v vâ‚‚ p = case is-ğŸ˜? p of Î» where
    -- Erased Î£:
    -- v reduces to vâ‚‚
    (yes pâ‰¡ğŸ˜) â†’ Lift a (v T.â‡’* vâ‚‚)
    -- There is a term vâ‚ such that v reduces to (vâ‚, vâ‚‚)
    -- and tâ‚ is related to vâ‚.
    (no pâ‰¢ğŸ˜) â†’ âˆƒ Î» vâ‚ â†’ (v T.â‡’* T.prod vâ‚ vâ‚‚)
                      Ã— (tâ‚ Â®âŸ¨ l âŸ© vâ‚ âˆ· U.wk id F / [F])

-- Logical relation for terms of quantified type
-- Under grade ğŸ˜, the terms need not be related.
_Â®âŸ¨_âŸ©_âˆ·_â—‚_/_ : (t : U.Term k) (l : TypeLevel) (v : T.Term k)
                 (A : U.Term k) (p : M)
                 ([A] : Î” âŠ©âŸ¨ l âŸ© A) â†’ Set a
t Â®âŸ¨ l âŸ© v âˆ· A â—‚ p / [A] = case is-ğŸ˜? p of Î» where
  (yes pâ‰¡ğŸ˜) â†’ Lift a âŠ¤
  (no pâ‰¢ğŸ˜) â†’ t Â®âŸ¨ l âŸ© v âˆ· A / [A]

-- Logical relation for substitutions
--
-- Substitutions are related if all terms are pairwise related
-- under the corresponding quantity of the grade context.

_Â®_âˆ·[_]_â—‚_/_/_ :
  (Ïƒâ‚œ : U.Subst k n)
  (Ïƒáµ¥ : T.Subst k n) (m : Mode) (Î“ : Con U.Term n) (Î³ : Conâ‚˜ n)
  ([Î“] : âŠ©áµ› Î“) ([Ïƒ] : Î” âŠ©Ë¢ Ïƒâ‚œ âˆ· Î“ / [Î“] / âŠ¢Î”) â†’ Set a
Ïƒâ‚œ Â® Ïƒáµ¥ âˆ·[ _ ] Îµ â—‚ Îµ / Îµ / (lift tt) = Lift a âŠ¤
Ïƒâ‚œ Â® Ïƒáµ¥ âˆ·[ m ] Î“ âˆ™ A â—‚ Î³ âˆ™ p / _âˆ™_ {l = l} [Î“] [A] / ([Ïƒ] , [ÏƒA]) =
  (U.tail Ïƒâ‚œ Â® T.tail Ïƒáµ¥ âˆ·[ m ] Î“ â—‚ Î³ / [Î“] / [Ïƒ]) Ã—
  (U.head Ïƒâ‚œ Â®âŸ¨ l âŸ© T.head Ïƒáµ¥ âˆ· A U.[ U.tail Ïƒâ‚œ ] â—‚ âŒœ m âŒ Â· p /
     projâ‚ (unwrap [A] âŠ¢Î” [Ïƒ]))

-- Validity of erasure
--
-- A term t is valid if t[Ïƒ] is related to (eraseÂ strÂ t)[Ïƒâ€²]
-- for all related contexts Ïƒ andÂ Ïƒâ€².

_â–¸_âŠ©Ê³âŸ¨_âŸ©_âˆ·[_]_/_/_ :
  (Î³ : Conâ‚˜ n) (Î“ : Con U.Term n) (l : TypeLevel)
  (t : U.Term n) (m : Mode) (A : U.Term n)
  ([Î“] : âŠ©áµ› Î“) ([A] : Î“ âŠ©áµ›âŸ¨ l âŸ© A / [Î“]) â†’ Set a
Î³ â–¸ Î“ âŠ©Ê³âŸ¨ l âŸ© t âˆ·[ m ] A / [Î“] / [A] =
  âˆ€ {Ïƒ Ïƒâ€²} â†’
  ([Ïƒ] : Î” âŠ©Ë¢ Ïƒ âˆ· Î“ / [Î“] / âŠ¢Î”) â†’
  Ïƒ Â® Ïƒâ€² âˆ·[ m ] Î“ â—‚ Î³ / [Î“] / [Ïƒ] â†’
  t U.[ Ïƒ ] Â®âŸ¨ l âŸ© erase str t T.[ Ïƒâ€² ] âˆ· A U.[ Ïƒ ] â—‚ âŒœ m âŒ /
    projâ‚ (unwrap [A] âŠ¢Î” [Ïƒ])

------------------------------------------------------------------------
-- Helper functions

opaque

  -- A "reduction" rule for Î -Â®.

  Î -Â®-ğŸ˜ :
    {âŠ©A : Î” âŠ©âŸ¨ l âŸ© U.wk id A}
    {âŠ©B[u] : Î” âŠ©âŸ¨ l âŸ© U.wk (lift id) B U.[ u ]â‚€}
    (d : Dec (ğŸ˜ PE.â‰¡ ğŸ˜)) â†’
    Î -Â® l A B t u v âŠ©A âŠ©B[u] ğŸ˜ d â†’
    (t U.âˆ˜âŸ¨ ğŸ˜ âŸ© u) Â®âŸ¨ l âŸ© app-ğŸ˜ str v âˆ·
      U.wk (lift id) B U.[ u ]â‚€ / âŠ©B[u]
  Î -Â®-ğŸ˜ (no ğŸ˜â‰¢ğŸ˜) = Î» _ â†’ âŠ¥-elim (ğŸ˜â‰¢ğŸ˜ PE.refl)
  Î -Â®-ğŸ˜ (yes _)  = idá¶ 

opaque

  -- A "reduction" rule for Î -Â®.

  Î -Â®-Ï‰ :
    {âŠ©A : Î” âŠ©âŸ¨ l âŸ© U.wk id A}
    {âŠ©B[u] : Î” âŠ©âŸ¨ l âŸ© U.wk (lift id) B U.[ u ]â‚€} â†’
    p PE.â‰¢ ğŸ˜ â†’
    (d : Dec (p PE.â‰¡ ğŸ˜)) â†’
    Î -Â® l A B t u v âŠ©A âŠ©B[u] p d â†’
    u Â®âŸ¨ l âŸ© w âˆ· U.wk id A / âŠ©A â†’
    (t U.âˆ˜âŸ¨ p âŸ© u) Â®âŸ¨ l âŸ© v T.âˆ˜âŸ¨ str âŸ© w âˆ·
      U.wk (lift id) B U.[ u ]â‚€ / âŠ©B[u]
  Î -Â®-Ï‰ pâ‰¢ğŸ˜ (yes pâ‰¡ğŸ˜) _   = âŠ¥-elim (pâ‰¢ğŸ˜ pâ‰¡ğŸ˜)
  Î -Â®-Ï‰ _   (no _)    hyp = hyp

-- Helper introduction and elimination lemmata for Î£-Â®

Î£-Â®-intro-ğŸ˜ : âˆ€ {l F [F] tâ‚ v vâ‚‚ p}
            â†’ v T.â‡’* vâ‚‚ â†’ p PE.â‰¡ ğŸ˜
            â†’ Î£-Â® l F [F] tâ‚ v vâ‚‚ p
Î£-Â®-intro-ğŸ˜ {p = p} vâ‡’vâ‚‚ pâ‰¡ğŸ˜ with is-ğŸ˜? p
... | yes _ = lift vâ‡’vâ‚‚
... | no pâ‰¢ğŸ˜ = âŠ¥-elim (pâ‰¢ğŸ˜ pâ‰¡ğŸ˜)

Î£-Â®-intro-Ï‰ : âˆ€ {l F [F] tâ‚ v vâ‚‚ p}
            â†’ (vâ‚ : T.Term k)
            â†’ v T.â‡’* T.prod vâ‚ vâ‚‚
            â†’ tâ‚ Â®âŸ¨ l âŸ© vâ‚ âˆ· U.wk id F / [F]
            â†’ p PE.â‰¢ ğŸ˜
            â†’ Î£-Â® l F [F] tâ‚ v vâ‚‚ p
Î£-Â®-intro-Ï‰ {p = p} vâ‚ vâ‡’vâ€² tâ‚Â®vâ‚ pâ‰¢ğŸ˜ with is-ğŸ˜? p
... | yes pâ‰¡ğŸ˜ = âŠ¥-elim (pâ‰¢ğŸ˜ pâ‰¡ğŸ˜)
... | no _ = vâ‚ , vâ‡’vâ€² , tâ‚Â®vâ‚

Î£-Â®-elim : âˆ€ {b l F [F] tâ‚ v vâ‚‚ p}
         â†’ (P : (p : M) â†’ Set b)
         â†’ Î£-Â® l F [F] tâ‚ v vâ‚‚ p
         â†’ (v T.â‡’* vâ‚‚ â†’ p PE.â‰¡ ğŸ˜ â†’ P p)
         â†’ ((vâ‚ : T.Term k) â†’ v T.â‡’* T.prod vâ‚ vâ‚‚ â†’ tâ‚ Â®âŸ¨ l âŸ© vâ‚ âˆ· U.wk id F / [F] â†’ p PE.â‰¢ ğŸ˜ â†’ P p)
         â†’ P p
Î£-Â®-elim {p = p} P extra f g with is-ğŸ˜? p
Î£-Â®-elim {p = p} P (lift vâ‡’vâ‚‚) f g | yes pâ‰¡ğŸ˜ = f vâ‡’vâ‚‚ pâ‰¡ğŸ˜
Î£-Â®-elim {p = p} P (vâ‚ , vâ‡’vâ‚,vâ‚‚ , tâ‚Â®vâ‚) f g | no pâ‰¢ğŸ˜ = g vâ‚ vâ‡’vâ‚,vâ‚‚ tâ‚Â®vâ‚ pâ‰¢ğŸ˜

opaque

  -- A "reduction" rule for Î£-Â®.

  Î£-Â®-ğŸ˜ :
    {âŠ©A : Î” âŠ©âŸ¨ l âŸ© U.wk id A} â†’
    Î£-Â® l A âŠ©A tâ‚ v vâ‚‚ ğŸ˜ â†’
    v T.â‡’* vâ‚‚
  Î£-Â®-ğŸ˜ x =
    Î£-Â®-elim (Î» _ â†’ _ T.â‡’* _) x (Î» vâ‡’ _ â†’ vâ‡’)
      (Î» _ _ _ ğŸ˜â‰¢ğŸ˜ â†’ âŠ¥-elim $ ğŸ˜â‰¢ğŸ˜ PE.refl)

opaque

  -- A "reduction" rule for Î£-Â®.

  Î£-Â®-Ï‰ :
    {âŠ©A : Î” âŠ©âŸ¨ l âŸ© U.wk id A} â†’
    p PE.â‰¢ ğŸ˜ â†’
    Î£-Â® l A âŠ©A tâ‚ v vâ‚‚ p â†’
    âˆƒ Î» vâ‚ â†’ v T.â‡’* T.prod vâ‚ vâ‚‚ Ã— tâ‚ Â®âŸ¨ l âŸ© vâ‚ âˆ· U.wk id A / âŠ©A
  Î£-Â®-Ï‰ pâ‰¢ğŸ˜ x =
    Î£-Â®-elim _ x (Î» _ pâ‰¡ğŸ˜ â†’ âŠ¥-elim $ pâ‰¢ğŸ˜ pâ‰¡ğŸ˜)
      (Î» _ vâ‡’ tâ‚Â®vâ‚ _ â†’ _ , vâ‡’ , tâ‚Â®vâ‚)
