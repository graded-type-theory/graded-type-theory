------------------------------------------------------------------------
-- A normalization procedure for evaluating states to normal form.
------------------------------------------------------------------------

open import Graded.Modality
open import Graded.Usage.Restrictions
open import Definition.Typed.Variant

module Graded.Heap.Normalization
  {a} {M : Set a} {ğ•„ : Modality M}
  (type-variant : Type-variant)
  (UR : Usage-restrictions ğ•„)
  (open Modality ğ•„)
  â¦ƒ _ : Has-nr M semiring-with-meet â¦„
  â¦ƒ _ : Has-factoring-nr M semiring-with-meet â¦„
  where

open Type-variant type-variant

open import Tools.Bool
open import Tools.Fin
open import Tools.Function
open import Tools.Nat
open import Tools.Product
open import Tools.PropositionalEquality as PE
open import Tools.Relation
open import Tools.Sum

open import Graded.Heap.Reduction type-variant UR
open import Graded.Heap.Reduction.Properties type-variant UR
open import Graded.Heap.Untyped type-variant UR
open import Graded.Heap.Untyped.Properties type-variant UR

open import Definition.Untyped M hiding (head)
open import Definition.Untyped.Names-below M

private variable
  k m n nâ€² : Nat

opaque mutual

  -- Normalization of states with variables in head position

  normalize-var :
    (H : Heap k m) â†’ No-namesÊ° H â†’ (x : Fin m) â†’
    âˆƒâ‚„ Î» n t (Ïâ€² : Wk m n) S â†’
    Normal âŸ¨ H , t , Ïâ€² , S âŸ© Ã—
    âŸ¨ H , var x , id , Îµ âŸ© â‡¢â‚‘* âŸ¨ H , t , Ïâ€² , S âŸ©
  normalize-var Îµ _ ()

  normalize-var (H âˆ™ (p , t , Ï)) (H-nn âˆ™ t-nn) y0 =
    case normalize H t Ï Îµ H-nn t-nn of Î»
      (_ , tâ€² , Ïâ€² , S , n , d) â†’
    _ , tâ€² , step Ïâ€² , wk1Ë¢ S , wk1-Normal n
      , var here â‡¨ wk1-â‡¢â‚‘* d

  normalize-var (H âˆ™ c) (nn âˆ™ _) (y +1) =
    case normalize-var H nn y of Î»
      (_ , t , Ï , S , n , d) â†’
    case wk1-Normal n of Î»
      nâ€² â†’
    case var-env-â‡¢â‚‘* (wk1-â‡¢â‚‘* d) refl nâ€² of Î» where
      (injâ‚ dâ€²) â†’
        _ , t , step Ï , wk1Ë¢ S , nâ€² , dâ€²
      (injâ‚‚ (refl , sâ‰¡sâ€²)) â†’
        case State-injectivity sâ‰¡sâ€² of Î» {
          (_ , refl , refl , _) â†’
        case nâ€² of Î» where
          (var Â¬d) â†’ _ , var (y +1) , id , Îµ , var Â¬d , id
          (val ()) }


  normalize-var (H âˆ™â—) _ y0 =
    _ , var x0 , id  , Îµ , var here , id

  normalize-var (H âˆ™â—) (nn âˆ™â—) (y +1) =
    case normalize-var H nn y of Î»
      (_ , t , Ï , S , n , d) â†’
    case wk1â—-Normal n of Î»
      nâ€² â†’
    case var-env-â‡¢â‚‘* (wk1â—-â‡¢â‚‘* d) refl nâ€² of Î» where
      (injâ‚ dâ€²) â†’
        _ , t , step Ï , wk1Ë¢ S , nâ€² , dâ€²
      (injâ‚‚ (refl , sâ‰¡sâ€²)) â†’
        case State-injectivity sâ‰¡sâ€² of Î» {
          (_ , refl , refl , _) â†’
        case nâ€² of Î» where
          (var d)  â†’ _ , var (y +1) , id , Îµ , var d , id
          (val ()) }

  -- Normalization of states

  normalize : (H : Heap k m) (t : Term n) (Ï : Wk m n) (S : Stack m)
            â†’ No-namesÊ° H â†’ No-names t
            â†’ âˆƒâ‚„ Î» nâ€² tâ€² (Ïâ€² : Wk m nâ€²) (Sâ€² : Stack m) â†’ Normal âŸ¨ H , tâ€² , Ïâ€² , Sâ€² âŸ© Ã—
              âŸ¨ H , t , Ï , S âŸ© â‡¢â‚‘* âŸ¨ H , tâ€² , Ïâ€² , Sâ€² âŸ©
  normalize _ (defn _) _ _ _ (defn ())
  normalize H (var x) Ï S nn _ =
    case normalize-var H nn (wkVar Ï x) of Î»
      (_ , t , Ïâ€² , Sâ€² , n , d) â†’
    case var-env-â‡¢â‚‘* d refl n of Î» where
      (injâ‚ dâ€²) â†’
        _ , t , Ïâ€² , Sâ€² ++ S , Normal-stack n
          , ++-â‡¢â‚‘* S dâ€²
      (injâ‚‚ (refl , sâ‰¡sâ€²)) â†’
        case State-injectivity sâ‰¡sâ€² of Î» {
          (_ , refl , refl , refl) â†’
        case n of Î» where
          (var Â¬d) â†’ _ , var x , Ï , S , var Â¬d , id
          (val ()) }
  normalize H (lam p t) Ï S _ _ =
    _ , lam p t , Ï , S , val lamáµ¥ , id
  normalize H (t âˆ˜âŸ¨ p âŸ© u) Ï S H-nn (app t-nn _) =
    case normalize H t Ï (âˆ˜â‚‘ p u Ï âˆ™ S) H-nn t-nn of Î»
      (_ , _ , _ , _ , n , d) â†’
    _ , _ , _ , _ , n , â‡’â‚‘ appâ‚• â‡¨ d
  normalize H (prod s p t u) Ï S _ _ =
    _ , prod s p t u , Ï , S , val prodáµ¥ , id
  normalize H (fst p t) Ï S H-nn (fst t-nn) =
    case normalize H t Ï (fstâ‚‘ p âˆ™ S) H-nn t-nn of Î»
      (_ , _ , _ , _ , n , d) â†’
    _ , _ , _ , _ , n , â‡’â‚‘ fstâ‚• â‡¨ d
  normalize H (snd p t) Ï S H-nn (snd t-nn) =
    case normalize H t Ï (sndâ‚‘ p âˆ™ S) H-nn t-nn of Î»
      (_ , _ , _ , _ , n , d) â†’
    _ , _ , _ , _ , n , â‡’â‚‘ sndâ‚• â‡¨ d
  normalize H (prodrec r p q A t u) Ï S H-nn (prodrec _ t-nn _) =
    case normalize H t Ï (prodrecâ‚‘ r p q A u Ï âˆ™ S) H-nn t-nn of Î»
      (_ , _ , _ , _ , n , d) â†’
    _ , _ , _ , _ , n , â‡’â‚‘ prodrecâ‚• â‡¨ d
  normalize H (star s l) Ï S _ _ =
    _ , star s l , Ï , S , val staráµ¥ , id
  normalize H (unitrec l p q A t u) Ï S H-nn (unitrec _ t-nn _) =
    case UnitÊ·-Î·? of Î» where
      (yes Î·) â†’
        _ , unitrec l p q A t u , Ï , S , val (unitrec-Î·áµ¥ Î·) , id
      (no no-Î·) â†’
        case normalize H t Ï (unitrecâ‚‘ l p q A u Ï âˆ™ S) H-nn t-nn of Î»
          (_ , _ , _ , _ , n , d) â†’
        _ , _ , _ , _ , n , â‡’â‚‘ unitrecâ‚• no-Î· â‡¨ d
  normalize H zero Ï S _ _ =
    _ , zero , Ï , S , val zeroáµ¥ , id
  normalize H (suc t) Ï S _ _ =
    _ , suc t , Ï , S , val sucáµ¥ , id
  normalize H (natrec p q r A z s n) Ï S H-nn (natrec _ _ _ n-nn) =
    case normalize H n Ï (natrecâ‚‘ p q r A z s Ï âˆ™ S) H-nn n-nn of Î»
      (_ , _ , _ , _ , n , d) â†’
    _ , _ , _ , _ , n , â‡’â‚‘ natrecâ‚• â‡¨ d
  normalize H (emptyrec p A t) Ï S H-nn (emptyrec _ t-nn) =
    case normalize H t Ï (emptyrecâ‚‘ p A Ï âˆ™ S) H-nn t-nn of Î»
      (_ , _ , _ , _ , n , d) â†’
    _ , _ , _ , _ , n , â‡’â‚‘ emptyrecâ‚• â‡¨ d
  normalize H rfl Ï S _ _ =
    _ , rfl , Ï , S , val rfláµ¥ , id
  normalize H (J p q A t B u v w) Ï S H-nn (J _ _ _ _ _ w-nn) =
    case normalize H w Ï (Jâ‚‘ p q A t B u v Ï âˆ™ S) H-nn w-nn of Î»
      (_ , _ , _ , _ , n , d) â†’
    _ , _ , _ , _ , n , â‡’â‚‘ Jâ‚• â‡¨ d
  normalize H (K p A t B u v) Ï S H-nn (K _ _ _ _ v-nn) =
    case normalize H v Ï (Kâ‚‘ p A t B u Ï âˆ™ S) H-nn v-nn of Î»
      (_ , _ , _ , _ , n , d) â†’
    _ , _ , _ , _ , n , â‡’â‚‘ Kâ‚• â‡¨ d
  normalize H ([]-cong s A t u v) Ï S H-nn ([]-cong _ _ _ v-nn) =
    case normalize H v Ï ([]-congâ‚‘ s A t u Ï âˆ™ S) H-nn v-nn of Î»
      (_ , _ , _ , _ , n , d) â†’
    _ , _ , _ , _ , n , â‡’â‚‘ []-congâ‚• â‡¨ d
  normalize H (U l) Ï S _ _ =
    _ , U l , Ï , S , val Uáµ¥ , id
  normalize H â„• Ï S _ _ =
    _ , â„• , Ï , S , val â„•áµ¥ , id
  normalize H Empty Ï S _ _ =
    _ , Empty , Ï , S , val Emptyáµ¥ , id
  normalize H (Unit s l) Ï S _ _ =
    _ , Unit s l , Ï , S , val Unitáµ¥ , id
  normalize H (Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B) Ï S _ _ =
    _ , Î Î£âŸ¨ b âŸ© p , q â–· A â–¹ B , Ï , S , val Î Î£áµ¥ , id
  normalize H (Id A t u) Ï S _ _ =
    _ , Id A t u , Ï , S , val Idáµ¥ , id

opaque

  -- A version of normalize that acts on states

  normalizeâ‚› :
    (s : State k m n) â†’ No-namesâ‚›â€² s â†’
    âˆƒâ‚‚ Î» nâ€² (sâ€² : State k m nâ€²) â†’ Normal sâ€² Ã— s â‡¢â‚‘* sâ€²
  normalizeâ‚› âŸ¨ H , t , Ï , S âŸ© (H-nn , t-nn) =
    case normalize H t Ï S H-nn t-nn of Î»
      (_ , tâ€² , Ïâ€² , Sâ€² , n , d) â†’
    _ , _ , n , d
