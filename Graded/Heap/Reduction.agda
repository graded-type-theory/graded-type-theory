------------------------------------------------------------------------
-- A resource aware heap semantics. The reduction relations.
------------------------------------------------------------------------

open import Graded.Modality
open import Graded.Usage.Restrictions
open import Definition.Typed.Variant
open import Graded.Usage.Restrictions.Natrec

module Graded.Heap.Reduction
  {a} {M : Set a}
  {ğ•„ : Modality M}
  (type-variant : Type-variant)
  (UR : Usage-restrictions ğ•„)
  (open Usage-restrictions UR)
  (factoring-nr :
    â¦ƒ has-nr : Nr-available â¦„ â†’
    Is-factoring-nr M (Natrec-mode-Has-nr ğ•„ has-nr))
  where

open import Tools.Empty
open import Tools.Fin
open import Tools.Nat
open import Tools.Product
open import Tools.Relation

open import Definition.Untyped M
open import Graded.Heap.Untyped type-variant UR factoring-nr

open Type-variant type-variant
open Modality ğ•„

private variable
  m mâ€² n nâ€² k â„“ : Nat
  H Hâ€² : Heap _ _
  Ï Ïâ€² : Wk _ _
  t tâ€² u v w z s A B tâ‚ tâ‚‚ : Term _
  x : Fin _
  S Sâ€² : Stack _
  p pâ€² q qâ€² r : M
  str : Strength
  l : Universe-level
  sâ‚ sâ‚‚ sâ‚ƒ : State _ _ _

-- The abstract machine has three different semantics.
-- 1. A call-by-name semantics with resource tracking that reduces terms
--    to WHNF. Denoted by _â‡¾_.
-- 2. A call-by-name semantics without resource tracking that reduces
--    terms to WHNF. Denoted by _â‡¢_.
-- 3. A call-by-name semantics with resource tracking that fully
--    evaluates natural numbers to numerals (but that otherwise reduces
--    terms to WHNF). Denoted by _â† _.
--
-- These are defined using a few additional reduction relations.

------------------------------------------------------------------------
-- The preliminary reduction relations.

-- The relation _â‡’â‚‘_ describes evaluation of states with an eliminator
-- in head position.

infix 28 _â‡’â‚‘_

data _â‡’â‚‘_ {k m n} : State k m n â†’ State k m n â†’ Set a where
  appâ‚• : âŸ¨ H , t âˆ˜âŸ¨ p âŸ© u , Ï , S âŸ© â‡’â‚‘ âŸ¨ H , t , Ï , âˆ˜â‚‘ p u Ï âˆ™ S âŸ©
  fstâ‚• : âŸ¨ H , fst p t , Ï , S âŸ© â‡’â‚‘ âŸ¨ H , t , Ï , fstâ‚‘ p âˆ™ S âŸ©
  sndâ‚• : âŸ¨ H , snd p t , Ï , S âŸ© â‡’â‚‘ âŸ¨ H , t , Ï , sndâ‚‘ p âˆ™ S âŸ©
  prodrecâ‚• : âŸ¨ H , prodrec r p q A t u , Ï , S âŸ© â‡’â‚‘
             âŸ¨ H , t , Ï , prodrecâ‚‘ r p q A u Ï âˆ™ S âŸ©
  natrecâ‚• : âŸ¨ H , natrec p q r A z s t , Ï , S âŸ© â‡’â‚‘
            âŸ¨ H , t , Ï , natrecâ‚‘ p q r A z s Ï âˆ™ S âŸ©
  unitrecâ‚• : Â¬ UnitÊ·-Î· â†’
             âŸ¨ H , unitrec l p q A t u , Ï , S âŸ© â‡’â‚‘
             âŸ¨ H , t , Ï , unitrecâ‚‘ l p q A u Ï âˆ™ S âŸ©
  emptyrecâ‚• : âŸ¨ H , emptyrec p A t , Ï , S âŸ© â‡’â‚‘
              âŸ¨ H , t , Ï , emptyrecâ‚‘ p A Ï âˆ™ S âŸ©
  Jâ‚• : âŸ¨ H , J p q A t B u v w , Ï , S âŸ© â‡’â‚‘
       âŸ¨ H , w , Ï , Jâ‚‘ p q A t B u v Ï âˆ™ S âŸ©
  Kâ‚• : âŸ¨ H , K p A t B u v , Ï , S âŸ© â‡’â‚‘
       âŸ¨ H , v , Ï , Kâ‚‘ p A t B u Ï âˆ™ S âŸ©
  []-congâ‚• : âŸ¨ H , []-cong str A t u v , Ï , S âŸ© â‡’â‚‘
             âŸ¨ H , v , Ï , []-congâ‚‘ str A t u Ï âˆ™ S âŸ©

-- The relation _â‡¾â‚‘_ describes evaluation of states with an eliminator
-- or a variable in head position with resource tracking.

infix 28 _â‡¾â‚‘_
infix 30 â‡’â‚‘_

data _â‡¾â‚‘_ {k m n} : State k m n â†’ State k m nâ€² â†’ Set a where
  var : âˆ£ S âˆ£â‰¡ p â†’
        H âŠ¢ wkVar Ï x â†¦[ p ] (t , Ïâ€²) â¨¾ Hâ€² â†’
        âŸ¨ H , var x , Ï , S âŸ© â‡¾â‚‘ âŸ¨ Hâ€² , t , Ïâ€² , S âŸ©
  â‡’â‚‘_ : sâ‚ â‡’â‚‘ sâ‚‚ â†’ sâ‚ â‡¾â‚‘ sâ‚‚

-- Reflexive, transistive closure of _â‡¾â‚‘_.

infix 28 _â‡¾â‚‘*_
infixr 29 _â‡¨_

data _â‡¾â‚‘*_ (s : State k m n) : (sâ€² : State k m nâ€²) â†’ Set a where
  id : s â‡¾â‚‘* s
  _â‡¨_ : âˆ€ {nâ€³} {sâ€² : State k m nâ€²} {sâ€³ : State k m nâ€³}
      â†’ s â‡¾â‚‘ sâ€² â†’ sâ€² â‡¾â‚‘* sâ€³ â†’ s â‡¾â‚‘* sâ€³

-- The relation _â‡¢â‚‘_ describes evaluation of states with an eliminator
-- or a variable in head position without resource tracking.

infix 28 _â‡¢â‚‘_

data _â‡¢â‚‘_ {k m n} : State k m n â†’ State k m nâ€² â†’ Set a where
  var : H âŠ¢ wkVar Ï x â†¦ (t , Ïâ€²) â†’
        âŸ¨ H , var x , Ï , S âŸ© â‡¢â‚‘ âŸ¨ H , t , Ïâ€² , S âŸ©
  â‡’â‚‘_ : sâ‚ â‡’â‚‘ sâ‚‚ â†’ sâ‚ â‡¢â‚‘ sâ‚‚

-- Reflexive, transistive closure of _â‡¢â‚‘*_

infix 28 _â‡¢â‚‘*_

data _â‡¢â‚‘*_ (s : State k m n) : (sâ€² : State k m nâ€²) â†’ Set a where
  id : s â‡¢â‚‘* s
  _â‡¨_ : âˆ€ {nâ€³} {sâ€² : State k m nâ€²} {sâ€³ : State k m nâ€³}
      â†’ s â‡¢â‚‘ sâ€² â†’ sâ€² â‡¢â‚‘* sâ€³ â†’ s â‡¢â‚‘* sâ€³

-- The relation _â‡’áµ¥_ evaluates states with values in head position and a
-- matching continuation on the top of the stack.

infix 28 _â‡’áµ¥_

data _â‡’áµ¥_ {k m n} : State k m n â†’ State k mâ€² nâ€² â†’ Set a where
  lamâ‚• : âˆ£ S âˆ£â‰¡ q
       â†’ âŸ¨ H , lam p t , Ï , âˆ˜â‚‘ p u Ïâ€² âˆ™ S âŸ© â‡’áµ¥
         âŸ¨ H âˆ™ (q Â· p , u , Ïâ€²) , t , lift Ï , wk1Ë¢ S âŸ©
  prodË¢â‚•â‚ : âŸ¨ H , prodË¢ p tâ‚ tâ‚‚ , Ï , fstâ‚‘ p âˆ™ S âŸ© â‡’áµ¥
            âŸ¨ H , tâ‚           , Ï , S          âŸ©
  prodË¢â‚•â‚‚ : âŸ¨ H , prodË¢ p tâ‚ tâ‚‚ , Ï , sndâ‚‘ p âˆ™ S âŸ©
          â‡’áµ¥ âŸ¨ H , tâ‚‚           , Ï , S          âŸ©
  prodÊ·â‚• : âˆ£ S âˆ£â‰¡ qâ€²
         â†’ âŸ¨ H , prodÊ· p tâ‚ tâ‚‚ , Ï , prodrecâ‚‘ r p q A u Ïâ€² âˆ™ S âŸ© â‡’áµ¥
           âŸ¨ H âˆ™ (qâ€² Â· r Â· p , tâ‚ , Ï) âˆ™ (qâ€² Â· r , tâ‚‚ , step Ï)
              , u             , liftn Ïâ€² 2 , wk2Ë¢ S âŸ©
  zeroâ‚• : âŸ¨ H , zero , Ï  , natrecâ‚‘ p q r A z s Ïâ€² âˆ™ S âŸ© â‡’áµ¥
          âŸ¨ H , z    , Ïâ€² , S                          âŸ©

  sucâ‚• : -- pâ€² is the multiplicity of the natrec continuation
         -- on top of the stack and qâ€² is the multiplicity of
         -- the rest of the stack.
         âˆ£ S âˆ£â‰¡ qâ€² â†’ âˆ£natrec p , r âˆ£â‰¡ pâ€² â†’
         âŸ¨ H , suc t , Ï , natrecâ‚‘ p q r A z s Ïâ€² âˆ™ S âŸ© â‡’áµ¥
         âŸ¨ H âˆ™ (qâ€² Â· pâ€² , t , Ï)
             âˆ™ (qâ€² Â· r , natrec p q r (wk (lift (step id)) A) (wk1 z)
                              (wk (liftn (step id) 2) s) (var x0)
                          , lift Ïâ€²)
             , s , liftn Ïâ€² 2 , wk2Ë¢ S âŸ©
  starÊ·â‚• :  âŸ¨ H , starÊ· l , Ï  , unitrecâ‚‘ l p q A u Ïâ€² âˆ™ S âŸ© â‡’áµ¥
            âŸ¨ H , u       , Ïâ€² ,                         S âŸ©
  unitrec-Î·â‚• : UnitÊ·-Î· â†’
               âŸ¨ H , unitrec l p q A t u , Ï , S âŸ© â‡’áµ¥
               âŸ¨ H ,                   u , Ï , S âŸ©
  rflâ‚•â±¼ : âŸ¨ H , rfl , Ï  , Jâ‚‘ p q A t B u v Ïâ€² âˆ™ S âŸ© â‡’áµ¥
          âŸ¨ H , u   , Ïâ€² , S âŸ©
  rflâ‚•â‚– : âŸ¨ H , rfl , Ï ,  Kâ‚‘ p A t B u Ïâ€² âˆ™ S âŸ© â‡’áµ¥ âŸ¨ H , u , Ïâ€² , S âŸ©
  rflâ‚•â‚‘ : âŸ¨ H , rfl , Ï  , []-congâ‚‘ str A t u Ïâ€² âˆ™ S âŸ© â‡’áµ¥
          âŸ¨ H , rfl , Ïâ€² , S âŸ©

-- The relation _â‡’â‚™_ allows evaluation under the successor constructor
-- in order to fully evaluate terms to numerals.

infix 28 _â‡’â‚™_

data _â‡’â‚™_ {k m n} : State k m n â†’ State k m n â†’ Set a where
  sucâ‚• : Â¬ Numeral t â†’
         âŸ¨ H , suc t , Ï , sucâ‚› â„“ âŸ© â‡’â‚™ âŸ¨ H , t , Ï , sucâ‚‘ âˆ™ sucâ‚› â„“ âŸ©
  numâ‚• : Numeral t â†’
         âŸ¨ H , t , Ï , sucâ‚‘ âˆ™ S âŸ© â‡’â‚™ âŸ¨ H , suc t , Ï , S âŸ©

------------------------------------------------------------------------
-- The main reduction relations.

infix 30 â‡¾â‚‘_
infix 30 â‡’áµ¥_
infix 30 â‡’â‚™_
infix 30 â‡¢â‚‘_

-- Evaluation to WHNF with resource tracking.

data _â‡¾_ (sâ‚ : State k m n) : State k mâ€² nâ€² â†’ Set a where
  â‡¾â‚‘_ : sâ‚ â‡¾â‚‘ sâ‚‚ â†’ sâ‚ â‡¾ sâ‚‚
  â‡’áµ¥_ : sâ‚ â‡’áµ¥ sâ‚‚ â†’ sâ‚ â‡¾ sâ‚‚

-- Evaluation of natural numbers to numerals with resource tracking.

data _â† _ (sâ‚ : State k m n) : State k mâ€² nâ€² â†’ Set a where
  â‡¾â‚‘_ : sâ‚ â‡¾â‚‘ sâ‚‚ â†’ sâ‚ â†  sâ‚‚
  â‡’áµ¥_ : sâ‚ â‡’áµ¥ sâ‚‚ â†’ sâ‚ â†  sâ‚‚
  â‡’â‚™_ : sâ‚ â‡’â‚™ sâ‚‚ â†’ sâ‚ â†  sâ‚‚

pattern â‡¾â‚‘â€² d = â‡¾â‚‘ (â‡’â‚‘ d)

-- Evaluation to WHNF without resource tracking.

data _â‡¢_ (sâ‚ : State k m n) : State k mâ€² nâ€² â†’ Set a where
  â‡¢â‚‘_ : sâ‚ â‡¢â‚‘ sâ‚‚ â†’ sâ‚ â‡¢ sâ‚‚
  â‡’áµ¥_ : sâ‚ â‡’áµ¥ sâ‚‚ â†’ sâ‚ â‡¢ sâ‚‚

infix 28 _â‡¾*_
infix 28 _â† *_
infix 28 _â‡¢*_

-- Reflexive, transitive closure of _â‡¾_.

data _â‡¾*_ (sâ‚ : State k m n) : State k mâ€² nâ€² â†’ Set a where
  id  : sâ‚ â‡¾* sâ‚
  _â‡¨_ : sâ‚ â‡¾ sâ‚‚ â†’ sâ‚‚ â‡¾* sâ‚ƒ â†’ sâ‚ â‡¾* sâ‚ƒ

-- Reflexive, transitive closure of _â† _.

data _â† *_ (sâ‚ : State k m n) : State k mâ€² nâ€² â†’ Set a where
  id  : sâ‚ â† * sâ‚
  _â‡¨_ : sâ‚ â†  sâ‚‚ â†’ sâ‚‚ â† * sâ‚ƒ â†’ sâ‚ â† * sâ‚ƒ

-- Reflexive, transitive closure of _â‡¢_.

data _â‡¢*_ (sâ‚ : State k m n) : State k mâ€² nâ€² â†’ Set a where
  id  : sâ‚ â‡¢* sâ‚
  _â‡¨_ : sâ‚ â‡¢ sâ‚‚ â†’ sâ‚‚ â‡¢* sâ‚ƒ â†’ sâ‚ â‡¢* sâ‚ƒ

Final : State k m n â†’ Set a
Final s = âˆ€ {m n} {sâ€² : State _ m n} â†’ s â‡¾ sâ€² â†’ âŠ¥

_â‡˜_ : State k m n â†’ State k mâ€² nâ€² â†’ Set a
s â‡˜ sâ€² = s â‡¾* sâ€² Ã— Final sâ€²
