------------------------------------------------------------------------
-- Termination properties of the reduction relation
------------------------------------------------------------------------

open import Graded.Modality
open import Graded.Usage.Restrictions
open import Definition.Typed.Restrictions
open import Tools.Bool
import Graded.Heap.Bisimilarity

module Graded.Heap.Termination
  {a} {M : Set a} {ğ•„ : Modality M}
  (UR : Usage-restrictions ğ•„)
  (TR : Type-restrictions ğ•„)
  (erased-heap : Bool)
  (open Graded.Heap.Bisimilarity UR TR erased-heap)
  (As : Assumptions)
  where

open Type-restrictions TR
open Usage-restrictions UR
open Assumptions As
open Modality ğ•„

open import Tools.Empty
open import Tools.Function
open import Tools.Nat
open import Tools.Product
open import Tools.PropositionalEquality as PE hiding (sym)
open import Tools.Sum hiding (sym; id)

open import Definition.Untyped M
open import Definition.Untyped.Neutral M type-variant
open import Definition.Untyped.Properties M
open import Definition.Untyped.Properties.Neutral M type-variant
open import Definition.Typed TR
open import Definition.Typed.Consequences.Canonicity TR
open import Definition.Typed.Consequences.Reduction TR
open import Definition.Typed.Properties TR

open import Graded.Context ğ•„ hiding (_âŸ¨_âŸ©)
open import Graded.Usage ğ•„ UR
open import Graded.Mode ğ•„

open import Graded.Heap.Normalization type-variant UR
open import Graded.Heap.Options
open import Graded.Heap.Untyped type-variant UR
open import Graded.Heap.Untyped.Properties type-variant UR
open import Graded.Heap.Typed UR TR false
open import Graded.Heap.Typed.Properties UR TR false
import Graded.Heap.Typed.Reduction UR TR (tracking-and-â„•-fullred-if false) as RTâ‚œ
import Graded.Heap.Typed.Reduction UR TR (not-tracking-and-â„•-fullred-if false) as RTâ‚™â‚œ
open import Graded.Heap.Usage type-variant UR erased-heap
open import Graded.Heap.Usage.Properties type-variant UR erased-heap
open import Graded.Heap.Usage.Reduction type-variant UR erased-heap (tracking-and-â„•-fullred-if false) UnitÊ·-Î·â†’
open import Graded.Heap.Reduction type-variant UR (tracking-and-â„•-fullred-if false)
import Graded.Heap.Reduction.Properties type-variant UR (tracking-and-â„•-fullred-if false) as RPâ‚œ
import Graded.Heap.Reduction.Properties type-variant UR (not-tracking-and-â„•-fullred-if false) as RPâ‚™â‚œ

private variable
  t u A B : Term _
  Î³ Î´ Î· : Conâ‚˜ _
  H : Heap _ _
  Ï : Wk _ _
  S : Stack _
  e : Elim _
  Î“ Î” : Con Term _
  s : State _ _ _
  m : Mode
  k : Nat

opaque

  -- Well-typed and well-resourced terms evaluate to values with empty stacks
  -- corresponding to terms in Whnf.

  whBisim : (Emptyrec-allowed ğŸ™áµ ğŸ˜ â†’ Consistent Î”)
          â†’ Î” âŠ¢ â¦… s â¦† â†˜ u âˆ· A
          â†’ Î” â¨¾ Î“ âŠ¢ s âˆ· B
          â†’ Î³ â¨¾ Î´ â¨¾ Î· â–¸[ m ] s
          â†’ âˆƒâ‚‚ Î» m n â†’ âˆƒâ‚ƒ Î» H t (Ï : Wk m n)
          â†’ s â‡’* âŸ¨ H , t , Ï , Îµ âŸ© Ã— wk Ï t [ H ]â‚• â‰¡ u Ã— Value t
  whBisim {s = âŸ¨ H , t , Ï , S âŸ©} consistent (d , w) âŠ¢s â–¸s =
    case bisimâ‚†* As d âŠ¢s â–¸s of Î» {
      (_ , _ , âŸ¨ H , tâ€² , Ï , S âŸ© , dâ‚ , refl) â†’
    case normalize H tâ€² Ï S of Î»
      (_ , tâ€³ , Ïâ€² , Sâ€² , n , dâ‚™) â†’
    case RPâ‚™â‚œ.â‡’â‚™*-â¦…â¦†-â‰¡ dâ‚™ of Î» {
      tâ€²â‰¡tâ€³ â†’
    case â–¸-â‡’* â–¸s dâ‚ of Î»
      (_ , _ , _ , _ , â–¸sâ€²) â†’
    case RTâ‚œ.âŠ¢â‚›-â‡’* âŠ¢s dâ‚ of Î»
      (_ , _ , _ , âŠ¢sâ€²) â†’
    case bisimâ‚‚* false As (RPâ‚™â‚œ.â‡’â‚™* dâ‚™) ~Ê°-refl â–¸sâ€² of Î»
      (Hâ€² , dâ‚œ , H~Hâ€²) â†’
    case RTâ‚™â‚œ.âŠ¢â‚›-â‡’* âŠ¢sâ€² (RPâ‚™â‚œ.â‡’â‚™* dâ‚™) of Î»
      (_ , _ , _ , âŠ¢sâ€³@(B , _ , âŠ¢tâ€³ , âŠ¢Sâ€²)) â†’
    case n of Î» where
      (val v) â†’
        case lemma {H = H} {S = Sâ€²} w v âŠ¢sâ€³ (RPâ‚™â‚œ.â‡’â‚™*-â¦…â¦†-â‰¡ dâ‚™) of Î» {
          refl â†’
        _ , _ , _ , tâ€³ , Ïâ€² , dâ‚ RPâ‚œ.â‡¨* dâ‚œ
          , PE.sym (PE.trans tâ€²â‰¡tâ€³ (cong (wk Ïâ€² tâ€³ [_]) (~Ê°-subst H~Hâ€²))) , v}
      (var d) â†’
        case ~Ê°-lookupâ— H~Hâ€² d of Î»
          dâ€² â†’
        case â–¸-â‡’* â–¸sâ€² dâ‚œ of Î»
              (_ , _ , _ , _ , â–¸sâ€³@(â–¸H , _ , â–¸S , _)) â†’
        case erased-assumption of Î» where
          (injâ‚ Â¬eh) â†’ âŠ¥-elim (Â¬erased-heapâ†’Â¬â†¦â— â¦ƒ neh = Â¬eh â¦„ â–¸H dâ€²)
          (injâ‚‚ nem) â†’
            case â–¸sâ— subtraction-ok dâ€² â–¸sâ€³ of Î»
              (âˆ£Sâˆ£â‰¡ğŸ˜ , _) â†’
            case â–¸âˆ£Sâˆ£â‰¢ğŸ˜ nem â–¸S of Î» where
              (injâ‚ âˆ£Sâˆ£â‰¢ğŸ˜) â†’
                âŠ¥-elim (âˆ£Sâˆ£â‰¢ğŸ˜ âˆ£Sâˆ£â‰¡ğŸ˜)
              (injâ‚‚ (erâˆˆS , ok)) â†’
                âŠ¥-elim (âŠ¢emptyrecâ‚€âˆ‰S {Ï = Ïâ€²} (consistent ok) âŠ¢sâ€³ erâˆˆS) }}
    where
    lemma : âˆ€ {n} {t : Term n} {H Ï S}
          â†’ Whnf u â†’ Value t â†’ Î” â¨¾ Î“ âŠ¢ âŸ¨ H , t , Ï , S âŸ© âˆ· A
          â†’ u PE.â‰¡ â¦… âŸ¨ H , t , Ï , S âŸ© â¦† â†’ S PE.â‰¡ Îµ
    lemma {S = Îµ} w n _ uâ‰¡ = refl
    lemma {t} {H} {Ï} {S = e âˆ™ S} w v (_ , _ , _ , âŠ¢S) uâ‰¡ =
      case whnf-subst {t = â¦… e âˆ™ S â¦†Ë¢ (wk Ï t)} (subst Whnf uâ‰¡ w) of Î»
        wâ€² â†’
      case subst Neutral (wkâ‰¡subst Ï t) (âŠ¢whnfâ¦…â¦†Ë¢â€² âŠ¢S wâ€²) of Î»
        nâ€² â†’
      âŠ¥-elim (Valueâ†’Â¬Neutral v (neutral-subst nâ€²))

opaque

  -- A variant of the above, starting with the initial state

  whBisim-initial : {Î” : Con Term k}
                  â†’ k â‰¡ 0 âŠ ((Emptyrec-allowed ğŸ™áµ ğŸ˜ â†’ Consistent Î”) Ã— T erased-heap)
                  â†’ Î” âŠ¢ t â†˜ u âˆ· A â†’ ğŸ˜á¶œ â–¸ t
                  â†’ âˆƒâ‚‚ Î» m n â†’ âˆƒâ‚ƒ Î» H uâ€² (Ï : Wk m n)
                  â†’ initial t â‡’* âŸ¨ H , uâ€² , Ï , Îµ âŸ© Ã— wk Ï uâ€² [ H ]â‚• â‰¡ u Ã— Value uâ€²
  whBisim-initial {k} {Î”} as d â–¸t =
    whBisim consistent
      (subst (_ âŠ¢_â†˜ _ âˆ· _)
        (PE.sym (PE.trans (erasedHeap-subst (wk id _)) (wk-id _))) d)
      (âŠ¢initial (redFirst*Term (projâ‚ d)))
      (â–¸initial kâ‰¡0âŠerased-heap â–¸t)
    where
    consistent : Emptyrec-allowed ğŸ™áµ ğŸ˜ â†’ Consistent Î”
    consistent ok =
      case as of Î» where
        (injâ‚‚ (c , _)) â†’ c ok
        (injâ‚ refl) â†’
          case singleton Î” of Î» where
            (Îµ , refl) â†’ Î» _ â†’ Â¬Empty
    kâ‰¡0âŠerased-heap : k â‰¡ 0 âŠ T erased-heap
    kâ‰¡0âŠerased-heap =
      case as of Î» where
        (injâ‚ x) â†’ injâ‚ x
        (injâ‚‚ (_ , x)) â†’ injâ‚‚ x

opaque

  -- Well-typed and well-resourced terms evaluate to values with empty stacks
  -- corresponding to terms in Whnf.

  whRed : {Î” : Con Term k}
        â†’ (k â‰¡ 0 âŠ (Emptyrec-allowed ğŸ™áµ ğŸ˜ â†’ Consistent Î”) Ã— T erased-heap)
        â†’ Î” âŠ¢ t âˆ· A â†’ ğŸ˜á¶œ â–¸ t
        â†’ âˆƒâ‚‚ Î» m n â†’ âˆƒâ‚ƒ Î» H u (Ï : Wk m n)
          â†’ initial t â‡’* âŸ¨ H , u , Ï , Îµ âŸ© Ã— Value u Ã— Whnf â¦… âŸ¨ H , u , Ï , Îµ âŸ© â¦†
  whRed as âŠ¢t â–¸t =
    case whNormTerm âŠ¢t of Î»
      (u , w , d) â†’
    case whBisim-initial as (redâ‚œ d , w) â–¸t of Î» {
      (_ , _ , _ , _ , _ , dâ€² , refl , v) â†’
    _ , _ , _ , _ , _ , dâ€² , v , w }
