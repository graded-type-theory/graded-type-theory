------------------------------------------------------------------------
-- Properties of the heap reduction relation.
------------------------------------------------------------------------

open import Graded.Modality
open import Graded.Usage.Restrictions
open import Definition.Typed.Variant
open import Tools.Relation
open import Graded.Usage.Restrictions.Natrec

module Graded.Heap.Reduction.Properties
  {a} {M : Set a} {ğ•„ : Modality M}
  (type-variant : Type-variant)
  (UR : Usage-restrictions ğ•„)
  (open Usage-restrictions UR)
  (factoring-nr :
    â¦ƒ has-nr : Nr-available â¦„ â†’
    Is-factoring-nr M (Natrec-mode-Has-nr ğ•„ has-nr))
  where

open Type-variant type-variant
open Modality ğ•„

open import Tools.Empty
open import Tools.Fin
open import Tools.Function
open import Tools.Nat using (Nat; 1+; 2+; Nat-set)
open import Tools.PropositionalEquality
open import Tools.Product
open import Tools.Sum

open import Definition.Untyped M

open import Graded.Heap.Untyped type-variant UR factoring-nr
open import Graded.Heap.Untyped.Properties type-variant UR factoring-nr
open import Graded.Heap.Reduction type-variant UR factoring-nr
open import Graded.Heap.Reduction.Inversion type-variant UR factoring-nr
open import Graded.Modality.Properties.Subtraction semiring-with-meet

private variable
  m n mâ€² nâ€² mâ€³ nâ€³ k : Nat
  t tâ€² u A : Term _
  H Hâ€² Hâ€³ Hâ€´ : Heap _ _
  Ï Ïâ€² Ïâ€³ : Wk _ _
  e : Elim _
  S Sâ€² Sâ€³ : Stack _
  p pâ€² q r râ€² : M
  s sâ€² sâ€³ : State _ _ _
  c : Entryâ‚˜ _ _
  x xâ€² : Fin _

opaque
  infixr 28 _â‡¨*_
  -- Concatenation of reduction

  _â‡¨*_ : s â‡¾* sâ€² â†’ sâ€² â‡¾* sâ€³ â†’ s â‡¾* sâ€³
  id â‡¨* dâ€² = dâ€²
  (x â‡¨ d) â‡¨* dâ€² = x â‡¨ (d â‡¨* dâ€²)

opaque

  -- Concatenation of reduction

  â† *-concat : s â† * sâ€² â†’ sâ€² â† * sâ€³ â†’ s â† * sâ€³
  â† *-concat id dâ€² = dâ€²
  â† *-concat (x â‡¨ d) dâ€² = x â‡¨ (â† *-concat d dâ€²)

opaque
  infix 30 â‡¾â‚‘*_

  -- Lifting normalising reduction to main reduction

  â‡¾â‚‘*_ : s â‡¾â‚‘* sâ€² â†’ s â‡¾* sâ€²
  â‡¾â‚‘* id = id
  â‡¾â‚‘* (x â‡¨ d) = (â‡¾â‚‘ x) â‡¨ (â‡¾â‚‘* d)

------------------------------------------------------------------------
-- The heap semantics are deterministic

opaque

  -- The reduction relation for eliminators is deterministic

  â‡’â‚‘-det : s â‡’â‚‘ sâ€² â†’ s â‡’â‚‘ sâ€³ â†’ sâ€² â‰¡ sâ€³
  â‡’â‚‘-det d lowerâ‚• = â‡’â‚‘-inv-lower d
  â‡’â‚‘-det d appâ‚• = â‡’â‚‘-inv-âˆ˜ d
  â‡’â‚‘-det d fstâ‚• = â‡’â‚‘-inv-fst d
  â‡’â‚‘-det d sndâ‚• = â‡’â‚‘-inv-snd d
  â‡’â‚‘-det d prodrecâ‚• = â‡’â‚‘-inv-prodrec d
  â‡’â‚‘-det d natrecâ‚• = â‡’â‚‘-inv-natrec d
  â‡’â‚‘-det d (unitrecâ‚• x) = â‡’â‚‘-inv-unitrec d .projâ‚
  â‡’â‚‘-det d emptyrecâ‚• = â‡’â‚‘-inv-emptyrec d
  â‡’â‚‘-det d Jâ‚• = â‡’â‚‘-inv-J d
  â‡’â‚‘-det d Kâ‚• = â‡’â‚‘-inv-K d
  â‡’â‚‘-det d []-congâ‚• = â‡’â‚‘-inv-[]-cong d

opaque

  -- The normalising reduction relation is deterministic

  â‡¾â‚‘-det :
    {sâ€² : State k m n} {sâ€³ : State k m nâ€²} â†’
    s â‡¾â‚‘ sâ€² â†’ s â‡¾â‚‘ sâ€³ â†’
    Î£ (n â‰¡ nâ€²) Î» nâ‰¡nâ€² â†’ subst (State k m) nâ‰¡nâ€² sâ€² â‰¡ sâ€³
  â‡¾â‚‘-det {sâ€² = record{}} d (var âˆ£Sâˆ£â‰¡ xâ€²) =
    case â‡¾â‚‘-inv-var d of Î» {
      (_ , âˆ£Sâˆ£â‰¡â€² , refl , x) â†’
    case âˆ£âˆ£-functional âˆ£Sâˆ£â‰¡ âˆ£Sâˆ£â‰¡â€² of Î» {
      refl â†’
    case lookup-det x xâ€² of Î» {
      (refl , refl , refl , refl) â†’
    refl , refl }}}
  â‡¾â‚‘-det (â‡’â‚‘ d) (â‡’â‚‘ dâ€²) =
    refl , â‡’â‚‘-det d dâ€²
  â‡¾â‚‘-det (var _ _) (â‡’â‚‘ ())

opaque

  -- The non-tracking reduction relation is deterministic

  â‡¢â‚‘-det :
    {sâ€² : State k m n} {sâ€³ : State k m nâ€²} â†’
    s â‡¢â‚‘ sâ€² â†’ s â‡¢â‚‘ sâ€³ â†’
    Î£ (n â‰¡ nâ€²) Î» nâ‰¡nâ€² â†’ subst (State k m) nâ‰¡nâ€² sâ€² â‰¡ sâ€³
  â‡¢â‚‘-det {sâ€² = record{}} d (var xâ€²) =
    case â‡¢â‚‘-inv-var d of Î» {
      (refl , refl , x) â†’
    case lookup-detâ€² x xâ€² of Î» {
      (refl , refl , refl) â†’
    refl , refl }}
  â‡¢â‚‘-det (â‡’â‚‘ d) (â‡’â‚‘ dâ€²) = refl , â‡’â‚‘-det d dâ€²
  â‡¢â‚‘-det (var _) (â‡’â‚‘ ())

opaque

  -- The reduction relation for values is deterministic

  â‡’áµ¥-det : {sâ€² : State k m n} {sâ€³ : State k mâ€² nâ€²}
         â†’ (d : s â‡’áµ¥ sâ€²) (dâ€² : s â‡’áµ¥ sâ€³)
         â†’ Î£ (m â‰¡ mâ€²) Î» mâ‰¡mâ€² â†’ Î£ (n â‰¡ nâ€²) Î» nâ‰¡nâ€² â†’
            substâ‚‚ (State k) mâ‰¡mâ€² nâ‰¡nâ€² sâ€² â‰¡ sâ€³
  â‡’áµ¥-det d liftâ‚• = â‡’áµ¥-inv-lift-lowerâ‚‘ d
  â‡’áµ¥-det d (lamâ‚• âˆ£Sâˆ£â‰¡) =
    let _ , âˆ£Sâˆ£â‰¡â€² , rest = â‡’áµ¥-inv-lam-âˆ˜â‚‘ d
    in  case âˆ£âˆ£-functional âˆ£Sâˆ£â‰¡ âˆ£Sâˆ£â‰¡â€² of Î» where
          refl â†’ rest
  â‡’áµ¥-det d prodË¢â‚•â‚ = â‡’áµ¥-inv-prodË¢-fstâ‚‘ d
  â‡’áµ¥-det d prodË¢â‚•â‚‚ = â‡’áµ¥-inv-prodË¢-sndâ‚‘ d
  â‡’áµ¥-det d (prodÊ·â‚• âˆ£Sâˆ£â‰¡) =
    let _ , âˆ£Sâˆ£â‰¡â€² , rest = â‡’áµ¥-inv-prodÊ·-prodrecâ‚‘ d
    in  case âˆ£âˆ£-functional âˆ£Sâˆ£â‰¡ âˆ£Sâˆ£â‰¡â€² of Î» where
          refl â†’ rest
  â‡’áµ¥-det d zeroâ‚• = â‡’áµ¥-inv-zero-natrecâ‚‘ d
  â‡’áµ¥-det d (sucâ‚• âˆ£Sâˆ£â‰¡ âˆ£nrâˆ£â‰¡) =
    let _ , _ , âˆ£Sâˆ£â‰¡â€² , âˆ£nrâˆ£â‰¡â€² , rest = â‡’áµ¥-inv-suc-natrecâ‚‘ d
    in  case âˆ£âˆ£-functional âˆ£Sâˆ£â‰¡ âˆ£Sâˆ£â‰¡â€² of Î» {
          refl â†’
        case âˆ£natrecâˆ£áµ‰-functional âˆ£nrâˆ£â‰¡ âˆ£nrâˆ£â‰¡â€² of Î» {
          refl â†’
        rest }}
  â‡’áµ¥-det d starÊ·â‚• = â‡’áµ¥-inv-starÊ·-unitrecâ‚‘ d
  â‡’áµ¥-det d (unitrec-Î·â‚• x) = â‡’áµ¥-inv-unitrec-Î· d .projâ‚‚
  â‡’áµ¥-det d rflâ‚•â±¼ = â‡’áµ¥-inv-rfl-Jâ‚‘ d
  â‡’áµ¥-det d rflâ‚•â‚– = â‡’áµ¥-inv-rfl-Kâ‚‘ d
  â‡’áµ¥-det d rflâ‚•â‚‘ = â‡’áµ¥-inv-rfl-[]-congâ‚‘ d

opaque

  -- The reduction relation for reducing to numerals is deterministic

  â‡’â‚™-det : {sâ€² : State k m n} {sâ€³ : State k m n}
         â†’ (d : s â‡’â‚™ sâ€²) (dâ€² : s â‡’â‚™ sâ€³)
         â†’ sâ€² â‰¡ sâ€³
  â‡’â‚™-det d (sucâ‚• x) = â‡’â‚™-inv-suc x d .projâ‚‚ .projâ‚‚
  â‡’â‚™-det d (numâ‚• x) =
    case â‡’â‚™-inv-num x d of Î» {
      (S , refl , refl) â†’ refl }

opaque

  -- A state cannot reduce in both â‡’áµ¥ and â‡’â‚™

  not-â‡’áµ¥-and-â‡’â‚‘ : s â‡’áµ¥ sâ€² â†’ s â‡’â‚‘ sâ€³ â†’ âŠ¥
  not-â‡’áµ¥-and-â‡’â‚‘ liftâ‚• d = â‡’â‚‘-inv-lift d
  not-â‡’áµ¥-and-â‡’â‚‘ (lamâ‚• _) d = â‡’â‚‘-inv-lam d
  not-â‡’áµ¥-and-â‡’â‚‘ prodË¢â‚•â‚ d = â‡’â‚‘-inv-prod d
  not-â‡’áµ¥-and-â‡’â‚‘ prodË¢â‚•â‚‚ d = â‡’â‚‘-inv-prod d
  not-â‡’áµ¥-and-â‡’â‚‘ (prodÊ·â‚• _) d = â‡’â‚‘-inv-prod d
  not-â‡’áµ¥-and-â‡’â‚‘ zeroâ‚• d = â‡’â‚‘-inv-zero d
  not-â‡’áµ¥-and-â‡’â‚‘ (sucâ‚• _ _) d = â‡’â‚‘-inv-suc d
  not-â‡’áµ¥-and-â‡’â‚‘ starÊ·â‚• d = â‡’â‚‘-inv-star d
  not-â‡’áµ¥-and-â‡’â‚‘ (unitrec-Î·â‚• Î·) d = â‡’â‚‘-inv-unitrec-Î· Î· d
  not-â‡’áµ¥-and-â‡’â‚‘ rflâ‚•â±¼ d = â‡’â‚‘-inv-rfl d
  not-â‡’áµ¥-and-â‡’â‚‘ rflâ‚•â‚– d = â‡’â‚‘-inv-rfl d
  not-â‡’áµ¥-and-â‡’â‚‘ rflâ‚•â‚‘ d = â‡’â‚‘-inv-rfl d

opaque

  not-â‡’áµ¥-and-â‡¾â‚‘ : s â‡’áµ¥ sâ€² â†’ s â‡¾â‚‘ sâ€³ â†’ âŠ¥
  not-â‡’áµ¥-and-â‡¾â‚‘ d  (â‡’â‚‘ dâ€²) = not-â‡’áµ¥-and-â‡’â‚‘ d dâ€²
  not-â‡’áµ¥-and-â‡¾â‚‘ () (var _ _)

opaque

  not-â‡’áµ¥-and-â‡¢â‚‘ : s â‡’áµ¥ sâ€² â†’ s â‡¢â‚‘ sâ€³ â†’ âŠ¥
  not-â‡’áµ¥-and-â‡¢â‚‘ d (var x) = â‡’áµ¥-inv-var d
  not-â‡’áµ¥-and-â‡¢â‚‘ d (â‡’â‚‘ dâ€²) = not-â‡’áµ¥-and-â‡’â‚‘ d dâ€²

opaque

  -- A state cannot reduce in both â‡’â‚™ and â‡’áµ¥

  not-â‡’â‚™-and-â‡’áµ¥ : s â‡’â‚™ sâ€² â†’ s â‡’áµ¥ sâ€³ â†’ âŠ¥
  not-â‡’â‚™-and-â‡’áµ¥ (sucâ‚• {â„“ = 0} x) d =
    case â‡’áµ¥-inv-suc d of Î» {
      (_ , _ , _ , _ , _ , _ , _ , _ , _ , _ , _ , _ , _ , () , _)}
  not-â‡’â‚™-and-â‡’áµ¥ (sucâ‚• {â„“ = 1+ â„“} x) d =
    case â‡’áµ¥-inv-suc d of Î» {
      (_ , _ , _ , _ , _ , _ , _ , _ , _ , _ , _ , _ , _ , () , _)}
  not-â‡’â‚™-and-â‡’áµ¥ (numâ‚• zeroâ‚™) d =
    case â‡’áµ¥-inv-zero d of Î» {
      (_ , _ , _ , _ , _ , _ , _ , _ , _ , () , _)}
  not-â‡’â‚™-and-â‡’áµ¥ (numâ‚• (sucâ‚™ x)) d =
    case â‡’áµ¥-inv-suc d of Î» {
      (_ , _ , _ , _ , _ , _ , _ , _ , _ , _ , _ , _ , _ , () , _)}

opaque

  -- A state cannot reduce in both â‡’â‚™ and â‡¾â‚‘

  not-â‡’â‚™-and-â‡’â‚‘ : s â‡’â‚™ sâ€² â†’ s â‡’â‚‘ sâ€³ â†’ âŠ¥
  not-â‡’â‚™-and-â‡’â‚‘ (sucâ‚• x) d = â‡’â‚‘-inv-suc d
  not-â‡’â‚™-and-â‡’â‚‘ (numâ‚• zeroâ‚™) d = â‡’â‚‘-inv-zero d
  not-â‡’â‚™-and-â‡’â‚‘ (numâ‚• (sucâ‚™ x)) d = â‡’â‚‘-inv-suc d

opaque

  -- A state cannot reduce in both â‡’â‚™ and â‡¾â‚‘

  not-â‡’â‚™-and-â‡¾â‚‘ : s â‡’â‚™ sâ€² â†’ s â‡¾â‚‘ sâ€³ â†’ âŠ¥
  not-â‡’â‚™-and-â‡¾â‚‘ d (var _ _) = â‡’â‚™-inv-var d
  not-â‡’â‚™-and-â‡¾â‚‘ d (â‡’â‚‘ dâ€²) = not-â‡’â‚™-and-â‡’â‚‘ d dâ€²

opaque

  -- The small-step heap semantics is deterministic

  â‡¾-det : {sâ€² : State k m n} {sâ€³ : State k mâ€² nâ€²}
        â†’ (d : s â‡¾ sâ€²) (dâ€² : s â‡¾ sâ€³)
        â†’ Î£ (m â‰¡ mâ€²) Î» mâ‰¡mâ€² â†’
          Î£ (n â‰¡ nâ€²) Î» nâ‰¡nâ€² â†’
            substâ‚‚ (State k) mâ‰¡mâ€² nâ‰¡nâ€² sâ€² â‰¡ sâ€³
  â‡¾-det (â‡¾â‚‘ d) (â‡¾â‚‘ dâ€²) =
    case â‡¾â‚‘-det d dâ€² of Î» where
      (refl , s~sâ€²) â†’ refl , refl , s~sâ€²
  â‡¾-det (â‡’áµ¥ d) (â‡’áµ¥ dâ€²) = â‡’áµ¥-det d dâ€²
  â‡¾-det (â‡¾â‚‘ d) (â‡’áµ¥ dâ€²) =
    âŠ¥-elim (not-â‡’áµ¥-and-â‡¾â‚‘ dâ€² d)
  â‡¾-det (â‡’áµ¥ d) (â‡¾â‚‘ dâ€²) =
    âŠ¥-elim (not-â‡’áµ¥-and-â‡¾â‚‘ d dâ€²)

opaque

  -- The non-trackigng small-step heap semantics is deterministic.

  â‡¢-det : {sâ€² : State k m n} {sâ€³ : State k mâ€² nâ€²}
        â†’ (d : s â‡¢ sâ€²) (dâ€² : s â‡¢ sâ€³)
        â†’ Î£ (m â‰¡ mâ€²) Î» mâ‰¡mâ€² â†’
          Î£ (n â‰¡ nâ€²) Î» nâ‰¡nâ€² â†’
            substâ‚‚ (State k) mâ‰¡mâ€² nâ‰¡nâ€² sâ€² â‰¡ sâ€³
  â‡¢-det (â‡¢â‚‘ d) (â‡¢â‚‘ dâ€²) =
    case â‡¢â‚‘-det d dâ€² of Î» where
      (refl , refl) â†’ refl , refl , refl
  â‡¢-det (â‡’áµ¥ d) (â‡’áµ¥ dâ€²) = â‡’áµ¥-det d dâ€²
  â‡¢-det (â‡¢â‚‘ d) (â‡’áµ¥ dâ€²) = âŠ¥-elim (not-â‡’áµ¥-and-â‡¢â‚‘ dâ€² d)
  â‡¢-det (â‡’áµ¥ d) (â‡¢â‚‘ dâ€²) = âŠ¥-elim (not-â‡’áµ¥-and-â‡¢â‚‘ d dâ€²)

opaque

  -- The fully evaluating small-step heap semantics is deterministic.

  â† -det : {sâ€² : State k m n} {sâ€³ : State k mâ€² nâ€²}
        â†’ (d : s â†  sâ€²) (dâ€² : s â†  sâ€³)
        â†’ Î£ (m â‰¡ mâ€²) Î» mâ‰¡mâ€² â†’
          Î£ (n â‰¡ nâ€²) Î» nâ‰¡nâ€² â†’
            substâ‚‚ (State k) mâ‰¡mâ€² nâ‰¡nâ€² sâ€² â‰¡ sâ€³
  â† -det (â‡¾â‚‘ d) (â‡¾â‚‘ dâ€²) =
    case â‡¾â‚‘-det d dâ€² of Î» where
      (refl , s~sâ€²) â†’ refl , refl , s~sâ€²
  â† -det (â‡’áµ¥ d) (â‡’áµ¥ dâ€²) = â‡’áµ¥-det d dâ€²
  â† -det (â‡’â‚™ d) (â‡’â‚™ dâ€²) = refl , refl , â‡’â‚™-det d dâ€²
  â† -det (â‡¾â‚‘ d) (â‡’áµ¥ dâ€²) =
    âŠ¥-elim (not-â‡’áµ¥-and-â‡¾â‚‘ dâ€² d)
  â† -det (â‡¾â‚‘ d) (â‡’â‚™ dâ€²) =
    âŠ¥-elim (not-â‡’â‚™-and-â‡¾â‚‘ dâ€² d)
  â† -det (â‡’áµ¥ d) (â‡¾â‚‘ dâ€²) =
    âŠ¥-elim (not-â‡’áµ¥-and-â‡¾â‚‘ d dâ€²)
  â† -det (â‡’áµ¥ d) (â‡’â‚™ dâ€²) =
    âŠ¥-elim (not-â‡’â‚™-and-â‡’áµ¥ dâ€² d)
  â† -det (â‡’â‚™ d) (â‡¾â‚‘ dâ€²) =
    âŠ¥-elim (not-â‡’â‚™-and-â‡¾â‚‘ d dâ€²)
  â† -det (â‡’â‚™ d) (â‡’áµ¥ dâ€²) =
    âŠ¥-elim (not-â‡’â‚™-and-â‡’áµ¥ d dâ€²)

opaque

  -- The reflexive, transitive closure of the fully evaluating
  -- heap semantics is deterministic.

  â† *-det : {sâ€² : State k m n} {sâ€³ : State k mâ€² nâ€²}
         â†’ (d : s â† * sâ€²) (dâ€² : s â† * sâ€³)
         â†’ (âˆ€ {m n} (sâ€´ : State k m n) â†’ Â¬ sâ€² â†  sâ€´)
         â†’ (âˆ€ {m n} (sâ€´ : State k m n) â†’ Â¬ sâ€³ â†  sâ€´)
         â†’ Î£ (m â‰¡ mâ€²) Î» mâ‰¡mâ€² â†’
           Î£ (n â‰¡ nâ€²) Î» nâ‰¡nâ€² â†’
             substâ‚‚ (State k) mâ‰¡mâ€² nâ‰¡nâ€² sâ€² â‰¡ sâ€³
  â† *-det id id Â¬d Â¬dâ€² = refl , refl , refl
  â† *-det id (x â‡¨ dâ€²) Â¬d Â¬dâ€² = âŠ¥-elim (Â¬d _ x)
  â† *-det (x â‡¨ d) id Â¬d Â¬dâ€² = âŠ¥-elim (Â¬dâ€² _ x)
  â† *-det (x â‡¨ d) (xâ€² â‡¨ dâ€²) Â¬d Â¬dâ€² =
    case â† -det x xâ€² of Î» where
      (refl , refl , refl) â†’
        â† *-det d dâ€² Â¬d Â¬dâ€²

opaque

  -- The normalising reduction preserves equality in a certain way

  â‡’â‚‘-â¦…â¦†-â‰¡ : s â‡’â‚‘ sâ€² â†’ â¦… s â¦† â‰¡ â¦… sâ€² â¦†
  â‡’â‚‘-â¦…â¦†-â‰¡ lowerâ‚• = refl
  â‡’â‚‘-â¦…â¦†-â‰¡ appâ‚• = refl
  â‡’â‚‘-â¦…â¦†-â‰¡ fstâ‚• = refl
  â‡’â‚‘-â¦…â¦†-â‰¡ sndâ‚• = refl
  â‡’â‚‘-â¦…â¦†-â‰¡ prodrecâ‚• = refl
  â‡’â‚‘-â¦…â¦†-â‰¡ natrecâ‚• = refl
  â‡’â‚‘-â¦…â¦†-â‰¡ (unitrecâ‚• _) = refl
  â‡’â‚‘-â¦…â¦†-â‰¡ emptyrecâ‚• = refl
  â‡’â‚‘-â¦…â¦†-â‰¡ Jâ‚• = refl
  â‡’â‚‘-â¦…â¦†-â‰¡ Kâ‚• = refl
  â‡’â‚‘-â¦…â¦†-â‰¡ []-congâ‚• = refl

opaque

  -- The normalising reduction preserves equality in a certain way

  â‡¾â‚‘-â¦…â¦†-â‰¡ : s â‡¾â‚‘ sâ€² â†’ â¦… s â¦† â‰¡ â¦… sâ€² â¦†
  â‡¾â‚‘-â¦…â¦†-â‰¡ {s = âŸ¨ _ , _ , _ , S âŸ©} (var _ d) =
    trans (â¦…â¦†Ë¢-cong S (heapSubstVar d))
      (cong (Î» x â†’ â¦… S â¦†Ë¢ _ [ x ]) (heapUpdateSubst d))
  â‡¾â‚‘-â¦…â¦†-â‰¡ (â‡’â‚‘ d) = â‡’â‚‘-â¦…â¦†-â‰¡ d

opaque

  -- The normalising reduction preserves equality in a certain way

  â‡¾â‚‘*-â¦…â¦†-â‰¡ : s â‡¾â‚‘* sâ€² â†’ â¦… s â¦† â‰¡ â¦… sâ€² â¦†
  â‡¾â‚‘*-â¦…â¦†-â‰¡ id = refl
  â‡¾â‚‘*-â¦…â¦†-â‰¡ (x â‡¨ d) = trans (â‡¾â‚‘-â¦…â¦†-â‰¡ x) (â‡¾â‚‘*-â¦…â¦†-â‰¡ d)

opaque

  -- The normalising reduction preserves equality in a certain way

  â‡¢â‚‘-â¦…â¦†-â‰¡ : s â‡¢â‚‘ sâ€² â†’ â¦… s â¦† â‰¡ â¦… sâ€² â¦†
  â‡¢â‚‘-â¦…â¦†-â‰¡ {s = âŸ¨ _ , _ , _ , S âŸ©} (var d) = â¦…â¦†Ë¢-cong S (heapSubstVarâ€² d)
  â‡¢â‚‘-â¦…â¦†-â‰¡ (â‡’â‚‘ d) = â‡’â‚‘-â¦…â¦†-â‰¡ d

opaque

  -- The reduction evaluating numerals preserves equality in a certain
  -- way

  â‡’â‚™-â¦…â¦†-â‰¡ : s â‡’â‚™ sâ€² â†’ â¦… s â¦† â‰¡ â¦… sâ€² â¦†
  â‡’â‚™-â¦…â¦†-â‰¡ (sucâ‚• x) = refl
  â‡’â‚™-â¦…â¦†-â‰¡ (numâ‚• x) = refl

opaque

  -- Lifting a normalising reduction to a larger heap

  wk1-â‡’â‚‘ : âŸ¨ H , t , Ï , S âŸ© â‡’â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©
          â†’ âŸ¨ H âˆ™ c , t , step Ï , wk1Ë¢ S âŸ© â‡’â‚‘ âŸ¨ Hâ€² âˆ™ c , tâ€² , step Ïâ€² , wk1Ë¢ Sâ€² âŸ©
  wk1-â‡’â‚‘ lowerâ‚• = lowerâ‚•
  wk1-â‡’â‚‘ appâ‚• = appâ‚•
  wk1-â‡’â‚‘ fstâ‚• = fstâ‚•
  wk1-â‡’â‚‘ sndâ‚• = sndâ‚•
  wk1-â‡’â‚‘ prodrecâ‚• = prodrecâ‚•
  wk1-â‡’â‚‘ natrecâ‚• = natrecâ‚•
  wk1-â‡’â‚‘ (unitrecâ‚• x) = unitrecâ‚• x
  wk1-â‡’â‚‘ emptyrecâ‚• = emptyrecâ‚•
  wk1-â‡’â‚‘ Jâ‚• = Jâ‚•
  wk1-â‡’â‚‘ Kâ‚• = Kâ‚•
  wk1-â‡’â‚‘ []-congâ‚• = []-congâ‚•

opaque

  -- Lifting a normalising reduction to a larger heap

  wk1-â‡¾â‚‘ : âŸ¨ H , t , Ï , S âŸ© â‡¾â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©
         â†’ âŸ¨ H âˆ™ c , t , step Ï , wk1Ë¢ S âŸ© â‡¾â‚‘ âŸ¨ Hâ€² âˆ™ c , tâ€² , step Ïâ€² , wk1Ë¢ Sâ€² âŸ©
  wk1-â‡¾â‚‘ {S} (var âˆ£Sâˆ£â‰¡ d) =
    var (wk-âˆ£âˆ£ âˆ£Sâˆ£â‰¡) (there d)
  wk1-â‡¾â‚‘ (â‡’â‚‘ d) = â‡’â‚‘ (wk1-â‡’â‚‘ d)

opaque

  -- Lifting a normalising reduction to a larger heap

  wk1-â‡¢â‚‘ : âŸ¨ H , t , Ï , S âŸ© â‡¢â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©
         â†’ âŸ¨ H âˆ™ c , t , step Ï , wk1Ë¢ S âŸ© â‡¢â‚‘ âŸ¨ Hâ€² âˆ™ c , tâ€² , step Ïâ€² , wk1Ë¢ Sâ€² âŸ©
  wk1-â‡¢â‚‘ (var d) = var (there d)
  wk1-â‡¢â‚‘ (â‡’â‚‘ d) = â‡’â‚‘ (wk1-â‡’â‚‘ d)

opaque

  -- Lifting a normalising reduction to a larger heap

  wk1-â‡¢â‚‘* : âŸ¨ H , t , Ï , S âŸ© â‡¢â‚‘* âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©
          â†’ âŸ¨ H âˆ™ c , t , step Ï , wk1Ë¢ S âŸ© â‡¢â‚‘* âŸ¨ Hâ€² âˆ™ c , tâ€² , step Ïâ€² , wk1Ë¢ Sâ€² âŸ©
  wk1-â‡¢â‚‘* id = id
  wk1-â‡¢â‚‘* (_â‡¨_ {sâ€² = record{}} x d) = wk1-â‡¢â‚‘ x â‡¨ wk1-â‡¢â‚‘* d

opaque

  -- Lifting a normalising reduction to a larger heap

  wk1â—-â‡’â‚‘ : âŸ¨ H , t , Ï , S âŸ© â‡’â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©
           â†’ âŸ¨ H âˆ™â— , t , step Ï , wk1Ë¢ S âŸ© â‡’â‚‘ âŸ¨ Hâ€² âˆ™â— , tâ€² , step Ïâ€² , wk1Ë¢ Sâ€² âŸ©
  wk1â—-â‡’â‚‘ lowerâ‚• = lowerâ‚•
  wk1â—-â‡’â‚‘ appâ‚• = appâ‚•
  wk1â—-â‡’â‚‘ fstâ‚• = fstâ‚•
  wk1â—-â‡’â‚‘ sndâ‚• = sndâ‚•
  wk1â—-â‡’â‚‘ prodrecâ‚• = prodrecâ‚•
  wk1â—-â‡’â‚‘ natrecâ‚• = natrecâ‚•
  wk1â—-â‡’â‚‘ (unitrecâ‚• x) = unitrecâ‚• x
  wk1â—-â‡’â‚‘ emptyrecâ‚• = emptyrecâ‚•
  wk1â—-â‡’â‚‘ Jâ‚• = Jâ‚•
  wk1â—-â‡’â‚‘ Kâ‚• = Kâ‚•
  wk1â—-â‡’â‚‘ []-congâ‚• = []-congâ‚•

opaque

  -- Lifting a normalising reduction to a larger heap

  wk1â—-â‡¾â‚‘ : âŸ¨ H , t , Ï , S âŸ© â‡¾â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©
          â†’ âŸ¨ H âˆ™â— , t , step Ï , wk1Ë¢ S âŸ© â‡¾â‚‘ âŸ¨ Hâ€² âˆ™â— , tâ€² , step Ïâ€² , wk1Ë¢ Sâ€² âŸ©
  wk1â—-â‡¾â‚‘ {S} (var âˆ£Sâˆ£â‰¡ d) =
    var (wk-âˆ£âˆ£ âˆ£Sâˆ£â‰¡) (thereâ— d)
  wk1â—-â‡¾â‚‘ (â‡’â‚‘ d) = â‡’â‚‘ wk1â—-â‡’â‚‘ d

opaque

  -- Lifting a normalising reduction to a larger heap

  wk1â—-â‡¢â‚‘ : âŸ¨ H , t , Ï , S âŸ© â‡¢â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©
           â†’ âŸ¨ H âˆ™â— , t , step Ï , wk1Ë¢ S âŸ© â‡¢â‚‘ âŸ¨ Hâ€² âˆ™â— , tâ€² , step Ïâ€² , wk1Ë¢ Sâ€² âŸ©
  wk1â—-â‡¢â‚‘ (var d) = var (thereâ— d)
  wk1â—-â‡¢â‚‘ (â‡’â‚‘ d) = â‡’â‚‘ (wk1â—-â‡’â‚‘ d)

opaque

  -- Lifting a normalising reduction to a larger heap

  wk1â—-â‡¢â‚‘* : (d : âŸ¨ H , t , Ï , S âŸ© â‡¢â‚‘* âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©)
          â†’ âŸ¨ H âˆ™â— , t , step Ï , wk1Ë¢ S âŸ© â‡¢â‚‘* âŸ¨ Hâ€² âˆ™â— , tâ€² , step Ïâ€² , wk1Ë¢ Sâ€² âŸ©
  wk1â—-â‡¢â‚‘* id = id
  wk1â—-â‡¢â‚‘* (_â‡¨_ {sâ€² = record{}} x d) = wk1â—-â‡¢â‚‘ x â‡¨ wk1â—-â‡¢â‚‘* d

opaque

  -- Replacing a variable and environment in a state

  var-env-â‡¢â‚‘ : âŸ¨ H , var x , Ï , S âŸ© â‡¢â‚‘ s
            â†’ wkVar Ï x â‰¡ wkVar Ïâ€² xâ€²
            â†’ âŸ¨ H , var xâ€² , Ïâ€² , S âŸ© â‡¢â‚‘ s
  var-env-â‡¢â‚‘ (var d) eq = var (subst (_ âŠ¢_â†¦ _) eq d)
  var-env-â‡¢â‚‘ (â‡’â‚‘ ())

opaque

  -- Replacing a variable and environment in a state

  var-env-â‡¢â‚‘* : {Ï : Wk m n} {Ïâ€³ : Wk m nâ€²}
              â†’ âŸ¨ H , var x , Ï , S âŸ© â‡¢â‚‘* âŸ¨ Hâ€² , t , Ïâ€³ , Sâ€² âŸ©
             â†’ wkVar Ï x â‰¡ wkVar Ïâ€² xâ€²
             â†’ Normal âŸ¨ Hâ€² , t , Ïâ€³ , Sâ€² âŸ©
             â†’ âŸ¨ H , var xâ€² , Ïâ€² , S âŸ© â‡¢â‚‘* âŸ¨ Hâ€² , t , Ïâ€³ , Sâ€² âŸ©
             âŠ Î£ (nâ€² â‰¡ n) Î» nâ€²â‰¡n â†’ âŸ¨ H , var x , Ï , S âŸ© â‰¡ subst (State _ _) nâ€²â‰¡n âŸ¨ Hâ€² , t , Ïâ€³ , Sâ€² âŸ©
  var-env-â‡¢â‚‘* id eq (var x) = injâ‚‚ (refl , refl)
  var-env-â‡¢â‚‘* id _ (val ())
  var-env-â‡¢â‚‘* (x â‡¨ d) eq n = injâ‚ (var-env-â‡¢â‚‘ x eq â‡¨ d)

opaque

  -- Extending the stack of a reduction

  ++-â‡’â‚‘ : âˆ€ Sâ‚€ â†’ âŸ¨ H , t , Ï , S âŸ© â‡’â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©
         â†’ âŸ¨ H , t , Ï , S ++ Sâ‚€ âŸ© â‡’â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² ++ Sâ‚€ âŸ©
  ++-â‡’â‚‘ Sâ‚€ appâ‚• = appâ‚•
  ++-â‡’â‚‘ Sâ‚€ fstâ‚• = fstâ‚•
  ++-â‡’â‚‘ Sâ‚€ sndâ‚• = sndâ‚•
  ++-â‡’â‚‘ Sâ‚€ prodrecâ‚• = prodrecâ‚•
  ++-â‡’â‚‘ Sâ‚€ natrecâ‚• = natrecâ‚•
  ++-â‡’â‚‘ Sâ‚€ (unitrecâ‚• x) = unitrecâ‚• x
  ++-â‡’â‚‘ Sâ‚€ emptyrecâ‚• = emptyrecâ‚•
  ++-â‡’â‚‘ Sâ‚€ Jâ‚• = Jâ‚•
  ++-â‡’â‚‘ Sâ‚€ Kâ‚• = Kâ‚•
  ++-â‡’â‚‘ Sâ‚€ []-congâ‚• = []-congâ‚•
  ++-â‡’â‚‘ Sâ‚€ lowerâ‚• = lowerâ‚•

opaque

  -- Extending the stack of a reduction

  ++-â‡¢â‚‘ : âˆ€ Sâ‚€ â†’ âŸ¨ H , t , Ï , S âŸ© â‡¢â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©
        â†’ âŸ¨ H , t , Ï , S ++ Sâ‚€ âŸ© â‡¢â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² ++ Sâ‚€ âŸ©
  ++-â‡¢â‚‘ Sâ‚€ (var d) = var d
  ++-â‡¢â‚‘ Sâ‚€ (â‡’â‚‘ d) = â‡’â‚‘ (++-â‡’â‚‘ Sâ‚€ d)

opaque

  -- Extending the stack of a reduction

  ++-â‡¢â‚‘* : âˆ€ Sâ‚€ â†’ (d : âŸ¨ H , t , Ï , S âŸ© â‡¢â‚‘* âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©)
         â†’ âŸ¨ H , t , Ï , S ++ Sâ‚€ âŸ© â‡¢â‚‘* âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² ++ Sâ‚€ âŸ©
  ++-â‡¢â‚‘* Sâ‚€ id = id
  ++-â‡¢â‚‘* Sâ‚€ (_â‡¨_ {sâ€² = record{}} x d) = ++-â‡¢â‚‘ Sâ‚€ x â‡¨ ++-â‡¢â‚‘* Sâ‚€ d

opaque

  -- Extending the stack of a reduction with sucâ‚›

  ++sucâ‚›-â‡¾â‚‘ : (d : âŸ¨ H , t , Ï , S âŸ© â‡¾â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©)
            â†’ âŸ¨ H , t , Ï , S ++ sucâ‚› k âŸ© â‡¾â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² ++ sucâ‚› k âŸ©
  ++sucâ‚›-â‡¾â‚‘ {S} (var âˆ£Sâˆ£â‰¡ d) =
    var (âˆ£S++sucâ‚›âˆ£â‰¡âˆ£Sâˆ£ âˆ£Sâˆ£â‰¡) d
  ++sucâ‚›-â‡¾â‚‘ (â‡’â‚‘ x) = â‡’â‚‘ (++-â‡’â‚‘ _ x)

opaque

  -- Extending the stack of a reduction with sucâ‚›

  ++sucâ‚›-â‡’áµ¥ : (d : âŸ¨ H , t , Ï , S âŸ© â‡’áµ¥ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©)
            â†’ âŸ¨ H , t , Ï , S ++ sucâ‚› k âŸ© â‡’áµ¥ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² ++ sucâ‚› k âŸ©
  ++sucâ‚›-â‡’áµ¥ liftâ‚• = liftâ‚•
  ++sucâ‚›-â‡’áµ¥ {k} (lamâ‚• {S} {H} {p} {t} {Ï} {u} {Ïâ€²} âˆ£Sâˆ£â‰¡) =
    subst
      (Î» x â†’
         âŸ¨ H , lam p t , Ï , (âˆ˜â‚‘ p u Ïâ€² âˆ™ S) ++ sucâ‚› k âŸ© â‡’áµ¥
         âŸ¨ H âˆ™ (_ Â· p , u , Ïâ€²) , t , lift Ï , x âŸ©)
         (wk-++sucâ‚› (step id) S) (lamâ‚• (âˆ£S++sucâ‚›âˆ£â‰¡âˆ£Sâˆ£ âˆ£Sâˆ£â‰¡))
  ++sucâ‚›-â‡’áµ¥ prodË¢â‚•â‚ = prodË¢â‚•â‚
  ++sucâ‚›-â‡’áµ¥ prodË¢â‚•â‚‚ = prodË¢â‚•â‚‚
  ++sucâ‚›-â‡’áµ¥ {k} (prodÊ·â‚• {S} {H} {p} {tâ‚} {tâ‚‚} {Ï} {r} {q} {A} {u} {Ïâ€²} âˆ£Sâˆ£â‰¡) =
    subst
      (Î» x â†’
         âŸ¨ H , prodÊ· p tâ‚ tâ‚‚ , Ï , (prodrecâ‚‘ r p q A u Ïâ€² âˆ™ S) ++ sucâ‚› k âŸ© â‡’áµ¥
         âŸ¨ H âˆ™ (_ Â· r Â· p , tâ‚ , Ï) âˆ™ (_ Â· r , tâ‚‚ , step Ï) , u , liftn Ïâ€² 2 , x âŸ©)
      (wk-++sucâ‚› (step (step id)) S) (prodÊ·â‚• (âˆ£S++sucâ‚›âˆ£â‰¡âˆ£Sâˆ£ âˆ£Sâˆ£â‰¡ ))
  ++sucâ‚›-â‡’áµ¥ zeroâ‚• = zeroâ‚•
  ++sucâ‚›-â‡’áµ¥ {k} (sucâ‚• {S} {r} {H} {t} {Ï} {A} {z} {s} {Ïâ€²} âˆ£Sâˆ£â‰¡ âˆ£nrâˆ£â‰¡) =
    subst
      (Î» x â†’
        âŸ¨ H , suc t , Ï , natrecâ‚‘ _ _ r A z s Ïâ€² âˆ™ S ++ sucâ‚› k âŸ© â‡’áµ¥
        âŸ¨ H âˆ™ (_ Â· _ , t , Ï) âˆ™ (_ Â· r , _ , lift Ïâ€²) , s , liftn Ïâ€² 2 , x âŸ©)
      (wk-++sucâ‚› (step (step id)) S) (sucâ‚• (âˆ£S++sucâ‚›âˆ£â‰¡âˆ£Sâˆ£ âˆ£Sâˆ£â‰¡) âˆ£nrâˆ£â‰¡)
  ++sucâ‚›-â‡’áµ¥ starÊ·â‚• = starÊ·â‚•
  ++sucâ‚›-â‡’áµ¥ (unitrec-Î·â‚• Î·) = unitrec-Î·â‚• Î·
  ++sucâ‚›-â‡’áµ¥ rflâ‚•â±¼ = rflâ‚•â±¼
  ++sucâ‚›-â‡’áµ¥ rflâ‚•â‚– = rflâ‚•â‚–
  ++sucâ‚›-â‡’áµ¥ rflâ‚•â‚‘ = rflâ‚•â‚‘

opaque

  -- Extending the stack of a reduction with sucâ‚›

  ++sucâ‚›-â‡’â‚™ : (d : âŸ¨ H , t , Ï , S âŸ© â‡’â‚™ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©)
            â†’ âŸ¨ H , t , Ï , S ++ sucâ‚› k âŸ© â‡’â‚™ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² ++ sucâ‚› k âŸ©
  ++sucâ‚›-â‡’â‚™ {k} (sucâ‚• {t} {H} {Ï} {â„“} x) =
    subst (Î» x â†’ âŸ¨ H , suc t , Ï , x âŸ© â‡’â‚™ âŸ¨ H , t , Ï , sucâ‚‘ âˆ™ x âŸ©)
      (sym (sucâ‚›++sucâ‚› â„“ k)) (sucâ‚• x)
  ++sucâ‚›-â‡’â‚™ (numâ‚• x) = numâ‚• x

opaque

  ++sucâ‚›-â†  : âŸ¨ H , t , Ï , S âŸ© â†  âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©
           â†’ âŸ¨ H , t , Ï , S ++ sucâ‚› k âŸ© â†  âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² ++ sucâ‚› k âŸ©
  ++sucâ‚›-â†  (â‡¾â‚‘ x) = â‡¾â‚‘ ++sucâ‚›-â‡¾â‚‘ x
  ++sucâ‚›-â†  (â‡’áµ¥ x) = â‡’áµ¥ ++sucâ‚›-â‡’áµ¥ x
  ++sucâ‚›-â†  (â‡’â‚™ x) = â‡’â‚™ ++sucâ‚›-â‡’â‚™ x

opaque

  ++sucâ‚›-â† * : âŸ¨ H , t , Ï , S âŸ© â† * âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ©
            â†’ âŸ¨ H , t , Ï , S ++ sucâ‚› k âŸ© â† * âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² ++ sucâ‚› k âŸ©
  ++sucâ‚›-â† * id = id
  ++sucâ‚›-â† * (_â‡¨_ {sâ‚‚ = record{}} x d) =
    ++sucâ‚›-â†  x â‡¨ ++sucâ‚›-â† * d

opaque

  -- The relation _â‡’â‚‘_ preserves the heap

  â‡’â‚‘-Heapâ‰¡ : âŸ¨ H , t , Ï , S âŸ© â‡’â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ© â†’ H â‰¡ Hâ€²
  â‡’â‚‘-Heapâ‰¡ lowerâ‚• = refl
  â‡’â‚‘-Heapâ‰¡ appâ‚• = refl
  â‡’â‚‘-Heapâ‰¡ fstâ‚• = refl
  â‡’â‚‘-Heapâ‰¡ sndâ‚• = refl
  â‡’â‚‘-Heapâ‰¡ prodrecâ‚• = refl
  â‡’â‚‘-Heapâ‰¡ natrecâ‚• = refl
  â‡’â‚‘-Heapâ‰¡ (unitrecâ‚• x) = refl
  â‡’â‚‘-Heapâ‰¡ emptyrecâ‚• = refl
  â‡’â‚‘-Heapâ‰¡ Jâ‚• = refl
  â‡’â‚‘-Heapâ‰¡ Kâ‚• = refl
  â‡’â‚‘-Heapâ‰¡ []-congâ‚• = refl

opaque

  -- The relation _â‡¢â‚‘_ preserves the heap

  â‡¢â‚‘-Heapâ‰¡ : âŸ¨ H , t , Ï , S âŸ© â‡¢â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ© â†’ H â‰¡ Hâ€²
  â‡¢â‚‘-Heapâ‰¡ (var d) = refl
  â‡¢â‚‘-Heapâ‰¡ (â‡’â‚‘ d) = â‡’â‚‘-Heapâ‰¡ d

opaque

  -- The relation _â‡’â‚™_ preserves the heap

  â‡’â‚™-Heapâ‰¡ : âŸ¨ H , t , Ï , S âŸ© â‡’â‚™ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ© â†’ H â‰¡ Hâ€²
  â‡’â‚™-Heapâ‰¡ (sucâ‚• x) = refl
  â‡’â‚™-Heapâ‰¡ (numâ‚• x) = refl

opaque

  -- The reduction for eliminators behaves the same on equal heaps

  ~Ê°-â‡’â‚‘ :
    âŸ¨ H , t , Ï , S âŸ© â‡’â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ© â†’ H ~Ê° Hâ€³ â†’
    âˆƒ Î» Hâ€´ â†’ âŸ¨ Hâ€³ , t , Ï , S âŸ© â‡’â‚‘ âŸ¨ Hâ€´ , tâ€² , Ïâ€² , Sâ€² âŸ© Ã— Hâ€² ~Ê° Hâ€´
  ~Ê°-â‡’â‚‘ lowerâ‚• H~Hâ€³ =
    _ , lowerâ‚• , H~Hâ€³
  ~Ê°-â‡’â‚‘ appâ‚• H~Hâ€³ =
    _ , appâ‚• , H~Hâ€³
  ~Ê°-â‡’â‚‘ fstâ‚• H~Hâ€³ =
    _ , fstâ‚• , H~Hâ€³
  ~Ê°-â‡’â‚‘ sndâ‚• H~Hâ€³ =
    _ , sndâ‚• , H~Hâ€³
  ~Ê°-â‡’â‚‘ prodrecâ‚• H~Hâ€³ =
    _ , prodrecâ‚• , H~Hâ€³
  ~Ê°-â‡’â‚‘ natrecâ‚• H~Hâ€³ =
    _ , natrecâ‚• , H~Hâ€³
  ~Ê°-â‡’â‚‘ (unitrecâ‚• x) H~Hâ€³ =
    _ , unitrecâ‚• x , H~Hâ€³
  ~Ê°-â‡’â‚‘ emptyrecâ‚• H~Hâ€³ =
    _ , emptyrecâ‚• , H~Hâ€³
  ~Ê°-â‡’â‚‘ Jâ‚• H~Hâ€³ =
    _ , Jâ‚• , H~Hâ€³
  ~Ê°-â‡’â‚‘ Kâ‚• H~Hâ€³ =
    _ , Kâ‚• , H~Hâ€³
  ~Ê°-â‡’â‚‘ []-congâ‚• H~Hâ€³ =
    _ , []-congâ‚• , H~Hâ€³

opaque

  -- The non resource tracking reduction for eliminators behaves the
  -- same on equal heaps

  ~Ê°-â‡¢â‚‘ :
    âŸ¨ H , t , Ï , S âŸ© â‡¢â‚‘ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ© â†’ H ~Ê° Hâ€³ â†’
    âˆƒ Î» Hâ€´ â†’ âŸ¨ Hâ€³ , t , Ï , S âŸ© â‡¢â‚‘ âŸ¨ Hâ€´ , tâ€² , Ïâ€² , Sâ€² âŸ© Ã— Hâ€² ~Ê° Hâ€´
  ~Ê°-â‡¢â‚‘ (var d) H~Hâ€³ =
    _ ,  var (~Ê°-lookup H~Hâ€³ d) , H~Hâ€³
  ~Ê°-â‡¢â‚‘ (â‡’â‚‘ d) H~Hâ€³ =
    let _ , dâ€² , H~Hâ€² = ~Ê°-â‡’â‚‘ d H~Hâ€³
    in  _ , â‡’â‚‘ dâ€² , H~Hâ€²

opaque

  -- The reduction for values behaves the same on equal heaps

  ~Ê°-â‡’áµ¥ :
    âŸ¨ H , t , Ï , S âŸ© â‡’áµ¥ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ© â†’ H ~Ê° Hâ€³ â†’
    âˆƒ Î» Hâ€´ â†’ âŸ¨ Hâ€³ , t , Ï , S âŸ© â‡’áµ¥ âŸ¨ Hâ€´ , tâ€² , Ïâ€² , Sâ€² âŸ© Ã— Hâ€² ~Ê° Hâ€´
  ~Ê°-â‡’áµ¥ liftâ‚• H~Hâ€³ = _ , liftâ‚• , H~Hâ€³
  ~Ê°-â‡’áµ¥ (lamâ‚• âˆ£Sâˆ£â‰¡) H~Hâ€³ = _ , lamâ‚• âˆ£Sâˆ£â‰¡ , H~Hâ€³ âˆ™ _
  ~Ê°-â‡’áµ¥ prodË¢â‚•â‚ H~Hâ€³ = _ , prodË¢â‚•â‚ , H~Hâ€³
  ~Ê°-â‡’áµ¥ prodË¢â‚•â‚‚ H~Hâ€³ = _ , prodË¢â‚•â‚‚ , H~Hâ€³
  ~Ê°-â‡’áµ¥ (prodÊ·â‚• âˆ£Sâˆ£â‰¡) H~Hâ€³ = _ , prodÊ·â‚• âˆ£Sâˆ£â‰¡ , H~Hâ€³ âˆ™ _ âˆ™ _
  ~Ê°-â‡’áµ¥ zeroâ‚• H~Hâ€³ = _ , zeroâ‚• , H~Hâ€³
  ~Ê°-â‡’áµ¥ (sucâ‚• âˆ£Sâˆ£â‰¡ âˆ£nrâˆ£â‰¡) H~Hâ€³ = _ , sucâ‚• âˆ£Sâˆ£â‰¡ âˆ£nrâˆ£â‰¡ , H~Hâ€³ âˆ™ _ âˆ™ _
  ~Ê°-â‡’áµ¥ starÊ·â‚• H~Hâ€³ = _ , starÊ·â‚• , H~Hâ€³
  ~Ê°-â‡’áµ¥ (unitrec-Î·â‚• x) H~Hâ€³ = _ , unitrec-Î·â‚• x , H~Hâ€³
  ~Ê°-â‡’áµ¥ rflâ‚•â±¼ H~Hâ€³ = _ , rflâ‚•â±¼ , H~Hâ€³
  ~Ê°-â‡’áµ¥ rflâ‚•â‚– H~Hâ€³ = _ , rflâ‚•â‚– , H~Hâ€³
  ~Ê°-â‡’áµ¥ rflâ‚•â‚‘ H~Hâ€³ = _ , rflâ‚•â‚‘ , H~Hâ€³

opaque

  -- The non resource tracking reduction behaves the same on equal heaps

  ~Ê°-â‡¢ :
    âŸ¨ H , t , Ï , S âŸ© â‡¢ âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ© â†’ H ~Ê° Hâ€³ â†’
    âˆƒ Î» Hâ€´ â†’ âŸ¨ Hâ€³ , t , Ï , S âŸ© â‡¢ âŸ¨ Hâ€´ , tâ€² , Ïâ€² , Sâ€² âŸ© Ã— Hâ€² ~Ê° Hâ€´
  ~Ê°-â‡¢ (â‡¢â‚‘ d) H~Hâ€³ =
    let _ , dâ€² , H~Hâ€² = ~Ê°-â‡¢â‚‘ d H~Hâ€³
    in  _ , â‡¢â‚‘ dâ€² , H~Hâ€²
  ~Ê°-â‡¢ (â‡’áµ¥ d) H~Hâ€³ =
    let _ , dâ€² , H~Hâ€² = ~Ê°-â‡’áµ¥ d H~Hâ€³
    in  _ , â‡’áµ¥ dâ€² , H~Hâ€²

opaque

  -- The non resource tracking reduction behaves the same on equal heaps

  ~Ê°-â‡¢* :
    âŸ¨ H , t , Ï , S âŸ© â‡¢* âŸ¨ Hâ€² , tâ€² , Ïâ€² , Sâ€² âŸ© â†’ H ~Ê° Hâ€³ â†’
    âˆƒ Î» Hâ€´ â†’ âŸ¨ Hâ€³ , t , Ï , S âŸ© â‡¢* âŸ¨ Hâ€´ , tâ€² , Ïâ€² , Sâ€² âŸ© Ã— Hâ€² ~Ê° Hâ€´
  ~Ê°-â‡¢* id H~Hâ€³ =
    _ , id , H~Hâ€³
  ~Ê°-â‡¢* (_â‡¨_ {sâ‚‚ = record{}} x d) H~Hâ€³ =
    let _ , xâ€² , H~Hâ€² = ~Ê°-â‡¢ x H~Hâ€³
        _ , dâ€² , H~Hâ€´ = ~Ê°-â‡¢* d H~Hâ€²
    in  _ , xâ€² â‡¨ dâ€² , H~Hâ€´

opaque

  -- _â‡¾_ is a subset of _â† _, if s â‡¾ sâ€² then s â†  sâ€².

  â‡¾â†’â†  : s â‡¾ sâ€² â†’ s â†  sâ€²
  â‡¾â†’â†  (â‡¾â‚‘ d) = â‡¾â‚‘ d
  â‡¾â†’â†  (â‡’áµ¥ d) = â‡’áµ¥ d

opaque

  -- _â‡¾*_ is a subset of _â† *_, if s â‡¾* sâ€² then s â† * sâ€².

  â‡¾*â†’â† * : s â‡¾* sâ€² â†’ s â† * sâ€²
  â‡¾*â†’â† * id = id
  â‡¾*â†’â† * (x â‡¨ d) = â‡¾â†’â†  x â‡¨ â‡¾*â†’â† * d

opaque

  -- If subtraction of the grade correspoding to a heap entry cannot
  -- by subtracted by the current stack multiplicity then states with
  -- a variable in head position "pointing" to that entry do not reduce.

  var-noRed :
    H âŸ¨ wkVar Ï x âŸ©Ê° â‰¡ p â†’ âˆ£ S âˆ£â‰¡ q â†’
    (âˆ€ {r} â†’ p - q â‰¡ r â†’ âŠ¥) â†’
    âŸ¨ H , var x , Ï , S âŸ© â‡¾ âŸ¨ Hâ€² , t , Ïâ€² , Sâ€² âŸ© â†’ âŠ¥
  var-noRed HâŸ¨âŸ©â‰¡ âˆ£Sâˆ£â‰¡ p-qâ‰¢r (â‡¾â‚‘ d) =
    let qâ€² , âˆ£Sâˆ£â‰¡â€² , _ , dâ€² = â‡¾â‚‘-inv-var d
    in  -â‰¢-no-lookup
          (substâ‚‚ (Î» p q â†’ âˆ€ {r} â†’ p - q â‰¡ r â†’ âŠ¥)
            (sym HâŸ¨âŸ©â‰¡) (âˆ£âˆ£-functional âˆ£Sâˆ£â‰¡ âˆ£Sâˆ£â‰¡â€²) p-qâ‰¢r) dâ€²
  var-noRed _ _ _ (â‡’áµ¥ d) = âŠ¥-elim (â‡’áµ¥-inv-var d)

opaque

  -- States with a matching head and stack reduce

  Matchingâ†’â‡’áµ¥ :
    Matching t S â†’
    âˆ£ S âˆ£â‰¡ p â†’
    âˆƒâ‚ƒ Î» m n (s : State _ m n) â†’ âŸ¨ H , t , Ï , S âŸ© â‡’áµ¥ s
  Matchingâ†’â‡’áµ¥ lowerâ‚‘ _ = _ , _ , _ , liftâ‚•
  Matchingâ†’â‡’áµ¥ âˆ˜â‚‘ (_ âˆ™ ok) = _ , _ , _ , lamâ‚• ok
  Matchingâ†’â‡’áµ¥ fstâ‚‘ _ = _ , _ , _ , prodË¢â‚•â‚
  Matchingâ†’â‡’áµ¥ sndâ‚‘ _ = _ , _ , _ , prodË¢â‚•â‚‚
  Matchingâ†’â‡’áµ¥ prodrecâ‚‘ (_ âˆ™ ok) = _ , _ , _ , prodÊ·â‚• ok
  Matchingâ†’â‡’áµ¥ natrecâ‚‘â‚€ _ = _ , _ , _ , zeroâ‚•
  Matchingâ†’â‡’áµ¥ natrecâ‚‘â‚Š (natrecâ‚‘ ok âˆ™ okâ€²) = _ , _ , _ , sucâ‚• okâ€² ok
  Matchingâ†’â‡’áµ¥ unitrecâ‚‘ _ = _ , _ , _ , starÊ·â‚•
  Matchingâ†’â‡’áµ¥ (unitrec-Î· Î·) _ = _ , _ , _ , unitrec-Î·â‚• Î·
  Matchingâ†’â‡’áµ¥ Jâ‚‘ _ = _ , _ , _ , rflâ‚•â±¼
  Matchingâ†’â‡’áµ¥ Kâ‚‘ _ = _ , _ , _ , rflâ‚•â‚–
  Matchingâ†’â‡’áµ¥ []-congâ‚‘ _ = _ , _ , _ , rflâ‚•â‚‘

opaque

  -- States reducing with the reduction for values have a matching
  -- head and stack

  â‡’áµ¥â†’Matching :
    âŸ¨ H , t , Ï , S âŸ© â‡’áµ¥ s â†’
    Matching t S
  â‡’áµ¥â†’Matching liftâ‚• = lowerâ‚‘
  â‡’áµ¥â†’Matching (lamâ‚• _) = âˆ˜â‚‘
  â‡’áµ¥â†’Matching prodË¢â‚•â‚ = fstâ‚‘
  â‡’áµ¥â†’Matching prodË¢â‚•â‚‚ = sndâ‚‘
  â‡’áµ¥â†’Matching (prodÊ·â‚• _) = prodrecâ‚‘
  â‡’áµ¥â†’Matching zeroâ‚• = natrecâ‚‘â‚€
  â‡’áµ¥â†’Matching (sucâ‚• _ _) = natrecâ‚‘â‚Š
  â‡’áµ¥â†’Matching starÊ·â‚• = unitrecâ‚‘
  â‡’áµ¥â†’Matching (unitrec-Î·â‚• Î·) = unitrec-Î· Î·
  â‡’áµ¥â†’Matching rflâ‚•â±¼ = Jâ‚‘
  â‡’áµ¥â†’Matching rflâ‚•â‚– = Kâ‚‘
  â‡’áµ¥â†’Matching rflâ‚•â‚‘ = []-congâ‚‘

opaque

  -- A variant of the previous property

  Â¬Matchingâ†’Â¬â‡’Ì¬ :
    Â¬ Matching t S
    â†’ âŸ¨ H , t , Ï , S âŸ© â‡’áµ¥ s â†’ âŠ¥
  Â¬Matchingâ†’Â¬â‡’Ì¬ Â¬m d = Â¬m (â‡’áµ¥â†’Matching d)

opaque

  -- A kind of inversion lemma for Final
  -- There are three different reasons a state can be Final:
  -- 1. It has a variable in head position but lookup does not succeed
  --    (for the number of copies matching the current stack
  --    multiplicity).
  -- 1b. It has a level of the form t âŠ” u in head position.
  -- 2. It has a value in head position, the stack is not empty and it
  --    is not the case that the head matches the stack and that the
  --    stack multiplicity does not exist.
  -- 3. It has a value in head position and the stack is empty.

  Final-reasons :
      âˆ€ t â†’ Final âŸ¨ H , t , Ï , S âŸ© â†’
      ((âˆƒ Î» x â†’ t â‰¡ var x Ã—
         (âˆ€ {p n Hâ€²} {c : Entry _ n} â†’ âˆ£ S âˆ£â‰¡ p â†’ H âŠ¢ wkVar Ï x â†¦[ p ] c â¨¾ Hâ€² â†’ âŠ¥)) âŠ
      (âˆƒâ‚‚ Î» u v â†’ t â‰¡ u supáµ˜ v)) âŠ
      (âˆƒâ‚‚ Î» e Sâ€² â†’ S â‰¡ e âˆ™ Sâ€² Ã— Value t Ã— Â¬ (Matching t S Ã— âˆƒ âˆ£ S âˆ£â‰¡_)) âŠ
      Value t Ã— S â‰¡ Îµ

  Final-reasons = Î» where
      (var x) Â¬d â†’ injâ‚ (injâ‚ (_ , refl , Î» x y â†’ Â¬d (â‡¾â‚‘ (var x y))))
      Level Â¬d â†’ injâ‚‚ (lemma Leveláµ¥ Â¬d)
      zeroáµ˜ Â¬d â†’ injâ‚‚ (lemma zeroáµ˜áµ¥ Â¬d)
      (sucáµ˜ t) Â¬d â†’ injâ‚‚ (lemma sucáµ˜áµ¥ Â¬d)
      (t supáµ˜ u) Â¬d â†’ injâ‚ (injâ‚‚ (_ , _ , refl))
      (Lift t A) Â¬d â†’ injâ‚‚ (lemma Liftáµ¥ Â¬d)
      (lift t) Â¬d â†’ injâ‚‚ (lemma liftáµ¥ Â¬d)
      (lower t) Â¬d â†’ âŠ¥-elim (Â¬d (â‡¾â‚‘â€² lowerâ‚•))
      (U x) Â¬d â†’ injâ‚‚ (lemma Uáµ¥ Â¬d)
      (Î Î£âŸ¨ b âŸ© p , q â–· t â–¹ tâ‚) Â¬d â†’ injâ‚‚ (lemma Î Î£áµ¥ Â¬d)
      (lam p t) Â¬d â†’ injâ‚‚ (lemma lamáµ¥ Â¬d)
      (t âˆ˜âŸ¨ p âŸ© tâ‚) Â¬d â†’ âŠ¥-elim (Â¬d (â‡¾â‚‘â€² appâ‚•))
      (prod x p t tâ‚) Â¬d â†’ injâ‚‚ (lemma prodáµ¥ Â¬d)
      (fst p t) Â¬d â†’ âŠ¥-elim (Â¬d (â‡¾â‚‘â€² fstâ‚•))
      (snd p t) Â¬d â†’ âŠ¥-elim (Â¬d (â‡¾â‚‘â€² sndâ‚•))
      (prodrec r p q t tâ‚ tâ‚‚) Â¬d â†’ âŠ¥-elim (Â¬d (â‡¾â‚‘â€² prodrecâ‚•))
      â„• Â¬d â†’ injâ‚‚ (lemma â„•áµ¥ Â¬d)
      zero Â¬d â†’ injâ‚‚ (lemma zeroáµ¥ Â¬d)
      (suc t) Â¬d â†’ injâ‚‚ (lemma sucáµ¥ Â¬d)
      (natrec p q r t tâ‚ tâ‚‚ tâ‚ƒ) Â¬d â†’ âŠ¥-elim (Â¬d (â‡¾â‚‘â€² natrecâ‚•))
      (Unit x) Â¬d â†’ injâ‚‚ (lemma Unitáµ¥ Â¬d)
      (star x) Â¬d â†’ injâ‚‚ (lemma staráµ¥ Â¬d)
      (unitrec p q t tâ‚ tâ‚‚) Â¬d â†’
        case UnitÊ·-Î·? of Î» where
          (yes Î·) â†’ injâ‚‚ (lemma (unitrec-Î·áµ¥ Î·) Â¬d)
          (no no-Î·) â†’ âŠ¥-elim (Â¬d (â‡¾â‚‘â€² (unitrecâ‚• no-Î·)))
      Empty Â¬d â†’ injâ‚‚ (lemma Emptyáµ¥ Â¬d)
      (emptyrec p t tâ‚) Â¬d â†’ âŠ¥-elim (Â¬d (â‡¾â‚‘â€² emptyrecâ‚•))
      (Id t tâ‚ tâ‚‚) Â¬d â†’ injâ‚‚ (lemma Idáµ¥ Â¬d)
      rfl Â¬d â†’ injâ‚‚ (lemma rfláµ¥ Â¬d)
      (J p q t tâ‚ tâ‚‚ tâ‚ƒ tâ‚„ tâ‚…) Â¬d â†’ âŠ¥-elim (Â¬d (â‡¾â‚‘â€² Jâ‚•))
      (K p t tâ‚ tâ‚‚ tâ‚ƒ tâ‚„) Â¬d â†’ âŠ¥-elim (Â¬d (â‡¾â‚‘â€² Kâ‚•))
      ([]-cong _ _ _ _ _ _) Â¬d â†’ âŠ¥-elim (Â¬d (â‡¾â‚‘â€² []-congâ‚•))
        where
        lemmaâ€² :
          Value t â†’ Final âŸ¨ H , t , Ï , e âˆ™ S âŸ© â†’
          Â¬ (Matching t (e âˆ™ S) Ã— âˆƒ âˆ£ e âˆ™ S âˆ£â‰¡_)
        lemmaâ€² liftáµ¥ Â¬d (lowerâ‚‘ , _ , _) =
          Â¬d (â‡’áµ¥ liftâ‚•)
        lemmaâ€² lamáµ¥ Â¬d (âˆ˜â‚‘ , _ , _ âˆ™ âˆ£Sâˆ£â‰¡) =
          Â¬d (â‡’áµ¥ lamâ‚• âˆ£Sâˆ£â‰¡)
        lemmaâ€² zeroáµ¥ Â¬d (natrecâ‚‘â‚€ , _ , _) =
          Â¬d (â‡’áµ¥ zeroâ‚•)
        lemmaâ€² sucáµ¥ Â¬d (natrecâ‚‘â‚Š , _ , natrecâ‚‘ âˆ£nrâˆ£â‰¡ âˆ™ âˆ£Sâˆ£â‰¡) =
          Â¬d (â‡’áµ¥ sucâ‚• âˆ£Sâˆ£â‰¡ âˆ£nrâˆ£â‰¡)
        lemmaâ€² staráµ¥ Â¬d (unitrecâ‚‘ , _ , _) =
          Â¬d (â‡’áµ¥ starÊ·â‚•)
        lemmaâ€² prodáµ¥ Â¬d (prodrecâ‚‘ , _ , _ âˆ™ âˆ£Sâˆ£â‰¡) =
          Â¬d (â‡’áµ¥ prodÊ·â‚• âˆ£Sâˆ£â‰¡)
        lemmaâ€² prodáµ¥ Â¬d (fstâ‚‘ , _ , _) =
          Â¬d (â‡’áµ¥ prodË¢â‚•â‚)
        lemmaâ€² prodáµ¥ Â¬d (sndâ‚‘ , _ , _) =
          Â¬d (â‡’áµ¥ prodË¢â‚•â‚‚)
        lemmaâ€² rfláµ¥ Â¬d (Jâ‚‘ , _ , _) =
          Â¬d (â‡’áµ¥ rflâ‚•â±¼)
        lemmaâ€² rfláµ¥ Â¬d (Kâ‚‘ , _ , _) =
          Â¬d (â‡’áµ¥ rflâ‚•â‚–)
        lemmaâ€² rfláµ¥ Â¬d ([]-congâ‚‘ , _ , _) =
          Â¬d (â‡’áµ¥ rflâ‚•â‚‘)
        lemmaâ€² Leveláµ¥ Â¬d (() , _ , _)
        lemmaâ€² zeroáµ˜áµ¥ Â¬d (() , _ , _)
        lemmaâ€² sucáµ˜áµ¥ Â¬d (() , _ , _)
        lemmaâ€² Liftáµ¥ Â¬d (() , _ , _)
        lemmaâ€² Uáµ¥ Â¬d (() , _ , _)
        lemmaâ€² Î Î£áµ¥ Â¬d (() , _ , _)
        lemmaâ€² â„•áµ¥ Â¬d (() , _ , _)
        lemmaâ€² Unitáµ¥ Â¬d (() , _ , _)
        lemmaâ€² Emptyáµ¥ Â¬d (() , _ , _)
        lemmaâ€² Idáµ¥ Â¬d (() , _ , _)
        lemmaâ€² (unitrec-Î·áµ¥ x) Â¬d _ =
          Â¬d (â‡’áµ¥ unitrec-Î·â‚• x)
        lemma : âˆ€ {S : Stack m} â†’ Value t â†’ Final âŸ¨ H , t , Ï , S âŸ© â†’
                (âˆƒâ‚‚ Î» e Sâ€² â†’ S â‰¡ e âˆ™ Sâ€² Ã— Value t Ã— Â¬ (Matching t S Ã— âˆƒ âˆ£ S âˆ£â‰¡_)) âŠ
                Value t Ã— S â‰¡ Îµ
        lemma {S = Îµ} v _ = injâ‚‚ (v , refl)
        lemma {S = e âˆ™ S} v Â¬d = injâ‚ (_ , _ , refl , v , lemmaâ€² v Â¬d)

  opaque

    -- A variant of the above property.

    â‡˜-reasons :
      s â‡˜ âŸ¨ H , t , Ï , S âŸ© â†’
      ((âˆƒ Î» x â†’ t â‰¡ var x Ã—
         (âˆ€ {p n Hâ€²} {c : Entry _ n} â†’ âˆ£ S âˆ£â‰¡ p â†’ H âŠ¢ wkVar Ï x â†¦[ p ] c â¨¾ Hâ€² â†’ âŠ¥)) âŠ
      (âˆƒâ‚‚ Î» u v â†’ t â‰¡ u supáµ˜ v)) âŠ
      (âˆƒâ‚‚ Î» e Sâ€² â†’ S â‰¡ e âˆ™ Sâ€² Ã— Value t Ã— Â¬ (Matching t S Ã— (âˆƒ âˆ£ S âˆ£â‰¡_))) âŠ
      Value t Ã— S â‰¡ Îµ
    â‡˜-reasons (d , Â¬d) = Final-reasons _ Â¬d

opaque

  -- A kind of inversion lemma for Final when having natrec tokens on
  -- the stack implies that the usage rule for natrec with an nr function
  -- is used.
  --
  -- In this case there are three different reasons a state can be Final:
  -- 1. It has a variable in head position but lookup does not succeed
  --    (for the number of copies matching the current stack
  --    multiplicity).
  -- 1b. It has a level of the form t âŠ” u in head position.
  -- 2. It has a value in head position, the stack is not empty and the
  --    head does not match the stack.
  -- 3. It has a value in head position and the stack is empty.

  nrâˆ‰-Final-reasons :
      âˆ€ t â†’ (âˆ€ {p r} â†’ natrec p , r âˆˆ S â†’ Nr-available) â†’
      Final âŸ¨ H , t , Ï , S âŸ© â†’
      ((âˆƒ Î» x â†’ t â‰¡ var x Ã—
         (âˆ€ {p n Hâ€²} {c : Entry _ n} â†’ âˆ£ S âˆ£â‰¡ p â†’ H âŠ¢ wkVar Ï x â†¦[ p ] c â¨¾ Hâ€² â†’ âŠ¥)) âŠ
      (âˆƒâ‚‚ Î» u v â†’ t â‰¡ u supáµ˜ v)) âŠ
      (âˆƒâ‚‚ Î» e Sâ€² â†’ S â‰¡ e âˆ™ Sâ€² Ã— Value t Ã— Â¬ Matching t S) âŠ
      Value t Ã— S â‰¡ Îµ
  nrâˆ‰-Final-reasons t has-nr Â¬d =
    case Final-reasons t Â¬d of Î» where
      (injâ‚ x) â†’ injâ‚ x
      (injâ‚‚ (injâ‚ (e , Sâ€² , Sâ‰¡ , v , prop))) â†’
        injâ‚‚ (injâ‚ (e , Sâ€² , Sâ‰¡ , v , Î» m â†’ prop (m , âˆ£âˆ£â‰¡ has-nr)))
      (injâ‚‚ (injâ‚‚ x)) â†’ injâ‚‚ (injâ‚‚ x)

opaque

  -- A variant of the above where the stack is assumed to not contain
  -- any natrecâ‚‘ tokens.
  --
  -- In this case there are three different reasons a state can be Final:
  -- 1. It has a variable in head position but lookup does not succeed
  --    (for the number of copies matching the current stack
  --    multiplicity).
  -- 1b. It has a level of the form t âŠ” u in head position.
  -- 2. It has a value in head position, the stack is not empty and the
  --    head does not match the stack.
  -- 3. It has a value in head position and the stack is empty.

  nrâˆ‰-Final-reasonsâ€² :
      âˆ€ t â†’ (âˆ€ {p r} â†’ natrec p , r âˆˆ S â†’ âŠ¥) â†’
      Final âŸ¨ H , t , Ï , S âŸ© â†’
      ((âˆƒ Î» x â†’ t â‰¡ var x Ã—
         (âˆ€ {p n Hâ€²} {c : Entry _ n} â†’ âˆ£ S âˆ£â‰¡ p â†’ H âŠ¢ wkVar Ï x â†¦[ p ] c â¨¾ Hâ€² â†’ âŠ¥)) âŠ
      (âˆƒâ‚‚ Î» u v â†’ t â‰¡ u supáµ˜ v)) âŠ
      (âˆƒâ‚‚ Î» e Sâ€² â†’ S â‰¡ e âˆ™ Sâ€² Ã— Value t Ã— Â¬ Matching t S) âŠ
      Value t Ã— S â‰¡ Îµ
  nrâˆ‰-Final-reasonsâ€² t nrâˆ‰ Â¬d =
    nrâˆ‰-Final-reasons t (âŠ¥-elim âˆ˜â†’ nrâˆ‰) Â¬d

opaque

  -- Values do not reduce with the reduction for elims.

  Value-Â¬â‡’â‚‘ : Value t â†’ âŸ¨ H , t , Ï , S âŸ© â‡’â‚‘ s â†’ âŠ¥
  Value-Â¬â‡’â‚‘ () lowerâ‚•
  Value-Â¬â‡’â‚‘ () appâ‚•
  Value-Â¬â‡’â‚‘ () fstâ‚•
  Value-Â¬â‡’â‚‘ () sndâ‚•
  Value-Â¬â‡’â‚‘ () prodrecâ‚•
  Value-Â¬â‡’â‚‘ () natrecâ‚•
  Value-Â¬â‡’â‚‘ (unitrec-Î·áµ¥ Î·) (unitrecâ‚• no-Î·) = no-Î· Î·
  Value-Â¬â‡’â‚‘ () emptyrecâ‚•
  Value-Â¬â‡’â‚‘ () Jâ‚•
  Value-Â¬â‡’â‚‘ () Kâ‚•
  Value-Â¬â‡’â‚‘ () []-congâ‚•

opaque

  -- Values that reduce do so with the reduction for values

  Value-â‡¾â†’â‡’áµ¥ : Value t â†’ âŸ¨ H , t , Ï , S âŸ© â‡¾ sâ€² â†’ âŸ¨ H , t , Ï , S âŸ© â‡’áµ¥ sâ€²
  Value-â‡¾â†’â‡’áµ¥ v (â‡¾â‚‘â€² d) = âŠ¥-elim (Value-Â¬â‡’â‚‘ v d)
  Value-â‡¾â†’â‡’áµ¥ () (â‡¾â‚‘ (var _ _))
  Value-â‡¾â†’â‡’áµ¥ _ (â‡’áµ¥ d) = d

opaque

  -- Normal form states that reduce do so with the reduction for values

  Normal-â‡¾â†’â‡’áµ¥ : Normal s â†’ s â‡¾ sâ€² â†’ s â‡’áµ¥ sâ€²
  Normal-â‡¾â†’â‡’áµ¥ (val v) d = Value-â‡¾â†’â‡’áµ¥ v d
  Normal-â‡¾â†’â‡’áµ¥ (var x) (â‡¾â‚‘ d) =
    let _ , _ , _ , dâ€² = â‡¾â‚‘-inv-var d
    in  âŠ¥-elim (Â¬â†¦âˆ§â†¦â— (â†¦[]â†’â†¦ dâ€²) x)
  Normal-â‡¾â†’â‡’áµ¥ (var _) (â‡’áµ¥ ())
  Normal-â‡¾â†’â‡’áµ¥ sup (â‡¾â‚‘â€² ())
  Normal-â‡¾â†’â‡’áµ¥ sup (â‡’áµ¥ ())
