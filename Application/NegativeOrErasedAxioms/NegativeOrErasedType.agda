------------------------------------------------------------------------
-- Negative types (with support for erasure)
------------------------------------------------------------------------

open import Graded.Modality
open import Definition.Typed.Restrictions

module Application.NegativeOrErasedAxioms.NegativeOrErasedType
  {a} {M : Set a}
  {ğ•„ : Modality M}
  (R : Type-restrictions ğ•„)
  where

open Modality ğ•„

open import Definition.Untyped M as U

open import Definition.Typed R
open import Definition.Typed.Properties R
open import Definition.Typed.Weakening R as T
open import Definition.Typed.Consequences.Inequality R as I
open import Definition.Typed.Consequences.Injectivity R
open import Definition.Typed.Consequences.Substitution R

open import Tools.Empty
open import Tools.Function
open import Tools.Nat
open import Tools.Product
open import Tools.PropositionalEquality using (_â‰¢_)
open import Tools.Relation

private variable
  m n   : Nat
  Ï     : Wk m n
  Ïƒ     : Subst m n
  Î“ Î”   : Con Term m
  A B C : Term m
  t u   : Term m
  l     : Universe-level
  p q   : M

-- Negative types.

data NegativeType (Î“ : Con Term m) : Term m â†’ Set a where
  empty : NegativeType Î“ Empty

  pi : Î“ âŠ¢ A â†’
       NegativeType (Î“ âˆ™ A) B â†’
       NegativeType Î“ (Î  p , q â–· A â–¹ B)

  sigma-ğŸ˜ : Î“ âŠ¢ A â†’
            NegativeType (Î“ âˆ™ A) B â†’
            NegativeType Î“ (Î£Ë¢ ğŸ˜ , q â–· A â–¹ B)

  sigma : Î“ âŠ¢ A â†’
          NegativeType Î“ A â†’
          NegativeType (Î“ âˆ™ A) B â†’
          NegativeType Î“ (Î£Ë¢ p , q â–· A â–¹ B)

  conv  : NegativeType Î“ A â†’
          Î“ âŠ¢ A â‰¡ B â†’
          NegativeType Î“ B

-- Negative types are closed under weakening.

wkNeg : Ï âˆ· Î” âŠ‡ Î“ â†’ âŠ¢ Î” â†’ NegativeType Î“ A â†’ NegativeType Î” (U.wk Ï A)
wkNeg w âŠ¢Î” empty =
  empty

wkNeg w âŠ¢Î” (pi dA nB) =
  pi dAâ€² (wkNeg (lift w) (âŠ¢Î” âˆ™ dAâ€²) nB)
  where dAâ€² = T.wk w âŠ¢Î” dA

wkNeg w âŠ¢Î” (sigma-ğŸ˜ dA nB) =
  sigma-ğŸ˜ dAâ€² (wkNeg (lift w) (âŠ¢Î” âˆ™ dAâ€²) nB)
  where dAâ€² = T.wk w âŠ¢Î” dA

wkNeg w âŠ¢Î” (sigma dA nA nB) =
  sigma dAâ€² (wkNeg w âŠ¢Î” nA) (wkNeg (lift w) (âŠ¢Î” âˆ™ dAâ€²) nB)
  where dAâ€² = T.wk w âŠ¢Î” dA

wkNeg w âŠ¢Î” (conv n c) =
  conv (wkNeg w âŠ¢Î” n) (wkEq w âŠ¢Î” c)

-- Negative types are closed under parallel substitution.

subNeg :
  NegativeType Î“ A â†’ Î” âŠ¢Ë¢ Ïƒ âˆ· Î“ â†’ âŠ¢ Î” â†’ NegativeType Î” (A [ Ïƒ ])

subNeg empty _ _ = empty

subNeg (pi âŠ¢A n) s âŠ¢Î” =
  pi âŠ¢ÏƒA (subNeg n (liftSubstâ€² (wf âŠ¢A) âŠ¢Î” âŠ¢A s) (âŠ¢Î” âˆ™ âŠ¢ÏƒA))
  where âŠ¢ÏƒA = substitution âŠ¢A s âŠ¢Î”

subNeg (sigma-ğŸ˜ âŠ¢A n) s âŠ¢Î” =
  sigma-ğŸ˜ âŠ¢ÏƒA (subNeg n (liftSubstâ€² (wf âŠ¢A) âŠ¢Î” âŠ¢A s) (âŠ¢Î” âˆ™ âŠ¢ÏƒA))
  where âŠ¢ÏƒA = substitution âŠ¢A s âŠ¢Î”

subNeg (sigma âŠ¢A nA nB) s âŠ¢Î” =
  sigma âŠ¢ÏƒA (subNeg nA s âŠ¢Î”)
    (subNeg nB (liftSubstâ€² (wf âŠ¢A) âŠ¢Î” âŠ¢A s) (âŠ¢Î” âˆ™ âŠ¢ÏƒA))
  where âŠ¢ÏƒA = substitution âŠ¢A s âŠ¢Î”

subNeg (conv n c) s âŠ¢Î” =
  conv (subNeg n s âŠ¢Î”) (substitutionEq c (substRefl s) âŠ¢Î”)

-- Negative types are closed under single substitutions.

subNeg1 : NegativeType (Î“ âˆ™ A) B â†’ Î“ âŠ¢ t âˆ· A â†’ NegativeType Î“ (B [ t ]â‚€)
subNeg1 n âŠ¢t = subNeg n (singleSubst âŠ¢t) (wfTerm âŠ¢t)

-- The first component of a negative Î£-type is negative if the
-- quantity is not ğŸ˜.

fstNeg :
  NegativeType Î“ C â†’
  Î“ âŠ¢ C â‰¡ Î£Ë¢ p , q â–· A â–¹ B â†’
  ğŸ˜ â‰¢ p â†’
  NegativeType Î“ A
fstNeg empty          c  _   = âŠ¥-elim (Emptyâ‰¢Î£â±¼ c)
fstNeg (pi _ _)       c  _   = âŠ¥-elim (Î â‰¢Î£â±¼ c)
fstNeg (sigma-ğŸ˜ _ _)  c  ğŸ˜â‰¢p = let _ , _ , ğŸ˜â‰¡p , _ = Î£-injectivity c in
                               âŠ¥-elim (ğŸ˜â‰¢p ğŸ˜â‰¡p)
fstNeg (sigma _ nA _) c  _   = conv nA (projâ‚ (Î£-injectivity c))
fstNeg (conv n c)     câ€² ğŸ˜â‰¢p = fstNeg n (trans c câ€²) ğŸ˜â‰¢p

-- Any instance of the second component of a negative Î£-type is
-- negative.

sndNeg :
  NegativeType Î“ C â†’
  Î“ âŠ¢ C â‰¡ Î£Ë¢ p , q â–· A â–¹ B â†’
  Î“ âŠ¢ t âˆ· A â†’
  NegativeType Î“ (B [ t ]â‚€)
sndNeg empty          c    = âŠ¥-elim (Emptyâ‰¢Î£â±¼ c)
sndNeg (pi _ _)       c    = âŠ¥-elim (Î â‰¢Î£â±¼ c)
sndNeg (sigma-ğŸ˜ _ nB) c âŠ¢t =
  let (cA , cB , _ , _) = Î£-injectivity c in
  subNeg (conv nB cB) (singleSubst (conv âŠ¢t (sym cA))) (wfTerm âŠ¢t)
sndNeg (sigma _ _ nB) c âŠ¢t =
  let (cA , cB , _ , _) = Î£-injectivity c in
  subNeg (conv nB cB) (singleSubst (conv âŠ¢t (sym cA))) (wfTerm âŠ¢t)
sndNeg (conv n c) câ€² = sndNeg n (trans c câ€²)

-- Any instance of the codomain of a negative Î -type is negative.

appNeg :
  NegativeType Î“ C â†’
  Î“ âŠ¢ C â‰¡ Î  p , q â–· A â–¹ B â†’
  Î“ âŠ¢ t âˆ· A â†’
  NegativeType Î“ (B [ t ]â‚€)
appNeg empty          c = âŠ¥-elim (Emptyâ‰¢Î â±¼ c)
appNeg (sigma-ğŸ˜ _ _)  c = âŠ¥-elim (Î â‰¢Î£â±¼ (sym c))
appNeg (sigma _ _ _)  c = âŠ¥-elim (Î â‰¢Î£â±¼ (sym c))
appNeg (pi _ nB) c âŠ¢t =
  let (cA , cB , _ , _) = injectivity c in
  subNeg (conv nB cB) (singleSubst (conv âŠ¢t (sym cA))) (wfTerm âŠ¢t)
appNeg (conv n c) câ€² = appNeg n (trans c câ€²)

-- The type â„• is not negative.

Â¬negâ„• : NegativeType Î“ C â†’ Î“ âŠ¢ C â‰¡ â„• â†’ âŠ¥
Â¬negâ„• empty         c  = â„•â‰¢Emptyâ±¼ (sym c)
Â¬negâ„• (pi _ _)      c  = â„•â‰¢Î  (sym c)
Â¬negâ„• (sigma-ğŸ˜ _ _) c  = â„•â‰¢Î£ (sym c)
Â¬negâ„• (sigma _ _ _) c  = â„•â‰¢Î£ (sym c)
Â¬negâ„• (conv n c)    câ€² = Â¬negâ„• n (trans c câ€²)

-- Î£Ê·-types are not negative.

Â¬negÎ£Ê· : NegativeType Î“ C â†’ Î“ âŠ¢ C â‰¡ Î£Ê· p , q â–· A â–¹ B â†’ âŠ¥
Â¬negÎ£Ê· empty         c  = Emptyâ‰¢Bâ±¼ BÎ£! c
Â¬negÎ£Ê· (pi _ _)      c  = Î â‰¢Î£â±¼ c
Â¬negÎ£Ê· (sigma-ğŸ˜ _ _) c  = Î£Ë¢â‰¢Î£Ê·â±¼ c
Â¬negÎ£Ê· (sigma _ _ _) c  = Î£Ë¢â‰¢Î£Ê·â±¼ c
Â¬negÎ£Ê· (conv n c)    câ€² = Â¬negÎ£Ê· n (trans c câ€²)

-- Unit types are not negative

Â¬negUnit : âˆ€ {s} â†’ NegativeType Î“ C â†’ Î“ âŠ¢ C â‰¡ Unit s l â†’ âŠ¥
Â¬negUnit empty         c  = Emptyâ‰¢Unitâ±¼ c
Â¬negUnit (pi _ _)      c  = Unitâ‰¢Î â±¼ (sym c)
Â¬negUnit (sigma-ğŸ˜ _ _) c  = Unitâ‰¢Î£â±¼ (sym c)
Â¬negUnit (sigma _ _ _) c  = Unitâ‰¢Î£â±¼ (sym c)
Â¬negUnit (conv n c)    câ€² = Â¬negUnit n (trans c câ€²)

opaque

  -- Identity types are not negative.

  Â¬negId : NegativeType Î“ A â†’ Â¬ Î“ âŠ¢ A â‰¡ Id B t u
  Â¬negId empty         = Idâ‰¢Empty âˆ˜â†’ sym
  Â¬negId (pi _ _)      = I.Idâ‰¢Î Î£ âˆ˜â†’ sym
  Â¬negId (sigma-ğŸ˜ _ _) = I.Idâ‰¢Î Î£ âˆ˜â†’ sym
  Â¬negId (sigma _ _ _) = I.Idâ‰¢Î Î£ âˆ˜â†’ sym
  Â¬negId (conv n Bâ‰¡A)  = Â¬negId n âˆ˜â†’ trans Bâ‰¡A
