-- Proof that consistent negative axioms do not jeopardize canonicity.
-- https://www.cs.bham.ac.uk/~mhe/papers/negative-axioms.pdf

{-# OPTIONS --without-K --safe #-}

module Application.NegativeAxioms.Canonicity where

open import Definition.Modality.Instances.Erasure
open import Definition.Modality.Instances.Erasure.Modality (_â‰¤ Ï‰)
open import Definition.Modality.Instances.Erasure.Properties (_â‰¤ Ï‰)
open import Definition.Modality.Context ErasureModality
open import Definition.Modality.Usage ErasureModality
open import Definition.Modality.Usage.Inversion ErasureModality
open import Definition.Modality.FullReduction ErasureModality greatest-elem

open import Definition.Untyped Erasure as U hiding (_âˆ·_; â„•â‰¢B)

open import Definition.Typed Erasureâ€²
open import Definition.Typed.Properties Erasureâ€²
open import Definition.Typed.Weakening Erasureâ€² as T
open import Definition.Typed.Consequences.Inequality Erasureâ€²
open import Definition.Typed.Consequences.Injectivity Erasureâ€²
open import Definition.Typed.Consequences.Inversion Erasureâ€²
open import Definition.Typed.Consequences.Substitution Erasureâ€²
open import Definition.Typed.Consequences.Syntactic Erasureâ€²

open import Definition.Conversion.FullReduction Erasureâ€² hiding (fullRedTerm)

open import Tools.Empty
open import Tools.Fin
open import Tools.Nat
open import Tools.Product
open import Tools.Sum using (_âŠ_; injâ‚; injâ‚‚)
open import Tools.Unit

-- Preliminaries
---------------------------------------------------------------------------

private
  Ty  = Term
  Cxt = Con Ty
  variable
    m m'  : Nat
    x     : Fin m
    Ï     : Wk m m'
    Ïƒ     : Subst m m'
    Î“ Î”   : Con Term m
    A B C : Term m
    t u   : Term m
    p q   : Erasure
    Î³     : Conâ‚˜ m

-- Numerals

data Numeral {m : Nat} : Term m â†’ Set where
  zeroâ‚™ : Numeral zero
  sucâ‚™  : Numeral t â†’ Numeral (suc t)

-- Negative types
---------------------------------------------------------------------------

-- A type is negative if all of its branches end in âŠ¥.
-- The prime example is negation Â¬A.

data NegativeType (Î“ : Cxt m) : Ty m â†’ Set where

  empty : NegativeType Î“ Empty

  pi    : Î“ âŠ¢ A
        â†’ NegativeType (Î“ âˆ™ A) B
        â†’ NegativeType Î“ (Î  p , q â–· A â–¹ B)

  sigma : Î“ âŠ¢ A
        â†’ NegativeType Î“ A
        â†’ NegativeType (Î“ âˆ™ A) B
        â†’ NegativeType Î“ (Î£â‚š q â–· A â–¹ B)

  conv  : NegativeType Î“ A
        â†’ Î“ âŠ¢ A â‰¡ B
        â†’ NegativeType Î“ B

-- Lemma: Negative types are closed under weakening.

wkNeg : Ï âˆ· Î” âŠ† Î“ â†’ âŠ¢ Î” â†’ NegativeType Î“ A â†’ NegativeType Î” (U.wk Ï A)

wkNeg w âŠ¢Î” empty
  = empty

wkNeg w âŠ¢Î” (pi dA nB)
  = pi dA' (wkNeg (lift w) (âŠ¢Î” âˆ™ dA') nB)
    where dA' = T.wk w âŠ¢Î” dA

wkNeg w âŠ¢Î” (sigma dA nA nB)
  = sigma dA' (wkNeg w âŠ¢Î” nA) (wkNeg (lift w) (âŠ¢Î” âˆ™ dA') nB)
    where dA' = T.wk w âŠ¢Î” dA

wkNeg w âŠ¢Î” (conv n c)
  = conv (wkNeg w âŠ¢Î” n) (wkEq w âŠ¢Î” c)

-- Lemma: Negative types are closed under parallel substitution.

subNeg : NegativeType Î“ A â†’ Î” âŠ¢Ë¢ Ïƒ âˆ· Î“ â†’ âŠ¢ Î” â†’ NegativeType Î” (subst Ïƒ A)

subNeg empty _ _ = empty

subNeg (pi âŠ¢A n) s âŠ¢Î”
  = pi âŠ¢ÏƒA (subNeg n (liftSubstâ€² (wf âŠ¢A) âŠ¢Î” âŠ¢A s) (âŠ¢Î” âˆ™ âŠ¢ÏƒA))
    where âŠ¢ÏƒA = substitution âŠ¢A s âŠ¢Î”

subNeg (sigma âŠ¢A nA nB) s âŠ¢Î”
  = sigma âŠ¢ÏƒA (subNeg nA s âŠ¢Î”) (subNeg nB (liftSubstâ€² (wf âŠ¢A) âŠ¢Î” âŠ¢A s) (âŠ¢Î” âˆ™ âŠ¢ÏƒA))
    where âŠ¢ÏƒA = substitution âŠ¢A s âŠ¢Î”

subNeg (conv n c) s âŠ¢Î” = conv (subNeg n s âŠ¢Î”) (substitutionEq c (substRefl s) âŠ¢Î”)

-- Corollary: Negative types are closed under single substitution.

subNeg1 : NegativeType (Î“ âˆ™ A) B â†’ Î“ âŠ¢ t âˆ· A â†’ NegativeType Î“ (B [ t ])
subNeg1 n âŠ¢t = subNeg n (singleSubst âŠ¢t) (wfTerm âŠ¢t)

-- Lemma: The first component of a negative Î£-type is negative.

fstNeg : NegativeType Î“ C â†’ Î“ âŠ¢ C â‰¡ Î£â‚š q â–· A â–¹ B â†’ NegativeType Î“ A
fstNeg empty          c = âŠ¥-elim (Emptyâ‰¢Î£â±¼ c)
fstNeg (pi _ _)       c = âŠ¥-elim (Î â‰¢Î£â±¼ c)
fstNeg (sigma _ nA _) c = conv nA (projâ‚ (Î£-injectivity c))
fstNeg (conv n c)    c' = fstNeg n (trans c c')

-- Lemma: Any instance of the second component of a negative Î£-type is negative.

sndNeg : NegativeType Î“ C â†’ Î“ âŠ¢ C â‰¡ Î£â‚š q â–· A â–¹ B â†’ Î“ âŠ¢ t âˆ· A â†’ NegativeType Î“ (B [ t ])
sndNeg empty          c = âŠ¥-elim (Emptyâ‰¢Î£â±¼ c)
sndNeg (pi _ _)       c = âŠ¥-elim (Î â‰¢Î£â±¼ c)
sndNeg (sigma _ _ nB) c âŠ¢t = let (cA , cB , _ , _) = Î£-injectivity c in
    subNeg (conv nB cB) (singleSubst (conv âŠ¢t (sym cA))) (wfTerm âŠ¢t)
sndNeg (conv n c)    c' = sndNeg n (trans c c')

-- Lemma: Any instance of the codomain of a negative Î -type is negative.

appNeg : NegativeType Î“ C â†’ Î“ âŠ¢ C â‰¡ Î  p , q â–· A â–¹ B â†’ Î“ âŠ¢ t âˆ· A â†’ NegativeType Î“ (B [ t ])
appNeg empty          c = âŠ¥-elim (Emptyâ‰¢Î â±¼ c)
appNeg (sigma _ _ _)  c = âŠ¥-elim (Î â‰¢Î£â±¼ (sym c))
appNeg (pi _ nB) c âŠ¢t = let (cA , cB , _ , _) = injectivity c in
  subNeg (conv nB cB) (singleSubst (conv âŠ¢t (sym cA))) (wfTerm âŠ¢t)
appNeg (conv n c)    c' = appNeg n (trans c c')

-- Lemma: The type â„• is not negative.

Â¬negâ„• : NegativeType Î“ C â†’ Î“ âŠ¢ C â‰¡ â„• â†’ âŠ¥
Â¬negâ„• empty         c = â„•â‰¢Emptyâ±¼ (sym c)
Â¬negâ„• (pi _ _)      c = â„•â‰¢Î  (sym c)
Â¬negâ„• (sigma _ _ _) c = â„•â‰¢Î£ (sym c)
Â¬negâ„• (conv n c)   c' = Â¬negâ„• n (trans c c')

-- Lemma: The type Î£áµ£ is not negative

Â¬negÎ£áµ£ : NegativeType Î“ C â†’ Î“ âŠ¢ C â‰¡ Î£áµ£ q â–· A â–¹ B â†’ âŠ¥
Â¬negÎ£áµ£ empty         c = Emptyâ‰¢Bâ±¼ BÎ£! c
Â¬negÎ£áµ£ (pi _ _)      c = Î â‰¢Î£â±¼ c
Â¬negÎ£áµ£ (sigma _ _ _) c = Î£â‚šâ‰¢Î£áµ£â±¼ c
Â¬negÎ£áµ£ (conv n c)   c' = Â¬negÎ£áµ£ n (trans c c')

-- Negative contexts
---------------------------------------------------------------------------

-- A context is negative if all of its type entries are negative.

data NegativeErasedContext : Con Ty m â†’ Conâ‚˜ m â†’ Set where
  Îµ   : NegativeErasedContext Îµ Îµ
  _âˆ™_ : NegativeErasedContext Î“ Î³ â†’ NegativeType Î“ A â†’ NegativeErasedContext (Î“ âˆ™ A) (Î³ âˆ™ p)
  _âˆ™ğŸ˜ : NegativeErasedContext Î“ Î³ â†’ NegativeErasedContext (Î“ âˆ™ A) (Î³ âˆ™ ğŸ˜)

-- Lemma: Any entry in negative context is a negative type (needs weakening).

lookupNegative : âŠ¢ Î“ â†’ NegativeErasedContext Î“ Î³ â†’ (x âˆ· A âˆˆ Î“) â†’ (x â—‚ Ï‰ âˆˆ Î³) â†’ NegativeType Î“ A
lookupNegative âŠ¢Î“âˆ™A            (nÎ“ âˆ™ nA) here _
  = wkNeg (step id) âŠ¢Î“âˆ™A nA
lookupNegative âŠ¢Î“âˆ™A@(âŠ¢Î“ âˆ™ Î“âŠ¢A) (nÎ“ âˆ™ nA) (there h) (there j)
  = wkNeg (step id) âŠ¢Î“âˆ™A (lookupNegative âŠ¢Î“ nÎ“ h j)
lookupNegative âŠ¢Î“âˆ™A (nÎ“Î³ âˆ™ğŸ˜) here ()
lookupNegative âŠ¢Î“âˆ™A@(âŠ¢Î“ âˆ™ Î“âŠ¢A) (nÎ“Î³ âˆ™ğŸ˜) (there h) (there j) =
  wkNeg (step id) âŠ¢Î“âˆ™A (lookupNegative âŠ¢Î“ nÎ“Î³ h j)

-- Main results
---------------------------------------------------------------------------

-- We assume a negative, consistent context.

module Main (nÎ“Î³ : NegativeErasedContext Î“ Î³) (consistent : âˆ€{t} â†’ Î“ âŠ¢ t âˆ· Empty â†’ âŠ¥) where

  open import Definition.Typed.Consequences.Reduction Erasureâ€²
  open import Definition.Typed.Usage ErasureModality

  -- Lemma: A neutral has negative type in a consistent negative/erased context.

  neNeg : (d : Î“ âŠ¢ u âˆ· A) (n : Neutral u) (f : Î³ â–¸ u) â†’ NegativeType Î“ A
  neNeg (var âŠ¢Î“ h          ) (var _      ) Î³â–¸u = lookupNegative âŠ¢Î“ nÎ“Î³ h (valid-var-usage Î³â–¸u)
  neNeg (d âˆ˜â±¼ âŠ¢t           ) (âˆ˜â‚™ n       ) Î³â–¸u =
    let invUsageApp Î´â–¸g Î·â–¸a Î³â‰¤Î³â€² = inv-usage-app Î³â–¸u
    in  appNeg (neNeg d n (sub Î´â–¸g (â‰¤á¶œ-trans Î³â‰¤Î³â€² (+á¶œ-decreasingË¡ _ _))))
               (refl (syntacticTerm d)) âŠ¢t
  neNeg (fstâ±¼ âŠ¢A AâŠ¢B d     ) (fstâ‚™ n     ) Î³â–¸u =
    let invUsageProj Î´â–¸t Î³â‰¤Î´ = inv-usage-fst Î³â–¸u
    in  fstNeg (neNeg d n (sub Î´â–¸t Î³â‰¤Î´))
               (refl (Î£â±¼ âŠ¢A â–¹ AâŠ¢B))
  neNeg (sndâ±¼ âŠ¢A AâŠ¢B d     ) (sndâ‚™ n     ) Î³â–¸u =
    let invUsageProj Î´â–¸t Î³â‰¤Î´ = inv-usage-snd Î³â–¸u
    in  sndNeg (neNeg d n (sub Î´â–¸t Î³â‰¤Î´))
               (refl (Î£â±¼ âŠ¢A â–¹ AâŠ¢B)) (fstâ±¼ âŠ¢A AâŠ¢B d)
  neNeg (natrecâ±¼ _ _ _ d   ) (natrecâ‚™ n  ) Î³â–¸u =
    let invUsageNatrec _ _ Î´â–¸n Î³â‰¤Î³â€² = inv-usage-natrec Î³â–¸u
        âŠ¢â„• = refl (â„•â±¼ (wfTerm d))
        Î³â–¸n = sub Î´â–¸n (â‰¤á¶œ-trans Î³â‰¤Î³â€² (â‰¤á¶œ-trans (âŠ›á¶œ-ineqâ‚‚ _ _ _) (âˆ§á¶œ-decreasingÊ³ _ _)))
    in  âŠ¥-elim (Â¬negâ„• (neNeg d n Î³â–¸n) âŠ¢â„•)
  neNeg (prodrecâ±¼ âŠ¢A AâŠ¢B _ d _) (prodrecâ‚™ n ) Î³â–¸u =
    let invUsageProdrec Î´â–¸t Î·â–¸u pâ‰¤Ï‰ Î³â‰¤Î³â€² = inv-usage-prodrec Î³â–¸u
        Î³â–¸t = sub Î´â–¸t (â‰¤á¶œ-trans Î³â‰¤Î³â€² (â‰¤á¶œ-trans (+á¶œ-decreasingË¡ _ _)
                                (â‰¤á¶œ-trans (Â·á¶œ-monotoneË¡ pâ‰¤Ï‰) (â‰¤á¶œ-reflexive (Â·á¶œ-identityË¡ _)))))
        âŠ¢Î£ = refl (Î£â±¼ âŠ¢A â–¹ AâŠ¢B)
    in  âŠ¥-elim (Â¬negÎ£áµ£ (neNeg d n Î³â–¸t) âŠ¢Î£)
  neNeg (Emptyrecâ±¼ _ d     ) (Emptyrecâ‚™ n) Î³â–¸u = âŠ¥-elim (consistent d)
  neNeg (conv d c          ) n             Î³â–¸u = conv (neNeg d n Î³â–¸u) c

  -- Lemma: A normal form of type â„• is a numeral in a consistent negative context.

  nfN : (d : Î“ âŠ¢ u âˆ· A)
      â†’ (m : Î³ â–¸ u)
      â†’ (n : Nf u)
      â†’ (c : Î“ âŠ¢ A â‰¡ â„•)
      â†’ Numeral u

  -- Case: neutrals. The type cannot be â„• since it must be negative.
  nfN d Î³â–¸u (ne n) c = âŠ¥-elim (Â¬negâ„• (neNeg d (nfNeutral n) Î³â–¸u) c)
  -- âŠ¥-elim (Â¬negâ„• (neNeg d n) c)

  -- Case: numerals.
  nfN (zeroâ±¼ x) Î³â–¸u zeroâ‚™   c = zeroâ‚™
  nfN (sucâ±¼ d) Î³â–¸u (sucâ‚™ n) c =
    let invUsageSuc Î´â–¸n Î³â‰¤Î´ = inv-usage-suc Î³â–¸u
    in  sucâ‚™ (nfN d (sub Î´â–¸n Î³â‰¤Î´) n c)

  -- Case: conversion.
  nfN (conv d c) Î³â–¸u n c' = nfN d Î³â–¸u n (trans c c')

  -- Impossible cases: type is not â„•.

  -- * Canonical types
  nfN (Î â±¼ _ â–¹ _)       Î³â–¸u (Î â‚™ _ _)   c = âŠ¥-elim (Uâ‰¢â„• c)
  nfN (Î£â±¼ _ â–¹ _)       Î³â–¸u (Î£â‚™ _ _)   c = âŠ¥-elim (Uâ‰¢â„• c)
  nfN (â„•â±¼ _)           Î³â–¸u â„•â‚™         c = âŠ¥-elim (Uâ‰¢â„• c)
  nfN (Emptyâ±¼ _)       Î³â–¸u Emptyâ‚™     c = âŠ¥-elim (Uâ‰¢â„• c)
  nfN (Unitâ±¼ _)        Î³â–¸u Unitâ‚™      c = âŠ¥-elim (Uâ‰¢â„• c)

  -- * Canonical forms
  nfN (lamâ±¼ _ _)      Î³â–¸u (lamâ‚™ _)    c = âŠ¥-elim (â„•â‰¢Î  (sym c))
  nfN (prodâ±¼ _ _ _ _) Î³â–¸u (prodâ‚™ _ _) c = âŠ¥-elim (â„•â‰¢Î£ (sym c))
  nfN (starâ±¼ _)       Î³â–¸u starâ‚™       c = âŠ¥-elim (â„•â‰¢Unitâ±¼ (sym c))
  -- q.e.d

  -- Canonicity theorem: Any well-typed term Î“ âŠ¢ t : â„• is convertible to a numeral.

  thm : (âŠ¢t : Î“ âŠ¢ t âˆ· â„•) â†’ (Î³â–¸t : Î³ â–¸ t) â†’ âˆƒ Î» u â†’ Numeral u Ã— Î“ âŠ¢ t â‰¡ u âˆ· â„•
  thm âŠ¢t Î³â–¸t with fullRedTerm âŠ¢t Î³â–¸t
  ... | u , nf , eq , Î³â–¸u = u , nfN (projâ‚‚ (projâ‚‚ (syntacticEqTerm eq))) Î³â–¸u nf (refl (â„•â±¼ (wfTerm âŠ¢t))) , eq

  -- Any well-typed term Î“ âŠ¢ t : â„• WH-reduces to zero or suc u for some u

  lem : Î“ âŠ¢ t âˆ· â„• â†’ Î³ â–¸ t â†’ âˆƒ Î» u â†’ Î“ âŠ¢ t â‡’* u âˆ· â„• Ã— Whnf u Ã— (Neutral u â†’ âŠ¥)
  lem âŠ¢t Î³â–¸t =
    let u , whnfU , d = whNormTerm âŠ¢t
        Î³â–¸u = usagePres*Term Î³â–¸t (redâ‚œ d)
        âŠ¢â„• = refl (â„•â±¼ (wfTerm âŠ¢t))
    in  u , redâ‚œ d , whnfU , Î» x â†’ Â¬negâ„• (neNeg (âŠ¢u-redâ‚œ d) x Î³â–¸u) âŠ¢â„•

  thmâ€² : Î“ âŠ¢ t âˆ· â„• â†’ Î³ â–¸ t â†’ (Î“ âŠ¢ t â‡’* zero âˆ· â„•) âŠ âˆƒ Î» u â†’ Î“ âŠ¢ t â‡’* suc u âˆ· â„•
  thmâ€² âŠ¢t Î³â–¸t with lem âŠ¢t Î³â–¸t
  -- True cases
  ... | _ , d , zeroâ‚™ , Â¬neU = injâ‚ d
  ... | _ , d , sucâ‚™ , Â¬neU = injâ‚‚ (_ , d)
  -- False cases
  ... | _ , d , Uâ‚™ , Â¬neU = âŠ¥-elim (redU*Term d)
  ... | _ , d , Î â‚™ , Â¬neU =
    let _ , _ , âŠ¢Î  = syntacticRedTerm d
        _ , _ , â„•â‰¡U = inversion-Î  âŠ¢Î 
    in  âŠ¥-elim (Uâ‰¢â„• (sym â„•â‰¡U))
  ... | _ , d , Î£â‚™ , Â¬neU =
    let _ , _ , âŠ¢Î£ = syntacticRedTerm d
        _ , _ , â„•â‰¡U = inversion-Î£ âŠ¢Î£
    in  âŠ¥-elim (Uâ‰¢â„• (sym â„•â‰¡U))
  ... | _ , d , â„•â‚™ , Â¬neU =
    let _ , _ , âŠ¢â„• = syntacticRedTerm d
        â„•â‰¡U = inversion-â„• âŠ¢â„•
    in  âŠ¥-elim (Uâ‰¢â„• (sym â„•â‰¡U))
  ... | _ , d , Unitâ‚™ , Â¬neU =
    let _ , _ , âŠ¢Unit = syntacticRedTerm d
        â„•â‰¡U = inversion-Unit âŠ¢Unit
    in  âŠ¥-elim (Uâ‰¢â„• (sym â„•â‰¡U))
  ... | _ , d , Emptyâ‚™ , Â¬neU =
    let _ , _ , âŠ¢Empty = syntacticRedTerm d
        â„•â‰¡U = inversion-Empty âŠ¢Empty
    in  âŠ¥-elim (Uâ‰¢â„• (sym â„•â‰¡U))
  ... | _ , d , lamâ‚™ , Â¬neU =
    let _ , _ , âŠ¢lam = syntacticRedTerm d
        _ , _ , _ , _ , _ , â„•â‰¡Î  = inversion-lam âŠ¢lam
    in  âŠ¥-elim (â„•â‰¢B BÎ ! â„•â‰¡Î )
  ... | _ , d , starâ‚™ , Â¬neU =
    let _ , _ , âŠ¢star = syntacticRedTerm d
        â„•â‰¡Unit = inversion-star âŠ¢star
    in  âŠ¥-elim (â„•â‰¢Unitâ±¼ â„•â‰¡Unit)
  ... | _ , d , prodâ‚™ , Â¬neU =
    let _ , _ , âŠ¢prod = syntacticRedTerm d
        _ , _ , _ , _ , _ , _ , _ , â„•â‰¡Î£ = inversion-prod âŠ¢prod
    in  âŠ¥-elim (â„•â‰¢B BÎ£! â„•â‰¡Î£)
  ... | _ , d , ne x , Â¬neU = âŠ¥-elim (Â¬neU x)

-- Q.E.D. 2023-01-20
