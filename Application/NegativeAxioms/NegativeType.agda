------------------------------------------------------------------------
-- Negative types (types for which all branches end in âŠ¥).
------------------------------------------------------------------------

open import Definition.Typed.Restrictions
open import Graded.Modality

module Application.NegativeAxioms.NegativeType
  {a} {M : Set a}
  {ğ•„ : Modality M}
  (R : Type-restrictions ğ•„)
  where

open import Definition.Untyped M as U hiding (_âˆ·_)

open import Definition.Typed R
open import Definition.Typed.Properties R
open import Definition.Typed.Weakening R as T
open import Definition.Typed.Consequences.Inequality R as I
open import Definition.Typed.Consequences.Injectivity R
open import Definition.Typed.Consequences.Substitution R

open import Tools.Empty
open import Tools.Function
open import Tools.Nat
open import Tools.Product
open import Tools.Relation

-- Preliminaries
---------------------------------------------------------------------------

private
  Ty  = Term
  Cxt = Con Ty
  variable
    m m'  : Nat
    Ï     : Wk m m'
    Ïƒ     : Subst m m'
    Î“ Î”   : Con Term m
    A B C : Term m
    t u   : Term m
    p q   : M
    s     : SigmaMode

-- Negative types
---------------------------------------------------------------------------

-- A type is negative if all of its branches end in âŠ¥.
-- The prime example is negation Â¬A.

data NegativeType (Î“ : Cxt m) : Ty m â†’ Set a where

  empty : NegativeType Î“ Empty

  pi    : Î“ âŠ¢ A
        â†’ NegativeType (Î“ âˆ™ A) B
        â†’ NegativeType Î“ (Î  p , q â–· A â–¹ B)

  sigma : Î“ âŠ¢ A
        â†’ NegativeType Î“ A
        â†’ NegativeType (Î“ âˆ™ A) B
        â†’ NegativeType Î“ (Î£â‚š p , q â–· A â–¹ B)

  conv  : NegativeType Î“ A
        â†’ Î“ âŠ¢ A â‰¡ B
        â†’ NegativeType Î“ B

-- Lemma: Negative types are closed under weakening.

wkNeg : Ï âˆ· Î” âŠ‡ Î“ â†’ âŠ¢ Î” â†’ NegativeType Î“ A â†’ NegativeType Î” (U.wk Ï A)

wkNeg w âŠ¢Î” empty
  = empty

wkNeg w âŠ¢Î” (pi dA nB)
  = pi dA' (wkNeg (lift w) (âŠ¢Î” âˆ™ dA') nB)
    where dA' = T.wk w âŠ¢Î” dA

wkNeg w âŠ¢Î” (sigma dA nA nB)
  = sigma dA' (wkNeg w âŠ¢Î” nA) (wkNeg (lift w) (âŠ¢Î” âˆ™ dA') nB)
    where dA' = T.wk w âŠ¢Î” dA

wkNeg w âŠ¢Î” (conv n c)
  = conv (wkNeg w âŠ¢Î” n) (wkEq w âŠ¢Î” c)

-- Lemma: Negative types are closed under parallel substitution.

subNeg : NegativeType Î“ A â†’ Î” âŠ¢Ë¢ Ïƒ âˆ· Î“ â†’ âŠ¢ Î” â†’ NegativeType Î” (A [ Ïƒ ])

subNeg empty _ _ = empty

subNeg (pi âŠ¢A n) s âŠ¢Î”
  = pi âŠ¢ÏƒA (subNeg n (liftSubstâ€² (wf âŠ¢A) âŠ¢Î” âŠ¢A s) (âŠ¢Î” âˆ™ âŠ¢ÏƒA))
    where âŠ¢ÏƒA = substitution âŠ¢A s âŠ¢Î”

subNeg (sigma âŠ¢A nA nB) s âŠ¢Î”
  = sigma âŠ¢ÏƒA (subNeg nA s âŠ¢Î”) (subNeg nB (liftSubstâ€² (wf âŠ¢A) âŠ¢Î” âŠ¢A s) (âŠ¢Î” âˆ™ âŠ¢ÏƒA))
    where âŠ¢ÏƒA = substitution âŠ¢A s âŠ¢Î”

subNeg (conv n c) s âŠ¢Î” = conv (subNeg n s âŠ¢Î”) (substitutionEq c (substRefl s) âŠ¢Î”)

-- Corollary: Negative types are closed under single substitution.

subNeg1 : NegativeType (Î“ âˆ™ A) B â†’ Î“ âŠ¢ t âˆ· A â†’ NegativeType Î“ (B [ t ]â‚€)
subNeg1 n âŠ¢t = subNeg n (singleSubst âŠ¢t) (wfTerm âŠ¢t)

-- Lemma: The first component of a negative Î£-type is negative.

fstNeg :
  NegativeType Î“ C â†’
  Î“ âŠ¢ C â‰¡ Î£â‚š p , q â–· A â–¹ B â†’
  NegativeType Î“ A
fstNeg empty          c = âŠ¥-elim (Emptyâ‰¢Î£â±¼ c)
fstNeg (pi _ _)       c = âŠ¥-elim (Î â‰¢Î£â±¼ c)
fstNeg (sigma _ nA _) c = conv nA (projâ‚ (Î£-injectivity c))
fstNeg (conv n c)    c' = fstNeg n (trans c c')

-- Lemma: Any instance of the second component of a negative Î£-type is negative.

sndNeg :
  NegativeType Î“ C â†’
  Î“ âŠ¢ C â‰¡ Î£â‚š p , q â–· A â–¹ B â†’
  Î“ âŠ¢ t âˆ· A â†’
  NegativeType Î“ (B [ t ]â‚€)
sndNeg empty          c = âŠ¥-elim (Emptyâ‰¢Î£â±¼ c)
sndNeg (pi _ _)       c = âŠ¥-elim (Î â‰¢Î£â±¼ c)
sndNeg (sigma _ _ nB) c âŠ¢t = let (cA , cB , _ , _) = Î£-injectivity c in
    subNeg (conv nB cB) (singleSubst (conv âŠ¢t (sym cA))) (wfTerm âŠ¢t)
sndNeg (conv n c)    c' = sndNeg n (trans c c')

-- Lemma: Any instance of the codomain of a negative Î -type is negative.

appNeg : NegativeType Î“ C â†’ Î“ âŠ¢ C â‰¡ Î  p , q â–· A â–¹ B â†’ Î“ âŠ¢ t âˆ· A â†’ NegativeType Î“ (B [ t ]â‚€)
appNeg empty          c = âŠ¥-elim (Emptyâ‰¢Î â±¼ c)
appNeg (sigma _ _ _)  c = âŠ¥-elim (Î â‰¢Î£â±¼ (sym c))
appNeg (pi _ nB) c âŠ¢t = let (cA , cB , _ , _) = injectivity c in
  subNeg (conv nB cB) (singleSubst (conv âŠ¢t (sym cA))) (wfTerm âŠ¢t)
appNeg (conv n c)    c' = appNeg n (trans c c')

-- Lemma: The type â„• is not negative.

Â¬negâ„• : NegativeType Î“ C â†’ Î“ âŠ¢ C â‰¡ â„• â†’ âŠ¥
Â¬negâ„• empty         c = â„•â‰¢Emptyâ±¼ (sym c)
Â¬negâ„• (pi _ _)      c = â„•â‰¢Î  (sym c)
Â¬negâ„• (sigma _ _ _) c = â„•â‰¢Î£ (sym c)
Â¬negâ„• (conv n c)   c' = Â¬negâ„• n (trans c c')

-- Lemma: The type Î£áµ£ is not negative

Â¬negÎ£áµ£ : NegativeType Î“ C â†’ Î“ âŠ¢ C â‰¡ Î£áµ£ p , q â–· A â–¹ B â†’ âŠ¥
Â¬negÎ£áµ£ empty         c = Emptyâ‰¢Bâ±¼ BÎ£! c
Â¬negÎ£áµ£ (pi _ _)      c = Î â‰¢Î£â±¼ c
Â¬negÎ£áµ£ (sigma _ _ _) c = Î£â‚šâ‰¢Î£áµ£â±¼ c
Â¬negÎ£áµ£ (conv n c)   c' = Â¬negÎ£áµ£ n (trans c c')

-- Lemma: The type Unit is not negative.

Â¬negUnit : NegativeType Î“ C â†’ Î“ âŠ¢ C â‰¡ Unit s â†’ âŠ¥
Â¬negUnit empty c = Emptyâ‰¢Unitâ±¼ c
Â¬negUnit (pi _ _) c = Unitâ‰¢Î â±¼ (sym c)
Â¬negUnit (sigma _ _ _) c = Unitâ‰¢Î£â±¼ (sym c)
Â¬negUnit (conv n c) câ€² = Â¬negUnit n (trans c câ€²)

opaque

  -- Identity types are not negative.

  Â¬negId : NegativeType Î“ A â†’ Â¬ Î“ âŠ¢ A â‰¡ Id B t u
  Â¬negId empty         = Idâ‰¢Empty âˆ˜â†’ sym
  Â¬negId (pi _ _)      = I.Idâ‰¢Î Î£ âˆ˜â†’ sym
  Â¬negId (sigma _ _ _) = I.Idâ‰¢Î Î£ âˆ˜â†’ sym
  Â¬negId (conv n Bâ‰¡A)  = Â¬negId n âˆ˜â†’ trans Bâ‰¡A
