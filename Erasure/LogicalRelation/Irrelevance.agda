{-# OPTIONS --without-K --safe #-}

open import Definition.Modality.Instances.Erasure
open import Definition.Typed.EqualityRelation
open import Definition.Typed Erasureâ€²
open import Definition.Untyped Erasure

module Erasure.LogicalRelation.Irrelevance {k} {Î” : Con Term k} (âŠ¢Î” : âŠ¢ Î”)
                                           (Prodrec : Erasure â†’ Set)
                                           {{eqrel : EqRelSet Erasureâ€²}} where
open EqRelSet {{...}}

open import Definition.Modality.Instances.Erasure.Modality Prodrec
open import Erasure.LogicalRelation âŠ¢Î” Prodrec

open import Definition.LogicalRelation Erasureâ€²
open import Definition.LogicalRelation.ShapeView Erasureâ€²
open import Definition.LogicalRelation.Substitution Erasureâ€²
import Definition.LogicalRelation.Irrelevance Erasureâ€² as I
import Definition.LogicalRelation.Substitution.Irrelevance Erasureâ€² as IS

import Definition.Untyped.BindingType Erasureâ€² as BT
open import Definition.Typed.Weakening Erasureâ€²
open import Definition.Typed.Properties Erasureâ€²

open import Definition.Modality.Context ErasureModality

open import Tools.Level
open import Tools.Nat
open import Tools.Product
import Tools.PropositionalEquality as PE
open import Tools.Unit

private
  variable
    n : Nat
    Î“ : Con Term n
    A Aâ€² t : Term n
    Î³ : Conâ‚˜ n
    p : Erasure

-- Irrelevance of logical relation for erasure using a ShapreView

irrelevanceTermSV : âˆ€ {l lâ€² t v A}
                  â†’ ([A] : Î” âŠ©âŸ¨ l âŸ© A)
                    ([A]â€² : Î” âŠ©âŸ¨ lâ€² âŸ© A)
                  â†’ t Â®âŸ¨ l âŸ© v âˆ· A / [A]
                  â†’ ShapeView Î” l lâ€² A A [A] [A]â€²
                  â†’ t Â®âŸ¨ lâ€² âŸ© v âˆ· A / [A]â€²
irrelevanceTermSV .(Uáµ£ UA) .(Uáµ£ UB) tÂ®v (Uáµ¥ UA UB) = tÂ®v
irrelevanceTermSV .(â„•áµ£ â„•A) .(â„•áµ£ â„•B) tÂ®v (â„•áµ¥ â„•A â„•B) = tÂ®v
irrelevanceTermSV .(Unitáµ£ UnitA) .(Unitáµ£ UnitB) tÂ®v (Unitáµ¥ UnitA UnitB) = tÂ®v
irrelevanceTermSV [A] [A]â€² tÂ®v (Báµ¥ (BÎ  ğŸ˜ q) BÎ ! (Báµ£ F G D âŠ¢F âŠ¢G Aâ‰¡A [F] [G] G-ext)
                               (Báµ£ Fâ‚ Gâ‚ Dâ‚ âŠ¢Fâ‚ âŠ¢Gâ‚ Aâ‰¡Aâ‚ [F]â‚ [G]â‚ G-extâ‚) (BT.Î â‰‹Î  PE.refl PE.refl)) [a]â€²
                               with whrDet* (red D , Î â‚™) (red Dâ‚ , Î â‚™)
... | Î â‰¡Î â€² with B-PE-injectivity (BÎ  ğŸ˜ q) (BÎ  ğŸ˜ q) Î â‰¡Î â€²
... | PE.refl , PE.refl , _ =
  let [a] = I.irrelevanceTerm ([F]â‚ id âŠ¢Î”) ([F] id âŠ¢Î”) [a]â€²
      tÂ®vâ€² = tÂ®v [a]
      SVâ€² = goodCasesRefl ([G] id âŠ¢Î” [a]) ([G]â‚ id âŠ¢Î” [a]â€²)
  in  irrelevanceTermSV ([G] id âŠ¢Î” [a]) ([G]â‚ id âŠ¢Î” [a]â€²) tÂ®vâ€² SVâ€²
irrelevanceTermSV [A] [A]â€² tÂ®v (Báµ¥ (BÎ  Ï‰ q) BÎ ! (Báµ£ F G D âŠ¢F âŠ¢G Aâ‰¡A [F] [G] G-ext)
                               (Báµ£ Fâ‚ Gâ‚ Dâ‚ âŠ¢Fâ‚ âŠ¢Gâ‚ Aâ‰¡Aâ‚ [F]â‚ [G]â‚ G-extâ‚) (BT.Î â‰‹Î  PE.refl PE.refl)) [a]â€² aÂ®wâ€²
                               with whrDet* (red D , Î â‚™) (red Dâ‚ , Î â‚™)
... | Î â‰¡Î â€² with B-PE-injectivity (BÎ  Ï‰ q) (BÎ  Ï‰ q) Î â‰¡Î â€²
... | PE.refl , PE.refl , _ =
  let [a] = I.irrelevanceTerm ([F]â‚ id âŠ¢Î”) ([F] id âŠ¢Î”) [a]â€²
      SV = goodCasesRefl ([F]â‚ id âŠ¢Î”) ([F] id âŠ¢Î”)
      aÂ®w = irrelevanceTermSV ([F]â‚ id âŠ¢Î”) ([F] id âŠ¢Î”) aÂ®wâ€² SV
      tÂ®vâ€² = tÂ®v [a] aÂ®w
      SVâ€² = goodCasesRefl ([G] id âŠ¢Î” [a]) ([G]â‚ id âŠ¢Î” [a]â€²)
      in  irrelevanceTermSV ([G] id âŠ¢Î” [a]) ([G]â‚ id âŠ¢Î” [a]â€²) tÂ®vâ€² SVâ€²
irrelevanceTermSV [A] [A]â€² (tâ‚ , tâ‚‚ , vâ‚ , vâ‚‚ , tâ‡’tâ€² , vâ‡’vâ€² , [tâ‚] , tâ‚Â®vâ‚ , tâ‚‚Â®vâ‚‚)
                  (Báµ¥ (BÎ£ q m) BÎ£! (Báµ£ F G D âŠ¢F âŠ¢G Aâ‰¡A [F] [G] G-ext)
                      (Báµ£ Fâ‚ Gâ‚ Dâ‚ âŠ¢Fâ‚ âŠ¢Gâ‚ Aâ‰¡Aâ‚ [F]â‚ [G]â‚ G-extâ‚) (BT.Î£â‰‹Î£ PE.refl))
                  with whrDet* (red D , Î£â‚™) (red Dâ‚ , Î£â‚™)
... | Î£â‰¡Î£â€² with B-PE-injectivity (BÎ£ q m) (BÎ£ q m) Î£â‰¡Î£â€²
... | PE.refl , PE.refl , _ =
  let [F]â€² = [F] id âŠ¢Î”
      [F]â‚â€² = [F]â‚ id âŠ¢Î”
      [tâ‚]â€² = I.irrelevanceTerm [F]â€² [F]â‚â€² [tâ‚]
      [Gtâ‚] = [G] id âŠ¢Î” [tâ‚]
      [Gtâ‚]â‚ = [G]â‚ id âŠ¢Î” [tâ‚]â€²
      tâ‚Â®vâ‚â€² = irrelevanceTermSV [F]â€² [F]â‚â€² tâ‚Â®vâ‚ (goodCasesRefl [F]â€² [F]â‚â€²)
      tâ‚‚Â®vâ‚‚â€² = irrelevanceTermSV [Gtâ‚] [Gtâ‚]â‚ tâ‚‚Â®vâ‚‚ (goodCasesRefl [Gtâ‚] [Gtâ‚]â‚)
  in  tâ‚ , tâ‚‚ , vâ‚ , vâ‚‚ , tâ‡’tâ€² , vâ‡’vâ€² , [tâ‚]â€² , tâ‚Â®vâ‚â€² , tâ‚‚Â®vâ‚‚â€²
irrelevanceTermSV (emb 0<1 [A]) [A]â€² tÂ®v (embâ°Â¹ SV) = irrelevanceTermSV [A] [A]â€² tÂ®v SV
irrelevanceTermSV [A] (emb 0<1 [A]â€²) tÂ®v (embÂ¹â° SV) = irrelevanceTermSV [A] [A]â€² tÂ®v SV
-- Impossible cases
irrelevanceTermSV .(Emptyáµ£ EmptyA) .(Emptyáµ£ EmptyB) () (Emptyáµ¥ EmptyA EmptyB)
irrelevanceTermSV .(ne neA) .(ne neB) () (ne neA neB)
irrelevanceTermSV _ _ tÂ®v (Báµ¥ BÎ£! BÎ ! BA BB ())
irrelevanceTermSV _ _ tÂ®v (Báµ¥ BÎ ! BÎ£! BA BB ())

-- Irrelevance of logical relation for erasure

irrelevanceTerm : âˆ€ {l lâ€² t v A}
                â†’ ([A] : Î” âŠ©âŸ¨ l âŸ© A)
                  ([A]â€² : Î” âŠ©âŸ¨ lâ€² âŸ© A)
                â†’ t Â®âŸ¨ l âŸ© v âˆ· A / [A]
                â†’ t Â®âŸ¨ lâ€² âŸ© v âˆ· A / [A]â€²
irrelevanceTerm [A] [A]â€² tÂ®v = irrelevanceTermSV [A] [A]â€² tÂ®v (goodCasesRefl [A] [A]â€²)

-- Irrelevance of logical relation for erasure with propositionally equal types

irrelevanceTermâ€² : âˆ€ {l lâ€² t v A}
                 â†’ A PE.â‰¡ Aâ€²
                 â†’ ([A] : Î” âŠ©âŸ¨ l âŸ© A)
                 â†’ ([A]â€² : Î” âŠ©âŸ¨ lâ€² âŸ© Aâ€²)
                 â†’ t Â®âŸ¨ l âŸ© v âˆ· A / [A]
                 â†’ t Â®âŸ¨ lâ€² âŸ© v âˆ· Aâ€² / [A]â€²
irrelevanceTermâ€² PE.refl [A] [A]â€² tÂ®v = irrelevanceTerm [A] [A]â€² tÂ®v

-- Irrelevance of quantified logical relation for erasure

irrelevanceQuant : âˆ€ {l lâ€² t v A}
                 â†’ ([A] : Î” âŠ©âŸ¨ l âŸ© A)
                 â†’ ([A]â€² : Î” âŠ©âŸ¨ lâ€² âŸ© A)
                 â†’ t Â®âŸ¨ l âŸ© v âˆ· A â—‚ p / [A]
                 â†’ t Â®âŸ¨ lâ€² âŸ© v âˆ· A â—‚ p / [A]â€²
irrelevanceQuant {ğŸ˜} [A] [A]â€² tÂ®v = tt
irrelevanceQuant {Ï‰} [A] [A]â€² tÂ®v = irrelevanceTerm [A] [A]â€² tÂ®v

-- Irrelevance of related substitutions

irrelevanceSubst : âˆ€ {Ïƒ Ïƒâ€² l}
                 â†’ ([Î“] [Î“]â€² : âŠ©áµ› Î“)
                   ([Ïƒ] : Î” âŠ©Ë¢ Ïƒ âˆ· Î“ / [Î“] / âŠ¢Î”)
                   ([Ïƒ]â€² : Î” âŠ©Ë¢ Ïƒ âˆ· Î“ / [Î“]â€² / âŠ¢Î”)
                   (ÏƒÂ®Ïƒâ€² : Ïƒ Â®âŸ¨ l âŸ© Ïƒâ€² âˆ· Î“ â—‚ Î³ / [Î“] / [Ïƒ])
                 â†’ (Ïƒ Â®âŸ¨ l âŸ© Ïƒâ€² âˆ· Î“ â—‚ Î³ / [Î“]â€² / [Ïƒ]â€²)
irrelevanceSubst {Î“ = Îµ} {Î³ = Îµ} Îµ Îµ (lift tt) (lift tt) tt = tt
irrelevanceSubst {Î“ = Î“ âˆ™ A} {Î³ = Î³ âˆ™ p} {l = l}
                 ([Î“] âˆ™ [A]) ([Î“]â€² âˆ™ [A]â€²) ([tailÏƒ] , b) ([tailÏƒ]â€² , d) (ÏƒÂ®Ïƒ , tÂ®v) =
  let ÏƒÂ®Ïƒâ€² = irrelevanceSubst {l = l} [Î“] [Î“]â€² [tailÏƒ] [tailÏƒ]â€² ÏƒÂ®Ïƒ
      [ÏƒA] = projâ‚ (unwrap [A] âŠ¢Î” [tailÏƒ])
      tÂ®vâ€² = irrelevanceQuant {p = p} (projâ‚ (unwrap [A] âŠ¢Î” [tailÏƒ]))
                              (projâ‚ (unwrap [A]â€² âŠ¢Î” [tailÏƒ]â€²)) tÂ®v
  in  ÏƒÂ®Ïƒâ€² , tÂ®vâ€²

-- Irrelevance of erasure validity

irrelevance : âˆ€ {l lâ€²}
            â†’ ([Î“] [Î“]â€² : âŠ©áµ› Î“)
              ([A] : Î“ âŠ©áµ›âŸ¨ l âŸ© A / [Î“])
              ([A]â€² : Î“ âŠ©áµ›âŸ¨ lâ€² âŸ© A / [Î“]â€²)
              (âŠ©Ê³t : Î³ â–¸ Î“ âŠ©Ê³âŸ¨ l âŸ© t âˆ· A / [Î“] / [A])
            â†’ (Î³ â–¸ Î“ âŠ©Ê³âŸ¨ lâ€² âŸ© t âˆ· A / [Î“]â€² / [A]â€²)
irrelevance {l = l} [Î“] [Î“]â€² [A] [A]â€² âŠ©Ê³t [Ïƒ]â€² ÏƒÂ®Ïƒâ€² =
  let [Ïƒ] = IS.irrelevanceSubst [Î“]â€² [Î“] âŠ¢Î” âŠ¢Î” [Ïƒ]â€²
      ÏƒÂ®Ïƒ = irrelevanceSubst {l = l} [Î“]â€² [Î“] [Ïƒ]â€² [Ïƒ] ÏƒÂ®Ïƒâ€²
      tÂ®v = âŠ©Ê³t [Ïƒ] ÏƒÂ®Ïƒ
  in  irrelevanceTerm (projâ‚ (unwrap [A] âŠ¢Î” [Ïƒ])) (projâ‚ (unwrap [A]â€² âŠ¢Î” [Ïƒ]â€²)) tÂ®v
